
class DMR_ResCounter : Inventory
{
	Default
	{
		Inventory.Amount 1;
		Inventory.MaxAmount 1;
	}
}

// after images
class DMR_TranslucentAfterImage : Actor
{   
	Default
	{
		+NOINTERACTION;
		RenderStyle "Translucent";
		Alpha 0.5;
	}
	States
	{
	Spawn:
		TNT1 A 3;
		"----" A 3 A_FadeOut(0.10);
		Wait;
	}
}

class DMR_SubtractAfterImage : DMR_TranslucentAfterImage
{   
	Default
	{
		RenderStyle "Subtract";
	}
}

//======================================================================
//  Original credits
//======================================================================
//	ZScript: 	DustedPandemonic, modified by Dutchygamer
//======================================================================
class DMR_BerzerkAfterImage : DMR_TranslucentAfterImage
{   
	Default
	{
		Renderstyle "AddStencil";
		+BRIGHT;
		Alpha 0.08;
	}
	States
	{
	Spawn:
		TNT1 A 3;
		"----" A 1 {
			A_FadeOut(frandom(0.01, 0.005));
			A_SetScale(Scale.X+frandom(0.01, 0.02));
		}
		Wait;
	}
}

// dropped shield
Class DMR_DroppedShield: Actor
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 6;
		BounceType "Doom";
		+DropOff;
		+Missile;
	}
	States
	{
	Spawn:
		HWSH ABCDEFGH 3;
		Loop;
	Death:
		HWSH I -1;
		Loop;
	}
}

// monster ram impact 'projectile'
class DMR_RamImpactor : Actor
{
	Default
	{
		+NOBLOCKMAP;
		+NOGRAVITY;
		+PUFFONACTORS;
		+BLOODLESSIMPACT;
		-ALLOWPARTICLES;
		ProjectileKickback 600;
		SeeSound "mummy/attack2";
	}
	States
	{
	Spawn:
		TNT1 A 10;
		Stop;
	}
}

//======================================================================
//  Credits
//	Sounds:		EA Games
//	ZScript:	Dutchygamer
//	DustedPandomic for improvements to the ShieldWall logic
//======================================================================

// to make HellWarriors and Paladin's shield block a bit less annoying...
class DMR_ShieldWall : Actor
{	
	Default
	{
		Radius 30; // just a tad wider then a hellknight is
		Height 64; // same height
		Health 125; // the damage the shield can take
		Mass 9999; // don't get pushed around by shots
		PainChance 256;
		+NOBLOOD;
		+SHOOTABLE;
		+NOGRAVITY;
		+NODAMAGETHRUST;
		+NOTELEPORT;
		+DONTRIP;
		+THRUSPECIES;
		Species "DMR_ShieldWall"; //so the shield projectiles can pass through the shield
	}
	States
	{
	Spawn: 
		TNT1 A 1 NoDelay A_Warp(AAPTR_MASTER,20,0,10);
		Loop;
	Pain:
		TNT1 A 1 {
			A_StartSound("shieldwall/pain", CHAN_WEAPON);
			A_Warp(AAPTR_MASTER,25,0,10);
		}
		Goto Spawn;
	Death:
		TNT1 A 1 A_DamageMaster(1,"DMR_ShieldBroken"); // on death deal a specific damagetype
		Stop;
 	}
}

// basically carbon copy of the original TeleporterFog so we can replace SWWM's TeleporterFog replacer back with the vanilla-like one as that one can cause major slowdowns when triggered often
class DMR_TeleportFog : Actor
{
	default
	{
		+NOBLOCKMAP
		+NOTELEPORT
		+NOGRAVITY
		RenderStyle "Add";
	}
	States
	{
	Spawn:
		TFOG ABABCDEFGHIJ 6 Bright;
		Stop;
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay ();
		A_StartSound ("misc/teleport", CHAN_BODY);
	}
	
}