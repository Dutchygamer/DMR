//======================================================================
//  Original credits
//======================================================================
//	ZScript: 	DustedPandemonic, modified by Dutchygamer
//======================================================================

class DMR_Projectile : Actor
{
	Default
	{
		Projectile;
		+FORCEXYBILLBOARD;
		DMR_Projectile.ParticleColors "000000", "000000", "FFFFFF", "FFFFFF";
	}
	
	int fxLoop;
	
	array<color> ParticleColor;
	color pcolor1;
	color pcolor2;
	color pcolor3;
	color pcolor4;
	property ParticleColors: pcolor1, pcolor2, pcolor3, pcolor4;
	
	override void BeginPlay()
	{
		Super.BeginPlay();
		ParticleColor.Push(pcolor1);
		ParticleColor.Push(pcolor2);
		ParticleColor.Push(pcolor3);
		ParticleColor.Push(pcolor4);
	}
	
	virtual color GetParticleColor()
	{
		return ParticleColor[random(0,ParticleColor.Size()-1)];
	}
	
	// the 'float down to lowest point' logic
	// after waaay too many trial and errors with various combinations of actor flags, I realized it would be way easier to do this with Zscript overrides
	void GravityTick()
	{
		// check whether we are not on the floor
		// double check it by comparing the z value and the dedicated TestMobjZ function
		bool isInAir = ( (pos.z > floorz) && TestMobjZ() );
		// if in air, set a z velocity so we float down slowly
		if ( isInAir ) {
			vel.z = -0.4;
		// if not in the air, set our z velocity to 0
		} else {
			vel.z = 0;
		}
		Super.Tick();
	}
	
}

class DMR_FastProjectile : FastProjectile
{
	Default
	{
		Projectile;
		+FORCEXYBILLBOARD;
	}
}


//======================================================================
//  Original credits
//======================================================================
//	ZScript: 	DustedPandemonic, modified by Dutchygamer
//======================================================================

class DMR_Particle : Actor
{
	Default
	{
		Projectile;
		+FORCEXYBILLBOARD;
		DMR_Particle.ParticleColors "000000", "000000", "FFFFFF", "FFFFFF";
	}
	
	int fxLoop;
	
	array<color> ParticleColor;
	color pcolor1;
	color pcolor2;
	color pcolor3;
	color pcolor4;
	property ParticleColors: pcolor1, pcolor2, pcolor3, pcolor4;
	
	override void BeginPlay()
	{
		Super.BeginPlay();
		ParticleColor.Push(pcolor1);
		ParticleColor.Push(pcolor2);
		ParticleColor.Push(pcolor3);
		ParticleColor.Push(pcolor4);
	}
	
	virtual color GetParticleColor()
	{
		return ParticleColor[random(0,ParticleColor.Size()-1)];
	}
}


//======================================================================

// base class so we can do some creativity for all 'fancy' effects
class DMR_FXBase : Actor
{
	Default
	{
		+FORCEXYBILLBOARD;
	}
}

//======================================================================
//  Original credits
//======================================================================
//	ZScript: 	DustedPandemonic, modified by Dutchygamer
//======================================================================

Class DMR_MonsterBase : Actor
{
	Default
	{
		Monster;
		+FLOORCLIP;
		
		// set a default so we can damage each other as intended unless we are the same species here as well
		DMR_MonsterBase.Species2 "None";
	}

	string spec2;
	property Species2: spec2;
	
	int attackLoop;
	int fxLoop;

	// check if the monster damaging us is part of the Species2
	// if so, treat it like it is the same species (unless DONTHARMSPECIES is false)
	override int DamageMobj(Actor inflictor, Actor source, int damage, Name mod, int flags, double angle)
	{
		if (source && source.bISMONSTER == true && source is "DMR_MonsterBase")
		{
			let source2 = DMR_MonsterBase(source);
			if (source.bDONTHARMSPECIES == true && spec2 != "None" && spec2 == source2.spec2)
			{
				return 0;
			}
		}
		return Super.DamageMobj(inflictor,source,damage,mod,flags,angle);
	}
	
	// variant of A_SpawnProjectile which adds a lob to the projectile's trajectory while taking the height difference between firer and target into account
	action actor A_LobProjectile(class<Actor> MissileType, double SpawnHeight = 32, double SpawnOfs_XY = 0, double Angle = 0, int Flags = 0, double Pitch = 0, int Ptr = AAPTR_TARGET)
	{
		actor proj = A_SpawnProjectile(MissileType,SpawnHeight,SpawnOfs_XY,Angle,Flags,Pitch,Ptr);
		actor other = GetPointer(Ptr);
		if (proj)
		{
			if (other)
			{
				int ZDiff = int(other.Pos.Z + other.Height / 2 - Pos.Z - SpawnHeight);
				proj.Vel.Z += (ZDiff + Distance2D(other)) / TICRATE * proj.GetGravity() / 2;
			}
			return proj;
		}
		return null;
	}
	
	// some creativity that for monster variants that can fly while the original monster doesn't (ie Afrit for HellKnight) they don't break certain Boom/ZDoom map things	
	void A_BeginFloat()
	{
		A_NoGravity(); 
		bFLOAT = true;
		bFLOORCLIP = false;
	}	
	
	// monster moves with given speed in given angle
	void A_Dash(double speed, double angle)
	{
		Thrust(speed,angle);
	}
}


Class DMR_FlyingMonsterBase : DMR_MonsterBase
{
	Default
	{
		-FLOORCLIP;
		+FLOAT;
		+NOGRAVITY;
	}
}

//======================================================================
//  Original credits
//======================================================================
//	ZScript: 	DustedPandemonic, modified by Dutchygamer
//======================================================================
Mixin Class DMR_BerserkingMonster
{
	bool criticalDamaged;
	int berserkThreshold;
	
	override void Tick()
  	{
		Super.Tick();
		if (!criticalDamaged && health > 0 && health < berserkThreshold && bNOPAIN == false) 
		{ 
			SetStateLabel("CriticalDamage"); 
		}
		if (criticalDamaged && health > 0 && !IsFrozen())
		{	
			A_SpawnItemEx("DMR_StencilledGhostAfterImage",-1,0,0,-0.2,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSTENCILCOL); 			
		}
  	}
	
	void A_SetBerserkerTics(int normalTics, int berserkTics)
	{
		if (criticalDamaged) {
			A_SetTics(berserkTics);
		}
		else {
			A_SetTics(normalTics);
		}
	}
}