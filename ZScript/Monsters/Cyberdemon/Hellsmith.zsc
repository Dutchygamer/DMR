//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Raven Software
//  Sprites: 	Raven Software (edits by Eriance)
//	GLDEFs: 	Ghastly Dragon
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Forgemaster : DMR_Cyberdemon
{ 
	Default
	{
		Tag "Forgemaster";
		Health 5500;
		PainChance 10;
		DropItem "CellPack", 256, 100;
		DropItem "CellPack", 128, 100;
		DropItem "Cell", 128, 20;
		DropItem "Cell", 128, 20;
		DropItem "Cell", 128, 20;
		Obituary "%o was no match for the might of the Forgemaster.";
		HitObituary "%o was crushed under the Forgemaster's hoof.";
		DamageFunction(random(30,90)); //for charging
		MeleeRange 86;
		SeeSound "hellsmith/see";
		PainSound "hellsmith/pain"; 
		DeathSound "hellsmith/death";
		ActiveSound "hellsmith/active";
	}
	States 
	{    
	Spawn: 
		BSMT AB 10 A_Look;
		Loop;
	See: 
		BSMT A 3 A_Playsound ("hellsmith/move");
		BSMT ABB 3 A_Chase;
		BSMT C 3 A_Playsound ("hellsmith/move");
		BSMT CDD 3 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 A_Jump(256, "Charge", "Balls", "Clustershot");
		Goto See;
	Charge:
		BSMT O 1 A_Playsound ("hellsmith/dash");
		BSMT OOOOOOOO 4 {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_SkullAttack();
		}
		TNT1 A 0 A_Stop;
		BSMT O 1;
		Goto See;
	Balls:
		BSMT E 6 A_FaceTarget;
		BSMT H 6 A_PlaySound("hellsmith/swing", CHAN_WEAPON);
		BSMT I 12 {
			A_PlaySound("weapons/hellfi", CHAN_WEAPON);
			A_SpawnProjectile("ForgemasterBall",52,0,8);
			A_SpawnProjectile("ForgemasterBall",52,0,0);
			A_SpawnProjectile("ForgemasterBall",52,0,-8);
		}
		TNT1 A 0 A_Jump (128,1);
		Goto See;
		BSMT B 6;
		BSMT J 6 A_FaceTarget;
		BSMT M 6 A_PlaySound("hellsmith/swing", CHAN_WEAPON);
		BSMT N 12 {
			A_PlaySound("weapons/hellfi", CHAN_WEAPON);
			A_SpawnProjectile("ForgemasterBall",52,0,8);
			A_SpawnProjectile("ForgemasterBall",52,0,0);
			A_SpawnProjectile("ForgemasterBall",52,0,-8);
		}
		TNT1 A 0 A_Jump (80,"ClusterShot"); //low chance to use the clustershot afterwards
		Goto See;
	Melee:
	ClusterShot:
		BSMT E 8 A_FaceTarget;
		BSMT F 1 A_Playsound ("hellsmith/swing");
		BSMT F 5 A_FaceTarget;
		BSMT G 10 {
			A_Quake(3,25,0,400);
			A_Quake(2,25,0,700);
			//A_Playsound("hellsmith/floor");
			A_CustomComboAttack ("ForgemasterClusterBall",0,random(30,90),"hellsmith/hit"); //A_SpawnProjectile("ForgemasterClusterBall",0,0);
		}
		TNT1 A 0 A_Jump (128,1);
		Goto See;
		BSMT J 8 A_FaceTarget;
		BSMT K 1 A_Playsound ("hellsmith/swing");
		BSMT K 5 A_FaceTarget;
		BSMT L 10 {
			A_Quake(3,25,0,400);
			A_Quake(2,25,0,700);
			//A_Playsound("hellsmith/floor");
			A_CustomComboAttack ("ForgemasterClusterBall",0,random(30,90),"hellsmith/hit"); //A_SpawnProjectile("ForgemasterClusterBall",0,0);
		}
		Goto See;		
	Pain: 
		BSMT P 10 A_Pain;
		Goto See;
	Death:	
		BSMT P 10;
		BSMT Q 6 {
			A_Scream();
			A_SpawnItemEx("ForgemasterDroppedHammer", 0, -45, 110, 6, 0, 0, 30, SXF_SETMASTER);
		}
		BSMT RST 6;
		BSMT U 6 A_NoBlocking;
		BSMT VWX 6;
		BSMT Y 50;
		BSMT Y -1 A_BossDeath;
		Stop;
	} 
}

//hellshot projectile
class ForgemasterBall : DMR_Rocket
{
	Default
	{
		Speed 22;
		DamageFunction(random(30,90));
		RenderStyle "Add";
		ALPHA 0.95;
		Scale 1.2;
		SeeSound "";
		DeathSound "weapons/hellex";
	}
	States
	{
	Spawn: 
		HBAL ABCDEF 3 Bright {
			A_SpawnItemEx("DMR_RedPuffTrail",0,0,0,0,0,0);
			A_SpawnItemEx("DMR_MediumFireballTrail",-7,0,5,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
		loop;
	Death:
		TNT1 AAAAA 0 {
			A_SpawnItemEx("DMR_MediumFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
			A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		HELX A 3 Bright A_Explode(64,96);
		HELX BCDEFGHIJ 3 Bright;
		stop;
	}
}

//groundhugging projectile that splits into clusters upon death
class ForgemasterClusterBall : Actor
{
	Default
	{
		Radius 5;
		Height 5;
		Speed 22;
		DamageFunction(random(30,90));
		RenderStyle "ADD";
		DamageType "fire";
		Alpha 0.67;
		PROJECTILE;
		+FLOORHUGGER;
		+THRUGHOST;
		-NOGRAVITY;
		+DONTSPLASH;
		Seesound "weapons/diasht";
		DeathSound "weapons/firex3";
	}
	States
	{
	Spawn:
		TNT1 A 1; //dummy entry else game freezes when spawning the projectile, silly engine
		TNT1 A 0 A_SpawnItemEx("ForgemasterClusterBallTrail",0,0,15);
		loop;
	Death:
		//TODO I assume floorhugger and spawning stuff doesn't cooperate that well...
		/*TNT1 AAAAAA 0 {
			A_SpawnItemEx("DMR_MediumFireDebris",random(-1,1),random(-1,1),random(1,3),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
			A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-1,1),random(-1,1),random(1,3),frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}*/
		TNT1 A 0 {		
			A_SpawnProjectile("ForgemasterClusterBallCluster",0,0,0,0);
			A_SpawnProjectile("ForgemasterClusterBallCluster",0,0,30,0);
			A_SpawnProjectile("ForgemasterClusterBallCluster",0,0,60,0);
			A_SpawnProjectile("ForgemasterClusterBallCluster",0,0,90,0);
			A_SpawnProjectile("ForgemasterClusterBallCluster",0,0,120,0);
			A_SpawnProjectile("ForgemasterClusterBallCluster",0,0,150,0);
			A_SpawnProjectile("ForgemasterClusterBallCluster",0,0,180,0);
			A_SpawnProjectile("ForgemasterClusterBallCluster",0,0,210,0);
			A_SpawnProjectile("ForgemasterClusterBallCluster",0,0,240,0);
			A_SpawnProjectile("ForgemasterClusterBallCluster",0,0,270,0);
			A_SpawnProjectile("ForgemasterClusterBallCluster",0,0,300,0);
			A_SpawnProjectile("ForgemasterClusterBallCluster",0,0,330,0);
		}
		HELX A 3 BRIGHT A_Explode(36,48);
		HELX BCDEFGHIJ 3 Bright;
		stop;
	}
}

class ForgemasterClusterBallTrail : DMR_ProjectileTrail
{
	States
	{
	Spawn:
		HEPA ABCDEF 2 Bright{
			A_FadeOut(0.15);
			A_SetScale(Scale.X-0.15);
		}
		Loop;
	}
}

class ForgemasterClusterBallCluster : Actor
{
	Default
	{
		Radius 2;
		Height 6;
		Speed 15;
		Damage 0;
		RENDERSTYLE "ADD";
		ALPHA 0.95;
		PROJECTILE;
		+FLOORHUGGER;
		+THRUGHOST;
		-NOGRAVITY;
		+DONTSPLASH;
		+RIPPER;
		Seesound "weapons/diasht";
		DeathSound "weapons/scorch";
	}
	int lifetime;
		
	States
	{
	Spawn:	
		TNT1 A 1 {			
			// stop if timer runs out
			if (lifetime >= 50) { SetStateLabel("Death"); }
			lifetime++;
			A_CStaffMissileSlither();
			A_SpawnItem("ForgemasterClusterBallClusterTrail",0,0);
		}
		Loop;
	Death:
		stop;
	}
}

class ForgemasterClusterBallClusterTrail : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		RENDERSTYLE "ADD";
		DamageType "fire";
		ALPHA 0.67;
		PROJECTILE;
		+FLOORHUGGER;
		-NOGRAVITY;
		+DONTSPLASH;
	}
	States
	{
	Spawn:
		FTRA ABCDEFGHIJ 3 Bright A_Explode(4,8);
		stop;
	}
}


//death
class ForgemasterDroppedHammer : DMR_DroppedShield
{
	Default
	{
		+FORCEXYBILLBOARD;
		DeathSound "hellsmith/hitfloor";
		Bouncefactor 0.5;
	}
	States
	{
	Spawn:
		MAUL ABC 4;
		Loop;
	Death:
		MAUL D -1;
		stop;
	}
}


//TODO remove all below
class SmithFire : Actor
{   
	Default
	{
		Radius 2;
		Height 2;
		Damage 0;
		+NOCLIP;
		PROJECTILE;
		RENDERSTYLE "ADD";
		ALPHA 0.75;
		SeeSound "Weapons/hellex";
	}
	States
	{
	Spawn:
		MNSM ABCDEFGHIJKLMNOPQ 3 Bright;
		stop;
	}
}


//pain pentagram
class HellSmithPentaLine1 : Actor
{
	Default
	{
		Radius 0;
		Height 32;
		Speed 200;
		Alpha 0.85;
		PROJECTILE;
		+FLOORHUGGER;
		+NOCLIP;
		SeeSound "weapons/diasht";
	}
	states
	{
	Spawn:
		TNT1 A 1;
		TNT1 A 0 A_SpawnProjectile("HellSmithPentaLine2",0,0,-198,2);
		TNT1 A 0 A_SpawnProjectile("HellSmithPentaLine2",0,0,198,2);
		stop;
	}
}

class HellSmithPentaLine2 : Actor
{
	Default
	{
		Radius 0;
		Height 32;
		Speed 16;
		Alpha 0.85;
		PROJECTILE;
		+FLOORHUGGER;
		+NOCLIP;
	}
	states
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAA 1 A_SpawnItem("HellSmithPentaFire",0,0);
		stop;
	}
}

class HellSmithPentaFire : Actor
{
	Default
	{
		Radius 0;
		Height 32;
		DamageType "fire";
		RenderStyle "Add";
		Alpha 0.85;
		ReactionTime 2;
		PROJECTILE;
		+FLOORHUGGER;
		+DONTSPLASH;
		-NOGRAVITY;
	}
	states
	{
	Spawn:
		CFCF A 1 Bright A_Explode(1,32,1);
		CFCF A 2 Bright A_PlaySound("weapons/onfire");
		CFCF BCDEFGHIJKLM 3 Bright A_Explode(1,32,1);
		CFCF A 0 Bright A_CountDown;
		loop;
    Death:
		CFCF NOP 3 Bright A_Explode(2,32,1);
		stop;
	}
}