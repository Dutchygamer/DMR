//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Vader
//	Sounds:		Id Software, Blizzard Entertainment, Vader
//  Sprites: 	Vader, Eriance (cannon base)
//	GLDEFs: 	Dreadopp, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class Terminator : DMR_Cyberdemon
{
	Default
	{
		Tag "Terminator";
		Health 5000;
		PainChance 15;
		Scale 1.4;
		Obituary "%o was liquidated by a Terminator.";
		SeeSound "monster/termsit";
		PainSound "monster/termpin";
		DeathSound "monster/termdth";
		ActiveSound "Terminator/termact";
	}
	int boolet;
	int attackLoop;
	States
	{
	Spawn:
		TERM AB 10 A_Look;
		Loop;
	See:
		TERM A 3 A_Playsound("Terminator/terstepA");
		TERM ABB 3 A_Chase;
		TERM C 3 A_Playsound("Terminator/terstepA");
		TERM CDD 3 A_Chase;
		Loop;
	Pain:
		TERM L 10 A_Pain;
		Goto See;
	Missile:
		TNT1 A 0 A_JumpIfCloser(256, "Flamethrower");
		TNT1 A 0 A_Jump(256, "Autocannon", "Shotcannon");
		Goto See;
	Autocannon:
		TERM E 12 A_FaceTarget;
		TERM F 8 Bright Light("DMR_RifleFire") A_SpawnProjectile("TerminatorAutoCannon",57,25);
		TNT1 A 0 A_Jump(16, "See", "Missile"); //small chance to stop firing or possible switch to a different weapon	
		TERM E 10 A_MonsterRefire(10,"See");
		Goto Autocannon+1;
	// spider momma has mega chaingun, this one has mega shotgun
	Shotcannon:
		TERM G 18 A_Facetarget;		
		TERM HI 5 Bright Light("DMR_RifleFire") {
			A_Playsound("Terminator/tershotB");
			for (boolet = 0;boolet<=2;boolet++) {
				A_SpawnProjectile("DMR_SlowBullet",55,-22,frandom(-10,10),CMF_OFFSETPITCH,frandom(-5,5));
			}	
		}
		TNT1 A 0 A_Jump(8, "See", "Missile"); //small chance to stop firing or possible switch to a different weapon
		TNT1 A 0 A_MonsterRefire(10,"See");
		Goto Shotcannon+1;
	Flamethrower:
		TNT1 A 0 {
			if(target && Distance3D(target) > 300) SetStateLabel("Shotcannon");
		}
		TERM G 12 Bright A_FaceTarget;
		TNT1 A 0 { attackloop = 0; }
		TNT1 A 0 Bright A_PlaySound ("weapons/flamethrower");		
	FlamethrowerLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 12,"FlamethrowerEnd");
		TERM K 1 Bright {
			A_FaceTarget();
			attackloop++;
			A_InterceptProjectile("DMR_FlamethrowerBallLarge",56,-24,random(-3,3));
		}
		Loop;
	FlamethrowerEnd:
		TNT1 A 0 A_Jump(128, "Autocannon", "Shotcannon"); //large chance to switch to a different weapon
		TERM G 12 A_MonsterRefire(10,"See");
		Goto Flamethrower;
	Death:	
		TERM M 10;
		TERM N 6 A_Scream;
		TERM OPQ 5 Bright;
		TERM R 5 Bright {
			A_NoBlocking();
			A_SpawnProjectile("TerminatorHead", 90, 15, -170, 0);
			A_SpawnProjectile("TerminatorShoulder", 75, 60, -70, 0);
			A_SpawnProjectile("TerminatorArm", 0, 60, -90, 0);
		}
		TERM STUV 5 Bright;
		TERM W 6;
		TERM XY 6;
		TERM Z 6 A_Playsound("Terminator/terstepA");
		TERM [ 50;
		TERM [ -1 A_BossDeath;
		Stop;
	}
	
	//TODO unsure if to keep
	action actor A_InterceptProjectile(class<Actor> MissileType, double SpawnHeight = 32, double SpawnOfs_XY = 0, double Angle = 0, int Flags = 0, double Pitch = 0, int Ptr = AAPTR_TARGET)
	{
		actor proj = A_SpawnProjectile(MissileType,SpawnHeight,SpawnOfs_XY,Angle,Flags,Pitch,Ptr);
		actor other = GetPointer(Ptr);
		if(proj)
		{
			if(other)
			{
				proj.VelIntercept(other);
			}
			return proj;
		}
		return null;
	}
}

class TerminatorAutoCannon : DMR_Rocket
{
	Default
	{
		DamageFunction(random(34,102));
		Speed 22;
		SeeSound "TiberDemon/cannon";
	}
	States
	{
	Spawn:
		SHLL A 1 Bright {
			A_SpawnItemEx("DMR_MediumSmokeCloud",-20,0,5,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0,STYLE_Add);
		}
		TNT1 AAAAAAAAAA 0 {			
			A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-2,2),random(-2,2),random(-2,2),frandom(-8,8),frandom(-8,8),frandom(-2,8)); 
		}
		MISL B 8 Bright A_Explode(96,72);
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}

//TODO nalopen hoe dit nou hoort te werken
class Terminatorpuff : Bulletpuff
{
	Default
	{
		Renderstyle "ADD";
		Alpha 0.75;
		scale 0.75;
		Decal "BulletChip";
	}
	States
	{
	Spawn:
	Melee:
		TPUF A 2 Bright;
		TPUF A 0 A_Jump(128,10);
		TPUF BCDEFGH 2 Bright;
		Stop;
		TPUF IJKLMNO 2 Bright;
		Stop;
	}
}

class TerminatorArm : DMR_DroppedShield
{
	Default
	{
		Radius 12;
		Height 15;
		Speed 3;
		scale 1.6;
	}
	States
	{
	Spawn:
		TARM A 5;
		TARM B 5;
		TARM CDE 5;
	Death:
		TARM F -1;
		Stop;
	}
}

class TerminatorHead : DMR_DroppedShield
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 10;
		Scale 1.6;
	}
	States
	{
	Spawn:
		THAD DEFGHABC 2;
		loop;
	Death:
		THAD I -1;
		loop;
	}
}

class TerminatorShoulder : DMR_DroppedShield
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 8;
		Scale 1.6;
	}
	States
	{
	Spawn:
		TSHO ABCDEFGH 2;
		loop;
	Death:
		TSHO I -1;
		loop;
	}
}