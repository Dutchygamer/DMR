//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Vader
//	Sounds:		Id Software, Blizzard Entertainment, Vader
//  Sprites: 	Vader, Eriance (cannon base)
//	GLDEFs: 	Dreadopp, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class Terminator : DMR_Cyberdemon
{
	Default
	{
		Tag "Terminator";
		Health 5000;
		PainChance 15;
		DropItem "RocketBox", 256, 5;
		DropItem "Cell", 128, 20;
		DropItem "Cell", 128, 20;
		Scale 1.4;
		Obituary "%o was liquidated by a Terminator.";
		SeeSound "monster/termsit";
		PainSound "monster/termpin";
		DeathSound "monster/termdth";
		ActiveSound "Terminator/termact";
	}
	int boolet;
	bool goneBerserk;
	
	States
	{
	Spawn:
		TERM AB 10 A_Look;
		Loop;
	See:
		TNT1 A 0 {
			if (!goneBerserk && health < 1000) { SetStateLabel("GoBeserk"); }
		}
		TERM A 3 {
			A_SetTerminatorTics(3,2);
			A_Playsound("Terminator/terstepA");
		}
		TERM ABB 3 {		
			A_SetTerminatorTics(3,2);
			A_Chase();
		}
		TERM C 3 {
			A_SetTerminatorTics(3,2);
			A_Playsound("Terminator/terstepA");
		}
		TERM CDD 3 {		
			A_SetTerminatorTics(3,2);
			A_Chase();
		}
		Loop;
	GoBeserk:
		TERM A 10 Light("TerminatorGoBerserk") {
			A_PlaySound("monster/termpin", 7);
			goneBerserk = true;
			bNOPAIN = true;
		}
		TNT1 A 0 {
			A_PlaySound("monster/termsit", 7);
		}
		TERM AAAAAAAAAAAAAAAAAAAA 1 {
			A_SpawnItemEx("TerminatorGoneBerserkSmokeCloud",-5,0,70,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("TerminatorGoneBerserkSmokeCloud",-5,0,70,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("DMR_FireBouncingDebris",-5,0,70,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("DMR_FireBouncingDebris",-5,0,70,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("DMR_FireBouncingDebris",-5,0,70,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
		TERM A 10 Light("TerminatorGoBerserk");
		TNT1 A 0 {
			bNOPAIN = false;
		}
		Goto See;		
	Missile:
		TNT1 A 0 A_Jump(128, "RocketBurst", "Flamethrower");
	Rockets:
		TERM E 6 A_FaceTarget;
		TERM F 12 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",56,25,0);
		}
		TERM E 12 A_FaceTarget;
		TERM F 12 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",56,25,0);
		}
		TERM E 12 A_FaceTarget;
		TERM F 12 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",56,25,0);
		}
		TNT1 A 0 {
			// when berserk, continue with this state
			if (!goneBerserk) { SetStateLabel("See"); }
		}
		TNT1 A 0 A_Jump(64, "See", "RocketBurst"); // 25% chance to stop firing or switch to other rockets attack
		TNT1 A 0 A_MonsterRefire(10,"See");
		TERM E 12 A_FaceTarget;
		Goto Rockets+1;
	RocketBurst:
		TERM E 12 A_FaceTarget;
		TERM FFF 6 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("TerminatorWeaveRocket",56,25,0);
		}
		TNT1 A 0 {
			if (!goneBerserk) { SetStateLabel("See"); }
		}
		TNT1 A 0 A_Jump(64, "See", "Rockets"); // 25% chance to stop firing or switch to other rockets attack
		TNT1 A 0 A_MonsterRefire(10,"See");
		Goto RocketBurst;
	// spider momma has mega chaingun, this one has mega shotgun
	Shotcannon:
		TERM G 18 A_Facetarget;		
		TERM HI 5 Bright Light("DMR_RifleFire") {
			A_Playsound("Terminator/tershotB");
			for (boolet = 0;boolet<=2;boolet++) {
				A_SpawnProjectile("DMR_SlowBullet",55,-22,frandom(-10,10),CMF_OFFSETPITCH,frandom(-5,5));
			}	
		}
		TNT1 A 0 A_Jump(16, "See", "Missile"); // small chance to stop firing or possible switch to a different weapon	
		TNT1 A 0 A_MonsterRefire(10,"See");
		Goto Shotcannon+1;
	Flamethrower:
		TNT1 A 0 {
			// switch to shotcannon if target is too far away
			if(target && Distance3D(target) > 300) SetStateLabel("Shotcannon");
		}
		TERM G 12 Bright A_FaceTarget;
		TNT1 A 0 { 
			attackloop = 0; 
			A_PlaySound ("weapons/flamethrower", CHAN_WEAPON);
		}
	FlamethrowerLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 12,"FlamethrowerEnd");
		TERM K 1 Bright {
			A_FaceTarget();
			attackloop++;
			A_SpawnProjectile("DMR_FlamethrowerBallLarge",56,-24,random(-3,3));
		}
		Loop;
	FlamethrowerEnd:
		TNT1 A 0 A_Jump(128, "Autocannon", "Shotcannon"); //large chance to switch to a different weapon
		TERM G 12 A_MonsterRefire(10,"See");
		Goto Flamethrower;
	Pain:
		TERM L 10 A_Pain;
		Goto See;
	Death:	
		TERM M 10;
		TERM N 6 A_Scream;
		TERM OPQ 5 Bright;
		TERM R 5 Bright {
			A_NoBlocking();
			A_SpawnProjectile("TerminatorHead", 90, 15, -170, 0);
			A_SpawnProjectile("TerminatorShoulder", 75, 60, -70, 0);
			A_SpawnProjectile("TerminatorArm", 0, 60, -90, 0);
		}
		TERM STUV 5 Bright;
		TERM W 6;
		TERM XY 6;
		TERM Z 6 A_Playsound("Terminator/terstepA");
		TERM [ 50;
		TERM [ -1 A_BossDeath;
		Stop;
	}
	
	override void Tick()
  	{
		Super.Tick();
		if (goneBerserk && health > 0 && !IsFrozen())
		{		
			A_SpawnItemEx("TerminatorBerserkingSmokeCloud",-5,0,70,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
  	}
	
	//TODO unsure if keep or just increase it's speed
	void A_SetTerminatorTics(int normalTics, int berserkTics)
	{
		if (goneBerserk) {
			A_SetTics(berserkTics);
		}
		else {
			A_SetTics(normalTics);
		}
	}
}

//======================================================================
//  Original credits
//======================================================================
//	ZScript: 	Cutmanmike, modified by Dutchygamer
//======================================================================
class TerminatorWeaveRocket : DMR_SmallRocket
{
	double dist;
	
	override void Tick()
	{
		if (dist == 0)
		{
			// get a random weave index, and set the distance to the default of 1.5
			WeaveIndexXY = random(0,63);
			dist = 1.5;
			
			FLineTraceData line;

			// if projectile would hit a wall by weaving into it, set weave distance to 0
			LineTrace(angle + 48, 128, 0, TRF_THRUACTORS, height/2, 0, 0, line);
			if (line.HitType == TRACE_HitWall)
			{
				dist = 0;
			}

			// check same for other direction
			LineTrace(angle - 48, 128, 0, TRF_THRUACTORS, height/2, 0, 0, line);
			if (line.HitType == TRACE_HitWall)
			{
				dist = 0;
			}
		}

		// perform the weave with the calculated distance if we're still moving
		if (Abs(vel.x + vel.y) >= 0.1)
		{
			A_Weave(2,2,dist,dist);
		}	
		
		Super.Tick();
	}
}

class TerminatorGoneBerserkSmokeCloud : DMR_SmokeCloud
{
	Default
	{
		Scale 0.55;
		Alpha 0.45;
	}
	States
	{
	Spawn:
		SMOK AB 3;
		SMOK CDEFGH 12;
		SMOK IJ 3;
		Stop;
	}
}

class TerminatorBerserkingSmokeCloud : DMR_SmokeCloud
{
	Default
	{
		Scale 0.55;
		Alpha 0.2;
	}
	States
	{
	Spawn:
		SMOK AB 2;
		SMOK CDEFGH 8;
		SMOK IJ 2;
		Stop;
	}
}


//TODO nalopen hoe dit nou hoort te werken
class Terminatorpuff : Bulletpuff
{
	Default
	{
		Renderstyle "ADD";
		Alpha 0.75;
		scale 0.75;
		Decal "BulletChip";
	}
	States
	{
	Spawn:
	Melee:
		TPUF A 2 Bright;
		TPUF A 0 A_Jump(128,10);
		TPUF BCDEFGH 2 Bright;
		Stop;
		TPUF IJKLMNO 2 Bright;
		Stop;
	}
}

class TerminatorArm : DMR_DroppedShield
{
	Default
	{
		Radius 12;
		Height 15;
		Speed 3;
		scale 1.6;
	}
	States
	{
	Spawn:
		TARM A 5;
		TARM B 5;
		TARM CDE 5;
	Death:
		TARM F -1;
		Stop;
	}
}

class TerminatorHead : DMR_DroppedShield
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 10;
		Scale 1.6;
	}
	States
	{
	Spawn:
		THAD DEFGHABC 2;
		loop;
	Death:
		THAD I -1;
		loop;
	}
}

class TerminatorShoulder : DMR_DroppedShield
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 8;
		Scale 1.6;
	}
	States
	{
	Spawn:
		TSHO ABCDEFGH 2;
		loop;
	Death:
		TSHO I -1;
		loop;
	}
}