//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Vader
//	Sounds:		Id Software, Blizzard Entertainment, Vader
//  Sprites: 	Vader, Eriance (cannon base)
//	GLDEFs: 	Dreadopp, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class KurCenturion : DMR_Cyberdemon
{
	Mixin DMR_BerserkingMonster;

	Default
	{
		Tag "Kur Centurion";
		Health 5000;
		PainChance 10;
		DropItem "RocketBox", 256, 5;
		DropItem "Cell", 128, 20;
		DropItem "Cell", 128, 20;
		Scale 1.4;
		Obituary "%o was shown true discipline by a Kur Centurion.";
		-MISSILEMORE;
		+MISSILEEVENMORE;
		SeeSound "kurcenturion/sight";
		PainSound "kurcenturion/pain";
		DeathSound "kurcenturion/death";
		ActiveSound "kurcenturion/termact";
		StencilColor "Yellow";
	}
	int boolet;
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			berserkThreshold = 1250;
		}
		TERM AB 10 A_Look;
		Loop;
	See:
		TERM AABB 3 {		
			A_SetBerserkerTics(3,2);
			A_Chase();
		}
		TNT1 A 0 A_StartSound("kurcenturion/step");
		TERM CCDD 3 {		
			A_SetBerserkerTics(3,2);
			A_Chase();
		}
		TNT1 A 0 A_StartSound("kurcenturion/step");
		Loop;	
	Missile:
		// when critically damaged fire more shots
        TNT1 A 0 {
			if (criticalDamaged)
				attackloop = -2;
			else
				attackloop = 0;
		}	
		TNT1 A 0 A_Jump(256, "Rockets", "RocketBurst", "Flamethrower");
		Goto See;
	Rockets:
		TERM E 6 A_FaceTarget;
	RocketsLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 3,"RocketsEnd");
		TERM F 10 Bright Light("DMR_RifleFire") {
			A_SetBerserkerTics(10,5);
			A_SpawnProjectile("DMR_Rocket",56,25,0);
			attackloop++;
		}
		TERM E 8 A_FaceTarget;
		Loop;
	RocketsEnd:
		TNT1 A 0 {
			// when not berserking stop attacking
			if (!criticalDamaged) { SetStateLabel("See"); }
			// when berserking small chance to stop attacking
			if (criticalDamaged && random(1,10) == 10) { SetStateLabel("See"); }
		}
		// otherwise switch to different attack if target is still in LoS
		TNT1 A 0 A_MonsterRefire(0,"See");
		TERM E 8 A_FaceTarget;
		Goto Missile;
	RocketBurst:
		TERM E 12 {
			A_FaceTarget();
			A_StartSound("heavyweapon/load");
		}
	RocketBurstLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 3,"RocketsEnd"); // as we use same attack frames we can re-use RocketsEnd here
		TERM F 5 Bright Light("DMR_RifleFire") {
			A_SetBerserkerTics(5,3);
			A_FaceTarget();
			A_SpawnProjectile("KurCenturionWeaveRocket",56,25,0);
			attackloop++;
		}
		Loop;
	Grenades:	
		TERM GGG 6 {
			A_Facetarget();
			A_StartSound("heavyweapon/load");
		}
		TERM G 6 A_Facetarget;
	GrenadesLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 6,"GrenadesEnd");
		TERM H 4 Bright Light("DMR_RifleFire") {
			A_SetBerserkerTics(4,2);
			A_LobProjectile("DMR_FiredGrenade",56,-24, frandom(-5,5));
			attackloop++;
		}
		TERM G 3 A_Facetarget;
		TERM I 4 Bright Light("DMR_RifleFire") {
			A_SetBerserkerTics(4,2);
			A_LobProjectile("DMR_FiredGrenade",56,-24, frandom(-5,5));
			attackloop++;
		}
		TERM G 3 A_Facetarget;
		Loop;
	GrenadesEnd:
		TERM G 8;
		TNT1 A 0 {
			// when not berserking stop attacking
			if (!criticalDamaged) { SetStateLabel("See"); }
			// when berserking small chance to stop attacking
			if (criticalDamaged && random(1,10) == 10) { SetStateLabel("See"); }
		}
		// otherwise switch to different attack if target is still in LoS
		TNT1 A 0 A_MonsterRefire(0,"See");
		Goto Missile;
	Flamethrower:
		TNT1 A 0 {
			// switch to grenades if target is too far away
			if(target && Distance3D(target) > 600) SetStateLabel("Grenades");
		}
		TERM G 12 Bright A_FaceTarget;
		TNT1 A 0 { 
			attackloop = 0; 
			A_StartSound ("flamethrower/fire",CHAN_WEAPON,CHANF_OVERLAP,volume:1.0,attenuation:1.0,pitch:0.8);
			A_StartSound ("flamethrower/fire",CHAN_WEAPON,CHANF_OVERLAP,volume:0.3,attenuation:0.7,pitch:0.7);
		}
	FlamethrowerLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 45,"FlamethrowerEnd");
		TERM K 1 Bright Light("DMR_RifleFire") {
			A_FaceTarget();
			attackloop++;
			A_SpawnProjectile("KurCenturionFlamethrowerBall",56,-24,random(-3,3),CMF_OFFSETPITCH,frandom(-1,1));
		}
		Loop;
	FlamethrowerEnd:
		// switch to different attack if target is still in LoS
		TERM G 12 A_MonsterRefire(0,"See");
		Goto Missile;
	CriticalDamage:
		TNT1 A 0 { 
			bNOPAIN = true; 
			bQUICKTORETALIATE = true;
			A_SetSpeed(speed*1.2);
			criticalDamaged = true;
		}
		TERM L 10 Light("MonsterGoBerserk") {
			A_StartSound("kurcenturion/pain",CHAN_VOICE);
			A_StartSound("kurcenturion/sight",CHAN_VOICE,CHANF_OVERLAP,pitch:0.7);
		}
		TERM AAAAAAAAAAAAAAAAAAAA 1 {
			A_SpawnItemEx("GoneBerserkSmokeCloud",-5,0,70,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("GoneBerserkSmokeCloud",-5,0,70,frandom(-1,0),frandom(-1,1),frandom(0,1));
			A_SpawnItemEx("DMR_MediumFireballTrail",-5,frandom(-1,1),70,frandom(-1,1),frandom(-1,1),frandom(-2,0));
			A_SpawnItemEx("DMR_MediumFireballTrail",-5,frandom(-1,1),70,frandom(-1,1),frandom(-1,1),frandom(-2,0));
			A_SpawnItemEx("DMR_LargeFireballTrail",-5,frandom(-1,1),70,frandom(-1,1),frandom(-1,1),frandom(-2,0));
			if (random(1,4) == 1){
				A_SpawnItemEx("DMR_BossDeathExplosion",random(8,-8),0,random(20,40));				
			}
		}
		TERM G 10 Light("MonsterGoBerserk") {
			A_StartSound("kurcenturion/sight",CHAN_VOICE);
			A_StartSound("kurcenturion/sight",CHAN_VOICE,CHANF_OVERLAP,volume:0.5,pitch:0.7);
			A_FaceTarget();
		}
		TERM G 10 A_FaceTarget;
		TNT1 A 0 {
			bNOPAIN = false;
			attackloop = -2;
		}
		Goto Grenades;
	Pain:
		TERM L 10 A_Pain;
		Goto See;
	Death:	
		TERM LLLLLL 6 {
			A_SpawnItemEx("DMR_KurCenturionDeathExplosionSpawner");
		}
		TERM M 6 A_Scream;
		TERM N 6;
		TERM OPQ 5 Bright;
		TERM R 6 Bright {
			A_SpawnItemEx("DMR_BossDeathFinalExplosion");
			A_SpawnProjectile("KurCenturionHead",90,15,-170,0);
			A_SpawnProjectile("KurCenturionShoulder",75,60,-70,0);
			A_SpawnProjectile("KurCenturionArm",0,60,-90,0);
		}
		TERM STUV 4 Bright;
		TERM W 4;
		TERM X 6 A_NoBlocking;
		TERM Y 6;
		TERM Z 6 A_StartSound("kurcenturion/step");
		TERM [ 50;
		TERM [ -1 A_BossDeath;
		Stop;
	}
}

//======================================================================
//  Original credits
//======================================================================
//	ZScript: 	Cutmanmike, modified by Dutchygamer
//======================================================================
class KurCenturionWeaveRocket : DMR_SmallRocket
{
	double dist;
	
	override void Tick()
	{
		if (dist == 0)
		{
			// get a random weave index, and set the distance to the default of 1.5
			WeaveIndexXY = random(0,63);
			dist = 1.5;
			
			FLineTraceData line;

			// if projectile would hit a wall by weaving into it, set weave distance to 0
			LineTrace(angle + 48, 128, 0, TRF_THRUACTORS, height/2, 0, 0, line);
			if (line.HitType == TRACE_HitWall)
			{
				dist = 0;
			}

			// check same for other direction
			LineTrace(angle - 48, 128, 0, TRF_THRUACTORS, height/2, 0, 0, line);
			if (line.HitType == TRACE_HitWall)
			{
				dist = 0;
			}
		}

		// perform the weave with the calculated distance if we're still moving
		if (Abs(vel.x + vel.y) >= 0.1)
		{
			A_Weave(2,2,dist,dist);
		}	
		
		Super.Tick();
	}
}


class KurCenturionFlamethrowerBall : DMR_FlamethrowerBall
{
	Default
	{
		Radius 6;
		Height 9;
		Scale 0.7;
		Speed 35;
	}
	States
	{
	Spawn:
		FRFX AAABBBCCCDDDEEEFFFGGG 1 Light("DMR_HellWarriorBall") {
			A_ScaleVelocity(0.98);
			A_SetScale(Scale.X + 0.02);
			A_FadeOut(0.01);
			
			// only deal damage X amount of tics instead of every tick
			dmgtic++;
			if(dmgtic >= 3)
			{
				A_Explode(8,48,0,0,40);
				dmgtic = 0;
			}		
		}
		FRFX HHHIIIJJJ 1 Light("DMR_HellWarriorBall") {
			A_SetScale(Scale.X + 0.01);
			A_FadeOut(0.08);
		}
		Stop;
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		A_SetRoll(random(0,360));
	}
	
	override void Tick()
	{
		Super.Tick();
		if (vel.x == 0 && vel.y == 0 && fire == false && random(1,2) == 1) {
			A_SpawnProjectile("DMR_BurningFire",1,0,0,CMF_TRACKOWNER);
			fire = true;
		}
	}
}



class KurCenturionArm : DMR_DroppedShield
{
	Default
	{
		Radius 12;
		Height 15;
		Speed 3;
		scale 1.6;
	}
	States
	{
	Spawn:
		TARM A 5;
		TARM B 5;
		TARM CDE 5;
	Death:
		TARM F -1;
		Stop;
	}
}

class KurCenturionHead : DMR_DroppedShield
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 10;
		Scale 1.6;
	}
	States
	{
	Spawn:
		THAD DEFGHABC 2;
		loop;
	Death:
		THAD I -1;
		loop;
	}
}

class KurCenturionShoulder : DMR_DroppedShield
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 8;
		Scale 1.6;
	}
	States
	{
	Spawn:
		TSHO ABCDEFGH 2;
		loop;
	Death:
		TSHO I -1;
		loop;
	}
}