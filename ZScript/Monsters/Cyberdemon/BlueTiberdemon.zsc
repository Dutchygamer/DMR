//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	zrrion the insect
//	Sounds:		Blizzard (Diablo)
//  Sprites: 	Id Software (edits by Jimmy, zrrion the insect, Espi, Ebola)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class InfusedTyrant : DMR_Cyberdemon
{
	Default
	{
		Translation "112:127=192:207";
		Tag "Infused Tyrant";
		Health 5000;
		PainChance 5;
		DropItem "RocketBox", 256, 5;
		DropItem "RocketBox", 256, 5;
		DropItem "CellPack", 256, 100;
		DropItem "CellPack", 128, 100;
		Scale 1.05;
		Bloodcolor "blue";
		SeeSound "tyrant/sight";
		PainSound "cyber/Pain";
		DeathSound "tyrant/death";
		ActiveSound "cyber/active";
		Obituary "%o was destroyed by a Cybifera.";
		DamageFactor "ToxinCloud", 0.0;
		+MISSILEMORE;
	}
	States 
	{ 
	Spawn:
		CARD AB 10 Bright A_Look;
		Loop;
	See:
		CARD AABB 3 Bright {
			A_Chase();
			// chance to dash left or right	
			if(random(1,8) == 8) A_DashSideways(12);	
			A_SpawnItemEx("DMR_TranslucentAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION);
		}
		Loop;
	Missile:
		// normal launch coords are 72,-25
		TNT1 A 0 A_Jump(128,"Flechette","BFGBall","ChemicalMissile");
	Rockets:
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		Goto See;
	Flechette:
		CARD C 8 A_FaceTarget;
		CARD DD 3 A_SpawnProjectile("TiberiumFlechette",72,-25,random(-2,2));
		CARD C 5 A_FaceTarget;
		CARD DD 3 A_SpawnProjectile("TiberiumFlechette",72,-25,random(-2,2));
		CARD C 5 A_FaceTarget;
		CARD DD 3 A_SpawnProjectile("TiberiumFlechette",72,-25,random(-2,2));
		CARD C 5 A_FaceTarget;
		CARD DD 3 A_SpawnProjectile("TiberiumFlechette",72,-25,random(-2,2));
		CARD C 5 A_FaceTarget;
		Goto See;
	BFGBall:
		TNT1 A 0 {
			A_StartSound ("tibshard/blue/charge", CHAN_WEAPON);
			bNOPAIN = true;
		}
		CARD C 12 Light("DMR_BlueBFGCharge_X1") A_FaceTarget;
		CARD CCCCCCCCCCCC 1 BRIGHT Light("DMR_BlueBFGCharge_X2") {
			A_FaceTarget();
			A_SpawnItemEx("TiberiumBlueLightningBolt",0,-25,72);
		}
		CARD D 6 Bright Light("DMR_BlueBFGFire") {
			A_FaceTarget();
			A_SpawnProjectile("TiberiumBlueFlechette",72,-25,0);
		}
		CARD D 6 {
			bNOPAIN = false;
		}
		Goto See;
	ChemicalMissile:
		CARD C 10 {
			A_FaceTarget();
			A_StartSound("heavyweapon/load");
		}
		CARD CC 10 A_FaceTarget;
		CARD D 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("TiberiumGasRocket",72,-25,0);
		}
		TNT1 A 0 A_Jump(96,"ChemicalMissile");
		Goto See;
	Pain:
		CARD E 10 A_Pain;
		Goto See;
	Death:
		CARD E 10 Bright;
		CARD F 6 Bright A_Scream;
		CARD GHI 6 Bright {
			A_SpawnItemEx("DMR_CybiferaDeathExplosionSpawner");
		}
		TNT1 A 0 A_NoBlocking;
		CARD JK 6 Bright {
			A_SpawnItemEx("DMR_CybiferaDeathExplosionSpawner");
		}
		CARD L 6 Bright;
		TNT1 A 50;
		TNT1 A 1 A_BossDeath;
		Stop;
	} 
}