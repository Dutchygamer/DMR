//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	zrrion the insect
//	Sounds:		Blizzard (Diablo)
//  Sprites: 	Id Software (edits by Jimmy, zrrion the insect, Espi, Ebola)
//	GLDEFs: 	m/a
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class BlueTiberdemon : Tiberdemon
{ 
	Default
	{
		Translation "112:127=192:207";
		Tag "Enraged abomination";
		Health 3000;
		Speed 12;
		Mass 700;
		Scale 1.05;
		-FLOORCLIP;
		+NOGRAVITY;
		+FLOATBOB;
		+FLOAT;
		+NOPAIN; //teehee
		MinMissileChance 140;
		bloodcolor "blue";
		Obituary "%o was destroyed by an enraged abomination.";
	}
	States 
	{ 
	Spawn:
		CARD X 0 NoDelay A_FaceTarget;
		CARD AB 10 Bright A_Look;
		Loop;
	See:
		Goto ChaseA;
	ChaseA:
		TNT1 A 0 A_Jump(128,"FastChaseA"); //half chance to use the sideways dashing chase
		CARD AA 3 Bright {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase();
		}
		Goto ChaseB;
	FastChaseA:
		CARD AA 3 Bright {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase(null,"Missile", CHF_FASTCHASE);
		}
		Goto ChaseB;
	ChaseB:
		TNT1 A 0 A_Jump(128,"FastChaseB"); //half chance to use the sideways dashing chase
		CARD BB 3 Bright {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase();
		}
		Goto See;
	FastChaseB:
		CARD BB 3 Bright {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase(null,"Missile", CHF_FASTCHASE);
		}
		Goto See;
	Missile:
		//normal launch coords are 72,-25
		TNT1 A 0 A_Jump(192,"Flechette");
		Goto SuperPlasma;
	Flechette:
		CARD C 8 A_FaceTarget;
		CARD D 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 5 A_FaceTarget;
		CARD D 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 5 A_FaceTarget;
		CARD D 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 5 A_FaceTarget;
		CARD D 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 5 A_FaceTarget;
		Goto See;
	SuperPlasma:
		CARD C 24 {
			A_FaceTarget();
			A_PlaySound("weapons/bfgf");
		}
		CARD C 1 A_FaceTarget;
		CARD D 12 A_SpawnProjectile("TiberiumBlueSuperPlasma",72,-25,0);
		Goto See;
	Death:
		TNT1 A 0 {
			bFloatBob = false;
		}
		CARD E 10 Bright;
		CARD F 6 Bright A_Scream;
		CARD GHI 6 Bright;
		CARD J 6 Bright A_NoBlocking;
		CARD K 6 Bright;
		CARD L 50 Bright;
		CARD L 1 Bright A_BossDeath;
		Stop;
	} 
}

//flechettes
class TiberiumBlueFlechette : TiberiumFlechette
{
	Default
	{
		poisondamage 8;
	}
	States
	{
	Spawn:
		BSRD A 1 Bright {
			If(GetCvar("dmr_fancydoom")) {
				A_SpawnItemEx("TiberiumBlueFlechetteTrail",-20,0,1); 
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0, STYLE_Add);
			A_SetScale(1.5);
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
		}
		BSDE ABCDEFG 3 Bright;
		Stop;
	}
}

class TiberiumBlueFlechetteTrail : DMR_ProjectileTrail
{   
	Default
	{
		RenderStyle "Add";
		Alpha 0.6;
		Scale 0.65;
	}
	States
	{
	Spawn:
		BSRD A 1 Bright A_FadeOut(0.06);
		Loop;
	}
}

class TiberiumBlueSuperPlasma : TiberiumSuperPlasma
{
	Default
	{
		Translation "112:127=192:207";	
	}
	States
	{
	Spawn:
		TBBL ABCDEFGHIJKLMN 2 Bright {
			If(GetCvar("dmr_fancydoom")) { A_SpawnItemEx("BlueTiberiumSuperPlasmaTrail",-5,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1)); }
		}
		Loop;
	Death:
		TNT1 A 0 A_SetScale(2.5);
		ARCB J 3 Bright A_Explode(160,96);
		ARCB KLMN 3 Bright;
		Stop;
	}
}

class BlueTiberiumSuperPlasmaTrail : DMR_ProjectileTrail
{   
	Default
	{
		Translation "112:127=192:207";	
	}
	Default
	{
		RenderStyle "Add";
		Alpha 0.6;
		Scale 0.35;
	}
	States
	{
	Spawn:
		CBTS LMNOPQRSTUVW 2 Bright A_FadeOut;
		Stop;
	}
}
