//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	zrrion the insect
//	Sounds:		Blizzard (Diablo)
//  Sprites: 	Id Software (edits by Jimmy, zrrion the insect, Espi, Ebola)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class BlueTiberdemon : DMR_Cyberdemon
{
	Default
	{
		Translation "112:127=192:207";
		Tag "Cybifera"; // Cyberdemon + Vinifera
		Health 5000;
		PainChance 5;
		DropItem "RocketBox", 256, 5;
		DropItem "RocketBox", 256, 5;
		DropItem "CellPack", 256, 100;
		DropItem "CellPack", 128, 100;
		Scale 1.05;
		Bloodcolor "blue";
		SeeSound "TiberDemon/sight";
		PainSound "cyber/Pain";
		DeathSound "TiberDemon/death";
		ActiveSound "cyber/active";
		Obituary "%o was destroyed by a cybifera.";
		DamageFactor "ToxinCloud", 0.0;
		+NODROPOFF; //don't yeet yourself off platforms	
	}
	States 
	{ 
	Spawn:
		CARD X 0 NoDelay A_FaceTarget;
		CARD AB 10 Bright A_Look;
		Loop;
	See:
		Goto ChaseA;
	ChaseA:
		TNT1 A 0 A_Jump(128,"FastChaseA"); //half chance to use the sideways dashing chase
		CARD AA 3 Bright {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase();
		}
		Goto ChaseB;
	FastChaseA:
		CARD AA 3 Bright {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase(null,"Missile", CHF_FASTCHASE);
		}
		Goto ChaseB;
	ChaseB:
		TNT1 A 0 A_Jump(128,"FastChaseB"); //half chance to use the sideways dashing chase
		CARD BB 3 Bright {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase();
		}
		Goto See;
	FastChaseB:
		CARD BB 3 Bright {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase(null,"Missile", CHF_FASTCHASE);
		}
		Goto See;
	Missile:
		//normal launch coords are 72,-25
		TNT1 A 0 A_Jump(128,"Flechette","BFGBall");
	Rockets:
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		Goto See;
	Flechette:
		CARD C 8 A_FaceTarget;
		CARD DD 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 5 A_FaceTarget;
		CARD DD 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 5 A_FaceTarget;
		CARD DD 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 5 A_FaceTarget;
		CARD DD 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 5 A_FaceTarget;
		Goto See;
	BFGBall:
		TNT1 A 0 {
			A_PlaySound ("weapons/bfgf", CHAN_WEAPON);
			bNOPAIN = true;
		}
		CARD C 12 Light("DMR_BlueBFGCharge_X1") A_FaceTarget;
		CARD C 12 Bright Light("DMR_BlueBFGCharge_X2") A_FaceTarget;
		CARD D 6 Bright Light("DMR_BlueBFGFire") {
			A_FaceTarget();
			A_SpawnProjectile("TiberiumBlueBFGBall",72,-25,0);
		}
		CARD D 6 {
			bNOPAIN = false;
		}
	Pain:
		CARD E 10 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 {
			bFloatBob = false;
		}
		CARD E 10 Bright;
		CARD F 6 Bright A_Scream;
		CARD GHI 6 Bright;
		CARD J 6 Bright A_NoBlocking;
		CARD K 6 Bright;
		CARD L 50 Bright;
		CARD L 1 Bright A_BossDeath;
		Stop;
	} 
}

//flechettes
class TiberiumBlueFlechette : TiberiumFlechette
{
	States
	{
	Spawn:
		BSRD A 1 Bright {
			A_SpawnItemEx("TiberiumBlueFlechetteTrail",-20,0,1); 
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0, STYLE_Add);
			A_SetScale(1.5);
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_Explode(48,96);
		}
		BSDE ABCDEFG 3 Bright;
		Stop;
	}
}

class TiberiumBlueFlechetteTrail : DMR_ProjectileTrail
{   
	Default
	{
		RenderStyle "Add";
		Alpha 0.6;
		Scale 0.65;
	}
	States
	{
	Spawn:
		BSRD A 1 Bright A_FadeOut(0.06);
		Loop;
	}
}

class TiberiumBlueBFGBall : TiberiumBFGBall
{
	Default
	{
		Translation "112:127=192:207";	
	}
	States
	{
	Spawn:
		TBBL ABCDEFGHIJKLMN 2 Bright {
			A_SpawnItemEx("BlueTiberiumBFGBallTrail",-5,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1));
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetScale(2.5);
			A_Explode(224,196);
		}
		ARCB JKLMN 3 Bright;
		Stop;
	}
}

class BlueTiberiumBFGBallTrail : DMR_ProjectileTrail
{   
	Default
	{
		Translation "112:127=192:207";	
	}
	Default
	{
		RenderStyle "Add";
		Alpha 0.6;
		Scale 0.35;
	}
	States
	{
	Spawn:
		CBTS LMNOPQRSTUVW 2 Bright A_FadeOut;
		Stop;
	}
}
