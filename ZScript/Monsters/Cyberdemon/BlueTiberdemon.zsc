//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	zrrion the insect
//	Sounds:		Blizzard (Diablo)
//  Sprites: 	Id Software (edits by Jimmy, zrrion the insect, Espi, Ebola)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class BlueTiberdemon : DMR_Cyberdemon
{
	Default
	{
		Translation "112:127=192:207";
		Tag "Enraged abomination";
		Health 5000;
		PainChance 5;
		Scale 1.05;
		Bloodcolor "blue";
		SeeSound "TiberDemon/sight";
		PainSound "cyber/Pain";
		DeathSound "TiberDemon/death";
		ActiveSound "cyber/active";
		Obituary "%o was destroyed by an enraged abomination.";
		DamageFactor "ToxinCloud", 0.0;
	}
	States 
	{ 
	Spawn:
		CARD X 0 NoDelay A_FaceTarget;
		CARD AB 10 Bright A_Look;
		Loop;
	See:
		Goto ChaseA;
	ChaseA:
		TNT1 A 0 A_Jump(128,"FastChaseA"); //half chance to use the sideways dashing chase
		CARD AA 3 Bright {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase();
		}
		Goto ChaseB;
	FastChaseA:
		CARD AA 3 Bright {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase(null,"Missile", CHF_FASTCHASE);
		}
		Goto ChaseB;
	ChaseB:
		TNT1 A 0 A_Jump(128,"FastChaseB"); //half chance to use the sideways dashing chase
		CARD BB 3 Bright {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase();
		}
		Goto See;
	FastChaseB:
		CARD BB 3 Bright {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase(null,"Missile", CHF_FASTCHASE);
		}
		Goto See;
	Missile:
		//normal launch coords are 72,-25
		TNT1 A 0 A_Jump(128,"Flechette","SuperPlasma");
	Rockets:
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		CARD C 6 A_FaceTarget;
		CARD D 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",72,-25,0);
		}
		Goto See;
	Flechette:
		CARD C 8 A_FaceTarget;
		CARD DD 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 5 A_FaceTarget;
		CARD DD 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 5 A_FaceTarget;
		CARD DD 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 5 A_FaceTarget;
		CARD DD 3 A_SpawnProjectile("TiberiumBlueFlechette",72,-25,random(-2,2));
		CARD D 5 A_FaceTarget;
		Goto See;
	SuperPlasma:
		CARD C 24 {
			A_FaceTarget();
			A_PlaySound("weapons/bfgf");
		}
		CARD D 12 A_SpawnProjectile("TiberiumBlueSuperPlasma",72,-25,0);
		Goto See;
	Pain:
		CARD E 10 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 {
			bFloatBob = false;
		}
		CARD E 10 Bright;
		CARD F 6 Bright A_Scream;
		CARD GHI 6 Bright;
		CARD J 6 Bright A_NoBlocking;
		CARD K 6 Bright;
		CARD L 50 Bright;
		CARD L 1 Bright A_BossDeath;
		Stop;
	} 
}

//flechettes
class TiberiumBlueFlechette : TiberiumFlechette
{
	States
	{
	Spawn:
		BSRD A 1 Bright {
			A_SpawnItemEx("TiberiumBlueFlechetteTrail",-20,0,1); 
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0, STYLE_Add);
			A_SetScale(1.5);
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_Explode(48,96);
		}
		BSDE ABCDEFG 3 Bright;
		Stop;
	}
}

class TiberiumBlueFlechetteTrail : DMR_ProjectileTrail
{   
	Default
	{
		RenderStyle "Add";
		Alpha 0.6;
		Scale 0.65;
	}
	States
	{
	Spawn:
		BSRD A 1 Bright A_FadeOut(0.06);
		Loop;
	}
}

class TiberiumBlueSuperPlasma : TiberiumSuperPlasma
{
	Default
	{
		Translation "112:127=192:207";	
	}
	States
	{
	Spawn:
		TBBL ABCDEFGHIJKLMN 2 Bright {
			A_SpawnItemEx("BlueTiberiumSuperPlasmaTrail",-5,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1));
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetScale(2.5);
			A_Explode(160,96);
		}
		ARCB JJJ 1 Bright;
		ARCB KKK 1 Bright;
		ARCB LLL 1 Bright;
		ARCB MMM 1 Bright;
		ARCB NNN 1 Bright;
		Stop;
	}
}

class BlueTiberiumSuperPlasmaTrail : DMR_ProjectileTrail
{   
	Default
	{
		Translation "112:127=192:207";	
	}
	Default
	{
		RenderStyle "Add";
		Alpha 0.6;
		Scale 0.35;
	}
	States
	{
	Spawn:
		CBTS LMNOPQRSTUVW 2 Bright A_FadeOut;
		Stop;
	}
}
