//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Id Software, Eriance
//  Sprites: 	Id Software, Midway, Eriance (edits by Eriance)
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class FireborneArchon : DMR_Cyberdemon
{
	Default
	{
		Tag "Fireborne Archon";
		Health 5000;
		Scale 1.5;
		DropItem "CellPack", 256, 100;
		DropItem "CellPack", 128, 100;
		Obituary "%o was burnt to a crisp by a Fireborne Archon";
		HitObituary "%o was melted my a Fireborne Archon";
		bloodcolor "orange";
		SeeSound "pyro/see";
		PainSound "pyro/pain";
		DeathSound "pyro/death";
		ActiveSound "pyro/idle";
	}
	
	int spamBallAngle;
	
	States
	{
	Spawn:
		BOS4 AB 10 A_Look;
		Loop;
	See:
		BOS4 AABB 3 A_Chase();
		TNT1 A 0 A_StartSound("cyber/hoof");
		BOS4 CCDD 3 A_Chase();
		TNT1 A 0 A_StartSound("cyber/hoof");
		Loop;
	Missile:
		TNT1 A 0 A_Jump(72,"Comet","Spam");
		TNT1 A 0 A_Jump(128,"RapidFire","TwinBall");
	Ball:
		// ball 1
		BOS4 EF 6 Bright A_FaceTarget;
		BOS4 G 4 Bright {
			A_SpawnProjectile("FireborneArchonBall",56);
		}
		BOS4 H 4 Bright;
		// ball 2
		BOS4 IJ 6 Bright A_FaceTarget;	
		BOS4 K 4 Bright {
			A_SpawnProjectile("FireborneArchonBall",56);
		}
		BOS4 L 4 Bright;
		// ball 3
		BOS4 EF 6 Bright A_FaceTarget;
		BOS4 G 4 Bright {
			A_SpawnProjectile("FireborneArchonBall",56);
		}
		BOS4 H 4 Bright;
		TNT1 A 0 Bright A_Jump(96, "See");		
		// optional ball 4
		BOS4 IJ 6 Bright A_FaceTarget;	
		BOS4 K 4 Bright {
			A_SpawnProjectile("FireborneArchonBall",56);
		}
		BOS4 L 4 Bright;
		Goto See;
	TwinBall:
		BOS4 EEF 8 Bright A_FaceTarget;
		BOS4 G 6 Bright {
			A_SpawnProjectile("FireborneArchonBall",56);
			A_SpawnProjectile("FireborneArchonBall",56,0,-4);
		}
		BOS4 H 6 Bright;
		TNT1 A 0 Bright A_Jump(96, "See");	
		BOS4 IJ 8 Bright A_FaceTarget;	
		BOS4 K 6 Bright {
			A_SpawnProjectile("FireborneArchonBall",56);
			A_SpawnProjectile("FireborneArchonBall",56,0,4);
		}
		BOS4 L 6 Bright;
		Goto See;
	Spam:
		TNT1 A 0 { 
			spamBallAngle = -20; 
		}
		BOS4 MN 10 Bright A_FaceTarget;
		BOS4 OOOOOOOO 1 Bright {
			A_SpawnProjectile("DMR_BaronBall",56,0,spamBallAngle);
			spamBallAngle += 5;
		}
		BOS4 P 6 Bright;
		Goto See;
	RapidFire:
		BOS4 E 8 Bright A_FaceTarget;
		TNT1 A 0 { attackloop = 0; }
	RapidFireLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 8,"See");
		BOS4 EF 3 Bright A_FaceTarget;
		BOS4 G 3 Bright {
			A_SpawnProjectile("FireborneArchonSmallBall",56);
			attackloop++;
		}
		BOS4 H 3 Bright;
		BOS4 IJ 3 Bright A_FaceTarget;	
		BOS4 K 3 Bright {
			A_SpawnProjectile("FireborneArchonSmallBall",56);
			attackloop++;
		}
		BOS4 L 3 Bright;
		Loop;
	Comet:
		TNT1 A 0 {
			if (target && Distance3D(target) > 300) SetStateLabel("Ball");
		}
		TNTA A 0 A_StartSound("pyro/see");
		BOS4 MN 12 Bright A_FaceTarget;
		BOS4 O 8 Bright {
			A_SpawnProjectile("FireborneArchonCometSpawner",56,0,0);
			A_SpawnProjectile("FireborneArchonCometSpawner",56,0,60);
			A_SpawnProjectile("FireborneArchonCometSpawner",56,0,120);
			A_SpawnProjectile("FireborneArchonCometSpawner",56,0,180);
			A_SpawnProjectile("FireborneArchonCometSpawner",56,0,240);
			A_SpawnProjectile("FireborneArchonCometSpawner",56,0,300);
		}
		BOS4 P 6 Bright;
		Goto See;
	Pain:
		BOS4 Q 10 A_Pain;
		Goto See;
	Death:
		BOS4 R 10 Bright;
		BOS4 S 10 Bright A_Scream;
		BOS4 TUV 10 Bright;
		BOS4 W 6 Bright;
		BOS4 X 6 Bright {
			A_NoBlocking();
			A_SpawnProjectile("FireborneArchonDeathSoul",86,0,0,2,-90);
		}
		BOS4 YZ[ 6 Bright;
		TNT1 A 50;
		TNT1 A -1 A_BossDeath;
		Stop;
	}
}

class FireborneArchonBall : DMR_Rocket
{
	Default
	{
		DamageFunction(random(40,120));
		RenderStyle "Normal";
		//DamageType "Fire";
		SeeSound "pyro/flame";
		DeathSound "pyro/flamepoof";
		Scale 1.2;
		DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	States
	{
	Spawn:
		ARCB AABBCC 2 BRIGHT {
			A_SpawnItemEx("DMR_LargeGreenFireballTrail",-5,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1));
			//A_SpawnItemEx("FireborneArchonBallTail",0,0,0,0,0,0);
			for (fxLoop = 0;fxLoop<=3;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0,STYLE_Add);
			for (fxLoop = 0;fxLoop <= 14;fxLoop++) {
				A_SpawnItemEx("DMR_LargeGreenFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
				A_SpawnItemEx("DMR_LargeGreenFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
			}
			for (fxLoop = 0;fxLoop<=70;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}		
		ARCB J 6 Bright A_Explode(96,128);
		ARCB KLMN 5 Bright;
		Stop;
	}
}

class FireborneArchonBallTail : DMR_ProjectileTrail
{
	Default
	{
		Alpha 0.4;
	}
	States
	{
	Spawn:
		FRTB ABCDEFGHI 2 Bright;
		Stop;
	}
}


class FireborneArchonSmallBall : DMR_SmallRocket
{
	Default
	{
		DamageFunction(random(20,60));
		RenderStyle "Normal";
		//DamageType "Fire";
		SeeSound "pyro/flame";
		DeathSound "pyro/flamepoof";
		Scale 0.8;
		DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	States
	{
	Spawn:
		ARCB AABBCC 2 BRIGHT {
			//A_SpawnItemEx("AfritCometTrail",-15,0,0);
			A_SpawnItemEx("DMR_MediumGreenFireballTrail",-5,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0,STYLE_Add);
			for (fxLoop = 0;fxLoop <= 10;fxLoop++) {
				A_SpawnItemEx("DMR_MediumGreenFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
				A_SpawnItemEx("DMR_MediumGreenFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
			}
			for (fxLoop = 0;fxLoop<=50;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}		
		ARCB D 3 Bright A_Explode(72,96);
		ARCB EFGHI 3 Bright;
		Stop;
	}
}

class FireborneArchonCometSpawner : DMR_Projectile
{
	Default
	{
		Speed 15;
		+CEILINGHUGGER;
		+THRUACTORS;
	}
	
	int fuelLevel;	
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			fuelLevel = random(75,100); // set fuel lifetime
		}
	SpawnLoop:
		TNT1 A 1 {
			if (fuelLevel < 1) {
				SetStateLabel("Death");
			}
			fuelLevel--;
			
			A_Wander();
			
			if (fuelLevel % 10 == 1) {
				A_SpawnProjectile("FireborneArchonFallingComet",-24,random(0,128),random(0,360),2,random(1,89));
			}			
		}
		Loop;
	Death:
		Stop;
	}
}

class FireborneArchonFallingComet : AfritComet
{
	Default
	{
		Speed 0;
		DamageFunction(random(10,30));
		-NoGravity;
		-Float;
		+SpawnCeiling;
	}
	States
	{
	Spawn:
		COMT AABBCC 2 Bright { 
			A_SpawnItemEx("AfritCometTrail",0,0,0);
			A_SpawnItemEx("DMR_MediumFireballTrail",0,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetScale(1);
		}
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 6;fxLoop++) {
				A_SpawnItemEx("DMR_MediumFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
				A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
			}
		}
		COMT D 3 Bright A_Explode(20,80);
		COMT EFGHI 3 Bright;
		Stop;
	}
}


Class FireborneArchonDeathSoul : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Speed 3;
		RenderStyle "Add";
		Alpha 0.80;
		Projectile;
		+ClientSideOnly
	}

	States
	{
	Spawn:
		BFX1 ABCD 6 Bright;
		Stop;
	}
}
