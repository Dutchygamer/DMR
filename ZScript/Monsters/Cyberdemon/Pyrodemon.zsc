//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Maelstrom
//	Sounds:		Id Software, Monolith Games
//  Sprites: 	Id Software (edits by Eriance, Maelstrom)
//	GLDEFs: 	Dreadopp, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class FireborneArchon : DMR_Cyberdemon
{
	Default
	{
		Tag "Fireborne Archon";
		Health 5000;
		Scale 1.5;
		DropItem "CellPack", 256, 100;
		DropItem "CellPack", 128, 100;
		Obituary "%o was burnt to a crisp by a Fireborne Archon";
		HitObituary "%o was melted my a Fireborne Archon";
		bloodcolor "orange";
		SeeSound "pyro/see";
		PainSound "pyro/pain";
		DeathSound "pyro/death";
		ActiveSound "pyro/idle";
	}
	
	int spamBallAngle;
	
	States
	{
	Spawn:
		PYRO AB 10 A_Look;
		Loop;
	See:
		PYRO A 3 A_Hoof;
		PYRO ABBCC 3 A_Chase;
		PYRO D 3 A_Hoof;
		PYRO D 3 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 A_Jump(72,"Comet","Spam");
		TNT1 A 0 A_Jump(128,"RapidFire","TwinBall");
	Ball:
		// ball 1
		PYRO EF 6 Bright A_FaceTarget;
		PYRO G 4 Bright {
			A_SpawnProjectile("FireborneArchonBall",56);
		}
		PYRO H 4 Bright;
		// ball 2
		PYRO IJ 6 Bright A_FaceTarget;	
		PYRO K 4 Bright {
			A_SpawnProjectile("FireborneArchonBall",56);
		}
		PYRO L 4 Bright;
		// ball 3
		PYRO EF 6 Bright A_FaceTarget;
		PYRO G 4 Bright {
			A_SpawnProjectile("FireborneArchonBall",56);
		}
		PYRO H 4 Bright;
		TNT1 A 0 Bright A_Jump(96, "See");		
		// optional ball 4
		PYRO IJ 6 Bright A_FaceTarget;	
		PYRO K 4 Bright {
			A_SpawnProjectile("FireborneArchonBall",56);
		}
		PYRO L 4 Bright;
		Goto See;
	TwinBall:
		PYRO EEF 8 Bright A_FaceTarget;
		PYRO G 6 Bright {
			A_SpawnProjectile("FireborneArchonBall",56);
			A_SpawnProjectile("FireborneArchonBall",56,0,-4);
		}
		PYRO H 6 Bright;
		TNT1 A 0 Bright A_Jump(96, "See");	
		PYRO IJ 8 Bright A_FaceTarget;	
		PYRO K 6 Bright {
			A_SpawnProjectile("FireborneArchonBall",56);
			A_SpawnProjectile("FireborneArchonBall",56,0,4);
		}
		PYRO L 6 Bright;
		Goto See;
	Spam:
		TNT1 A 0 { 
			spamBallAngle = -20; 
		}
		PYRO MN 10 Bright A_FaceTarget;
		PYRO OOOOOOOO 1 Bright {
			A_SpawnProjectile("AfritBall",56,0,spamBallAngle);
			spamBallAngle += 5;
		}
		PYRO P 6 Bright;
		Goto See;
	RapidFire:
		PYRO E 8 Bright A_FaceTarget;
		TNT1 A 0 { attackloop = 0; }
	RapidFireLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 8,"See");
		PYRO EF 3 Bright A_FaceTarget;
		PYRO G 3 Bright {
			A_SpawnProjectile("FireborneArchonSmallBall",56);
			attackloop++;
		}
		PYRO H 3 Bright;
		PYRO IJ 3 Bright A_FaceTarget;	
		PYRO K 3 Bright {
			A_SpawnProjectile("FireborneArchonSmallBall",56);
			attackloop++;
		}
		PYRO L 3 Bright;
		Loop;
	Comet:
		TNT1 A 0 {
			if (target && Distance3D(target) > 300) SetStateLabel("Ball");
		}
		TNTA A 0 A_PlaySound("pyro/see");
		PYRO MN 12 Bright A_FaceTarget;
		PYRO O 8 Bright {
			A_SpawnProjectile("FireborneArchonCometSpawner",56,0,0);
			A_SpawnProjectile("FireborneArchonCometSpawner",56,0,60);
			A_SpawnProjectile("FireborneArchonCometSpawner",56,0,120);
			A_SpawnProjectile("FireborneArchonCometSpawner",56,0,180);
			A_SpawnProjectile("FireborneArchonCometSpawner",56,0,240);
			A_SpawnProjectile("FireborneArchonCometSpawner",56,0,300);
		}
		PYRO P 6 Bright;
		Goto See;
	Pain:
		PYRO Q 10 A_Pain;
		Goto See;
	Death:
		PYRO R 10 Bright;
		PYRO S 10 Bright A_Scream;
		PYRO TUV 10;
		PYRO W 6;
		PYRO X 6 A_NoBlocking;
		PYRO Y 6;
		PYRO Z 50;
		PYRO Z -1 A_BossDeath;
		Stop;
	}
}

class FireborneArchonBall : DMR_Rocket
{
	Default
	{
		DamageFunction(random(40,120));
		RenderStyle "Normal";
		DamageType "Fire";
		SeeSound "pyro/flame";
		DeathSound "pyro/flamepoof";
		Scale 1.2;
	}
	States
	{
	Spawn:
		COMT AABBCC 2 BRIGHT {
			A_SpawnItemEx("DMR_LargeFireballTrail",-5,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1));
			A_SpawnItemEx("FireborneArchonBallTail",0,0,0,0,0,0);
		}
		Loop;
	Death:
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 14;fxLoop++) {
				A_SpawnItemEx("DMR_MediumFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
				A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
			}
		}		
		COMT D 3 Bright A_Explode(96,128);
		COMT EFGHI 3 Bright;
		Stop;
	}
}

class FireborneArchonBallTail : DMR_ProjectileTrail
{
	Default
	{
		Alpha 0.4;
	}
	States
	{
	Spawn:
		FRTB ABCDEFGHI 2 Bright;
		Stop;
	}
}


class FireborneArchonSmallBall : DMR_SmallRocket
{
	Default
	{
		DamageFunction(random(20,60));
		RenderStyle "Normal";
		DamageType "Fire";
		SeeSound "pyro/flame";
		DeathSound "pyro/flamepoof";
		Scale 0.8;
	}
	States
	{
	Spawn:
		COMT AABBCC 2 BRIGHT {
			A_SpawnItemEx("AfritCometTrail",-15,0,0);
			A_SpawnItemEx("DMR_LargeFireballTrail",-5,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1));
		}
		Loop;
	Death:
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 10;fxLoop++) {
				A_SpawnItemEx("DMR_MediumFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
				A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
			}
		}		
		COMT D 3 Bright A_Explode(72,96);
		COMT EFGHI 3 Bright;
		Stop;
	}
}

class FireborneArchonCometSpawner : DMR_Projectile
{
	Default
	{
		Speed 15;
		+CEILINGHUGGER;
		+THRUACTORS;
	}
	
	int fuelLevel;	
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			fuelLevel = random(75,100); // set fuel lifetime
		}
	SpawnLoop:
		TNT1 A 1 {
			if (fuelLevel < 1) {
				SetStateLabel("Death");
			}
			fuelLevel--;
			
			A_Wander();
			
			if (fuelLevel % 10 == 1) {
				A_SpawnProjectile("FireborneArchonFallingComet",-24,random(0,128),random(0,360),2,random(1,89));
			}			
		}
		Loop;
	Death:
		Stop;
	}
}

class FireborneArchonFallingComet : AfritComet
{
	Default
	{
		Speed 0;
		DamageFunction(random(10,30));
		-NoGravity;
		-Float;
		+SpawnCeiling;
	}
	States
	{
	Spawn:
		COMT AABBCC 2 Bright { 
			A_SpawnItemEx("AfritCometTrail",0,0,0);
			A_SpawnItemEx("DMR_MediumFireballTrail",0,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetScale(1);
		}
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 6;fxLoop++) {
				A_SpawnItemEx("DMR_MediumFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
				A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
			}
		}
		COMT D 3 Bright A_Explode(20,80);
		COMT EFGHI 3 Bright;
		Stop;
	}
}