//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	DBThanatos
//	Sounds:		Blizzard (Diablo)
//  Sprites: 	Id Software (edits by Jimmy)
//	GLDEFs: 	Dreadopp, Virtue (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class Tyrant : DMR_Cyberdemon
{
	Default
	{
		Tag "Tyrant";
		Health 5000;
		DropItem "RocketBox", 256, 5;
		DropItem "RocketBox", 128, 5;
		DropItem "Cell", 128, 20;
		Scale 1.05;
		Bloodcolor "green";
		SeeSound "tyrant/sight";
		PainSound "cyber/Pain";
		DeathSound "tyrant/death";
		ActiveSound "cyber/active"	;
		Obituary "%o fell before a Tyrant.";
		DamageFactor "ToxinCloud", 0.0;
	}
	States
	{
	Spawn:
		DCYB AB 10 A_Look;
		Loop;
	See:
		DCYB A 3 {		
			A_SpawnItemEx("DMR_TranslucentAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Hoof();
		}
		DCYB ABBCC 3 {
			A_SpawnItemEx("DMR_TranslucentAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase();
		}
		DCYB D 3 {
			A_SpawnItemEx("DMR_TranslucentAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Metal();
		}
		DCYB D 3 {
			A_SpawnItemEx("DMR_TranslucentAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase();
		}
		Loop;
	Missile:
		// normal launch coords are 56,-25
		TNT1 A 0 A_Jump(128,"Flechette");
		TNT1 A 0 A_Jump(96,"BFGBall","ChemicalMissile");
	Rockets:
		DCYB E 6 A_FaceTarget;
		DCYB F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",56,-25,0);
		}
		DCYB E 10 A_FaceTarget;
		DCYB F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",56,-25,0);
		}
		DCYB E 10 A_FaceTarget;
		DCYB F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",56,-25,0);
		}
		Goto See;
	Flechette:
		DCYB E 8 A_FaceTarget;
		DCYB FF 3 A_SpawnProjectile("TiberiumFlechette",56,-25,random(-2,2));
		DCYB E 6 A_FaceTarget;
		DCYB FF 3 A_SpawnProjectile("TiberiumFlechette",56,-25,random(-2,2));
		DCYB E 6 A_FaceTarget;
		DCYB FF 3 A_SpawnProjectile("TiberiumFlechette",56,-25,random(-2,2));
		DCYB E 6 A_FaceTarget;
		Goto See;
	BFGBall:
		TNT1 A 0 {
			A_StartSound ("tibshard/blue/charge", CHAN_WEAPON);
			bNOPAIN = true;
		}
		DCYB E 12 Light("DMR_BlueBFGCharge_X1") A_FaceTarget;
		DCYB EEEEEEEEEEEE 1 BRIGHT Light("DMR_BlueBFGCharge_X2") {
			A_FaceTarget();
			A_SpawnItemEx("BlueLightningBolt",0,-25,56);
		}
		DCYB F 6 Bright Light("DMR_BlueBFGFire") {
			A_FaceTarget();
			A_SpawnProjectile("TiberiumBlueFlechette",56,-25,0);
		}
		DCYB F 6 {
			bNOPAIN = false;
		}
		Goto See;
	ChemicalMissile:
		DCYB E 10 {
			A_FaceTarget();
			A_StartSound("heavyweapon/load");
		}
		DCYB EE 10 A_FaceTarget;
		DCYB F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("TiberiumGasRocket",56,-25,0);
		}
		Goto See;
	Pain:
		DCYB G 10 A_Pain;
		Goto See;
	Death:
		DCYB H 10;
		DCYB I 6 Bright A_Scream;
		DCYB JKL 6 Bright {
			A_SpawnItemEx("DMR_CyberDeathExplosionSpawner");
		}
		TNT1 A 0 A_NoBlocking;
		DCYB MN 6 Bright {
			A_SpawnItemEx("DMR_CyberDeathExplosionSpawner");
		}
		DCYB O 6 Bright;
		DCYB P 50;
		DCYB P -1 A_BossDeath;
		Stop;
	}
}

// flechettes
class TiberiumFlechette : DMR_SmallRocket
{
	Default
	{
		Speed 25;
		DamageFunction(random(16,48));
		SeeSound "tibshard/green/fire";
		DeathSound "tibshard/green/explode";
		Scale 0.9;
		Decal "BFGLightning";
		DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	States
	{
	Spawn:
		TSRD A 1 Bright {
			A_SpawnItemEx("TiberiumFlechetteTrail",-20,0,1); 
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-30,-26),frandom(-6,6),frandom(-5,5),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0, STYLE_Add);
			A_SetScale(1.5);
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_Explode(40,72);
			A_QuakeEx(3,3,3,15,0,600,"",QF_RELATIVE|QF_SCALEDOWN|QF_3D,falloff:400,rollIntensity:.75);
			for (fxLoop = 0;fxLoop<=50;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(18,24),frandom(12,14),random(0,360),frandom(0,9),0,frandom(-7,7),frandom(0,7),0,frandom(-7,7),0,0,-0.1,1,-1,-0.6);
			}
		}
		TSDE ABCDEFG 3 Bright;
		Stop;
	}
}

class TiberiumFlechetteTrail : DMR_ProjectileTrail
{   
	Default
	{
		RenderStyle "Add";
		Alpha 0.6;
		Scale 0.65;
		-ROLLSPRITE;
	}
	States
	{
	Spawn:
		TSRD A 1 Bright A_FadeOut(0.07);
		Loop;
	}
}

// BFG
class TiberiumBlueFlechette : TiberiumFlechette
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 30;
		DamageFunction (400); // don't use the default formula, just don't get hit by it ;o
		+EXTREMEDEATH;
		+FORCERADIUSDMG;
		SeeSound "tibshard/blue/fire";
		DeathSound "tibshard/blue/explode";
		Scale 1.2;
		Decal "BlueBFGLightning";
		DMR_Projectile.ParticleColors "#0055ab", "#0067cd", "#003e7d", "#004e9c";
	}
	States
	{
	Spawn:
		BSRD A 1 Bright {
			A_SpawnItemEx("TiberiumBlueFlechetteTrail",-20,0,1);
			A_SpawnItemEx("BlueLightningBolt",frandom(0,-20));
			A_SpawnItemEx("BlueLightningBolt",frandom(0,-20));
			for (fxLoop = 0;fxLoop<=3;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-35,-30),frandom(-6,6),frandom(-6,6),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetScale(1.5);
			A_Explode(196,196);
			A_SpawnItemEx("DMR_BlueBFGImpactFlare");
			A_SpawnItemEx("BlueLightningBurst");
			A_SetRenderStyle(1.0,STYLE_Add);
			A_QuakeEx(9,9,9,40,0,1800,"",QF_RELATIVE|QF_SCALEDOWN|QF_3D,falloff:1200,rollIntensity:1.);
			for (fxLoop = 0;fxLoop<=70;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(20,36),frandom(12,14),random(0,360),frandom(0,13),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
			for (fxLoop = 0;fxLoop<=50;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(36,48),frandom(24,28),random(0,360),frandom(0,13),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		ARCB JKLMN 8 Bright;
		Stop;
	}
}

class TiberiumBlueFlechetteTrail : TiberiumFlechetteTrail
{
	Default
	{
		Scale 0.9;
	}
	States
	{
	Spawn:
		BSRD A 1 Bright A_FadeOut(0.06);
		Loop;
	}
}

class TiberiumGasRocket : DMR_Rocket
{
	Default
	{
		Speed 25;
		Scale 1.2;
		+SEEKERMISSILE;
		SeeSound "coredefender/cluster";
		DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	States
	{
	Spawn:
		MISL AAA 1 Bright {
			A_SpawnItemEx("DMR_MediumGreenFireballTrail",-35,0,2,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			A_SpawnItemEx("DMR_GasTrail",-35,0,2,frandom(-1,0));
			for (fxLoop = 0;fxLoop<=3;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-35,-30),frandom(-6,6),frandom(-6,6),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		TNT1 A 0 A_SeekerMissile(1,8,SMF_CURSPEED);
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0,STYLE_Add);	
			A_FaceTracer();
			for (fxLoop = 0;fxLoop<=360;fxLoop = fxLoop+60) {
				let ball = A_SpawnProjectile("TiberiumGasRocketCluster",0,0,fxLoop,CMF_TRACKOWNER|CMF_AIMDIRECTION);
				if (ball) {
					ball.bNOGRAVITY = false;
					ball.vel.x = ball.vel.x*frandom(0.2,0.4);
					ball.vel.y = ball.vel.y*frandom(0.2,0.4);
					ball.vel.z = ball.vel.z+frandom(6,12);
				}
			}
			A_SpawnItemEx("DMR_LargeGreenFireballImpactSpawner");
			A_QuakeEx(4,4,4,15,0,800,"",QF_RELATIVE|QF_SCALEDOWN|QF_3D,falloff:500,rollIntensity:.75);
			for (fxLoop = 0;fxLoop<=70;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(18,24),frandom(12,14),random(0,360),frandom(0,11),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		HECF C 8 Bright A_Explode(128,128);
		HECF D 6 Bright;
		HECF E 4 Bright;
		Stop;
	}
}


class TiberiumGasRocketCluster : WithererGasBall
{
	Default
	{
		//DamageFunction(random(16,48));
		//RenderStyle "Add";
		//Alpha 1;
		//DamageType "ToxinCloud";
		Scale 1.0;
		//DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
}