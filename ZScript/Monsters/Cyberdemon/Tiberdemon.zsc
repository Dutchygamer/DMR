//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	DBThanatos
//	Sounds:		Blizzard (Diablo)
//  Sprites: 	Id Software (edits by Jimmy)
//	GLDEFs: 	Dreadopp, Virtue (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class Tiberdemon : DMR_Cyberdemon
{
	Default
	{
		Tag "Cyparius"; // Cyberdemon + Riparius
		Health 5000;
		PainChance 15;
		DropItem "RocketBox", 256, 5;
		DropItem "RocketBox", 128, 5;
		DropItem "Cell", 128, 20;
		Scale 1.05;
		Bloodcolor "green";
		SeeSound "TiberDemon/sight";
		PainSound "cyber/Pain";
		DeathSound "TiberDemon/death";
		ActiveSound "cyber/active"	;
		Obituary "%o fell before a cyparius.";
		DamageFactor "ToxinCloud", 0.0;
	}
	States
	{
	Spawn:
		DCYB AB 10 A_Look;
		Loop;
	See:
		DCYB A 3 {		
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Hoof();
		}
		DCYB ABBCC 3 {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase();
		}
		DCYB D 3 {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Metal();
		}
		DCYB D 3 {
			A_SpawnItemEx("DMR_GhostAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			A_Chase();
		}
		Loop;
	Missile:
		//normal launch coords are 56,-25
		TNT1 A 0 A_Jump(128,"Flechette");
		TNT1 A 0 A_Jump(96,"BFGBall");
	Rockets:
		DCYB E 6 A_FaceTarget;
		DCYB F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",56,-25,0);
		}
		DCYB E 10 A_FaceTarget;
		DCYB F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",56,-25,0);
		}
		DCYB E 10 A_FaceTarget;
		DCYB F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",56,-25,0);
		}
		Goto See;
	Flechette:
		DCYB E 8 A_FaceTarget;
		DCYB FF 3 A_SpawnProjectile("TiberiumFlechette",56,-25,random(-2,2));
		DCYB F 6 A_FaceTarget;
		DCYB FF 3 A_SpawnProjectile("TiberiumFlechette",56,-25,random(-2,2));
		DCYB F 6 A_FaceTarget;
		DCYB FF 3 A_SpawnProjectile("TiberiumFlechette",56,-25,random(-2,2));
		DCYB F 6 A_FaceTarget;
		Goto See;
	BFGBall:
		TNT1 A 0 {
			A_PlaySound ("weapons/bfgf", CHAN_WEAPON);
			bNOPAIN = true;
		}
		DCYB E 12 Light("DMR_BlueBFGCharge_X1") A_FaceTarget;
		DCYB E 12 Bright Light("DMR_BlueBFGCharge_X2") A_FaceTarget;
		DCYB F 6 Bright Light("DMR_BlueBFGFire") {
			A_FaceTarget();
			A_SpawnProjectile("TiberiumBlueFlechette",56,-25,0);
		}
		DCYB F 6 {
			bNOPAIN = false;
		}
		Goto See;
	Pain:
		DCYB G 10 A_Pain;
		Goto See;
	Death:
		DCYB H 10;
		DCYB I 6 Bright A_Scream;
		DCYB JKL 6 Bright {
			A_SpawnItemEx("DMR_CyberDeathExplosionSpawner");
		}
		TNT1 A 0 A_NoBlocking;
		DCYB MN 6 Bright {
			A_SpawnItemEx("DMR_CyberDeathExplosionSpawner");
		}
		DCYB O 6 Bright;
		DCYB P 50;
		DCYB P -1 A_BossDeath;
		Stop;
	}
}

// flechettes
class TiberiumFlechette : DMR_SmallRocket
{
	Default
	{
		Speed 25;
		DamageFunction(random(16,48));
		SeeSound "Thamuz/TibShard";
		DeathSound "Thamuz/TibExpl";
		Scale 0.9;
	}
	States
	{
	Spawn:
		TSRD A 1 Bright {
			A_SpawnItemEx("TiberiumFlechetteTrail",-20,0,1); 
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0, STYLE_Add);
			A_SetScale(1.5);
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_Explode(24,64);
		}
		TSDE ABCDEFG 3 Bright;
		Stop;
	}
}

class TiberiumFlechetteTrail : DMR_ProjectileTrail
{   
	Default
	{
		RenderStyle "Add";
		Alpha 0.6;
		Scale 0.65;
		-ROLLSPRITE;
	}
	States
	{
	Spawn:
		TSRD A 1 Bright A_FadeOut(0.07);
		Loop;
	}
}

// BFG
class TiberiumBlueFlechette : TiberiumFlechette
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 30;
		DamageFunction (400); // don't use the default formula, just don't get hit by it ;o
		+EXTREMEDEATH;
		+FORCERADIUSDMG;
		SeeSound "Thamuz/TibBall";
		DeathSound "q2weapon/bfgballexplo";
		Decal "BFGLightning";
		Scale 1.2;
	}
	States
	{
	Spawn:
		BSRD A 1 Bright {
			A_SpawnItemEx("TiberiumBlueFlechetteTrail",-20,0,1);
			A_SpawnItemEx("TiberiumBlueLightningBolt",frandom(0,-20));
			A_SpawnItemEx("TiberiumBlueLightningBolt",frandom(0,-20));
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetScale(1.5);
			A_Explode(196,196);
			A_SpawnItemEx("DMR_BlueBFGImpactFlare");
			A_SpawnItemEx("TiberiumBlueLightningBurst");
			A_SetRenderStyle(1.0,STYLE_Add);
		}
		ARCB JKLMN 8 Bright;
		Stop;
	}
}

class TiberiumBlueFlechetteTrail : TiberiumFlechetteTrail
{
	Default
	{
		Scale 0.9;
	}
	States
	{
	Spawn:
		BSRD A 1 Bright A_FadeOut(0.06);
		Loop;
	}
}