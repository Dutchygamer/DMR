//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Graf Zahl
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Xim, Vegeta, Ghastly_dragon, MagicWazard)
//	GLDEFs: 	Ghastly_dragon, MagicWazard (brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios, EA Games
//	ZScript:	Dutchygamer
//======================================================================

class FormerSpecOpsElite : DMR_ChaingunGuy
{
	Default
	{
		Tag "Former SpecOps Elite";
		OBITUARY "%o was gunned down by a Former SpecOps Elite.";
		Health 150;
		Speed 12;
		PainChance 70;
		DamageFactor "ZombieGrenade", 0.5; // take less damage from own grenades
		DamageFactor "ToxinCloud", 0.0; // immune to gas clouds
		+MISSILEMORE
		+NODROPOFF; // don't yeet yourself off platforms
		+DOHARMSPECIES; // zombies aren't grenade-proof
		DropItem "Clip", 256, 5;
		DropItem "Clip", 128, 5;
		DropItem "Clip", 64, 5;
		DropItem "RocketAmmo", 64, 1;
	}
	bool isStealthed;
	
 	States
	{
	Spawn:
		ZSP3 AB 10 A_Look;
		Loop;
	See:
		ZSP3 AABBCCDD 4 A_Chase;
		TNT1 A 0 A_Jump(64,"GoStealthed");
		Loop;
    StealthedSee:
        ZSP3 AABBCCDD 2 {
			A_Chase(null,null);			
			A_SpawnItemEx("SpecOpsAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION,128);
		}
        ZSP3 AABBCCDD 2 {
			A_Chase(null,null, CHF_FASTCHASE);			
			A_SpawnItemEx("SpecOpsAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION,128);
		}
    StealthedSeeLoop:
        ZSP3 AABBCCDD 2 {
			A_Chase();			
			A_SpawnItemEx("SpecOpsAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION,128);
		}
        Loop;
	Missile:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
		TNT1 A 0 A_Jump(96, "Grenade");
		Goto Rifle;
	Rifle:
		ZSP3 E 10 A_FaceTarget;		
		ZSP3 F 4 BRIGHT Light("DMR_RifleFire") {
			A_PlaySound("SpecOps/fire", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,0,1,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
		}
		ZSP3 E 2 A_FaceTarget;		
		ZSP3 F 4 BRIGHT Light("DMR_RifleFire") {
			A_PlaySound("SpecOps/fire", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,0,1,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
		}
		ZSP3 E 2 A_FaceTarget;		
		ZSP3 F 4 BRIGHT Light("DMR_RifleFire") {
			A_PlaySound("SpecOps/fire", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,0,1,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
		}
		ZSP3 E 6 A_MonsterRefire(40,"See");
		Goto Rifle+1;
	Grenade:
		TNT1 A 0 A_JumpIfCloser(96, "Rifle"); // don't grenade yourself when standing in front of the target
		TNT1 A 0 {
			A_PlaySound("heavyweapon/load", CHAN_WEAPON);
		}
		ZSP3 EEE 6 A_FaceTarget;
		ZSP3 F 5 Bright Light("DMR_RifleFire") A_LobProjectile("FormerCyborgGrenade",32);
		ZSP3 E 4;
		Goto See;
	GoStealthed:
		TNT1 A 0 {
			if (isStealthed) { SetStateLabel("See"); }
		}
		ZSP3 E 5 {
			isStealthed = true;
			A_PlaySound("Infiltrator/stealth");
		}
		ZSP3 EEEEE 1 { // to 0.0 alpha in total		
			//A_SpawnItemEx("DMR_SubtractAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION);
			A_SetRenderStyle(Alpha-0.2,STYLE_Translucent);
		}
		TNT1 A 0 A_Chase(null,null, CHF_FASTCHASE);
        Goto StealthedSee;
	Pain:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
		ZSP3 G 3;
		ZSP3 G 3 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
		ZSP3 H 5;
		ZSP3 I 5 A_Scream;
		ZSP3 J 5 A_NoBlocking;
		ZSP3 K 5;
		ZSP3 L -1;
		Stop;
	XDeath:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
		ZSP3 H 5;
		ZSP3 I 5 A_XScream;
		ZSP3 J 5 A_NoBlocking;
		ZSP3 K 5;
		ZSP3 L -1;
		Stop;
	Raise:
		ZSP3 KJIH 5;
		Goto See;
	}
}