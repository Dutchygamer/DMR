//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	TheMightyHeracross
//	Sounds:		Id Software
//  Sprites: 	Rougue, id, Collin Portratz, RBCSkins (zirron the insect, TheMightyHeracross, Xim)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class FormerCyborgCommando : DMR_ChaingunGuy
{
	Default
	{
		Tag "Former Cyborg Commando";
		obituary "%o realized the futility of fighting a Former Cyborg Commando.";
		health 700;
		painchance 20;
		Scale 1.2;
		dropitem "CellPack";
		dropitem "CellPack", 128;
		+DOHARMSPECIES; // zombies aren't BFG-ball-proof
		+MISSILEMORE;
	}
 	States
	{
	Spawn:
		MRBL AA 10 A_Look;
		Loop;
	See:
		MRBL AABBCCDD 5 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 A_Jump(256, "Plasma","HellRune","BFG");
		Goto See;
	Plasma:
		MRBL E 8 A_FaceTarget;
		MRBL F 3 BRIGHT Light("DMR_PlasmaFire") {
			A_PlaySound ("weapons/plasmaf", CHAN_WEAPON);
			A_SpawnProjectile("FormerCyborgCommandoPlasma",34,0,4);
			A_SpawnProjectile("FormerCyborgCommandoPlasma",34);
			A_SpawnProjectile("FormerCyborgCommandoPlasma",34,0,-4);
		}
		MRBL E 2 A_FaceTarget;
		MRBL F 3 BRIGHT Light("DMR_PlasmaFire") {
			A_PlaySound ("weapons/plasmaf", CHAN_WEAPON);
			A_SpawnProjectile("FormerCyborgCommandoPlasma",34,0,4);
			A_SpawnProjectile("FormerCyborgCommandoPlasma",34);
			A_SpawnProjectile("FormerCyborgCommandoPlasma",34,0,-4);
		}
		MRBL E 2 A_FaceTarget;
		MRBL F 3 BRIGHT Light("DMR_PlasmaFire") {
			A_PlaySound ("weapons/plasmaf", CHAN_WEAPON);
			A_SpawnProjectile("FormerCyborgCommandoPlasma",34,0,4);
			A_SpawnProjectile("FormerCyborgCommandoPlasma",34);
			A_SpawnProjectile("FormerCyborgCommandoPlasma",34,0,-4);
		}
		MRBL E 2 A_FaceTarget;
		Goto See;
	HellRune:
		MRBL E 8 A_FaceTarget;
		MRBL F 3 BRIGHT Light("DMR_HellRuneFire") {
			A_SpawnProjectile("FormerPlasmaCommandoBall",34,0,4);
			A_SpawnProjectile("FormerPlasmaCommandoBall",34);
		}
		MRBL E 2 A_FaceTarget;
		MRBL F 3 BRIGHT Light("DMR_HellRuneFire") {
			A_SpawnProjectile("FormerPlasmaCommandoBall",34);
			A_SpawnProjectile("FormerPlasmaCommandoBall",34,0,-4);
		}
		MRBL E 2 A_FaceTarget;
		Goto See;
	BFG:
		TNT1 A 0 {
			A_PlaySound ("weapons/bfgf", CHAN_WEAPON);
			bNOPAIN = true;
		}
		MRBL E 20 Light("DMR_BFGCharge_X1") A_FaceTarget;
		MRBL F 10 Bright Light("DMR_BFGCharge_X2") A_FaceTarget;
		MRBL F 10 Bright Light("DMR_BFGFire") {
			A_FaceTarget();
			A_SpawnProjectile("DMR_BFGBall",34);
		}
		MRBL E 20 {
			bNOPAIN = false;
		}
		goto See;
	Pain:
		MRBL G 3;
		MRBL G 3 A_Pain;
		Goto See;
	Death:
	XDeath:
		MRBL HHH 5 {
			A_NoBlocking();
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(8,-8),0,random(20,40));
		}
		MRBL I 5 {
			A_Scream();
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(8,-8),0,random(20,40));
		}
		MRBL J 5 {
			A_NoBlocking();
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(8,-8),0,random(20,40));
		}
		MRBL KL 5;
		MRBL L -1;
		Stop;
	Raise:
		MRBL LKJIH 5;
		Goto See;
	}
}

class FormerCyborgCommandoPlasma : DMR_ZombiePlasma
{
	Default
	{		
		SeeSound "";
	}
}
