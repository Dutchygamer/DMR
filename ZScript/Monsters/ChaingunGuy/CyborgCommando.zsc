//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	TheMightyHeracross
//	Sounds:		Id Software
//  Sprites: 	Rougue, id, Collin Portratz, RBCSkins (zirron the insect, TheMightyHeracross, Xim)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class FormerCyborgCommando : FormerCyborg
{
	Default
	{
		Tag "Former Cyborg Commando";
		obituary "%o realized the futility of resisting a Former Cyborg Commando.";
		health 1000;
		painchance 20;
		SeeSound "cyborgcommando/sight";
		PainSound "cyborg/pain";
		DeathSound "cyborgcommando/death";
		ActiveSound "cyborg/active";
		Scale 1.2;
		dropitem "CellPack", 256, 100;
		dropitem "CellPack", 128, 100;
		+MISSILEMORE;
		+MISSILEEVENMORE;
		+DONTHARMCLASS; // don't kill yourself with your explosives
		DMR_MonsterBase.Species2 "WolfSSReplacer";
	}
 	States
	{
	Spawn:
		MRBL AA 10 A_Look;
		Loop;
	See:
		MRBL AABB 4 {
			A_SetCyborgTics(4,8);
			A_Chase();
		}
		TNT1 A 0 A_PlaySound("cyborgcommando/step");
		MRBL CCDD 4 {
			A_SetCyborgTics(4,8);
			A_Chase();
		}
		TNT1 A 0 A_PlaySound("cyborgcommando/step");
		Loop;
	Missile:
		TNT1 A 0 {		
			attackloop = 0;
		}
		TNT1 A 0 A_Jump(96,"BFG");
		TNT1 A 0 A_Jump(256,"Plasma", "ChargedPlasma");
		Stop;
	Plasma:
		MRBL E 8 A_FaceTarget;
	PlasmaLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 6,"PlasmaEnd");
		MRBL F 3 BRIGHT Light("DMR_SmallBFGFire") {
			A_PlaySound ("cyborgcommando/fire", CHAN_WEAPON);
			A_SpawnProjectile("FormerCyborgCommandoPlasma",36);
		}
		MRBL E 2 A_FaceTarget;
		Loop;
	PlasmaEnd:
		MRBL E 2;
		Goto See;
	ChargedPlasma:
		TNT1 A 0 {
			A_PlaySound ("bfg/charge", CHAN_WEAPON);
		}
		MRBL EEE 8 Light("DMR_BFGCharge_X1") A_FaceTarget;
		MRBL F 5 BRIGHT Light("DMR_SmallBFGFire") {
			A_PlaySound ("cyborgcommando/fire2", CHAN_WEAPON);
			A_SpawnProjectile("FormerCyborgCommandoChargedPlasma",36);
		}
		MRBL E 4 A_FaceTarget;
		MRBL F 5 BRIGHT Light("DMR_SmallBFGFire") {
			A_PlaySound ("cyborgcommando/fire2", CHAN_WEAPON);
			A_SpawnProjectile("FormerCyborgCommandoChargedPlasma",36);
		}
		MRBL E 4 A_FaceTarget;
		MRBL F 5 BRIGHT Light("DMR_SmallBFGFire") {
			A_PlaySound ("cyborgcommando/fire2", CHAN_WEAPON);
			A_SpawnProjectile("FormerCyborgCommandoChargedPlasma",36);
		}
		MRBL E 20 A_FaceTarget;
		Goto See;
	BFG:
		TNT1 A 0 {
			A_PlaySound("cyborgcommando/bfg/taunt", CHAN_VOICE);
			bNOPAIN = true;
		}
		MRBL E 10 A_FaceTarget;
		TNT1 A 0 {
			A_PlaySound("cyborgcommando/bfg/charge", CHAN_WEAPON);
		}
		MRBL EEEEE 3 Light("DMR_BFGCharge_X1") {
			A_FaceTarget();	A_SpawnItemEx("FormerCyborgCommandoPlasmaTrail",random(-1,1),random(-1,1),36,frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		MRBL EEEEEFFFFF 2 Light("DMR_BFGCharge_X2") {
			A_FaceTarget();	A_SpawnItemEx("FormerCyborgCommandoBFGBallTrail",random(-1,1),random(-1,1),36,frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		MRBL F 10 Bright Light("DMR_BFGFire") {
			A_FaceTarget();
			A_PlaySound("cyborgcommando/bfg/fire", CHAN_WEAPON);
			A_SpawnProjectile("FormerCyborgCommandoBFG",36);
		}
		MRBL E 20 {
			bNOPAIN = false;
		}
		goto See;
	CriticalDamage: 
		TNT1 A 0 { bNOPAIN = true; }
		MRBL G 4;
		MRBL G 4 A_PlaySound("cyborg/pain", 7);
		MRBL G 4 {
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(8,-8),0,random(20,40));
		}
		TNT1 A 0 { 
			criticalDamaged = true;
			bNOPAIN = false; 
		}
		Goto See;
	Pain:
		MRBL G 3;
		MRBL G 3 A_Pain;
		Goto See;
	Death:
	XDeath:
		MRBL G 10 A_Scream;
		MRBL GGGHI 5 {
			A_SpawnItemEx("DMR_BossDeathExplosion",random(8,-8),0,random(20,40));
		}
		MRBL J 5 {
			A_NoBlocking();
			A_SpawnItemEx("DMR_MonsterDeathFinalExplosion");
		}
		MRBL KL 5;
		MRBL L -1;
		Stop;
	Raise:
		// when rezzed you get your legs back
		TNT1 A 0 {			
			criticalDamaged = false;
		}
		MRBL LKJIH 5;
		Goto See;
	}
	
	override void Tick()
  	{
		Super.Tick();
		if (!criticalDamaged && health > 0 && health < 100 && bNOPAIN == false) 
		{ 
			SetStateLabel("CriticalDamage"); 
		}
		if (criticalDamaged && health > 0 && !IsFrozen() && random(0,3) == 3)
		{
			A_SpawnItemEx("FormerCyborgCriticalDamagedSmokeCloud",-5,0,45,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
  	}
}

class FormerCyborgCommandoPlasma : DMR_ZombiePlasma
{
	Default
	{		
		SeeSound "cyborgcommando/fire";
		Scale 0.2;
		DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	States
	{
	Spawn:
		BFS1 AAABBB 2 Bright {
			A_SpawnItemEx("FormerCyborgCommandoPlasmaTrail",-18,0,0);
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-20,-18),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_SmallBFGImpactFlare");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		BFE1 ABCDEF 5 Bright;
		Stop;
	}
}

class FormerCyborgCommandoPlasmaTrail : DMR_ProjectileTrail
{
	Default
	{
		Scale 0.2;
		Alpha 0.75;
	}
	States
	{
	Spawn:
		BFE2 ABCD 2 Bright {
			A_FadeOut(0.06);
		}
		Stop;
	}
}


class FormerCyborgCommandoChargedPlasma : DMR_Rocket
{
	Default
	{
		Speed 30;
		+RANDOMIZE;
		+EXTREMEDEATH;
		RenderStyle "Add";
		Alpha 0.9;
		Scale 0.45;
		SeeSound "cyborgcommando/fire";
		DeathSound "weapons/bfgx";
		Decal "BFGLightning";
		DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	States
	{
	Spawn:
		TBBL AABBCC 1 Bright  {
			A_SpawnItemEx("FormerCyborgCommandoChargedPlasmaTrail",-30,0,0,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			for (fxLoop = 0;fxLoop<=3;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-18,-12),frandom(-6,6),frandom(-6,6),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetScale(0.9);
			A_Explode(128,128);
			A_SpawnItemEx("DMR_MediumBFGImpactFlare");
			A_SpawnItemEx("MediumBFGLightningBurst");
			for (fxLoop = 0;fxLoop<=70;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(20,36),frandom(12,14),random(0,360),frandom(0,11),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		CBTS KLMNOPQRSTUVW 2 Bright;
		Stop;
	}
}

class FormerCyborgCommandoChargedPlasmaTrail : DMR_ProjectileTrail
{
	Default
	{
		Scale 0.70;
		Alpha 0.75;
	}
	States
	{
	Spawn:
		CBTS ABCD 1 Bright {
			A_FadeOut(0.1);
			A_SetScale(Scale.X-0.15);
		}
		Stop;
	}
}


class FormerCyborgCommandoBFG : DMR_Projectile
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 15;
		DamageFunction(200); // don't use the default formula, just don't get hit by it ;o
		+RANDOMIZE;
		+EXTREMEDEATH;
		+FORCERADIUSDMG;
		+NODAMAGETHRUST;
		+BRIGHT;
		RenderStyle "Add";
		Alpha 0.9;
		Decal "BFGLightning";
		DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	
	int deathLoop;
	
	States
	{
	Spawn:
		TNT1 A 0 {
			A_PlaySound("cyborgcommando/bfg/loop",CHAN_VOICE);
		}
		TBBL AAABBBCCC 1  {
			A_SpawnItemEx("FormerCyborgCommandoBFGBallTrail",-30,0,0,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			A_SpawnItemEx("BFGLightningBolt");
			A_Explode(8,128,0,0,64);
			for (fxLoop = 0;fxLoop<=3;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-26,-13),frandom(-13,13),frandom(-13,13),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			deathLoop = 0;
		}
	DeathLoop:
		TNT1 A 0 A_JumpIf(deathLoop >= 12,"DeathEnd");
		TNT1 A 0 {
			A_PlaySound("cyborgcommando/bfg/loop",CHAN_VOICE);
		}
		TBBL ABC 3 {
			A_Explode(24,128,0,0,64);
			A_SpawnItemEx("BFGLightningBolt");
			
			deathLoop++;
			for (fxLoop = 0;fxLoop<=5;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(20,36),frandom(12,14),random(0,360),frandom(0,13),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		Loop;
	DeathEnd:
		TNT1 A 0 {
			A_StopSound(CHAN_VOICE);
			A_PlaySound("cyborgcommando/bfg/explode", CHAN_WEAPON);
			A_Explode(128,128,0,0,64);
			A_SpawnItemEx("DMR_BFGImpactFlare");
			A_SpawnItemEx("BFGLightningBurst");
			for (fxLoop = 0;fxLoop<=70;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(20,36),frandom(12,14),random(0,360),frandom(0,13),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
			for (fxLoop = 0;fxLoop<=50;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(36,48),frandom(24,28),random(0,360),frandom(0,13),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		CBTS KLMNOPQRSTUVW 4;
		Stop;
	}
}

class FormerCyborgCommandoBFGBallTrail : DMR_ProjectileTrail
{
	Default
	{
		Scale 0.9;
		Alpha 0.75;
	}
	States
	{
	Spawn:
		CBTS ABCD 1 Bright {
			A_FadeOut(0.1);
			A_SetScale(Scale.X-0.15);
		}
		Stop;
	}
}