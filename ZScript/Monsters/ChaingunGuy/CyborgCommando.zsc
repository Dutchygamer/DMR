//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	TheMightyHeracross
//	Sounds:		Id Software
//  Sprites: 	Rougue, id, Collin Portratz, RBCSkins (zirron the insect, TheMightyHeracross, Xim)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class CyborgCommando : CyborgAcolyte
{
	Default
	{
		Tag "Cyborg Commando";
		obituary "%o realized the futility of resisting a Cyborg Commando.";
		health 1300;
		painchance 20;
		Speed 12;
		SeeSound "cyborgcommando/sight";
		PainSound "cyborg/pain";
		DeathSound "cyborgcommando/death";
		ActiveSound "cyborg/active";
		Scale 1.2;
		dropitem "CellPack", 256, 100;
		DropItem "Cell", 128, 20;
		DropItem "Cell", 128, 20;
		DropItem "Cell", 128, 20;
		+MISSILEMORE;
		+MISSILEEVENMORE;
		+DONTHARMCLASS; // don't kill yourself with your explosives
		+BOSS; // for extra funsies
		DMR_MonsterBase.Species2 "WolfSSReplacer";
	}
	
	//int breathAngle;
	//bool inverseBreath;
		
 	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			legsThreshold = 150;
		}
		MRBL AA 10 A_Look;
		Loop;
	See:
		// heavier cyborgs are faster
		MRBL AABB 3 {
			A_SetCyborgTics(3,8);
			A_Chase();
		}
		TNT1 A 0 A_StartSound("cyborgcommando/step");
		MRBL CCDD 3 {
			A_SetCyborgTics(3,8);
			A_Chase();
		}
		TNT1 A 0 A_StartSound("cyborgcommando/step");
		Loop;
	Missile:
		TNT1 A 0 {		
			attackloop = 0;
		}
		TNT1 A 0 A_Jump(96,"BFG");
		TNT1 A 0 A_Jump(256,"Plasma", "ChargedPlasma");
		Stop;
	Plasma:
		MRBL E 8 A_FaceTarget;
	PlasmaLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 6,"PlasmaEnd");
		MRBL F 3 BRIGHT Light("DMR_YellowPlasmaFire") {
			A_StartSound ("cyborgcommando/fire", CHAN_WEAPON);
			A_SpawnProjectile("CyborgCommandoPlasma",36);
			attackloop++;
		}
		MRBL E 2 A_FaceTarget;
		Loop;
	PlasmaEnd:
		MRBL E 2;
		Goto See;
	ChargedPlasma:
		TNT1 A 0 {
			A_StartSound ("bfg/charge", CHAN_WEAPON);
		}
		MRBL EEE 8 Light("DMR_YellowBFGCharge_X1") A_FaceTarget;
		MRBL F 5 BRIGHT Light("DMR_YellowBFGFire") {
			A_StartSound ("cyborgcommando/fire2", CHAN_WEAPON);
			A_SpawnProjectile("CyborgCommandoChargedPlasma",36);
		}
		MRBL E 4 A_FaceTarget;
		MRBL F 5 BRIGHT Light("DMR_YellowBFGFire") {
			A_StartSound ("cyborgcommando/fire2", CHAN_WEAPON);
			A_SpawnProjectile("CyborgCommandoChargedPlasma",36);
		}
		MRBL E 4 A_FaceTarget;
		MRBL F 5 BRIGHT Light("DMR_YellowBFGFire") {
			A_StartSound ("cyborgcommando/fire2", CHAN_WEAPON);
			A_SpawnProjectile("CyborgCommandoChargedPlasma",36);
		}
		MRBL E 20 A_FaceTarget;
		Goto See;
	BFG:
		TNT1 A 0 {
			A_StartSound("cyborgcommando/bfg/taunt", CHAN_VOICE);
			bNOPAIN = true;
		}
		MRBL E 10 A_FaceTarget;
		TNT1 A 0 {
			A_StartSound("cyborgcommando/bfg/charge", CHAN_WEAPON);
		}
		MRBL EEEEE 3 Light("DMR_YellowBFGCharge_X1") {
			A_FaceTarget();	A_SpawnItemEx("CyborgCommandoPlasmaTrail",random(-1,1),random(-1,1),36,frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		MRBL EEEEEFFFFF 2 Light("DMR_YellowBFGCharge_X2") {
			A_FaceTarget();	A_SpawnItemEx("CyborgCommandoBFGBallTrail",random(-1,1),random(-1,1),36,frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		MRBL F 10 Bright Light("DMR_YellowBFGFire") {
			A_FaceTarget();
			A_StartSound("cyborgcommando/bfg/fire", CHAN_WEAPON);
			A_SpawnProjectile("CyborgCommandoBFG",36);
		}
		MRBL E 20 {
			bNOPAIN = false;
		}
		goto See;
	CriticalDamage: 
		TNT1 A 0 { bNOPAIN = true; }
		MRBL G 4;
		MRBL G 4 A_StartSound("cyborg/pain", 7);
		MRBL G 4 {
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(8,-8),0,random(20,40));
		}
		TNT1 A 0 { 
			criticalDamaged = true;
			bNOPAIN = false; 
		}
		Goto See;
	Pain:
		MRBL G 3;
		MRBL G 3 A_Pain;
		Goto See;
	Death:
	XDeath:
		MRBL G 10 A_Scream;
		MRBL GGGHI 5 {
			A_SpawnItemEx("DMR_BossDeathExplosion",random(8,-8),0,random(20,40));
		}
		MRBL J 5 {
			A_NoBlocking();
			A_SpawnItemEx("DMR_MonsterDeathFinalExplosion");
		}
		MRBL KL 5;
		MRBL L -1;
		Stop;
	Raise:
		// when rezzed you get your legs back
		TNT1 A 0 {			
			criticalDamaged = false;
		}
		MRBL LKJIH 5;
		Goto See;
	}
}

class CyborgCommandoPlasma : DMR_ZombiePlasma
{
	Default
	{		
		//SeeSound "cyborgcommando/fire";
		DMR_Projectile.ParticleColors "#fffb3c", "#ffe011", "#fbd800", "#fff12f";
	}
	States
	{
	Spawn:
		YPLS AAABBB 2 Bright {
			A_SpawnItemEx("CyborgCommandoPlasmaTrail",frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-20,-18),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_YellowPlasmaImpactFlare");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		YPLE ABCDEF 5 Bright;
		Stop;
	}
}

class CyborgCommandoPlasmaTrail : DMR_PlasmaTrail
{
	States
	{
	Spawn:
		// plasma trails need a small delay
		TNT1 A 2;
	SpawnLoop:
		YPLS AB 1 Bright {
			A_FadeOut();
			A_SetScale(Scale.X-0.05);
		}
		Loop;
	}
}


class CyborgCommandoChargedPlasma : DMR_Rocket
{
	Default
	{
		DamageFunction(random(40,120));
		Speed 40;
		+RANDOMIZE;
		+EXTREMEDEATH;
		+FORCERADIUSDMG;
		+DONTREFLECT;
		RenderStyle "Add";
		Scale 0.45;
		SeeSound "cyborgcommando/fire";
		DeathSound "weapons/bfgx";
		Decal "YellowBFGLightning";
		DMR_Projectile.ParticleColors "#fffb3c", "#ffe011", "#fbd800", "#fff12f";
	}
	States
	{
	Spawn:
		TBBL AABBCC 1 Bright  {
			A_SpawnItemEx("CyborgCommandoChargedPlasmaTrail",-30,0,0,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			for (fxLoop = 0;fxLoop<=3;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-18,-12),frandom(-6,6),frandom(-6,6),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetScale(0.4);
			A_Explode(128,128);
			A_SpawnItemEx("DMR_MediumYellowBFGImpactFlare");
			A_SpawnItemEx("MediumYellowLightningBurst");
			A_QuakeEx(4,4,4,15,0,800,"",QF_RELATIVE|QF_SCALEDOWN|QF_3D,falloff:500,rollIntensity:.75);
			for (fxLoop = 0;fxLoop<=70;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(20,36),frandom(12,14),random(0,360),frandom(0,11),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		TBLE ABCDEFGHIJKLMNO 1 Bright;
		Stop;
	}
}

class CyborgCommandoChargedPlasmaTrail : DMR_ProjectileTrail
{
	Default
	{
		Scale 0.15;
		Alpha 0.75;
	}
	States
	{
	Spawn:
		TBLE CBA 1 Bright {
			A_FadeOut(0.1);
			A_SetScale(Scale.X-0.15);
		}
		Stop;
	}
}


class CyborgCommandoBFG : DMR_Projectile
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 18;
		DamageFunction(200); // don't use the default formula, just don't get hit by it ;o
		+RANDOMIZE;
		+EXTREMEDEATH;
		+FORCERADIUSDMG;
		+NODAMAGETHRUST;
		+BRIGHT;
		+DONTREFLECT;
		RenderStyle "Add";
		Decal "YellowBFGLightning";
		DMR_Projectile.ParticleColors "#fffb3c", "#ffe011", "#fbd800", "#fff12f";
	}
	
	int deathLoop;
	
	States
	{
	Spawn:
		TNT1 A 0 {
			A_StartSound("cyborgcommando/bfg/loop",CHAN_VOICE);
		}
		TBBL AAABBBCCC 1  {
			A_SpawnItemEx("CyborgCommandoBFGBallTrail",-30,0,0,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			A_SpawnItemEx("YellowLightningBolt");
			A_Explode(8,128,0,0,64);
			A_QuakeEx(1,1,1,6,0,300,"",QF_RELATIVE|QF_SCALEDOWN|QF_3D,falloff:100,rollIntensity:.2);
			for (fxLoop = 0;fxLoop<=3;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-26,-13),frandom(-13,13),frandom(-13,13),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			deathLoop = 0;
		}
	DeathLoop:
		TNT1 A 0 A_JumpIf(deathLoop >= 12,"DeathEnd");
		TNT1 A 0 {
			A_StartSound("cyborgcommando/bfg/loop",CHAN_VOICE);
		}
		TBBL ABC 3 {
			A_Explode(24,128,0,0,64);
			A_SpawnItemEx("YellowLightningBolt");
			A_QuakeEx(3,3,3,15,0,400,"",QF_RELATIVE|QF_SCALEDOWN|QF_3D,falloff:150,rollIntensity:.4);
			
			deathLoop++;
			for (fxLoop = 0;fxLoop<=5;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(20,36),frandom(12,14),random(0,360),frandom(0,13),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		Loop;
	DeathEnd:
		TNT1 A 0 {
			A_StopSound(CHAN_VOICE);
			A_StartSound("cyborgcommando/bfg/explode", CHAN_WEAPON);
			A_SetScale(0.9);
			A_Explode(128,128,0,0,64);
			A_SpawnItemEx("DMR_YellowBFGImpactFlare");
			A_SpawnItemEx("YellowLightningBurst");
			A_QuakeEx(4,4,4,15,0,800,"",QF_RELATIVE|QF_SCALEDOWN|QF_3D,falloff:500,rollIntensity:.75);
			for (fxLoop = 0;fxLoop<=70;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(20,36),frandom(12,14),random(0,360),frandom(0,13),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
			for (fxLoop = 0;fxLoop<=50;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(36,48),frandom(24,28),random(0,360),frandom(0,13),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		TBLE ABCDEFGHIJKLMNO 2;
		Stop;
	}
}

class CyborgCommandoBFGBallTrail : DMR_ProjectileTrail
{
	Default
	{
		Scale 0.5;
		Alpha 0.75;
	}
	States
	{
	Spawn:
		TBLE GFED 1 Bright {
			A_FadeOut(0.1);
			A_SetScale(Scale.X-0.15);
		}
		Stop;
	}
}