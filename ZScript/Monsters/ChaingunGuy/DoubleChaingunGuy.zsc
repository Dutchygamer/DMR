//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Eriance)
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios	
//	ZScript:	Dutchygamer
//======================================================================

class FormerGunnerCommando : DMR_ChaingunGuy
{
	Default
	{
		Tag "Former Gunner Commando";
		Obituary "%o was eviscerated by a Former Gunner Commando.";
		Health 90;
		PainChance 150;
		DropItem "Chaingun";
		DropItem "Chaingun", 10;
	}
 	States
	{
	Spawn:
		DPOS AB 10 A_Look;
		Loop;
	See:
		DPOS AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		DPOS E 10 A_FaceTarget;
		TNT1 A 0 { attackloop = 0; }
	// start of firing like a normal chaingunner
	MissileLoopSlow:
		TNT1 A 0 A_JumpIf(attackloop >= 8,"MissileLoopFast");
		DPOS F 4 Bright Light("DMR_RifleFire") {
			A_CustomBulletAttack(22.5,0,1,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			A_PlaySound("Chaingunner/Fire", CHAN_WEAPON );
			attackloop++;
		}
		DPOS F 4 Bright Light("DMR_RifleFire") {
			A_CustomBulletAttack(22.5,0,1,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			A_PlaySound("Chaingunner/Fire", CHAN_WEAPON );
			attackloop++;
		}
		DPOS F 1 A_CPosRefire;
		Loop;
	// speed up after a couple of shots
	MissileLoopFast:
		DPOS F 2 Bright Light("DMR_RifleFire") {
			A_CustomBulletAttack(22.5,0,1,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			A_PlaySound("Chaingunner/Fire", CHAN_WEAPON );
		}
		DPOS F 2 Bright Light("DMR_RifleFire") {
			A_CustomBulletAttack(22.5,0,1,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			A_PlaySound("Chaingunner/Fire", CHAN_WEAPON );
		}
		DPOS F 1 A_CPosRefire;
		Loop;
	Pain:
		DPOS G 3;
		DPOS G 3 A_Pain;
		Goto See;
	Death:
		DPOS H 5;
		DPOS I 5 A_Scream;
		DPOS J 5 A_NoBlocking;
		DPOS KLM 5;
		DPOS N -1;
		Stop;
	XDeath:
		DPOS O 5;
		DPOS P 5 A_XScream;
		DPOS Q 5 A_NoBlocking;
		DPOS RS 5;
		DPOS T -1;
		Stop;
	Raise:
		DPOS NMLKJIH 5;
		Goto See;
	}
}