//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Scalliano
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by ItsNatureToDie and NeoWorm)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class BFGCommando : DMR_ChaingunGuy
{
	Default
	{
		Tag "BFG commando";
		obituary "%o was vaporized by a zombie's BFG9000.";
		health 150;
		painchance 100;
		Scale 1.1;
		dropitem "Cell";
		dropitem "Cell", 128;
		dropitem "BFG9000", 10;
		+DOHARMSPECIES; // zombies aren't BFG-ball-proof
	}
 	States
	{
	Spawn:
		BFGZ AB 10 A_Look;
		Loop;
	See:
		BFGZ AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 {
			A_PlaySound ("weapons/bfgf", CHAN_WEAPON);
			bNOPAIN = true;
		}
		BFGZ E 20 Light("DMR_BFGCharge_X1") A_FaceTarget;
		BFGZ F 10 Bright Light("DMR_BFGCharge_X2") A_FaceTarget;
		BFGZ F 10 Bright Light("DMR_BFGFire") {
			A_FaceTarget();
			A_SpawnProjectile("DMR_BFGBall", 32, 0);
		}
		BFGZ E 20 {
			bNOPAIN = false;
		}
		goto See;
	Pain:
		BFGZ G 3;
		BFGZ G 3 A_Pain;
		Goto See;
	Death:
		BFGZ H 5;
		BFGZ I 5 A_Scream;
		BFGZ J 5 A_NoBlocking;
		BFGZ KLM 5;
		BFGZ N -1;
		Stop;
	XDeath:
		BFGZ O 5;
		BFGZ P 5 A_XScream;
		BFGZ Q 5 A_NoBlocking;
		BFGZ RS 5;
		BFGZ T -1;
		Stop;
	Raise:
		BFGZ NMLKJIH 5;
		Goto See;
	}
}

class DMR_BFGBall : DMR_Projectile
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 25;
		DamageFunction (400); // don't use the default formula, just don't get hit by it ;o
		Projectile;
		+RANDOMIZE
		+EXTREMEDEATH;
		RenderStyle "Add";
		Alpha 0.75;
		DeathSound "weapons/bfgx";
		Obituary "$OB_MPBFG_BOOM";
		Decal "BFGLightning";
	}
	States
	{
	Spawn:
		BFS1 AAABBB 1 Bright  {
			A_SpawnItemEx("DMR_BFGBallTrail",-30,0,15,frandom(-1,0),frandom(-1,1),frandom(-1,0));
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_BFGImpactFlare");
		}
		BFE1 AB 8 Bright;
		BFE1 C 8 Bright A_BFGSpray;
		BFE1 DEF 8 Bright;
		Stop;
	}
}

class DMR_BFGBallTrail : DMR_ProjectileTrail
{
	Default
	{
		Scale 0.8;
		Alpha 0.75;
	}
	States
	{
	Spawn:
		BFE2 ABCD 2 Bright {
			A_FadeOut(0.06);
		}
		Stop;
	}
}