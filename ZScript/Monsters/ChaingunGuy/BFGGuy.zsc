//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Scalliano
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by ItsNatureToDie and NeoWorm)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class FormerBFGCommando : DMR_ChaingunGuy
{
	Default
	{
		Tag "Former BFG Commando";
		obituary "%o was vaporized by a Former BFG Commando.";
		health 250;
		painchance 50;
		Scale 1.1;
		Speed 6;
		dropitem "Cell", 256, 20;
		dropitem "Cell", 128, 20;
		dropitem "BFG9000", 10;
		+DOHARMSPECIES; // zombies aren't BFG-ball-proof
	}
 	States
	{
	Spawn:
		BFGZ AB 10 A_Look;
		Loop;
	See:
		BFGZ AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 {
			A_StartSound ("bfg/charge", CHAN_WEAPON);
			bNOPAIN = true;
		}
		BFGZ E 20 Light("DMR_BFGCharge_X1") A_FaceTarget;
		BFGZ F 10 Bright Light("DMR_BFGCharge_X2") A_FaceTarget;
		BFGZ F 10 Bright Light("DMR_BFGFire") {
			A_FaceTarget();
			A_StartSound ("bfg/fire", CHAN_WEAPON);
			A_SpawnProjectile("DMR_BFGBall",46);
		}
		BFGZ E 20 {
			bNOPAIN = false;
		}
		goto See;
	Pain:
		BFGZ G 3;
		BFGZ G 3 A_Pain;
		Goto See;
	Death:
		BFGZ H 5;
		BFGZ I 5 A_Scream;
		BFGZ J 5 A_NoBlocking;
		BFGZ KLM 5;
		BFGZ N -1;
		Stop;
	XDeath:
		BFGZ O 5;
		BFGZ P 5 A_XScream;
		BFGZ Q 5 A_NoBlocking;
		BFGZ RS 5;
		BFGZ T -1;
		Stop;
	Raise:
		BFGZ NMLKJIH 5;
		Goto See;
	}
}

class DMR_BFGBall : DMR_Projectile
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 25;
		DamageFunction(400); // don't use the default formula, just don't get hit by it ;o
		+RANDOMIZE;
		+EXTREMEDEATH;
		RenderStyle "Add";
		Alpha 0.75;
		DeathSound "weapons/bfgx";
		Obituary "$OB_MPBFG_BOOM";
		Decal "BFGLightning";		
		DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	States
	{
	Spawn:
		BFS1 AAABBB 1 Bright  {
			A_SpawnItemEx("DMR_BFGBallTrail",-30,0,0,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			A_SpawnItemEx("BFGLightningBolt");
			for (fxLoop = 0;fxLoop<=3;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-26,-13),frandom(-13,13),frandom(-13,13),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_BFGImpactFlare");
			A_SpawnItemEx("BFGLightningBurst");
			A_QuakeEx(9,9,9,30,0,1500,"",QF_RELATIVE|QF_SCALEDOWN|QF_3D,falloff:900,rollIntensity:.9);
			for (fxLoop = 0;fxLoop<=70;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(20,36),frandom(12,14),random(0,360),frandom(0,13),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
			for (fxLoop = 0;fxLoop<=50;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(36,48),frandom(24,28),random(0,360),frandom(0,13),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		BFE1 AB 8 Bright;
		BFE1 C 8 Bright A_BFGSpray;
		BFE1 DEF 8 Bright;
		Stop;
	}
}

class DMR_BFGBallTrail : DMR_ProjectileTrail
{
	Default
	{
		Scale 0.8;
		Alpha 0.75;
	}
	States
	{
	Spawn:
		BFE2 ABCD 2 Bright {
			A_FadeOut(0.06);
		}
		Stop;
	}
}