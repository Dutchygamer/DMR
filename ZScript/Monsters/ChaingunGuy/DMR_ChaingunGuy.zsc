//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios	
//	ZScript:	Dutchygamer
//======================================================================

Class DMR_ChaingunGuy : DMR_MonsterBase 
{
	Default
	{
		Tag "Former Heavy Gunner";
		Health 70;
		Gibhealth -20; // all lower tier monsters (zombies -> imp) have 20 gibhealth
		Species "DMR_Zambies";
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 170;
		SeeSound "chainguy/sight";
		PainSound "chainguy/pain";
		DeathSound "chainguy/death";
		ActiveSound "chainguy/active";
		Obituary "%o was mowed down by a Former Heavy Gunner.";
		Dropitem "Chaingun";
		+DOHARMSPECIES;
	}
	States
	{
	Spawn:
		CPOS AB 10 A_Look;
		Loop;
	See:
		CPOS AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		CPOS E 10 A_FaceTarget;
		CPOS FE 4 Bright Light("DMR_RifleFire") {
			A_CustomBulletAttack(22.5,0,1,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			A_StartSound("Chaingunner/Fire", CHAN_WEAPON );
		}
		CPOS F 1 A_CPosRefire;
		Goto Missile+1;
	Pain:
		CPOS G 3;
		CPOS G 3 A_Pain;
		Goto See;
	Death:
		CPOS H 5;
		CPOS I 5 A_Scream;
		CPOS J 5 A_NoBlocking;
		CPOS KLM 5;
		CPOS N -1;
		Stop;
	XDeath:
		CPOS O 5;
		CPOS P 5 A_XScream;
		CPOS Q 5 A_NoBlocking;
		CPOS RS 5;
		CPOS T -1;
		Stop;
	Raise:
		CPOS N 5;
		CPOS MLKJIH 5;
		Goto See;
	}
}