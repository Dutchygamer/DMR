//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Scalliano, Eriance
//	Sounds:		Eriance, Raven Software, 3d Realms
//  Sprites: 	Id Software, Eriance, 3D Realms (edits by ItsNatureToDie, neoworm, Captain Toenail, Xim, Ghastly_dragon, scalliano)
//	GLDEFs: 	Keksdose
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios	
//	ZScript:	Dutchygamer
//======================================================================

class FormerPyroTechnician : DMR_ChaingunGuy
{
	Default
	{
		Tag "Former Pyro Technician";
		Obituary "%o was flambeed by a Former Pyro Technician.";
		Health 200;
		PainChance 50;
		DamageFactor "Fire", 0.5;
		Dropitem "Cell", 256, 20;
		+DOHARMSPECIES; // zombies aren't fire-proof
		+MISSILEMORE;
	}
	
 	States
	{
	Spawn:
		PYRZ AB 10 A_Look;
		Loop;
	See:
		PYRZ AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 { attackloop = 0; }
		TNT1 A 0 A_Jump(128, "Flamethrower", "FatBalls");
	Fireballs:
		PYRZ E 10 A_FaceTarget;
	FireballLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 4,"FireballEnd");
		PYRZ F 6 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("PyroTechnicianBall",32);
			attackloop++;
		}
		PYRZ E 5 A_FaceTarget;
		TNT1 A 0 A_MonsterRefire(32,"FireballEnd");
		Loop;
	FireballEnd:
		PYRZ E 10;
		goto See;
	FatBalls:
		PYRZ E 20 A_FaceTarget;
		PYRZ F 6 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_FatShot",32);
		PYRZ E 8 A_FaceTarget;
		PYRZ F 6 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_FatShot",32,0,5.625);
		PYRZ E 8 A_FaceTarget;
		PYRZ F 6 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_FatShot",32,0,-5.625);
		PYRZ E 10;
		goto See;
	Flamethrower:
		TNT1 A 0 {
			if(target && Distance3D(target) > 300) SetStateLabel("Fireballs");
		}
		PYRZ E 10 A_FaceTarget;
		TNT1 A 0 { 
			A_PlaySound("flamethrower/fire",CHAN_WEAPON);	
		}
	FlamethrowerLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 12,"FlamethrowerEnd");
		PYRZ F 1 Bright {
			A_FaceTarget();
			attackloop++;
			A_SpawnProjectile("DMR_FlamethrowerBall",32,0,random(-3,3));
		}
		Loop;
	FlamethrowerEnd:
		PYRZ E 5;	
		Goto See;
	Pain:
		PYRZ G 3;
		PYRZ G 3 A_Pain;
		Goto See;
	Death:
		PYRZ H 5;
		PYRZ I 5 A_Scream;
		PYRZ J 5 A_NoBlocking;
		PYRZ KLM 5;
		PYRZ N -1;
		Stop;
	XDeath:
		PYRZ O 5;
		PYRZ P 5 A_XScream;
		PYRZ Q 5 A_NoBlocking;
		PYRZ RS 5;
		PYRZ T -1;
		Stop;
	Raise:
		PYRZ NMLKJIH 5;
		Goto See;
	}
}

Class PyroTechnicianBall : DMR_FatShot
{
	Default
	{
		Speed 17;
		DamageFunction(random(14,42));
		RenderStyle "Add";
		Alpha 1;
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn:
		SXAL AABBCC 2 Bright {
			A_SpawnItemEx("DMR_MediumFireballTrail",-5,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("DMR_FireballTrail",-5,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_MediumFireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		SXAL DEFGH 3 Bright;
		Stop;
	}
}