//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Scalliano, Eriance
//	Sounds:		Eriance, Raven Software, 3d Realms
//  Sprites: 	Id Software, Eriance, 3D Realms (edits by ItsNatureToDie, neoworm, Captain Toenail, Xim, Ghastly_dragon, scalliano)
//	GLDEFs: 	Keksdose
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios	
//	ZScript:	Dutchygamer
//======================================================================

class FormerPyroTechnician : DMR_ChaingunGuy
{
	Default
	{
		Tag "Former Pyro Technician";
		Obituary "%o was flambeed by a Former Pyro Technician.";
		Health 120;
		PainChance 150;
		DamageFactor "Fire", 0.5;
		DropItem "";
		+DOHARMSPECIES; //zombies aren't fire-proof
	}
	
 	States
	{
	Spawn:
		PYRZ AB 10 A_Look;
		Loop;
	See:
		PYRZ AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 A_JumpIfCloser(256, "Flamethrower");
		goto Fireballs;
	Fireballs:
		PYRZ E 10 A_FaceTarget;
		PYRZ F 10 Bright A_SpawnProjectile("PyroTechnicianBall", 32, 0, 0);
		PYRZ E 5 A_FaceTarget;
		PYRZ F 10 Bright A_SpawnProjectile("PyroTechnicianBall", 32, 0, 5.625);
		PYRZ E 5 A_FaceTarget;
		PYRZ F 10 Bright A_SpawnProjectile("PyroTechnicianBall", 32, 0, -5.625);
		PYRZ E 5;
		goto See;
	Flamethrower:
		TNT1 A 0 {
			if(target && Distance3D(target) > 300) SetStateLabel("Fireballs");
		}
		PYRZ E 10 A_FaceTarget;
		TNT1 A 0 { 
			attackloop = 0; 
			A_PlaySound ("weapons/flamethrower", CHAN_WEAPON);	
		}
	FlamethrowerLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 12,"FlamethrowerEnd");
		PYRZ F 1 Bright {
			A_FaceTarget();
			attackloop++;
			A_SpawnProjectile("DMR_FlamethrowerBall",32,0,random(-3,3));
		}
		Loop;
	FlamethrowerEnd:
		PYRZ E 5;	
		Goto See;
	Pain:
		PYRZ G 3;
		PYRZ G 3 A_Pain;
		Goto See;
	Death:
		PYRZ H 5;
		PYRZ I 5 A_Scream;
		PYRZ J 5 A_NoBlocking;
		PYRZ KLM 5;
		PYRZ N -1;
		Stop;
	XDeath:
		PYRZ O 5;
		PYRZ P 5 A_XScream;
		PYRZ Q 5 A_NoBlocking;
		PYRZ RS 5;
		PYRZ T -1;
		Stop;
	Raise:
		PYRZ NMLKJIH 5;
		Goto See;
	}
}

Class PyroTechnicianBall : DMR_FatShot
{
	Default
	{
		Speed 17;
		DamageFunction(random(14,42));
		RenderStyle "Add";
		Alpha 1;
	}
	States
	{
	Spawn:
		SXAL ABC 4 Bright {
			A_SpawnItemEx("DMR_MediumFireballTrail",-5,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("DMR_FireballTrail",-5,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1));
		}
		Loop;
	Death:
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 4;fxLoop++) {
				A_SpawnItemEx("DMR_MediumFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
				A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
			}
		}
		SXAL DEFGH 3 Bright;
		Stop;
	}
}