//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	The Dark Archon, The Ultimate Doomer
//	Sounds:		Id Software, Vader
//  Sprites: 	Id Software (edits by The Ultimate Doomer)
//	GLDEFs: 	Dreadopp
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class ToxinElemental : DMR_PainElemental
{
	Default
	{
		Tag "Toxin Elemental";
		Health 475;
		Obituary "%o was infected by an Toxin Elemental";
		DamageFactor "ToxinCloud", 0.0;
	}
	States 
	{
	Spawn:
		PLEM A 10 A_Look;
		Loop;
	See:
		PLEM AABBCC 3 A_Chase;
		Loop;
	Missile: 
		TNT1 A 0 A_Jump(128,"ToxinFire");
		Goto LostSoul;
	LostSoul:
		PLEM DE 5 A_FaceTarget;
		PLEM F 4 Bright A_FaceTarget;
		PLEM F 1 Bright A_PainAttack("ViralOne");
		Goto See;
	ToxinFire:
		PLEM DE 5 A_FaceTarget;
		PLEM F 5 Bright A_SpawnProjectile("ToxinElementalGasBall",24);
		Goto See;
	Pain:
		PLEM G 6;
		PLEM G 6 A_Pain;
		Goto See;
	Death: 
		PLEM H 8;
		PLEM I 8 A_Scream;
		PLEM JK 8 BRIGHT;
		PLEM L 8 BRIGHT A_PainDie("ViralOne");
		TNT1 A 0 {
			A_SpawnItemEx("ToxinElementalExplosion",0,0,5);
			A_PlaySound("gas/explode");
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,5,frandom(-1,1),frandom(-1,1));
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,5,frandom(-1,1),frandom(-1,1));
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-1,1),frandom(-1,1));
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-1,1),frandom(-1,1));
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-1,1),frandom(-1,1));
		}
		PLEM M 8;
		Stop;
	} 
}

class ToxinElementalGasBall : WithererGasBall
{
	Default
	{
		DamageFunction(random(12,36));
		Scale 1.0;
		Seesound "vulgar/fire";
		DeathSound "weapons/bloodx";
	}
	States
	{
	Death:
		HECF C 4 Bright {		
			A_SpawnItemEx("DMR_GasBallImpactSpawner");
			A_Explode(16,64);
			A_PlaySound("gas/explode");	
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
		}
		HECF DE 4 Bright;
		stop;
	}
}

Class ToxinElementalExplosion : DMR_ComplexFXBase
{
	Default
	{
		+NOINTERACTION;
		DMR_ComplexFXBase.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	
	States
	{
	Spawn:
		TNT1 A 1 NoDelay
		{	
			A_SpawnItemEx("DMR_LargeGasBallImpactSpawner");
			for (fxLoop = 0;fxLoop<=25;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		Stop;		
	}
}
