//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance, Blue Shadow, MagicWazard
//	Sounds:		Banjo Software, Raven Software, Rogue Entertainment, Eriance
//  Sprites: 	Eriance, Raven Software
//	GLDEFs: 	Ghastly Dragon, Keksdose, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class ThunderElemental : DMR_PainElemental
{
	Default
	{
		Tag "Thunder Elemental";
		Obituary "%o was electrocuted by a Thunder Elemental.";
		Health 650;
		PainChance 64;
		DamageFactor "lightning", 0.0;
		SeeSound "monster/ovlsit";
		PainSound "monster/ovlpai";
		DeathSound "monster/ovldth";
		ActiveSound "monster/ovlact";
	}
	States
	{
	Spawn:
		HELE A 10 A_Look;
		Loop;
	See:
		HELE AAAAAA 3 A_Chase;
		Loop;
	Missile: 
		TNT1 A 0 A_Jump(128,"Balls","ComboBalls","BigBall");
		Goto LostSoul;
	LostSoul:
		HELE BC 5 A_FaceTarget;
		HELE D 5 Bright A_FaceTarget;
		TNT1 A 0 A_PainAttack("Gorer");
		Goto See;
	Balls:
		HELE EF 5 Bright A_FaceTarget;
		HELE GGG 8 Bright A_SpawnProjectile("ThunderElementalBall",24);
		Goto See;
	ComboBalls:
		HELE EF 8 Bright A_FaceTarget;
		HELE G 10 Bright {
			A_SpawnProjectile("ThunderElementalFastBall",24,0,-7);
			A_SpawnProjectile("ThunderElementalBall",24,0,0);
			A_SpawnProjectile("ThunderElementalFastBall",24,0,7);
		}
		Goto See;
	BigBall:
		HELE EF 10 Bright A_FaceTarget;
		HELE G 12 Bright A_SpawnProjectile("OverlordMouthBall",24);
		Goto See;
	Pain:
		HELE H 6;
		HELE H 6 A_Pain;
		Goto See;
	Death:
		HELE I 8 Bright;
		HELE J 8 Bright A_Scream;
		HELE KL 8 BRIGHT;
		HELE M 8 BRIGHT A_PainDie("Gorer");
		HELE NOP 5 BRIGHT;
		HELE QRSTU 8;
		HELE V -1 A_SetFloorClip;
		Stop;
	}
}

class ThunderElementalBall : OverlordHandBall
{
	Default
	{
		Speed 10;
 		FastSpeed 20;
		DamageFunction(random(10,30));
	}
}

class ThunderElementalFastBall : OverlordHandFastBall
{
	Default
	{
		Speed 18;
 		FastSpeed 30;
		DamageFunction(random(10,30));
	}
}