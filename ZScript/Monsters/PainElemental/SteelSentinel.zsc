//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Raven Software
//  Sprites: 	Id Software, Raven Software (edits by Eriance)
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class GuardingElemental : DMR_PainElemental
{
	Default
	{
		Tag "Guarding Elemental";
		obituary "%o couldn't get past the Guarding Elemental.";
		health 600;
		DropItem "Backpack", 10;
		painchance 64;
		seesound "sentinel/see";
		painsound "sentinel/pain";
		deathsound "sentinel/death";
		activesound "sentinel/active";
		//Species "SentinelGuardian"; //TODO hier kunnen we Species2 gebruiken
		+NOBLOOD;
	}
	states
	{
	Spawn:
		SENT A 10 A_Look;
		loop;
	See:
		SENT AAAAAAA 3 A_Chase;
		loop;
	Missile:
		TNT1 A 0 A_Jump(128,"RegularMissile","GuardianShot");
		//SENT A 0 A_Jump(192,"GuardianShot","RegularMissile"); //,"ExplosiveMissile");
		//SENT A 0 A_Jump(256,"LostSoul", "LostSoulBall");
		goto LostSoul;
	LostSoul:
		SENT A 5 A_FaceTarget;
		SENT B 5 A_FaceTarget;
		SENT F 5 Bright A_FaceTarget;
		SENT F 0 Bright A_PainAttack("Guardian");
		Goto See;
	/*LostSoulBall: //alt Pain Elemental attack
		SENT A 5 A_FaceTarget;
		SENT B 5 A_FaceTarget;
		SENT F 5 A_FaceTarget;
		TNT1 A 0 Bright A_SpawnProjectile("GuardingElementalSummoner",24,0,0,0);
		Goto See;*/
	GuardianShot:
		SENT D 5 A_FaceTarget;
		SENT E 5 Bright {
			A_SpawnProjectile("GuardShot",52,12,0,1,0);
			A_SpawnProjectile("GuardShot",52,-12,0,1,0);
		}
		TNT1 A 0 A_Jump(24, "See"); //small chance to stop firing
		SENT D 1 A_SpidRefire;
		goto GuardianShot+1;
	RegularMissile:
		SENT A 8 A_FaceTarget;
		SENT C 8 Bright {
			A_SpawnProjectile("GuardingElementalBall",52,12,0,1,0);
			A_SpawnProjectile("GuardingElementalBall",52,-12,0,1,0);
		}
		SENT A 6 A_FaceTarget;
		SENT C 8 Bright {
			A_SpawnProjectile("GuardingElementalBall",52,12,0,1,0);
			A_SpawnProjectile("GuardingElementalBall",52,-12,0,1,0);
		}
		SENT A 8 A_FaceTarget;
		SENT C 8 Bright {
			A_SpawnProjectile("GuardingElementalBall",52,12,0,1,0);
			A_SpawnProjectile("GuardingElementalBall",52,-12,0,1,0);
		}
		goto See;
	Pain:
		SENT B 6;
		SENT B 6 A_Pain;
		goto See;
	Death:
		SENT G 8 BRIGHT;
		SENT H 8 BRIGHT A_Scream;
		SENT IJ 8 BRIGHT;
		SENT K 8 BRIGHT A_PainDie("Guardian");
		SENT K -1 A_SetFloorClip;
		Stop;
	}
}

class GuardingElementalBall : DMR_CacodemonBall
{
	Default
	{
		Speed 16;
		DamageFunction(random(8,24));
		Alpha 0.75;
		Seesound "weapons/boltfi";
		DeathSound "monster/disht1";
	}
	States
	{
	Spawn:
		CFX6 A 1 Bright;
		loop;
	Death:
		CFX6 BCDE 4 Bright;
		Stop;
	}
}

class GuardingElementalSummoner : SummonerBall
{
	States
	{
	Spawn:
		BOSF ABCD 2 Bright;
		loop;
	Death:
		FIRG A 3 Bright A_Explode(10, 96);
		FIRG B 3 Bright;
		TNT1 A 0 A_SpawnItem("Guardian",0,0);
		FIRG CDEFGH 3 Bright;
		Stop;
	}
}