//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Raven Software
//  Sprites: 	Id Software, Raven Software (edits by Eriance)
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class GuardingElemental : DMR_PainElemental
{
	Default
	{
		Tag "Guarding Elemental";
		obituary "%o couldn't get past the Guarding Elemental.";
		health 700;
		DropItem "Backpack", 10;
		painchance 64;
		seesound "sentinel/see";
		painsound "sentinel/pain";
		deathsound "sentinel/death";
		activesound "sentinel/active";
		//Species "SentinelGuardian"; //TODO hier kunnen we Species2 gebruiken
		+NOBLOOD;
	}
	states
	{
	Spawn:
		SENT A 10 A_Look;
		loop;
	See:
		SENT AAABBB 3 A_Chase;
		loop;
	Missile:
		TNT1 A 0 A_Jump(128,"BFG");
	Flechettes:
		SENT AA 5 A_FaceTarget;
		SENT CCC 4 BRIGHT {
			A_FaceTarget();
			A_SpawnProjectile("SmallTiberiumFlechette",52,12,0,1,0);
			A_SpawnProjectile("SmallTiberiumFlechette",52,-12,0,1,0);
		}
		Goto See;
	BFG:
		TNT1 A 0 {
			A_PlaySound ("weapons/bfgf", CHAN_WEAPON);
		}
		SENT DD 5 Light("DMR_BlueBFGCharge_X1") A_FaceTarget;
		SENT EEE 5 BRIGHT Light("DMR_BlueBFGCharge_X2") A_FaceTarget;
		SENT F 12 BRIGHT Light("DMR_BlueBFGFire") {
			A_FaceTarget();
			A_SpawnProjectile("SmallTiberiumBlueFlechette",24);
		}
		SENT DE 6;
		Goto See;
	Pain:
		SENT B 6;
		SENT B 6 A_Pain;
		goto See;
	Death:
		SENT G 8 BRIGHT;
		SENT H 8 BRIGHT A_Scream;
		SENT IJ 8 BRIGHT;
		SENT K 8 BRIGHT A_PainDie("Guardian");
		SENT K -1 A_SetFloorClip;
		Stop;
	}
}


// flechettes
class SmallTiberiumFlechette : TiberiumFlechette
{
	Default
	{
		Speed 20;
		DamageFunction(random(10,30));
		Scale 0.5;
	}
	States
	{
	Spawn:
		TSRD A 1 Bright {
			A_SpawnItemEx("TiberiumFlechetteTrail",-20,0,1); 
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0, STYLE_Add);
			//A_SetScale(1.5);
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_Explode(32,64);
		}
		TSDE ABCDEFG 3 Bright;
		Stop;
	}
}

class SmallTiberiumFlechetteTrail : TiberiumFlechetteTrail
{   
	Default
	{
		Scale 0.25;
	}
}


// BFG
class SmallTiberiumBlueFlechette : TiberiumBlueFlechette
{
	Default
	{
		Speed 30;
		DamageFunction (50); // don't use the default formula, just don't get hit by it ;o
		Scale 0.9;
	}
	States
	{
	Spawn:
		BSRD A 1 Bright {
			A_SpawnItemEx("SmallTiberiumBlueFlechetteTrail",-20,0,1);
			A_SpawnItemEx("TiberiumBlueLightningBolt",frandom(0,-20));
		}
		Loop;
	Death:
		TNT1 A 0 {
			//A_SetScale(2);
			A_Explode(96,96);
			A_SpawnItemEx("DMR_BlueBFGImpactFlare");
			A_SpawnItemEx("TiberiumBlueLightningBurst");
			A_SetRenderStyle(1.0,STYLE_Add);
		}
		ARCB JKLMN 8 Bright;
		Stop;
	}
}

class SmallTiberiumBlueFlechetteTrail : TiberiumFlechetteTrail
{
	Default
	{
		Scale 0.65;
	}
	States
	{
	Spawn:
		BSRD A 1 Bright A_FadeOut(0.06);
		Loop;
	}
}