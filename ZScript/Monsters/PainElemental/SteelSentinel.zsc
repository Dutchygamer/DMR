//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Raven Software
//  Sprites: 	Id Software, Raven Software (edits by Eriance)
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Ravager : DMR_PainElemental
{
	Default
	{
		Tag "Ravager";
		obituary "%o got shredded by a Ravager.";
		health 700;
		painchance 64;
		seesound "sentinel/see";
		painsound "sentinel/pain";
		deathsound "sentinel/death";
		activesound "sentinel/active";
		// don't damage your spawns
		DMR_MonsterBase.Species2 "TiberiumCreature";
		+NOBLOOD;
		+DONTGIB;
		DamageFactor "ToxinCloud", 0.0;
	}
	states
	{
	Spawn:
		SENT A 10 A_Look;
		loop;
	See:
		SENT AAABBB 3 A_Chase;
		loop;
	Missile:
		TNT1 A 0 A_Jump(128,"BFG");
	Flechettes:
		SENT AC 5 A_FaceTarget;
		SENT BBBBB 5 BRIGHT {
			A_FaceTarget();
			A_SpawnProjectile("SmallTiberiumFlechette",24);
		}
		Goto See;
	BFG:	
		TNT1 A 0 {
			A_StartSound ("tibshard/blue/charge", CHAN_WEAPON);
			bNOPAIN = true;
		}
		SENT DDDDDDDDDD 1 Light("DMR_BlueBFGCharge_X1") {
			A_FaceTarget();	
			//TODO Gravity?
			A_SpawnItemEx("DMR_BlueGasTrail",random(-1,1),random(-1,1),random(23,25),frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		SENT EEEEEEEEEEEEEEE 1 BRIGHT Light("DMR_BlueBFGCharge_X2") {
			A_FaceTarget();
			A_SpawnItemEx("TiberiumBlueLightningBolt",0,0,24);	
			//TODO Gravity?
			A_SpawnItemEx("DMR_BlueGasTrail",random(-1,1),random(-1,1),random(23,25),frandom(-1,1),frandom(-2,2),frandom(-1,-0.5));
		}
		SENT F 12 BRIGHT Light("DMR_BlueBFGFire") {
			A_FaceTarget();
			A_SpawnProjectile("SmallTiberiumBlueFlechette",24);
		}
		SENT D 6;
		SENT E 6 {
			bNOPAIN = false;
		}
		Goto See;
	Pain:
		SENT B 6;
		SENT B 6 A_Pain;
		goto See;
	Death:
		SENT B 6 BRIGHT;
		SENT GGGG 4 BRIGHT {
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(-12,12),0,random(-12,12));
		}
		SENT H 6 BRIGHT A_Scream;
		SENT IJ 6 BRIGHT;
		SENT K 6 BRIGHT A_PainDie("RavagerSpawn");
		SENT K -1 A_SetFloorClip;
		Stop;
	}
}

// flechettes
class SmallTiberiumFlechette : TiberiumFlechette
{
	Default
	{
		Speed 20;
		DamageFunction(random(10,30));
		Scale 0.5;
		DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	States
	{
	Spawn:
		TSRD A 1 Bright {
			A_SpawnItemEx("TiberiumFlechetteTrail",-27,0,0); 
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-30,-26),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0, STYLE_Add);
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_Explode(32,64);
			for (fxLoop = 0;fxLoop<=50;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		TSDE ABCDEFG 3 Bright;
		Stop;
	}
}

class SmallTiberiumFlechetteTrail : TiberiumFlechetteTrail
{   
	Default
	{
		Scale 0.25;
	}
}


// BFG
class SmallTiberiumBlueFlechette : TiberiumBlueFlechette
{
	Default
	{
		Speed 30;
		DamageFunction(50); // don't use the default formula, just don't get hit by it ;o
		Scale 0.9;
		DMR_Projectile.ParticleColors "#0055ab", "#0067cd", "#003e7d", "#004e9c";
	}
	States
	{
	Spawn:
		BSRD A 1 Bright {
			A_SpawnItemEx("SmallTiberiumBlueFlechetteTrail",-20,0,1);
			A_SpawnItemEx("TiberiumBlueLightningBolt",frandom(0,-20));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-30,-26),frandom(-6,6),frandom(-5,5),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_Explode(96,96);
			A_SpawnItemEx("DMR_BlueBFGImpactFlare");
			A_SpawnItemEx("TiberiumBlueLightningBurst");
			A_SetRenderStyle(1.0,STYLE_Add);
			for (fxLoop = 0;fxLoop<=50;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(18,24),frandom(12,14),random(0,360),frandom(0,9),0,frandom(-7,7),frandom(0,7),0,frandom(-7,7),0,0,-0.1,1,-1,-0.6);
			}
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(18,24),frandom(12,14),random(0,360),frandom(0,9),0,frandom(-7,7),frandom(0,7),0,frandom(-7,7),0,0,-0.1,1,-1,-0.6);
			}
		}
		ARCB JKLMN 8 Bright;
		Stop;
	}
}

class SmallTiberiumBlueFlechetteTrail : TiberiumFlechetteTrail
{
	Default
	{
		Scale 0.65;
	}
	States
	{
	Spawn:
		BSRD A 1 Bright A_FadeOut(0.06);
		Loop;
	}
}