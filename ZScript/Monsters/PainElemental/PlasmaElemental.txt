//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	The Dark Archon, The Ultimate Doomer
//	Sounds:		Id Software, Vader
//  Sprites: 	Id Software (edits by The Ultimate Doomer)
//	GLDEFs: 	Dreadopp
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Incubator : DMR_PainElemental
{
	Default
	{
		Tag "Incubator";
		Health 450;
		PainChance 100;
		Obituary "%o was infected by an incubator";
		DamageFactor "ToxinCloud", 0.0;
	}
	States 
	{
	Spawn:
		PLEM A 10 A_Look;
		Loop;
	See:
		PLEM AABBCC 3 A_Chase;
		Loop;
	Missile: 
		TNT1 A 0 A_Jump(128,"LostSoul");
		Goto ToxinFire;
	LostSoul:
		PLEM DE 5 A_FaceTarget;
		PLEM F 5 Bright A_FaceTarget;
		PLEM F 0 Bright A_PainAttack("PoisonSoul");
		Goto See;
	ToxinFire:
		PLEM DE 5 A_FaceTarget;
		PLEM F 5 Bright A_FaceTarget;
		PLEM F 0 Bright A_SpawnProjectile("IncubatorToxinBall",24,0,0,0,0);
		Goto See;
	Pain:
		PLEM G 6;
		PLEM G 6 A_Pain;
		Goto See;
	Death: 
		PLEM H 8;
		PLEM I 8 A_Scream;
		PLEM JK 8 BRIGHT;
		PLEM L 8 BRIGHT A_PainDie("PoisonSoul");
		TNT1 A 0 {
			A_PlaySound("gas/explode");
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
		}
		PLEM M 8;
		Stop;
	} 
}

class IncubatorToxinBall : CacolichToxinBall
{
	Default
	{
		DamageFunction(random(12,36));
		Scale 1.0;
		Seesound "poly/stmdr1";
		DeathSound "weapons/bloodx";
	}
	States
	{
	Death:
		TNT1 AAAA 0 {
			If(GetCvar("dmr_fancydoom")) { 
				A_SpawnItemEx("DMR_GasDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
				A_SpawnItemEx("DMR_GasBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
			}
		}
		HECF C 4 Bright A_Explode(15,56);
		TNT1 A 0 {		
			A_PlaySound("gas/explode");
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,-30,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,-30,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,-30,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
		}
		HECF DE 4 Bright;
		stop;
	}
}
