//======================================================================
//  Original credits
//
//  Note that this was originally the Hectebus; I switched names with the Corpulent cause I found it more fitting.
//======================================================================
//  DECORATE: 	AgentSpork, Carnevil, Ghastly
//	Sounds:		Id Software, Blizzard Entertainment, Vader
//  Sprites: 	 Id Software, Midway (edits by AgentSpork)
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Corpulent : DMR_Fatso
{
	Default
	{
		Tag "Corpulent";
		Health 1000;
		Scale 1.1;
		BloodColor "73 00 00";
		SeeSound "Hectebus/Sight";
		PainSound "Hectebus/Pain";
		DeathSound "Hectebus/Death";
		ActiveSound "Hectebus/Active";
		Obituary "%o was dissolved by a corpulent";
		DamageFactor "ToxinCloud", 0.0;
	}
	States
	{
	Spawn:
		HECT AAAAAAAAAABBBBBBBBBB 1 A_Look;
		Loop;
	See:
		HECT AABBCCDDEEFF 4 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 A_Jump(128,"NormalFire");
		Goto ToxinFire;
	ToxinFire:
		HECT G 0 A_Playsound("Hectebus/Attack");
		HECT G 30 A_Facetarget;
		TNT1 A 0 A_Jump(128,5); // jump to A_FatAttack2
		TNT1 A 0 A_Jump(128,7); // jump to A_FatAttack3
		// A_FatAttack1
		HECT H 10 Bright {
			A_SpawnProjectile("CorpulentToxin",18,-34,11.25);
			A_SpawnProjectile("CorpulentToxin",18,34,0);
		}
		HECT IG 5 A_FaceTarget;
		Goto See;
		// A_FatAttack2
		HECT H 10 Bright {
			A_SpawnProjectile("CorpulentToxin",18,-34,0);
			A_SpawnProjectile("CorpulentToxin",18,34,-11.25);
		}
		HECT IG 5 A_FaceTarget;
		Goto See;
		// A_FatAttack3
		HECT H 10 Bright {
			A_SpawnProjectile("CorpulentToxin",18,-34,5.625);
			A_SpawnProjectile("CorpulentToxin",18,34,-5.625);
		}
		HECT IG 5 A_FaceTarget;
		Goto See;
	NormalFire:
		HECT G 0 A_Playsound("Hectebus/attack");
		HECT G 20 A_FaceTarget;
		HECT H 10 Bright {
			A_SpawnProjectile("CorpulentBall",18,-34,5.625);
			A_SpawnProjectile("CorpulentBall",18,34,-5.625);
		}
		HECT IG 5 A_FaceTarget;
		HECT H 10 Bright {
			A_SpawnProjectile("CorpulentBall",18,-34,0);
			A_SpawnProjectile("CorpulentBall",18,34,-11.25);
		}
		HECT IG 5 A_FaceTarget;
		HECT H 10 Bright {
			A_SpawnProjectile("CorpulentBall",18,-34,11.25);
			A_SpawnProjectile("CorpulentBall",18,34,0);
		}
		HECT IG 5 A_FaceTarget;
		HECT H 10 Bright {
			A_SpawnProjectile("CorpulentBall",18,-34,5.625);
			A_SpawnProjectile("CorpulentBall",18,34,-5.625);
		}
		HECT IG 5 A_FaceTarget;
		Goto See;
	Pain:
		HECT J 3;
		HECT J 3 A_Pain;
		Goto See;
	Death:
	XDeath:
		HECT K 6;
		HECT L 6 A_Scream;
		TNT1 A 0 {
			A_PlaySound("gas/explode");
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,5,frandom(-1,1),frandom(-1,1));
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,5,frandom(-1,1),frandom(-1,1));
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,5,frandom(-1,1),frandom(-1,1));
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,5,frandom(-1,1),frandom(-1,1));
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,5,frandom(-1,1),frandom(-1,1));
		}
		HECT M 6 A_NoBlocking;
		HECT NOPQRS 6;
		HECT T -1 A_BossDeath;
		Stop;
	Raise:
		HECT RQPONMLK 5;
		goto See;
	}
}

class CorpulentBall : DMR_FatShot
{
	Default
	{
		DamageFunction(random(16,48));
		RenderStyle "Add";
		Alpha 0.8;
		DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	States
	{
	Spawn:
		HECF AABB 2 Bright {
			// use random X/Y/Z offset because projectile is so big
			A_SpawnItemEx("DMR_LargeGreenFireballTrail",frandom(-6,-8),frandom(-4,4),frandom(-4,4),frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("DMR_LargeGreenFireballTrail",frandom(-6,-8),frandom(-4,4),frandom(-4,4),frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_LargeGreenFireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		HECF CDE 4 Bright;
		Stop;
	}
}

class CorpulentToxin : DMR_FatShot
{
	Default
	{
		DamageFunction(random(16,48));
		RenderStyle "Add";
		Alpha 1;
		DamageType "ToxinCloud";
		Scale 1.2;
		DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	States
	{
	Spawn:
		HECF AABB 2 Bright {
			// use random X/Y/Z offset because projectile is so big
			A_SpawnItemEx("DMR_GasTrail",frandom(-6,-8),frandom(-4,4),frandom(-4,4),frandom(-0.5,0),frandom(-0.5,0.5),frandom(0,-0.5)); 
			A_SpawnItemEx("DMR_GasTrail",frandom(-6,-8),frandom(-4,4),frandom(-4,4),frandom(-0.5,0),frandom(-0.5,0.5),frandom(0,-0.5));
			for (fxLoop = 0;fxLoop<=3;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_LargeGasBallImpactSpawner");
			for (fxLoop = 0;fxLoop<=35;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		HECF C 4 Bright A_Explode(20,56);
		TNT1 A 0 {		
			A_PlaySound("gas/explode");
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,5,frandom(-0.5,0.5),frandom(-0.5,0.5),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,5,frandom(-0.5,0.5),frandom(-0.5,0.5),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
		}
		HECF DE 4 Bright;
		Stop;
	}
}