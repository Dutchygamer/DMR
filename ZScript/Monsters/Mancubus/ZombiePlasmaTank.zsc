//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Xim, Enjay
//	Sounds:		Id Software, 3DRealms
//  Sprites: 	Id Software, 3DRealms (edits by Xim, Enjay)
//	GLDEFs: 	Dreadopp
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class ZombiePlasmaTank : DMR_ZombieTankBase
{
	Default
	{
		Tag "Zombie Plasma Tank";
		Health 750;
		Obituary "%o was melted by a zombie in a plasma tank.";
	}
    States
    {
    Spawn:
        ZPTK AB 15 A_Look;
        Loop;
    See:
        TNT1 A 0 A_PlaySound ("ZombieTank/Run");
        ZPTK AABBCCAABBCC 4 A_Chase;
        Loop;
    Missile:
        ZPTK A 20 A_FaceTarget;
        ZPTK D 2 BRIGHT Light("DMR_PlasmaFire") A_SpawnProjectile("ZombiePlasmaTankPlasma",60,32,359,0);
        ZPTK D 2 BRIGHT Light("DMR_PlasmaFire") A_SpawnProjectile("ZombiePlasmaTankPlasma",50,32,359,0);
        ZPTK A 4 A_FaceTarget;
        ZPTK E 2 BRIGHT Light("DMR_PlasmaFire") A_SpawnProjectile("ZombiePlasmaTankPlasma",60,-29,359,0);
        ZPTK E 2 BRIGHT Light("DMR_PlasmaFire") A_SpawnProjectile("ZombiePlasmaTankPlasma",50,-29,359,0);
        ZPTK AA 5 A_FaceTarget;
		TNT1 A 0 A_MonsterRefire(10,"See");
        goto Missile+1;		
    Pain:
        ZPTK F 3 A_Pain;
        ZPTK F 3;
        Goto See;
    Death:
    XDeath:
		TNT1 A 0 A_FaceTarget; //else the debris won't line up 
        ZPTK G 4;
        ZPTK G 4 A_Scream;
		TNT1 A 0 {
			A_SpawnItemEx("ZombiePlasmaTankGun",-20,-20,47,frandom(-2.5,2.5),frandom(-2.5,-7.5),frandom(2.5,7.5));
			A_SpawnItemEx("DMR_MonsterExplosion",-20,-20,47);
		}
        ZPTK HHHHH random(1, 2);
		TNT1 A 0 {
			A_SpawnItemEx("ZombiePlasmaTankGun",-20,20,47,frandom(-2.5,2.5),frandom(2.5,7.5),frandom(2.5,7.5));
			A_SpawnItemEx("DMR_MonsterExplosion",-20,20,47);
		}		
        ZPTK I 8;
		TNT1 A 0 {
			A_SpawnItemEx("ZombieTankExplosion",0,0,0);
		}	
        ZPTK J 8;
		TNT1 A 0 {
			spawnPilot = random(1,3);
			if(spawnPilot == 3) //pilot may evacuate
			{
				A_DropItem("PlasmaZombieman"); 
			}
			else //...or not
			{
				A_SpawnItemEx("ZombieTankPilotGore",0,0,0,frandom(-8,8),frandom(-8,8),frandom(4,8)); 
			}
		}	
		TNT1 AAAAAAAAAA 0 {
			A_SpawnItemEx("DMR_LargeSmokeLongCloud",random(-2,2),random(-2,2),random(10,20),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(1,3));
			A_SpawnItemEx("DMR_LargeSmokeLongCloud",random(-2,2),random(-2,2),random(10,20),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(1,3));
			A_SpawnItemEx("DMR_XLSmokeLongCloud",random(-2,2),random(-2,2),random(10,20),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(1,3)); 
		}	
        ZPTK K 8 A_Fall;
        ZPTK LMN 8;
        ZPTK O -1 A_BossDeath;
        Stop;
    }
}

class ZombiePlasmaTankPlasma : DMR_ArachnotronPlasma
{
	Default
	{
		DamageFunction(random(8,24));
		Scale 0.9;
	}
	States
	{
	Spawn:
		APLS AABB 3 Bright  {
			A_SpawnItemEx("DMR_PlasmaTrail",frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
		}
		Loop;
	Death:
		APBX ABCDE 5 Bright;
		Stop;
	}
}

class ZombiePlasmaTankGun : DMR_DroppedShield
{
	States
	{
	Spawn:
		ZPTG ABCDEFG 3;
		Loop;
	Death:
		ZPTG G 1;
		ZPTG G -1;
		Stop;
	}
}