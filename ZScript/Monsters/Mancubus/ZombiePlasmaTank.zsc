//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Xim, Enjay
//	Sounds:		Id Software, 3DRealms
//  Sprites: 	Id Software, 3DRealms (edits by Xim, Enjay)
//	GLDEFs: 	Dreadopp
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class ZombiePlasmaTank : DMR_ZombieTankBase
{
	Default
	{
		Tag "Former Plasma Tank";
		Health 750;
		Obituary "%o was melted by a Former Plasma Tank.";
	}
    States
    {
    Spawn:
        ZPTK AB 15 A_Look;
        Loop;
    See:
        TNT1 A 0 A_PlaySound ("ZombieTank/Run");
        ZPTK AABBCCAABBCC 4 A_Chase;
        Loop;
    Missile:
        ZPTK A 20 {
			A_FaceTarget();
			A_PlaySound("heavyweapon/load");
		}
        ZPTK D 5 Bright Light("DMR_PlasmaFire") {
			A_PlaySound("weapons/plasmaf");
			A_SpawnProjectile("ZombiePlasmaTankPlasma",60,32,0);
			A_SpawnProjectile("ZombiePlasmaTankPlasma",50,32,-5);
		}
        ZPTK E 5 Bright Light("DMR_PlasmaFire") {
			A_PlaySound("weapons/plasmaf");
			A_SpawnProjectile("ZombiePlasmaTankPlasma",60,-29,0);
			A_SpawnProjectile("ZombiePlasmaTankPlasma",50,-29,5);
		}
        ZPTK A 10 A_FaceTarget;
        ZPTK D 5 Bright Light("DMR_PlasmaFire") {
			A_PlaySound("weapons/plasmaf");
			A_SpawnProjectile("ZombiePlasmaTankPlasma",60,32,0);
			A_SpawnProjectile("ZombiePlasmaTankPlasma",50,32,5);
		}
        ZPTK E 5 Bright Light("DMR_PlasmaFire") {
			A_PlaySound("weapons/plasmaf");
			A_SpawnProjectile("ZombiePlasmaTankPlasma",60,-29,0);
			A_SpawnProjectile("ZombiePlasmaTankPlasma",50,-29,-5);
		}
        ZPTK A 10 A_FaceTarget;
        Goto See;
    Pain:
        ZPTK F 3;
        ZPTK F 3 A_Pain;
        Goto See;
    Death:
    XDeath:		
		TNT1 A 0 A_FaceTarget; // else the debris won't line up 
        ZPTK G 6;
        ZPTK H 6 {
			A_Scream();
			A_SpawnItemEx("ZombiePlasmaTankGun",-20,-20,47,frandom(-2.5,2.5),frandom(-2.5,-7.5),frandom(2.5,7.5));
			A_SpawnItemEx("DMR_TankDeathExplosionSpawner");
		}	
        ZPTK I 6 {
			A_SpawnItemEx("ZombiePlasmaTankGun",-20,20,47,frandom(-2.5,2.5),frandom(2.5,7.5),frandom(2.5,7.5));
			A_SpawnItemEx("DMR_TankDeathExplosionSpawner");
		}	
        ZPTK J 6 {
			A_SpawnItemEx("DMR_TankDeathExplosionSpawner");
		}	
        ZPTK J 6 {
			A_SpawnItemEx("DMR_TankDeathExplosionSpawner");
			for (fxLoop = 0;fxLoop <= 10;fxLoop++) {
				A_SpawnItemEx("DMR_LargeSmokeLongCloud",random(-2,2),random(-2,2),random(10,20),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(1,3));
				A_SpawnItemEx("DMR_LargeSmokeLongCloud",random(-2,2),random(-2,2),random(10,20),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(1,3));
				A_SpawnItemEx("DMR_XLSmokeLongCloud",random(-2,2),random(-2,2),random(10,20),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(1,3)); 
			}
		}
        ZPTK K 6 {
			A_SpawnItemEx("DMR_MonsterDeathFinalExplosion");
			
			spawnPilot = random(1,3);
			if (spawnPilot == 3) // pilot may evacuate
			{
				A_DropItem("FormerPlasmagunner"); 
			}
			else // ...or not
			{
				A_SpawnItemEx("ZombieTankPilotGore",0,0,0,frandom(-8,8),frandom(-8,8),frandom(4,8)); 
			}
		}	
        ZPTK L 6 A_Fall;
        ZPTK MN 6;
        ZPTK O -1 A_BossDeath;
        Stop;
    }
}

class ZombiePlasmaTankPlasma : DMR_ZombiePlasma
{
	Default
	{
		DamageFunction(random(8,24));
		Scale 0.9;
		SeeSound "";
	}
	States
	{
	Spawn:
		PLSS AABB 3 Bright  {
			A_SpawnItemEx("DMR_PlasmaTrail",frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_PlasmaImpactFlare");
		}
		PLSS ABCDE 5 Bright;
		Stop;
	}
}

class ZombiePlasmaTankGun : DMR_DroppedShield
{
	States
	{
	Spawn:
		ZPTG ABCDEFG 3;
		Loop;
	Death:
		ZPTG G 1;
		ZPTG G -1;
		Stop;
	}
}