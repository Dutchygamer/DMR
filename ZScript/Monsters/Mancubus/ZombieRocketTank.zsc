//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Xim, Enjay
//	Sounds:		Id Software, 3DRealms
//  Sprites: 	Id Software, 3DRealms (edits by Xim, Enjay)
//	GLDEFs: 	Dreadopp
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class ZombieRocketTank : DMR_ZombieTankBase
{
	Default
	{
		Tag "Zombie Rocket Tank";
		Health 850;
		Obituary "%o failed to dodge rockets from a zombie in a rocket tank.";
	}
    States
    {
    Spawn:
        ZMTK AB 15 A_Look;
        Loop;
    See:
        ZMTK A 0 A_PlaySound ("ZombieTank/Run");
        ZMTK AABBCCAABBCC 4 A_Chase;
        Loop;
    Missile:
        ZMTK A 20 A_FaceTarget;
        ZMTK D 6 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",50,-29,359,0);
        ZMTK A 4 A_FaceTarget;
        ZMTK E 6 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",50,32,359,0);
        ZMTK AA 5 A_FaceTarget;
        Goto See;
    Pain:
        ZMTK F 3 A_Pain;
        ZMTK F 3;
        Goto See;
    Death:
    XDeath:
		TNT1 A 0 A_FaceTarget; //else the debris won't line up 
        ZMTK G 4;
        ZMTK G 4 A_Scream;
		TNT1 A 0 {
			A_SpawnItemEx("ZombieRocketTankGun",-20,-20,47,frandom(-2.5,2.5),frandom(-2.5,-7.5),frandom(2.5,7.5));
			A_SpawnItemEx("DMR_MonsterExplosion",-20,-20,47);
		}
        ZMTK HHHHH random(1, 2);
		TNT1 A 0 {
			A_SpawnItemEx("ZombieRocketTankGun",-20,20,47,frandom(-2.5,2.5),frandom(2.5,7.5),frandom(2.5,7.5));
			A_SpawnItemEx("DMR_MonsterExplosion",-20,20,47);
		}		
        ZMTK I 8;
		TNT1 A 0 {
			A_SpawnItemEx("ZombieTankExplosion",0,0,0);
		}	
        ZMTK J 8; 
		TNT1 A 0 {
			spawnPilot = random(1,3);
			if(spawnPilot == 3) //pilot may evacuate
			{
				A_DropItem("SuicideGuyOmniscient"); //aaaaaaaaaahhhh
			}
			else //...or not
			{
				A_SpawnItemEx("ZombieTankPilotGore",0,0,0,frandom(-8,8),frandom(-8,8),frandom(4,8)); 
			}
		}	
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 10;fxLoop++) {
				A_SpawnItemEx("DMR_LargeSmokeLongCloud",random(-2,2),random(-2,2),random(10,20),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(1,3));
				A_SpawnItemEx("DMR_LargeSmokeLongCloud",random(-2,2),random(-2,2),random(10,20),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(1,3));
				A_SpawnItemEx("DMR_XLSmokeLongCloud",random(-2,2),random(-2,2),random(10,20),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(1,3)); 
			}
		}	
        ZMTK K 8 A_Fall;
        ZMTK LMN 8;
        ZMTK O -1 A_BossDeath;
        Stop;
    }
}

class ZombieRocketTankGun : DMR_DroppedShield
{
	States
	{
	Spawn:
		ZTNM ABCDEFG 3;
		Loop;
	Death:
		ZTNM G 1;
		ZTNM G -1;
		Stop;
	}
}