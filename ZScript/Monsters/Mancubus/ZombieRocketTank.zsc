//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Xim, Enjay
//	Sounds:		Id Software, 3DRealms
//  Sprites: 	Id Software, 3DRealms (edits by Xim, Enjay)
//	GLDEFs: 	Dreadopp
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class ZombieRocketTank : DMR_ZombieTankBase
{
	Default
	{
		Tag "Former Rocket Tank";
		Health 850;
		Obituary "%o failed to dodge rockets from a Former Rocket Tank.";
	}
    States
    {
    Spawn:
        ZMTK AB 15 A_Look;
        Loop;
    See:
        ZMTK A 0 A_PlaySound ("ZombieTank/Run");
        ZMTK AABBCCAABBCC 4 A_Chase;
        Loop;
    Missile:
        ZMTK A 20 {
			A_FaceTarget();
			A_PlaySound("heavyweapon/load");
		}
        ZMTK D 5 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",54,-29,0);
        ZMTK E 5 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",54,32,7);
        ZMTK A 10 A_FaceTarget;
        ZMTK D 5 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",54,-29,-7);
        ZMTK E 5 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",54,32,0);
        ZMTK A 10 A_FaceTarget;
        Goto See;
    Pain:
        ZMTK F 3;
        ZMTK F 3 A_Pain;
        Goto See;
    Death:
    XDeath:	
		TNT1 A 0 A_FaceTarget; // else the debris won't line up 
        ZMTK G 6;
        ZMTK H 6 {
			A_Scream();
			A_SpawnItemEx("ZombiePlasmaTankGun",-20,-20,47,frandom(-2.5,2.5),frandom(-2.5,-7.5),frandom(2.5,7.5));
			A_SpawnItemEx("DMR_TankDeathExplosionSpawner");
		}	
        ZMTK I 6 {
			A_SpawnItemEx("ZombiePlasmaTankGun",-20,20,47,frandom(-2.5,2.5),frandom(2.5,7.5),frandom(2.5,7.5));
			A_SpawnItemEx("DMR_TankDeathExplosionSpawner");
		}	
        ZMTK J 6 {
			A_SpawnItemEx("DMR_TankDeathExplosionSpawner");
		}	
        ZMTK J 6 {
			A_SpawnItemEx("DMR_TankDeathExplosionSpawner");
			for (fxLoop = 0;fxLoop <= 10;fxLoop++) {
				A_SpawnItemEx("DMR_LargeSmokeLongCloud",random(-2,2),random(-2,2),random(10,20),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(1,3));
				A_SpawnItemEx("DMR_LargeSmokeLongCloud",random(-2,2),random(-2,2),random(10,20),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(1,3));
				A_SpawnItemEx("DMR_XLSmokeLongCloud",random(-2,2),random(-2,2),random(10,20),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(1,3)); 
			}
		}
        ZMTK K 6 {
			A_SpawnItemEx("DMR_MonsterDeathFinalExplosion");
			
			spawnPilot = random(1,3);
			if (spawnPilot == 3) // pilot may evacuate
			{
				A_DropItem("SuicideGuyOmniscient"); 
			}
			else // ...or not
			{
				A_SpawnItemEx("ZombieTankPilotGore",0,0,0,frandom(-8,8),frandom(-8,8),frandom(4,8)); 
			}
		}	
        ZMTK L 6 A_Fall;
        ZMTK MN 6;
        ZMTK O -1 A_BossDeath;
        Stop;
    }
}

class ZombieRocketTankGun : DMR_DroppedShield
{
	States
	{
	Spawn:
		ZTNM ABCDEFG 3;
		Loop;
	Death:
		ZTNM G 1;
		ZTNM G -1;
		Stop;
	}
}