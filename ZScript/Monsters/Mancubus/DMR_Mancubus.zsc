//======================================================================
//  Original credits
//======================================================================
//  XDeath Sprites: 	Dutchygamer
//======================================================================

Class DMR_Fatso: Fatso 
{
	Default
	{
		Tag "Mancubus";
		Species "DMR_Fatso";
		+DONTHARMSPECIES;
		+NOINFIGHTSPECIES;
		Gibhealth -40; // all higher tier monsters (baron -> archvile) have 40 gibhealth
	}
	States
	{	
	Missile:
		FATT G 20 A_FatRaise;
		// A_FatAttack1 will shoot one shot straight ahead, and the second 11.25 degrees to its left.
		FATT H 10 Bright {
			A_SpawnProjectile("FatShot",18,-34,11.25);
			A_SpawnProjectile("FatShot",18,34,0);
		}
		FATT IG 5 A_FaceTarget;
		// A_FatAttack2 will shoot one shot straight ahead, and the second 11.25 degrees to its right.
		FATT H 10 Bright {
			A_SpawnProjectile("FatShot",18,-34,0);
			A_SpawnProjectile("FatShot",18,34,-11.25);
		}
		FATT IG 5 A_FaceTarget;
		// A_FatAttack3 will shoot two shots, one 5.625 degrees to the left, and the other 5.625 degrees to the right. 
		FATT H 10 Bright {
			A_SpawnProjectile("FatShot",18,-34,5.625);
			A_SpawnProjectile("FatShot",18,34,-5.625);
		}
		FATT IG 5 A_FaceTarget;
		Goto See;
	XDeath:
		FATX A 5;
		FATX B 5 A_XScream;
		FATX C 5 A_NoBlocking;
		FATX DEF 5;
		FATX G -1;
		stop;
	}
}

class DMR_FatShot : FatShot replaces FatShot
{
	Default
	{
		DamageFunction(random(16,48));
	}
	States
	{
	Spawn:
		MANF AB 3 Bright {
			A_SpawnItemEx("DMR_LargeFireballTrail",-7,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("DMR_MediumFireballTrail",-7,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1));
		}
		Loop;
	Death:
		TNT1 AAAAA 0 {
			A_SpawnItemEx("DMR_LargeFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
			A_SpawnItemEx("DMR_LargeFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		MISL B 8 Bright;
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}