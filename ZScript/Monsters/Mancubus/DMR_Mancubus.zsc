//======================================================================
//  Additional credits
//======================================================================
//  XDeath Sprites: 	Dutchygamer
//	ZScript:			Dutchygamer
//======================================================================

Class DMR_Fatso: DMR_MonsterBase 
{
	Default
	{
		Tag "Mancubus";
		Health 600;
		Gibhealth -40; // all higher tier monsters (baron -> archvile) have 40 gibhealth
		Species "DMR_Fatso";
		Radius 48;
		Height 64;
		Mass 1000;
		Speed 8;
		PainChance 80;
		SeeSound "fatso/sight";
		PainSound "fatso/pain";
		DeathSound "fatso/death";
		ActiveSound "fatso/active";
		Obituary "$OB_FATSO";
		+BOSSDEATH;
		+DONTHARMSPECIES;
		+NOINFIGHTSPECIES;
	}
	States
	{
	Spawn:
		FATT AB 15 A_Look;
		Loop;
	See:
		FATT AABBCCDDEEFF 4 A_Chase;
		Loop;
	Missile:
		FATT G 20 A_FatRaise;
		// A_FatAttack1 will shoot one shot straight ahead, and the second 11.25 degrees to its left.
		FATT H 10 Bright {
			A_FaceTarget();
			A_SpawnProjectile("DMR_FatShot",18,-34,11.25);
			A_SpawnProjectile("DMR_FatShot",18,34,0);
		}
		FATT IG 5 A_FaceTarget;
		// A_FatAttack2 will shoot one shot straight ahead, and the second 11.25 degrees to its right.
		FATT H 10 Bright {
			A_FaceTarget();
			A_SpawnProjectile("DMR_FatShot",18,-34,0);
			A_SpawnProjectile("DMR_FatShot",18,34,-11.25);
		}
		FATT IG 5 A_FaceTarget;
		// A_FatAttack3 will shoot two shots, one 5.625 degrees to the left, and the other 5.625 degrees to the right. 
		FATT H 10 Bright {
			A_FaceTarget();
			A_SpawnProjectile("DMR_FatShot",18,-34,5.625);
			A_SpawnProjectile("DMR_FatShot",18,34,-5.625);
		}
		FATT IG 5 A_FaceTarget;
		Goto See;
	Pain:
		FATT J 3;
		FATT J 3 A_Pain;
		Goto See;
	Death:
		FATT K 6;
		FATT L 6 A_Scream;
		FATT M 6 A_NoBlocking;
		FATT NOPQRS 6;
		FATT T -1 A_BossDeath;
		Stop;
	XDeath:
		FATX A 5;
		FATX B 5 A_XScream;
		FATX C 5 A_NoBlocking;
		FATX DEF 5;
		FATX G -1 A_BossDeath;
		stop;
	 Raise:
		FATT R 5;
		FATT QPONMLK 5;
		Goto See;
	}
}

class DMR_FatShot : DMR_Projectile
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 20;
		DamageFunction(random(16,48));
		+RANDOMIZE
		RenderStyle "Add";
		Alpha 1;
		SeeSound "fatso/attack";
		DeathSound "fatso/shotx"; // "fatso/ballx";
		Decal "MancubusScorch";
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn:
		MANF AABB 2 Bright {
			// use random X/Y/Z offset because projectile is so big
			A_SpawnItemEx("DMR_LargeFireballTrail",frandom(-6,-8),frandom(-4,4),frandom(-4,4),frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("DMR_LargeFireballTrail",frandom(-6,-8),frandom(-4,4),frandom(-4,4),frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_LargeFireballImpactSpawner");
			A_SetScale(1.5);
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		XPL1 ABCD 1 Light("DMR_MacubusBall_X1");
		XPL1 EFGHI 1 Light("DMR_MacubusBall_X2");
		XPL1 JKLM 1 Light("DMR_MacubusBall_X3") A_FadeOut(0.15);
		Stop;
	}
}