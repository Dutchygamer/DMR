//======================================================================
//  Original credits
//
//Note that this was originally the Corpulent; I switched names with the Hectebus cause I found it more fitting.
//======================================================================
//  DECORATE: 	???
//	Sounds:		???
//  Sprites: 	Id Software (edits by ???)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Hectebus : DMR_Fatso
{
	Default
	{
		Tag "Hectebus";
		Health 750;
		Obituary "%o was burned alive by a hectebus";
		SeeSound "corpulent/see";
		PainSound "corpulent/pain";
		DeathSound "corpulent/death";
	}
	states
	{
	Spawn:
		FAT2 AB 15 A_Look;
		loop;
	See:
		FAT2 AABBCCDDEEFF 4 A_Chase;
		loop;
	Missile:
		TNT1 A 0 A_Jump(128,"SpreadShot");
		Goto RapidFire;
	RapidFire:
		TNT1 A 0 A_Playsound("corpulent/attack");
		FAT2 G 20 A_FaceTarget;
		FAT2 H 6 Bright {
			A_PlaySound("deathknight/missile", CHAN_WEAPON);
			A_SpawnProjectile("HectebusBall",18,-34,15);
			A_SpawnProjectile("HectebusBall",18,34,0);
		}
		FAT2 IG 5 A_FaceTarget;
		FAT2 H 6 Bright {
			A_PlaySound("deathknight/missile", CHAN_WEAPON);
			A_SpawnProjectile("HectebusBall",18,-34,0);
			A_SpawnProjectile("HectebusBall",18,34,-15);
		}
		FAT2 IG 5 A_FaceTarget;
		FAT2 H 6 Bright {
			A_PlaySound("deathknight/missile", CHAN_WEAPON);
			A_SpawnProjectile("HectebusBall",18,-34,7.5);
			A_SpawnProjectile("HectebusBall",18,34,-7.5);
		}
		FAT2 IG 5 A_FaceTarget;
		Goto See;
	SpreadShot:
		TNT1 A 0 A_Playsound("corpulent/attack");
		FAT2 G 20 A_FaceTarget;
		FAT2 H 10 Bright {
			A_PlaySound("deathknight/missile", CHAN_WEAPON);
			A_SpawnProjectile("HectebusBall",18,-34,8);
			A_SpawnProjectile("HectebusBall",18,34,0);
			A_SpawnProjectile("HectebusBall",18,-34,0);
			A_SpawnProjectile("HectebusBall",18,34,-8);
		}
		FAT2 IG 5 A_FaceTarget;
		Goto See;
	Pain:
		FAT2 J 3;
		FAT2 J 3 A_Pain;
		goto See;
	Death:
	XDeath:
		FAT2 K 6;
		FAT2 L 6 A_Scream;
		FAT2 M 6 A_NoBlocking;
		FAT2 NOPQRS 6;
		FAT2 T -1 A_BossDeath;
		stop;
	Raise:
		FAT2 RQPONMLK 5;
		goto See;
	}
}

class HectebusBall : DMR_FatShot
{
	Default
	{
		DamageFunction(random(16,48));
		Speed 22;
		Seesound "";
		DeathSound "weapons/boom1";
		Scale 1.2;
	}
	States
	{
	Spawn:
		FHED AABBCCDD 2 Bright {
			// use random X/Y/Z offset because projectile is so big
			A_SpawnItemEx("DMR_LargeFireballTrail",frandom(-6,-8),frandom(-4,4),frandom(-4,4),frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("DMR_LargeFireballTrail",frandom(-6,-8),frandom(-4,4),frandom(-4,4),frandom(-1,0),frandom(-1,1),frandom(0,1));
		}
		loop;
	Death:
		RIP1 DEFGH 3 Bright;
		stop;
	}
}