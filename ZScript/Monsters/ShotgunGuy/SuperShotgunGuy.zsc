//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Ghastly_Dragon
//	Sounds:		Id Software, Logan MTM
//  Sprites: 	Id Software, (edits by Logan MTM, Ghastly_Dragon, and Xim)
//	GLDEFs: 	Ghastly_dragon
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class FormerStaffSargeant : DMR_ShotgunGuy
{
	Default
	{
		Tag "Former Staff Sargeant";
		Health 60;
		Translation "112:127=16:47"; // recolour green parts to red
		OBITUARY "%o was blown open by a Former Staff Sargeant.";
		Scale 0.9;
		DropItem "Shell", 256, 4;
		DropItem "Shell", 64, 4;
		DropItem "SuperShotgun", 10;
	}
	int shotsFired;
	
 	States
	{
	Spawn:
		GPOS AB 10 A_Look;
		Loop;
	See:
		TNT1 A 0 {
			if (shotsFired >= 2) { SetStateLabel("Reload"); }
		}	
		GPOS AABBCCDD 4 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 { // if we are not fully loaded, use single shot
			if (shotsFired != 0) { SetStateLabel("SingleShot"); }
		}
		TNT1 A 0 A_JumpIfCloser(350, "SuperShot");
	SingleShot:
		GPOS E 10 A_FaceTarget;
		GPOS F 8 Bright Light("DMR_RifleFire") {
			A_StartSound("quadshotgun/fire/single", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,7.1,5,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			shotsFired++;
		}
		GPOS E 10;
		Goto See;
	SuperShot:
		GPOS EE 10 A_FaceTarget;
		GPOS F 10 Bright Light("DMR_RifleFire") {
			A_StartSound("weapons/sshotf", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,7.1,10,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			shotsFired += 2;
		}
		GPOS E 10;
		Goto See;
	Reload:
		GPOS E 7 A_StartSound("weapons/sshoto");
		GPOS E 7;
		GPOS A 10 A_StartSound("weapons/sshotl");
		GPOS A 8;
		GPOS E 12 {
			A_StartSound("weapons/sshotc");
			shotsFired = 0; // all 2 shots loaded again
		}
		Goto See;
	Pain:
		GPOS G 3;
		GPOS G 3 A_Pain;
        Goto See;
	Death:
		GPOS H 7;
		GPOS I 7 A_Scream;
		GPOS J 7 A_NoBlocking;
		GPOS KLM 7;
		GPOS N -1;
        Stop;
	XDeath:
		GPOS O 5;
		GPOS P 5 A_XScream;
		GPOS Q 5 A_NoBlocking;
		GPOS RS 5;
		GPOS T -1;
        Stop;
	Raise:
		GPOS MLKJIH 5;
        Goto See;
	}
}