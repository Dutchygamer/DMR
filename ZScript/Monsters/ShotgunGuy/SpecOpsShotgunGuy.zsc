//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Graf Zahl
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Xim, Vegeta, Ghastly_dragon, MagicWazard)
//	GLDEFs: 	Ghastly_dragon, MagicWazard (brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios, EA Games
//	ZScript:	Dutchygamer
//======================================================================

class FormerSpecOpsSargeant : DMR_ShotgunGuy
{
	Default
	{
		Tag "Former SpecOps Sargeant";
		OBITUARY "%o was slugged by a Former SpecOps Sargeant.";
		Health 95;
		Speed 12;
		PainChance 80;
		DamageFactor "ZombieGrenade", 0.5; // take less damage from own grenades
		+MISSILEMORE
		+DOHARMSPECIES; // zombies aren't grenade-proof
		DropItem "Shell", 256, 4;
		DropItem "Shell", 64, 4;
		DropItem "RocketAmmo", 64, 1;
	}
	bool isStealthed;
	
 	States
	{
	Spawn:
		ZSP1 AB 10 A_Look;
		Loop;
	See:
		ZSP1 AABBCCDD 4 A_Chase;
		TNT1 A 0 A_Jump(64,"GoStealthed");
		Loop;
    StealthedSee:
        ZSP1 AABBCCDDAABBCCDD 1 {
			A_Chase(null,null);
			// chance to dash left or right	
			if(random(1,4) == 4) A_DashSideways(8);	
			A_SpawnItemEx("SpecOpsAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION,128);
		}
    StealthedSeeLoop:
        ZSP1 AABBCCDD 1 {
			A_Chase();			
			A_SpawnItemEx("SpecOpsAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION,128);
		}
        Loop;
	Missile:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
			attackloop = 0;
		}
		TNT1 A 0 A_JumpIfCloser(512, "MissileClose"); // only throw grenades when we are close
		Goto Shotgun;
	MissileClose:
		TNT1 A 0 A_Jump(64, "Grenades");
		ZSP1 E 10 A_FaceTarget;		
		Goto Shotgun;
	Shotgun:
		TNT1 A 0 A_JumpIf(attackloop >= 3,"ShotgunEnd");
		ZSP1 F 8 BRIGHT Light("DMR_RifleFire") {
			A_StartSound("shotguy/attack", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,0,3,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			attackloop++;
		}
		ZSP1 E 8 A_FaceTarget;
		TNT1 A 0 A_MonsterRefire(32,"ShotgunEnd");
		//TNT1 A 0 Bright A_Jump(96, "See");
		//ZSP1 F 4 BRIGHT Light("DMR_RifleFire") {
		//	A_StartSound("shotguy/attack", CHAN_WEAPON);
		//	A_CustomBulletAttack(22.5,0,3,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
		//}
		//ZSP1 E 8;
		Loop;
	ShotgunEnd:
		ZSP1 E 8;
		Goto See;
	Grenades:
		TNT1 A 0 A_JumpIfCloser(96, "Shotgun"); // don't grenade yourself when standing in front of the target
		TNT1 A 0 {
			A_StartSound("heavyweapon/load", CHAN_WEAPON);
		}
		ZSP1 EEE 8 A_FaceTarget;
		ZSP1 F 5 {
			A_LobProjectile("CyborgAcolyteGrenade",32);
		}
		ZSP1 E 4;
		Goto See;
	GoStealthed:
		TNT1 A 0 {
			if (isStealthed) {
				return ResolveState("See");
			}
			return ResolveState(null);
		}
		ZSP1 E 5 {
			isStealthed = true;
			A_StartSound("Infiltrator/stealth");
		}
		ZSP1 EEEEE 1 { // to 0.0 alpha in total				
			A_SetRenderStyle(Alpha-0.2,STYLE_Translucent);
		}
		TNT1 A 0 A_Chase(null,null, CHF_FASTCHASE);
        Goto StealthedSee;
	Pain:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
		ZSP1 G 3;
		ZSP1 G 3 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
		ZSP1 H 5;
		ZSP1 I 5 A_Scream;
		ZSP1 J 5 A_NoBlocking;
		ZSP1 K 5;
		ZSP1 L -1;
		Stop;
	XDeath:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
		ZSP1 H 5;
		ZSP1 I 5 A_XScream;
		ZSP1 J 5 A_NoBlocking;
		ZSP1 K 5;
		ZSP1 L -1;
		Stop;
	Raise:
		ZSP1 KJIH 5;
		Goto See;
	}
}