//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Scalliano, Popsoap
//	Sounds:		Eriance
//  Sprites: 	Id Software, Ghastly_Dragon, Eriance, Xim (edits by Spectator)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class FormerAssaultSargeant : DMR_ShotgunGuy
{
	Default
	{
		Tag "Former Assault Sargeant";
		Health 50;
		PainChance 155;
		Obituary "%o was shredded by a Former Assault Sargeant.";
		DropItem "Shell", 256, 4;
		DropItem "Shell", 128, 4;
	}
	int shotsFired;
	
 	States
	{
	Spawn:
		ASGZ AB 10 A_Look;
		Loop;
	See:
		TNT1 A 0 {
			if (shotsFired >= 6) { SetStateLabel("Reload"); }
		}	
        ASGZ AABBCCDD 4 A_Chase;
		Loop;
	Missile:
		ASGZ E 10 A_FaceTarget;
        ASGZ F 10 Bright Light("DMR_RifleFire") {
			A_StartSound("autoshotgun/fire", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,4,3,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			shotsFired++; // expend a single shot
		}
        ASGZ E 5;
		TNT1 A 0 { // reload when we have expended 6 shots
			if (shotsFired >= 6) { SetStateLabel("Reload"); }
		}	
        ASGZ F 10 Bright Light("DMR_RifleFire") {
			A_StartSound("autoshotgun/fire", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,0,3,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			shotsFired++; // expend a single shot
		}
        ASGZ E 5;
		TNT1 A 0 { // reload when we have expended 6 shots
			if (shotsFired >= 6) { SetStateLabel("Reload"); }
		}	
        ASGZ F 10 Bright Light("DMR_RifleFire") {
			A_StartSound("autoshotgun/fire", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,0,3,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			shotsFired++; // expend a single shot
		}
        ASGZ E 10;
		Goto See;
	Reload:
		ASGZ E 14 A_StartSound("autoshotgun/reload");
		ASGZ A 15;
		TNT1 A 0 {
			shotsFired = 0; // all 6 shots loaded again
		}
		ASGZ E 7 A_StartSound("autoshotgun/reload");
		Goto See;
	Pain:
        ASGZ G 3;
        ASGZ G 3 A_Pain;
        Goto See;
	Death:
        ASGZ H 5;
        ASGZ I 5 A_Scream;
        ASGZ J 5 A_Fall;
        ASGZ KLM 5;
        ASGZ N -1;
        Stop;
	XDeath:
        ASGZ O 5;
        ASGZ P 5 A_XScream;
        ASGZ Q 5 A_Fall;
        ASGZ RSTUV 5;
        ASGZ W -1;
        Stop;
	Raise:
        ASGZ MLKJIH 5;
        Goto See;
	}
}