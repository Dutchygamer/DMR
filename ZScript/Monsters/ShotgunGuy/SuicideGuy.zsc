//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Tormentor667
//	Sounds:		Id Software, Croteam
//  Sprites: 	Id Software, Mark Quinn, Anthony Cole (edits by Tormentor667)
//	GLDEFs: 	Dreadopp, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

// own instance for obvious reasons
class SuicideGuyExplosive : DMR_MonsterBase 
{
	Default
	{
		Tag "Former Pilot";
		Health 60;
		Obituary "%o was booped by a Former Pilot.";
		Radius 20;
		Height 56;
		Mass 100;
		Speed 18;
		Painchance 20;
		Seesound "kamikaze/sight";
		Attacksound "shotguy/death";
		Painsound "shotguy/pain";
		Deathsound "shotguy/death";
		+DONTHARMCLASS;
		+AMBUSH; // for maximum annoyance, and less aaaaaaaaahh when they can't see you but did hear you
		Species "DMR_Aaaaaaaaaaaah";
		MaxTargetRange 400; // don't charge at max range
	}
 	States
	{
	Spawn:
		BMBE AB 10 A_Look;
		Loop;
	See:
		BMBE AABBCCDD 2 A_Chase;
		TNT1 A 0 A_StartSound("kamikaze/scream");
		Loop;
	Missile:
		BMBE A 10 A_FaceTarget;
		TNT1 A 0 {
			A_FaceTarget();
			A_Recoil(-3);
		}
	ChargeLoop:
		BMBE AAABBBCCCDDD 1 {
			if (target && Distance3D(target) < 80 || vel.x == 0 && vel.y == 0) { SetStateLabel("See"); }
			A_FaceTarget(1.5,180);
			A_Recoil(-3);
		}
		TNT1 A 0 A_StartSound("kamikaze/scream");
		Loop;
	Melee:
		BMBE A 0 A_Die;
		stop;
	Pain:
		BMBE A 3;
		BMBE A 3 A_Pain;
		Goto See;
	Death:
	XDeath:
		BMBE M 5 bright {
			A_Explode (50, 128, XF_NOTMISSILE);
			A_StartSound ("grenade/explode");	
			A_SpawnItemEx("DMR_MediumFireballImpactSpawner");
			A_QuakeEx(3,3,3,15,0,600,"",QF_RELATIVE|QF_SCALEDOWN|QF_3D,falloff:400,rollIntensity:.75);
		}
		BMBE N 5 bright A_XScream;
		BMBE O 5 A_NoBlocking;
		BMBE PQRST 5;
		BMBE U -1;
		Stop;
	Raise:
		BMBE TSRQPONM 5;
		Goto See;
	}
}

// less explosive damage, spawns gas clouds
class SuicideGuyGas : SuicideGuyExplosive
{
	states
	{
	Death:
	XDeath:
		BMBE M 0 A_Explode (30, 96, XF_NOTMISSILE);
		BMBE M 5 bright {
			A_StartSound("gas/explode");
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,5,frandom(-0.5,0.5),frandom(-0.5,0.5));
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-0.5,0.5),frandom(-0.5,0.5));
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-0.5,0.5),frandom(-0.5,0.5));
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-0.5,0.5),frandom(-0.5,0.5));
		}
		BMBE N 5 bright A_XScream;
		BMBE O 5 A_NoBlocking;
		BMBE PQRST 5;
		BMBE U -1;
		stop;
	}
}

// spawned from RocketGuy or ZombieRocketTank
// this way it will always immediately start attacking when spawned instead of having to both see and hear the player
class SuicideGuyOmniscient : SuicideGuyExplosive
{	
	Default
	{
		-AMBUSH;
		+LOOKALLAROUND;
	}
}