//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	scalliano, GreyGhost, Ghastly (ZScript conversion)
//	Sounds:		Eriance (?)
//  Sprites: 	Id Software, Xim, Eriance (edits by scalliano)
//	GLDEFs: 	Tormentor667
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios	
//	ZScript:	Dutchygamer
//======================================================================

class FormerPyroTechnician : DMR_ShotgunGuy
{
	Default
	{
		Tag "Former Pyro Technician";
		Obituary "%o was flambeed by a Former Pyro Technician.";
		Health 150;
		PainChance 100;
		DamageFactor "Fire", 0.5;
		Dropitem "Cell", 256, 20;
		+DOHARMSPECIES; // zombies aren't fire-proof
		+MISSILEMORE;
	}
	
 	States
	{
	Spawn:
		FLMZ AB 10 A_Look;
		Loop;
	See:
		FLMZ AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 { attackloop = 0; }
		TNT1 A 0 A_Jump(128, "Flamethrower");
	Fireballs:
		FLMZ E 10 A_FaceTarget;
	FireballLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 4,"FireballEnd");
		FLMZ F 6 Bright Light("DMR_RifleFire") {
			A_LobProjectile("PyroTechnicianBall",32,0,frandom(-3,3));
			attackloop++;
		}
		FLMZ E 5 A_FaceTarget;
		TNT1 A 0 A_MonsterRefire(32,"FireballEnd");
		Loop;
	FireballEnd:
		FLMZ E 10;
		goto See;
	Flamethrower:
		TNT1 A 0 {
			if(target && Distance3D(target) > 300) SetStateLabel("Fireballs");
		}
		FLMZ E 10 A_FaceTarget;
		TNT1 A 0 { 
			A_StartSound("flamethrower/fire",CHAN_WEAPON);	
		}
	FlamethrowerLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 12,"FlamethrowerEnd");
		FLMZ F 1 Bright Light("DMR_RifleFire") {
			A_FaceTarget();
			attackloop++;
			A_SpawnProjectile("DMR_FlamethrowerBall",32,0,random(-3,3),CMF_OFFSETPITCH,frandom(-1,1));
		}
		Loop;
	FlamethrowerEnd:
		FLMZ E 5;	
		Goto See;
	Pain:
		FLMZ G 3;
		FLMZ G 3 A_Pain;
		Goto See;
	Death:
		FLMZ H 5;
		FLMZ I 5 A_Scream;
		FLMZ J 5 A_NoBlocking;
		FLMZ K 5;
		FLMZ L -1;
		Stop;
	XDeath:
		FLMZ O 5;
		FLMZ P 5 A_XScream;
		FLMZ Q 5 A_NoBlocking;
		FLMZ RSTUV 5;
		FLMZ W -1;
		Stop;
	Raise:
		FLMZ KJIH 5;
		Goto See;
	}
}

Class PyroTechnicianBall : DMR_FatShot
{
	Default
	{
		Speed 30;
		DamageFunction(random(12,36));
		RenderStyle "Add";
		Alpha 1;
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
		Gravity 0.5;
		-NOGRAVITY;
	}
	States
	{
	Spawn:
		SXAL AABBCC 2 Bright {
			A_SpawnItemEx("DMR_MediumFireballTrail",-5,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("DMR_FireballTrail",-5,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_MediumFireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
			A_SpawnItemEx("DMR_BurningFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_ORIGINATOR|SXF_TRANSFERPOINTERS);
		}
		SXAL DEFGH 3 Bright;
		Stop;
	}
}