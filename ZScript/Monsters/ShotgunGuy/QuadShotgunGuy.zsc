//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Ghastly_Dragon
//	Sounds:		Id Software, Logan MTM
//  Sprites: 	Id Software, (edits by Logan MTM, Ghastly_Dragon, and Xim)
//	GLDEFs: 	Ghastly_dragon
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class FormerQuartermaster : DMR_ShotgunGuy
{
	Default
	{
		Tag "Former Quartermaster";
		Health 120;
		PainChance 100;
		+MISSILEMORE
		Obituary "%o was pink-misted by a Former Quartermaster.";
		DropItem "Shell", 256, 4;
		DropItem "Shell", 128, 4;
	}
	int shotsFired;
	
 	States
	{
	Spawn:
		QSZM AB 10 A_Look;
		Loop;
	See:
		TNT1 A 0 {
			if (shotsFired >= 4) { SetStateLabel("Reload"); }
		}	
		QSZM AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 { // if we are not fully loaded, use either single or super shot
			if (shotsFired != 0) { SetStateLabel("SingleOrSuperShot"); }
		}
		TNT1 A 0 A_JumpIfCloser(350, "CloseShot");
		Goto SingleShot;
	SingleOrSuperShot:
		TNT1 A 0 { // if we only have one shot left, use single shot
			if (shotsFired >= 3) { SetStateLabel("SingleShot"); }
		}
		TNT1 A 0 A_JumpIfCloser(350, "SuperShot");
		Goto SingleShot;
	CloseShot:
		GPOS E 0 A_Jump(196, "QuadShot");
		Goto SuperShot;
	SingleShot:
		TNT1 A 0 { // reload when we have expended 4 shots
			if (shotsFired >= 4) { SetStateLabel("Reload"); }
		}
		QSZM E 10 A_FaceTarget;
		QSZM F 8 Bright Light("DMR_RifleFire") {
			A_StartSound("quadshotgun/fire/single", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,7.1,5,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			shotsFired++;
		}
		TNT1 A 0 A_JumpIfTargetInLOS("Missile",0,JLOSF_DEADNOJUMP);
		QSZM E 10;
		Goto See;
	SuperShot:
		TNT1 A 0 { // reload when we have expended 4 shots
			if (shotsFired >= 4) { SetStateLabel("Reload"); }
		}
		QSZM E 15 A_FaceTarget;
		QSZM F 10 Bright Light("DMR_RifleFire") {
			A_StartSound("weapons/sshotf", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,7.1,10,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			shotsFired += 2;
		}
		TNT1 A 0 A_JumpIfTargetInLOS("Missile",0,JLOSF_DEADNOJUMP);
		QSZM E 10;
		Goto See;
	QuadShot:
		QSZM E 20 {
			A_FaceTarget();
			A_StartSound("quadshotgun/reload/3");
		}
		QSZM F 10 Bright Light("DMR_RifleFire") {
			A_StartSound("quadshotgun/fire/quad", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,7.1,20,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			shotsFired += 4;
			A_Recoil(8); // recoil is a bitch
		}
		QSZM E 15;
		Goto See;
	Reload:
		QSZM E 10 A_StartSound("quadshotgun/reload/1");
		QSZM E 10;
		QSZM A 20 A_StartSound("quadshotgun/reload/2");
		QSZM A 16 A_StartSound("quadshotgun/reload/2");
		QSZM E 15 {
			A_StartSound("quadshotgun/reload/3");
			shotsFired = 0; // all 4 shots loaded again
		}
		Goto See;
	Pain:
		QSZM G 3;
		QSZM G 3 A_Pain;
        Goto See;
	Death:
		QSZM H 8;
		QSZM I 8 A_Scream;
		QSZM J 8 A_NoBlocking;
		QSZM K 8;
		QSZM L -1;
        Stop;
	XDeath:
		QSZM M 5;
		QSZM N 5 A_XScream;
		QSZM O 5 A_NoBlocking;
		QSZM PQRST 5;
		QSZM U -1;
        Stop;
	Raise:
		QSZM KJIH 5;
        Goto See;
	}
}