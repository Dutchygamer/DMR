//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Vader, Tormentor667
//	Sounds:		Id Software, Vader
//  Sprites: 	Id Software (edits by Vader)
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		EA Games
//	ZScript:	Dutchygamer
//  Sprites:	Dutchygamer (modification of original sprites)
//	DustedPandomic for improvements to the ShieldWall logic
//======================================================================

Class HellServant : DMR_HellKnight
{
	Default
	{
		Tag "Hell Servant";
		Health 650;
		SeeSound "Paladin/Sight";
		PainSound "knight/pain";
		DeathSound "knight/death";
		ActiveSound "knight/active";
		Obituary "%o was tagged by a Hell Servant's fireball.";
		HitObituary "%o was torn apart by a Hell Servant.";		
		PainChance "DMR_ShieldBroken", 256;
	}
	
	int painYelp;
	
	States
	{
	Spawn:
		HWAR AB 10 A_Look;
		Loop;
	See:
		HWAR AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		HWAR E 0 A_Jump(128, "ShieldBlast");
		Goto ComboAttack;
	Melee:
	ComboAttack:
		HWAR EF 8 A_FaceTarget;
		HWAR G 8 A_CustomComboAttack("HellServantBall",44,random(16,48),"baron/melee");
		Goto See;
	RaiseShield:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_ShieldWall",0,0,0,0,0,0,0,SXF_SETMASTER);
		}
		HWAR HHHHH random(4,6) A_FaceTarget;
		TNT1 A 0 A_Jump(128, "ShieldBlast");
		HWAR HHHHH random(4,6) A_FaceTarget;
		TNT1 A 0 {
			A_RemoveChildren(1,RMVF_EVERYTHING);
		}
		Goto See;
	ShieldBlast:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_ShieldWall",0,0,0,0,0,0,0,SXF_SETMASTER);
		}
		HWAR H 4 A_FaceTarget;
		HWAR HHHHH 4 {
			A_FaceTarget();
			A_SpawnItemEx("DMR_MediumFireballTrail",0,frandom(-1,1),frandom(25,27),0,0,0,0,SXF_SETMASTER);
		}
		HWAR I 8 Bright A_SpawnProjectile("HellServantShieldBall",25);
		TNT1 A 0 {
			A_RemoveChildren(1,RMVF_EVERYTHING);
		}
		Goto See;
	Pain.DMR_ShieldBroken: // upon suffering a broken shield, stay in this painstate for a bit
		TNT1 A 0 {
			bNoPain = true; // don't switch to the default pain state when we are in this one
			A_PlaySound("shieldwall/death");
		}
		HWAR J 24 {
			if (random(1,5) == 5) {
				A_Pain(); 
			}
		}
		TNT1 A 0 {
			bNoPain = false;
		}
		Goto See;
	Pain:
		TNT1 A 0 {
			A_RemoveChildren(1,RMVF_EVERYTHING);
		}
		HWAR J 2;
		HWAR J 2 A_Pain;
		TNT1 A 0 A_Jump(128, "RaiseShield");
		Goto See;
	Death: 
	XDeath:
		HWAR K 5 {
			A_RemoveChildren(1,RMVF_EVERYTHING);
			A_SpawnItemEx("HellServantDroppedShield",0,0,25,6,0,0,60,SXF_SETMASTER);
		}
		HWAR L 5 A_Scream;
		HWAR M 5;
		HWAR N 5 A_NoBlocking;
		HWAR OPQRS 5;
		HWAR T -1;
		Stop ;
	Raise: 
		HWAR QPONMLK 5 A_KillChildren;
		Goto See;
	} 
}

class HellServantBall : DMR_BaronBall
{
	Default
	{
		Speed 18;
		FastSpeed 25;
		DamageFunction(random(16,48));
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn:
		HWFB AABB 2 Bright {
			A_SpawnItemEx("DMR_MediumFireballTrail",-7,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_MediumFireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		HWFB CDE 5 Bright;
		Stop;
	}
}

class HellServantShieldBall : HellServantBall
{
	Default
	{
		Speed 20;
		FastSpeed 30;
		+THRUSPECIES;
		+SEEKERMISSILE
		SeeSound "Paladin/Shoot";
		DeathSound "Paladin/Hit";
		Species "DMR_ShieldWall"; //so the shield projectiles can pass through the shield
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn:
		HWFB AABB 2 Bright {
			A_SeekerMissile(1, 1);
			A_SpawnItemEx("DMR_MediumFireballTrail",-7,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_MediumFireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		HWFB CDE 5 Bright;
		Stop;
	}
}

class HellServantDroppedShield : DMR_DroppedShield
{
	States
	{
	Spawn:
		HWSH ABCDEFGH 3;
		Loop;
	Death:
		HWSH I -1;
		Loop;
	}
}