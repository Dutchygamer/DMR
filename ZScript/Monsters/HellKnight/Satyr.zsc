//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Vader
//	Sounds:		Id Software, Vader
//  Sprites: 	Id Software (edits by Vader)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//  Sprites:	Dutchygamer (modification of original sprites)
//	DustedPandomic for the charge / ram logic
//======================================================================

class Satyr : DMR_HellKnight
{
	Default
	{
		Tag "Satyr";
		Health 450;
		Speed 14;
		SeeSound "satyr/sight";
		PainSound "knight/pain";
		DeathSound "satyr/death";
		ActiveSound "knight/active";
		Obituary "%o was mauled by a Satyr.";
		HitObituary "%o was mauled by a Satyr.";
		MeleeThreshold 128; // don't charge in min range
		MaxTargetRange 400; // don't charge at max range
	}
	States
	{
	Spawn:
		STYR AB 10 A_Look;
		Loop;
	See:
		STYR AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		STYR XXXXXX 2 A_FaceTarget;
		TNT1 A 0 {
			A_StartSound("satyr/sight", CHAN_VOICE);
			A_FaceTarget();
			A_Recoil(-3);
		}
	ChargeLoop:
		STYR AAABBBCCCDDD 1 {
			if (target && Distance3D(target) < 80 || vel.x == 0 && vel.y == 0) {
				return ResolveState("Ram");
			}
			A_FaceTarget(1.5,180);
			A_Recoil(-3);
			return ResolveState(null);
		}
		Loop;
	Ram:
		STYR E 2 A_FaceTarget(30,180);
		STYR F 6 {
			A_FaceTarget(20,180);
			A_CustomBulletAttack(0,0,1,random(20,60),"SatyrRamImpactor",80,CBAF_NORANDOM|CBAF_AIMFACING);
			A_Stop();
		}
		STYR A 2 A_FaceTarget;
		Goto See;
	Melee:
		STYR EF 6 A_FaceTarget;
		STYR G 6 A_CustomMeleeAttack(random(14,42),"baron/melee");
		STYR PQ 5 A_FaceTarget;
		STYR R 6 A_CustomMeleeAttack(random(14,42),"baron/melee");
		Goto See;
	Pain:
		STYR H 2;
		STYR H 2 A_Pain;
		Goto See;
	Death:
		STYR I 5;
		STYR J 5 A_Scream;
		STYR K 6;
		STYR L 7 A_Fall;
		STYR M 4;
		STYR N 4;
		STYR O -1;
		Stop;
	XDeath:
		STYX A 4;
		STYX B 4 A_XScream;
		STYX C 4;
		STYX D 4 A_Fall;
		STYX EFGHI 4;
		STYX J -1;
		Stop;
	Raise:
		STYR ONMLKJI 8;
		Goto See;
	}
}

Class SatyrRamImpactor : DMR_RamImpactor
{
	Default
	{
		ProjectileKickback 1000;
	}
}
