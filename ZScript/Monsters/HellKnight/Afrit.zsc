//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Nanami, Ghastly_dragon
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Espi, Ebola)
//	GLDEFs: 	Keksdose, Herculine (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//	DustedPandomic for the fix to prevent certain Boom/ZDoom map things from breaking with flying enemies (the first block in the 'See' state)
//======================================================================

Class Afrit : DMR_HellKnight
{
	Default
	{
		Tag "Afrit";
		Health 650;
		//Speed 8;
		PainChance 50;
		SeeSound "knight/sight";
		PainSound "knight/pain";
		DeathSound "knight/death";
		ActiveSound "knight/active";
		bloodcolor "orange";
		+BRIGHT;
		Obituary "%o was scorched by an afrit";
		HitObituary "%o found the afrit too hot to handle.";
	}
	States
	{
	Spawn:
		FRIT AB 10 A_Look;
		Loop;
	See:
		TNT1 A 0
		{
			A_NoGravity(); //these are only here to prevent certain Boom/ZDoom map things from breaking with flying enemies
			bFloat = true;
			bNoGravity = true;
			bFloorClip = false;
		}
		FRIT AABBCCDD 3 Light("DMR_AFRIT") A_Chase;
		Loop;
	Melee:
		//FRIT S 0 A_Jump(64,"HellFire") //low chance to perform hellfire summoning
		FRIT ST 8 Light("DMR_AFRIT") A_FaceTarget;
		FRIT U 8 Light("DMR_AFRIT") A_CustomComboAttack("AfritBall",44,random(20,60),"baron/melee");
		Goto See +2;
	Missile:
		TNT1 A 0 A_Jump(96,"Comet"); //chance to throw comet
		Goto Ball;
	Ball:
		FRIT ST 8 Light("DMR_AFRIT") A_FaceTarget;
		FRIT U 8 Light("DMR_AFRIT") A_CustomComboAttack("AfritBall",44,random(18,52),"baron/melee");
		Goto See +2;
	Comet:
		FRIT EF 10 Light("DMR_AFRIT") A_FaceTarget;
		FRIT G 10 Light("DMR_AFRIT") A_SpawnProjectile("AfritComet", 44, 0, 0);
		Goto See +2;
	Pain:
		FRIT H 4 Light("DMR_AFRIT");
		FRIT H 4 Light("DMR_AFRIT") A_Pain;
		Goto See +2;
	Death:
	XDeath:
		TNT1 A 0 Light("DMR_AFRIT") A_NoGravity;
		FRIT I 6 Light("DMR_AFRIT") A_Scream;
		TNT1 A 0 Light("DMR_AFRIT") A_PlaySound("dragonfam/attack");
		FRIT J 5 Light("DMR_AFRIT") A_NoBlocking;
		FRIT KLMNOPQR 4 Light("DMR_AFRIT");
		Stop;
	}
}

class AfritBall : DMR_BaronBall
{
	Default
	{
		DamageFunction(random(16,48));
		ALPHA 0.8;
	}
	States
	{
	Spawn:
		FRTM AABB 2 Bright { 
			A_SpawnItemEx("DMR_MediumFireballTrail",-3,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
		Loop;
	Death:
		TNT1 AAAA 0 {
			A_SpawnItemEx("DMR_MediumFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
			A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		FRTM CDE 6 Bright;
		Stop;
	}
}

class AfritComet : DMR_BaronBall
{
	Default
	{
		Speed 12;
		DamageFunction(random(10,30));
		Scale 0.65;
		RenderStyle "Normal";
		SeeSound "comet/launch";
		DeathSound "comet/explosion";
	}
	States
	{
	Spawn:
		COMT AABBCC 2 Bright { 
			A_SpawnItemEx("AfritCometTrail",-15,0,0);
			A_SpawnItemEx("DMR_MediumFireballTrail",-3,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetScale(1);
		}
		TNT1 AAAAAA 0 {
			A_SpawnItemEx("DMR_MediumFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
			A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		COMT D 3 Bright A_Explode(40,80);
		COMT EFGHI 3 Bright;
		Stop;
	}
}

class AfritCometTrail : DMR_ProjectileTrail
{
	Default
	{
		ALPHA 0.4;
	}
	States
	{
	Spawn:
		FRTB ABCDEFGHI 1 Bright;
		Stop;
	}
}

class AfritCometDeathGlow : DMR_ProjectileTrail
{
	Default
	{
		Scale 3.0;
		Alpha 0.6;
	}
	States
	{
	Spawn:
		FRTB ABCDEFGHI 3 Bright;
		Stop;
	}
}