//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Nanami, Ghastly_dragon
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Espi, Ebola)
//	GLDEFs: 	Keksdose, Herculine (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//	DustedPandomic for the fix to prevent certain Boom/ZDoom map things from breaking with flying enemies (the first block in the 'See' state)
//======================================================================

Class Afrit : DMR_HellKnight
{
	Default
	{
		Tag "Afrit";
		Health 650;
		PainChance 50;
		SeeSound "knight/sight";
		PainSound "knight/pain";
		DeathSound "knight/death";
		ActiveSound "knight/active";
		bloodcolor "orange";
		+BRIGHT;
		Obituary "%o was scorched by an Afrit";
		HitObituary "%o found the Afrit too hot to handle.";
	}
	States
	{
	Spawn:
		FRIT AB 10 A_Look;
		Loop;
	See:
		TNT1 A 0 A_BeginFloat();
		FRIT AABBCCDD 3 A_Chase;
		Loop;
	Melee:
		FRIT ST 8 A_FaceTarget;
		FRIT U 8 A_CustomComboAttack("AfritBall",44,random(20,60),"baron/melee");
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(96,"Comet");
		Goto Ball;
	Ball:
		FRIT ST 8 A_FaceTarget;
		FRIT U 8 A_CustomComboAttack("AfritBall",44,random(18,52),"baron/melee");
		Goto See;
	Comet:
		FRIT EF 10 A_FaceTarget;
		FRIT G 10 A_SpawnProjectile("AfritComet",44);
		Goto See;
	Pain:
		FRIT H 4;
		FRIT H 4 A_Pain;
		Goto See;
	Death:
	XDeath:
		TNT1 A 0 A_NoGravity;
		FRIT I 6 A_Scream;
		TNT1 A 0 A_StartSound("dragonfam/attack");
		FRIT J 5 A_NoBlocking;
		FRIT KLMNOPQR 4;
		Stop;
	}
}

class AfritBall : DMR_BaronBall
{
	Default
	{
		DamageFunction(random(16,48));
		ALPHA 0.8;
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn:
		FRTM AABB 2 Bright { 
			A_SpawnItemEx("DMR_MediumFireballTrail",-7,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_MediumFireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		FRTM CDE 6 Bright;
		Stop;
	}
}

class AfritComet : DMR_BaronBall
{
	Default
	{
		Speed 18;
		FastSpeed 25;
		DamageFunction(random(10,30));
		Scale 0.65;
		RenderStyle "Normal";
		SeeSound "comet/launch";
		DeathSound "comet/explosion";
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn:
		COMT AABBCC 2 Bright { 
			A_SpawnItemEx("AfritCometTrail",-15,0,0);
			A_SpawnItemEx("DMR_MediumFireballTrail",-7,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetScale(1);
			A_SpawnItemEx("DMR_MediumFireballImpactSpawner");
			A_QuakeEx(3,3,3,15,0,600,"",QF_RELATIVE|QF_SCALEDOWN|QF_3D,falloff:400,rollIntensity:.75);
			for (fxLoop = 0;fxLoop<=35;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		COMT D 3 Bright A_Explode(40,80);
		COMT EFGHI 3 Bright;
		Stop;
	}
}

class AfritCometTrail : DMR_ProjectileTrail
{
	Default
	{
		ALPHA 0.4;
	}
	States
	{
	Spawn:
		FRTB ABCDEFGHI 1 Bright;
		Stop;
	}
}