//======================================================================
//  Original credits
//======================================================================
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by mattbratt/DustedPandemonic)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class CyberHellKnight : DMR_HellKnight
{
	Default
	{
		Tag "Cyber Hell Knight";
		Health 750;
		DropItem "RocketAmmo", 128, 1;
		DropItem "RocketAmmo", 128, 1;
		obituary "%o was splayed by a cyber knight";
		hitobituary "%o was ripped to shreds by a cyber knight";
		SeeSound "cyknight/sight";
		PainSound "cyknight/pain";
		DeathSound "cyknight/death";
	}
	States
	{
	Spawn:
		CBO2 AB 10 A_Look;
		loop;
	See:
		CBO2 AABBCC 3 A_Chase;
		TNT1 A 0 A_PlaySound("cyknight/step");
		CBO2 DD 3 A_Chase;
		loop;
	Melee:
		CBO2 PQ 8 A_FaceTarget;
		CBO2 R 8 A_CustomComboAttack("DMR_BaronBall",32,random(16,48),"baron/melee");
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(128, "Rocket");
		Goto Ball;
	Ball:
		CBO2 PQ 8 A_FaceTarget;
		CBO2 R 8 A_CustomComboAttack("DMR_BaronBall",32,random(16,48),"baron/melee");
		Goto See;
	Rocket:
		CBO2 EE 10 A_FaceTarget;
		CBO2 F 10 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",31,16);
		CBO2 E 10 A_FaceTarget;
		Goto See;
	Pain:
		CBO2 H 2;
		CBO2 H 2 A_Pain;
		goto See;
	Death:
	XDeath:
		CBO2 I 15 A_Scream;
		CBO2 J 5;
		CBO2 K 5 bright {
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(8,-8),0,random(20,40));
		}
		CBO2 L 5 bright A_NoBlocking;
		CBO2 M 5 bright;
		CBO2 N 5 bright;
		CBO2 O -1;
		stop;
	Raise:
		CBO2 ONMLKJI 8;
		Goto See;
	}
}
