//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance, Blue Shadow, MagicWazard
//	Sounds:		Eriance
//  Sprites: 	Eriance
//	GLDEFs: 	Eriance, Dreadopp, Ghastly Dragon, Keksdose, MagicWazard
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class Overlord : DMR_SpiderMastermind
{
	Default
	{
		Tag "Emperor overlord";
		Health 4000;
		PainChance 30;
		DamageFactor "lightning", 0.0;
		Obituary "%o was smitten by the emperor overlord.";
		HitObituary "The emperor overlord ate %o for lunch.";
		+NODROPOFF; //don't yeet yourself off platforms
		SeeSound "monster/helsit";
		PainSound "monster/helpai";
		DeathSound "monster/heldth";
		ActiveSound "monster/helact";
		BloodColor "Red";
	}
	states
	{
	Spawn:
		OVER A 10 A_Look;
		loop;
	See:
		OVER A 0 A_UnSetInvulnerable;
		OVER AABBAACC 3 A_Chase;
		OVER A 0 A_Jump(32,"Teleport");
		goto See+1;
	Missile:		
		TNT1 A 0 A_Jump(64,"HadesSpheres");
		//TNT1 A 0 A_Jump(128,"LightningBolt");
		TNT1 A 0 A_Jump(128,"Laser");
		Goto Balls;
	Balls:
		OVER J 8 A_FaceTarget;
		OVER K 4 Bright {		
			A_SpawnProjectile("OverSpamBall",92,-40,random(-3,3),0,random(-3,3));
			A_SpawnProjectile("OverSpamBall",92,40,random(-3,3),0,random(-3,3));
			A_SpawnProjectile("OverBall",54,-50,random(-3,3),0,random(-3,3));
			A_SpawnProjectile("OverBall",54,50,random(-3,3),0,random(-3,3));
			A_SpawnProjectile("OverSpamBall",8,-40,random(-3,3),0,random(-3,3));
			A_SpawnProjectile("OverSpamBall",8,40,random(-3,3),0,random(-3,3));
		}
		OVER J 8 A_SpidRefire;
		TNT1 A 0 A_Jump(32,"See");
		goto Balls+1;
	/*LightningBolt:
		OVER GH 5 Bright A_FaceTarget;
		OVER H 0 Bright A_SpawnProjectile("EmperorStormBoltBall",43,0,0,0,0);
		goto See;*/
	Laser:
		OVER G 20 Bright {
			A_PlaySound("monster/helsit");
			A_FaceTarget();
		}
		OVER GH 4 Bright A_FaceTarget;		
		OVER HHHHHHHHHHHH 1 Bright {
			A_FaceTarget();
			A_SpawnProjectile("EmperorLaser",43,0,random(-1,1),0,0);
		}
		OVER G 6 Bright A_FaceTarget;
		OVER A 6;
		goto See;
	HadesSpheres:
		OVER DE 8 A_FaceTarget;
		OVER FF 4 bright A_PainAttack("HadesSphere");
		goto See;
	Pain:
		OVER L 3;
		OVER L 3 A_Pain;
		TNT1 A 0 A_Jump(40,"Teleport");
		goto See;
	Teleport:
		TNT1 A 0 A_SetInvulnerable;
		OVER F 1 A_PlaySound("monster/heltel");
		OVER F 1 A_SetTranslucent(0.90);
		OVER F 1 A_SetTranslucent(0.80);
		OVER F 1 A_SetTranslucent(0.70);
		OVER F 1 A_SetTranslucent(0.60);
		OVER F 1 A_SetTranslucent(0.50);
		OVER F 1 A_SetTranslucent(0.40);
		OVER F 1 A_SetTranslucent(0.30);
		OVER F 1 A_SetTranslucent(0.20);
		OVER F 1 A_SetTranslucent(0.10);
		TNT1 A 0 A_Jump(128,25);
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_Chase(null,null,CHF_FASTCHASE);
		TNT1 A 0 A_Jump(128,25);
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_Chase(null,null,CHF_FASTCHASE);
		OVER F 1 A_PlaySound("monster/heltel");
		OVER F 1 A_SetTranslucent(0.10);
		OVER F 1 A_SetTranslucent(0.20);
		OVER F 1 A_SetTranslucent(0.30);
		OVER F 1 A_SetTranslucent(0.40);
		OVER F 1 A_SetTranslucent(0.50);
		OVER F 1 A_SetTranslucent(0.60);
		OVER F 1 A_SetTranslucent(0.70);
		OVER F 1 A_SetTranslucent(0.80);
		OVER F 1 A_SetTranslucent(0.90);
		OVER F 1 A_SetTranslucent(1.0);
		TNT1 A 0 A_UnSetInvulnerable;
		goto See;
	Death:
		OVER M 8 bright A_Scream;
		OVER N 8 bright;
		OVER O 8 bright A_NoBlocking;
		TNT1 AAAAA 0 {
			A_SpawnProjectile("OverFlesh1",random(0,90),random(0,40),random(-180,180),2,random(-15,15));
			A_SpawnProjectile("OverFlesh2",random(0,90),random(0,40),random(-180,180),2,random(-15,15));
		}
		TNT1 AAAAAAAAAA 0 {
			A_SpawnProjectile("OverFlesh3",random(0,90),random(0,40),random(-180,180),2,random(-15,15));
			A_SpawnProjectile("OverFlesh4",random(0,90),random(0,40),random(-180,180),2,random(-15,15));
			A_SpawnProjectile("OverFlesh5",random(0,90),random(0,40),random(-180,180),2,random(-15,15));
			A_SpawnProjectile("OverFlesh6",random(0,90),random(0,40),random(-180,180),2,random(-15,15));
		}
		TNT1 A 0 {
			A_SpawnProjectile("OverBigArm1",40,-40,-90,2,random(-1,1));
			A_SpawnProjectile("OverBigArm2",40,40,90,2,random(-1,1));
			A_SpawnProjectile("OverSmallArm1",100,-30,-90,2,random(-15,15));
			A_SpawnProjectile("OverSmallArm1",100,30,90,2,random(-15,15));
			A_SpawnProjectile("OverSmallArm2",100,-30,-90,2,random(-15,15));
			A_SpawnProjectile("OverSmallArm2",100,30,90,2,random(-15,15));
			A_SpawnProjectile("OverHorn1",110,-16,-90,2,random(-15,15));
			A_SpawnProjectile("OverHorn2",110,16,90,2,random(-15,15));
			//A_PainDie("HadesSphere"); //TODO do we want this?
		}
		OVER PQRSTUV 8 bright;
		TNT1 A 30;
		TNT1 A -1 A_BossDeath;
		stop;
	}
}

class OverBall : DMR_CacodemonBall
{
	Default
	{
		Speed 15;
		DamageFunction(random(16,48));
		Alpha 0.80;
		DamageType "lightning";
		Decal "CacoScorch";
		DeathSound "weapons/devzap";
	}
	States
	{
	Spawn:
		HEFX AB 4 Bright;
		Loop;
	Death:
		HEFX CDEEFGH 3 bright;
		Stop;
	}
}

class OverSpamBall : OverBall
{
	Default
	{
		Speed 18;
		DamageFunction(random(6,16));
		Translation "192:207=168:191";
		Scale 0.8;
	}
	States
	{
	Spawn:
		AFX1 ABC 1 Bright;
		loop;
	Death:
		AFX1 DEFGHI 4 Bright;
		stop;
	}
}

//laser projectile
class EmperorLaser : DMR_LaserBase
{	
	Default
	{
		DamageFunction(random(4,12));
		Alpha 0.5;
		MissileType "EmperorLaserTrail";
		MissileHeight 10;
		SeeSound "darkinquisitor/laserfire"; //seesound "BeamRevenant/Fire";
		Scale 0.8;
		Damagetype "lightning";
		Translation "112:127=192:207";
	}
	States
	{
	Spawn:
		TNT1 A 1;
		Loop;
	Death:
		BFE1 A 6 Bright A_Explode(10,48);
		BFE1 BCDEF 6 Bright;
		Stop;
	}
}

class EmperorLaserTrail : DMR_LaserTrailBase
{
	Default
	{
		RenderStyle "Add";
		Alpha 0.9;
		Scale 1.0;
	}
}

// [MagicWazard] Giant ball that spawns smaller projectiles ("StormLite1") from sides
class EmperorStormBoltBall : DMR_CacodemonBall
{
	Default
	{
		Speed 30; //20
		Damage 10; //5
		RENDERSTYLE "ADD";
		Alpha 0.80;
		Scale 1.2;				
		Damagetype "lightning";
		//+THRUGHOST;
		//+NODAMAGETHRUST;
		//+FORCEXYBILLBOARD;
		SeeSound "Monster/hadtel";
		DeathSound "Monster/hadsit";
		//DeathSound "weapons/devexp";
	}
	States
	{
	Spawn:
		LFX1 STUVW 1 Bright;
		LFX1 W 0 Bright A_SpawnProjectile("EmperorStormShrapnel",0,0,90,6);
		LFX1 W 0 Bright A_SpawnProjectile("EmperorStormShrapnel",0,0,270,6);
		loop;
	Death:
		TNT1 A 0 A_SpawnProjectile("EmperorStormBolt",0,0,random(0,360),0,0);
		TNT1 A 0 A_SpawnProjectile("EmperorStormBolt",0,0,random(0,360),0,0);
		TNT1 A 0 A_SpawnProjectile("EmperorStormBolt",0,0,random(0,360),0,0);
		TNT1 A 0 A_SpawnProjectile("EmperorStormBolt",0,0,random(0,360),0,0);
		LFX1 STUVWSTUVW 1 Bright A_Explode(2,44,0);
		stop;
	}
}

// [MagicWazard] Projectiles spawned from the sides of the large StormShot projectile
class EmperorStormShrapnel : CacodemonBall
{	
	Default
	{
		Speed 32;
		Damage 5;
		//RENDERSTYLE "ADD";
		Alpha 0.80;
		Scale 1.1;
		Damagetype "lightning";
		DeathSound "weapons/devzap";
		//+THRUGHOST;
		+RIPPER;
		//+FORCEXYBILLBOARD;
	}
	States
	{
	Spawn:
		DLIT ABC 1 Bright;
		Loop;
	Death:
		DLIT DEFGHIJKLMNO 1 Bright;
		Stop;
	}
}

class EmperorStormBolt : CacodemonBall
{
	Default
	{
		Speed 4;
		BounceType "Hexen";
		Damagetype "lightning";
		SeeSound "weapons/none";
		DeathSound "weapons/gntidl";
		YScale 4.0;
		XScale 2.0;
		ReactionTime 25;
		+FloorHugger;
		+NODAMAGETHRUST;
		+Ripper;
		-NoGravity;
		-StrifeDamage;
	}
	States
	{
	Spawn:
		LFX2 F 1 Bright A_Explode(1,44,0);
		LFX2 F 0 ThrustThing(random(0,255),1,0,0);
		LFX2 G 1 Bright A_Explode(1,44,0);
		LFX2 H 1 Bright A_Explode(1,44,0);
		LFX2 I 1 Bright A_Explode(1,44,0);
		LFX2 J 1 Bright A_Explode(1,44,0);
		LFX2 J 0 Bright A_CountDown;
		Loop;
	Death:
		LFX2 FGHIJ 2 Bright A_Explode(5,64,0);
		Stop;
	}
}

//death pieces
class OverFlesh1 : DMR_DroppedShield
{
	States
	{
	Spawn:
		OVF1 ACEGIKM 5;
		loop;
	Death:
		OVF1 O 3;
		OVF1 Q -1;
		stop;
	}
}

class OverFlesh2 : OverFlesh1
{
	States
	{
	Spawn:
		OVF1 BDFHJLN 5;
		loop;
	Death:
		OVF1 P 3;
		OVF1 R -1;
		stop;
	}
}

class OverFlesh3 : OverFlesh1
{
	States
	{
	Spawn:
		OVF2 ACEG 5;
		loop;
	Death:
		OVF2 I -1;
		loop;
	}
}

class OverFlesh4 : OverFlesh1
{
	States
	{
	Spawn:
		OVF2 BDFH 5;
		loop;
	Death:
		OVF2 J -1;
		loop;
	}
}

class OverFlesh5 : OverFlesh1
{
	States
	{
	Spawn:
		OVF3 ACEGI 5;
		loop;
	Death:
		OVF3 K -1;
		loop;
	}
}

class OverFlesh6 : OverFlesh1
{
	States
	{
	Spawn:
		OVF3 BDFHJ 5;
		loop;
	Death:
		OVF3 L -1;
		loop;
	}
}

class OverBigArm1 : OverFlesh1
{
	States
	{
	Spawn:
		OVF4 ACEGI 5;
		loop;
	Death:
		OVF4 K 3;
		OVF4 M -1;
		stop;
	}
}

class OverBigArm2 : OverFlesh1
{
	States
	{
	Spawn:
		OVF4 BDFHJ 5;
		loop;
	Death:
		OVF4 L 3;
		OVF4 N -1;
		stop;
	}
}

class OverSmallArm1 : OverFlesh1
{
	States
	{
	Spawn:
		OVF5 ACEG 5;
		loop;
	Death:
		OVF5 I -1;
		stop;
	}
}

class OverSmallArm2 : OverFlesh1
{
	States
	{
	Spawn:
		OVF5 BDFH 5;
		loop;
	Death:
		OVF5 J -1;
		stop;
	}
}

class OverHorn1 : OverFlesh1
{
	States
	{
	Spawn:
		OVF6 ACEGI 5;
		loop;
	Death:
		OVF6 K -1;
		stop;
	}
}

class OverHorn2 : OverFlesh1
{
	States
	{
	Spawn:
		OVF6 BDFHJ 5;
		loop;
	Death:
		OVF6 L -1;
		stop;
	}
}
