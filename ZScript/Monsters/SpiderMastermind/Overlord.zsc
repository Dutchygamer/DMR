//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance, Blue Shadow, MagicWazard
//	Sounds:		Eriance
//  Sprites: 	Eriance
//	GLDEFs: 	Eriance, Dreadopp, Ghastly Dragon, Keksdose, MagicWazard
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class Overlord : DMR_SpiderMastermind
{
	Default
	{
		Tag "Emperor Overlord";
		Health 5500;
		PainChance 15;
		DropItem "CellPack", 256, 100;
		DropItem "CellPack", 128, 100;
		DropItem "Cell", 128, 20;
		DropItem "Cell", 128, 20;
		DropItem "Cell", 128, 20;		
		Obituary "%o was erased by the Emperor Overlord.";
		SeeSound "emperoroverlord/sight";
		PainSound "emperoroverlord/pain";
		DeathSound "emperoroverlord/death";
		ActiveSound "emperoroverlord/active";
		BloodColor "Red";
		//+NODROPOFF; // don't yeet yourself off platforms
		+MISSILEMORE;
	}
	
	int breathAngle;
	bool inverseBreath;
	
	states
	{
	Spawn:
		OVER A 10 A_Look;
		loop;
	See:
		OVER AABBAACC 3 A_Chase;
		TNT1 A 0 A_Jump(32,"Dash");
		Loop;		
	Dash:
		TNT1 A 0 {
			bNOPAIN = true;
			// dash left or right
			A_Dash(30,angle+randompick(64,192));
		}
		OVER DDDDDDDDDDDD 1;
		TNT1 A 0 {
			bNOPAIN = false;
			// stop moving
			A_Stop();
		}
		goto See;
	Missile:
		TNT1 A 0 A_Jump(160,"MouthBalls","BigMouthBalls","HandBalls");
		Goto BeamAttack;		
	MouthBalls:
		OVER DE 4 A_FaceTarget;
		OVER FFF 4 bright {
			A_FaceTarget();
			A_SpawnProjectile("OverlordMouthBall",43,0);
		}
		OVER ED 4;
		// either stop attacking, switch to large balls or attack again
		TNT1 A 0 A_Jump(128, "See", "BigMouthBalls");
		OVER DE 4 A_FaceTarget;
		OVER FFF 4 bright {
			A_FaceTarget();
			A_SpawnProjectile("OverlordMouthBall",43,0);
		}
		OVER ED 4;
		goto See;		
	BigMouthBalls:
		TNT1 A 0 A_PlaySound("ChaosImp/ElectricFire",1);
		OVER GH 6 A_FaceTarget;
		OVER III 5 bright {
			A_FaceTarget();
			A_SpawnProjectile("OverlordBigMouthBall",43,0);
		}
		OVER ED 8;
		goto See;		
	HandBalls:
		OVER JJ 8 A_FaceTarget;
		TNT1 A 0 { attackloop = 0; }
	HandBallsLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 8,"HandBallsEnd");
		OVER K 4 Bright {		
			A_SpawnProjectile("OverlordHandFastBall",92,-40,random(-3,3),0,random(-3,3));
			A_SpawnProjectile("OverlordHandFastBall",92,40,random(-3,3),0,random(-3,3));
			A_SpawnProjectile("OverlordHandBall",54,-50);
			A_SpawnProjectile("OverlordHandBall",54,50);
			A_SpawnProjectile("OverlordHandFastBall",8,-40,random(-3,3),0,random(-3,3));
			A_SpawnProjectile("OverlordHandFastBall",8,40,random(-3,3),0,random(-3,3));
			attackloop++;
		}
		OVER J 6 A_MonsterRefire(10,"HandBallsEnd");
		// small chance to not do the entire attack loop
		TNT1 A 0 A_Jump(10,"HandBallsEnd");
		Loop;
	HandBallsEnd:
		OVER JJ 8;
		goto See;		
	BeamAttack:
		TNT1 A 0 {
			A_PlaySound("emperoroverlord/breath", CHAN_VOICE);
			bNOPAIN = true;
			if (randompick(1,2) == 1) {
				inverseBreath = false;
				breathAngle = -6; 
			} else {
				inverseBreath = true;			
				breathAngle = 6; 
			}
		}
		OVER G 10 Bright {
			A_FaceTarget();
		}
		OVER G 10 Bright {
			A_FaceTarget();
			A_PlaySound("emperoroverlord/lasercharge", CHAN_WEAPON);
			A_SpawnItemEx("OverlordBreathLaserCharge",0,0,0,0,0,0,0,SXF_SETMASTER);
		}
		OVER GGGGHHHHIIII 2 Bright {
			A_FaceTarget();	
		}
		TNT1 A 0 {		
			A_RemoveChildren(1,RMVF_EVERYTHING,"OverlordBreathLaserCharge");
		}
		OVER IIIIIIIIIIII 1 Bright {
			// shoot in the direction you targetted last, not adjusting for the target moving
			A_SpawnProjectile("OverlordBreathLaser",43,0,breathAngle,CMF_AIMDIRECTION,pitch);
			if (inverseBreath) {
				breathAngle -= 1;
			} else {
				breathAngle += 1;			
			}			
		}
		OVER ED 15;
		TNT1 A 0 {
			bNOPAIN = false;
		}
		goto See;		
	Pain:
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING,"OverlordBreathLaserCharge");
		OVER L 3;
		OVER L 3 A_Pain;
		TNT1 A 0 A_Jump(96,"Dash");
		goto See;
	Death:
		OVER M 10 Bright {
			A_Scream();
			A_RemoveChildren(1,RMVF_EVERYTHING);
		}
		TNT1 A 0 A_NoBlocking;
		OVER NO 8 Bright {
			A_SpawnItemEx("DMR_BossDeathExplosion",random(-20,20),0,random(20,60),0,0,0,random(0,360));
		}
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 5;fxLoop++) {
				A_SpawnProjectile("OverlordFlesh1",random(0,90),random(0,40),random(-180,180),2,random(-15,15));
				A_SpawnProjectile("OverlordFlesh2",random(0,90),random(0,40),random(-180,180),2,random(-15,15));
			}
			for (fxLoop = 0;fxLoop <= 10;fxLoop++) {
				A_SpawnProjectile("OverlordFlesh3",random(0,90),random(0,40),random(-180,180),2,random(-15,15));
				A_SpawnProjectile("OverlordFlesh4",random(0,90),random(0,40),random(-180,180),2,random(-15,15));
				A_SpawnProjectile("OverlordFlesh5",random(0,90),random(0,40),random(-180,180),2,random(-15,15));
				A_SpawnProjectile("OverlordFlesh6",random(0,90),random(0,40),random(-180,180),2,random(-15,15));
			}			
			A_SpawnProjectile("OverlordBigArm1",40,-40,-90,2,random(-1,1));
			A_SpawnProjectile("OverlordBigArm2",40,40,90,2,random(-1,1));
			A_SpawnProjectile("OverlordSmallArm1",100,-30,-90,2,random(-15,15));
			A_SpawnProjectile("OverlordSmallArm1",100,30,90,2,random(-15,15));
			A_SpawnProjectile("OverlordSmallArm2",100,-30,-90,2,random(-15,15));
			A_SpawnProjectile("OverlordSmallArm2",100,30,90,2,random(-15,15));
			A_SpawnProjectile("OverlordHorn1",110,-16,-90,2,random(-15,15));
			A_SpawnProjectile("OverlordHorn2",110,16,90,2,random(-15,15));
		}
		OVER PQR 6 Bright {
			A_SpawnItemEx("DMR_BossDeathExplosion",random(-20,20),0,random(20,60),0,0,0,random(0,360));
		}
		OVER STUV 6 Bright;
		TNT1 A 50;
		TNT1 A 1 A_BossDeath;
		stop;
	}
}

class OverlordMouthBall : DMR_SmallRocket
{
	Default
	{
		RenderStyle "Add";
		Alpha 1;
		Decal "CacoScorch";
		SeeSound "caco/attack";
		DeathSound "weapons/devzap";
		+SEEKERMISSILE;
		+BRIGHT;
		DMR_Projectile.ParticleColors "#ff0000", "#ee0000", "#dd0000", "#cc0000";
	}
	States
	{
	Spawn:
		DLIT DEFGH 2 {
			A_SeekerMissile(1,1); 
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {		
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		DLIT IJKLMNO 3 bright;
		Stop;
	}
}

class OverlordBigMouthBall : DMR_Rocket
{
	Default
	{
		//RenderStyle "Add";
		//Alpha 1;
		Decal "CacoScorch";
		SeeSound "monster/hadsit";
		DeathSound "monster/hadexp";
		+SEEKERMISSILE;
		+BRIGHT;
		DMR_Projectile.ParticleColors "#ff0000", "#ee0000", "#dd0000", "#cc0000";
	}
	States
	{
	Spawn:
		HADE AABBCCDD 2 Bright {
			A_SeekerMissile(1,1); 
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {		
			A_SetRenderStyle(1.0,STYLE_Add);
			for (fxLoop = 0;fxLoop<=50;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		HADE M 4 {
			A_Explode(72,128);
		}
		HADE NOPQ 5;
		Stop;
	}
}


class OverlordBreathLaser : DMR_LaserBase
{	
	Default
	{
		DamageFunction(random(8,24));
		Alpha 0.5;
		MissileType "OverlordBreathLaserTrail";
		MissileHeight 10;
		SeeSound "coredefender/laserfire";
		Scale 0.8;
		Translation "112:127=192:207";
	}
	States
	{
	Spawn:
		TNT1 A 1 A_Explode(2,48);
		Loop;
	Death:
		BFE1 A 6 Bright Light("DMR_BlueLaserImpactBig_X1") {
			A_Explode(20,64,0,0,48);
			A_SpawnItemEx("DMR_BlueLaserImpactFlareBig");
		}
		BFE1 BCDEF 6 Bright;
		Stop;
	}
}

class OverlordBreathLaserTrail : DMR_LaserTrailBase
{
	Default
	{
		RenderStyle "Add";
		Alpha 0.9;
		Scale 1.0;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay			A_SpawnItemEx("DMR_BlueLaserFlareBig",0,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1));
		LASB B 1 bright ;
	SpawnLoop:
		LASB B 1 bright A_FadeOut(0.1);
		Loop;
	}
}

class OverlordBreathLaserCharge : DMR_ProjectileTrail
{
	Default
	{
		Scale 0.3;
		Translation "192:207=168:191";
	}
	States
	{
	Spawn:
		LFX1 SSTTUUVVWW 1 Bright {
			A_Warp(AAPTR_MASTER,10,0,43,0,WARPF_NOCHECKPOSITION);
			if (Scale.x < 1.2) { 
				A_SetScale(Scale.X+0.08); 
			}	
		}
		Loop;
	}
}

class OverlordHandBall : DMR_CacodemonBall
{
	Default
	{
		Speed 18;
 		FastSpeed 30;
		DamageFunction(random(16,48));
		Scale 1.1;
		DeathSound "weapons/devzap";
		+BRIGHT;
		DMR_Projectile.ParticleColors "#ff0000", "#ee0000", "#dd0000", "#cc0000";
	}
	States
	{
	Spawn:
		HEFX AB 4 {
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {	
			A_SpawnItemEx("DMR_FireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=20;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		HEFX CDEEFGH 3;
		Stop;
	}
}

class OverlordHandFastBall : OverlordHandBall
{
	Default
	{
		Speed 25;
 		FastSpeed 40;
		DamageFunction(random(10,30));
		Scale 0.9;
		+BRIGHT;
		DMR_Projectile.ParticleColors "#5353ff", "#9b9bff", "#8f8fff", "#1b1bff";
	}
	States
	{
	Spawn:
		AFX1 ABC 2 {			
			A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
		}
		loop;
	Death:
		TNT1 A 0 {	
			A_SpawnItemEx("DMR_FireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=20;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		AFX1 DEFGHI 4;
		stop;
	}
}



// death pieces
class OverlordFlesh1 : DMR_DroppedShield
{
	States
	{
	Spawn:
		OVF1 ACEGIKM 5;
		loop;
	Death:
		OVF1 O 3;
		OVF1 Q -1;
		stop;
	}
}

class OverlordFlesh2 : OverlordFlesh1
{
	States
	{
	Spawn:
		OVF1 BDFHJLN 5;
		loop;
	Death:
		OVF1 P 3;
		OVF1 R -1;
		stop;
	}
}

class OverlordFlesh3 : OverlordFlesh1
{
	States
	{
	Spawn:
		OVF2 ACEG 5;
		loop;
	Death:
		OVF2 I -1;
		loop;
	}
}

class OverlordFlesh4 : OverlordFlesh1
{
	States
	{
	Spawn:
		OVF2 BDFH 5;
		loop;
	Death:
		OVF2 J -1;
		loop;
	}
}

class OverlordFlesh5 : OverlordFlesh1
{
	States
	{
	Spawn:
		OVF3 ACEGI 5;
		loop;
	Death:
		OVF3 K -1;
		loop;
	}
}

class OverlordFlesh6 : OverlordFlesh1
{
	States
	{
	Spawn:
		OVF3 BDFHJ 5;
		loop;
	Death:
		OVF3 L -1;
		loop;
	}
}

class OverlordBigArm1 : OverlordFlesh1
{
	States
	{
	Spawn:
		OVF4 ACEGI 5;
		loop;
	Death:
		OVF4 K 3;
		OVF4 M -1;
		stop;
	}
}

class OverlordBigArm2 : OverlordFlesh1
{
	States
	{
	Spawn:
		OVF4 BDFHJ 5;
		loop;
	Death:
		OVF4 L 3;
		OVF4 N -1;
		stop;
	}
}

class OverlordSmallArm1 : OverlordFlesh1
{
	States
	{
	Spawn:
		OVF5 ACEG 5;
		loop;
	Death:
		OVF5 I -1;
		stop;
	}
}

class OverlordSmallArm2 : OverlordFlesh1
{
	States
	{
	Spawn:
		OVF5 BDFH 5;
		loop;
	Death:
		OVF5 J -1;
		stop;
	}
}

class OverlordHorn1 : OverlordFlesh1
{
	States
	{
	Spawn:
		OVF6 ACEGI 5;
		loop;
	Death:
		OVF6 K -1;
		stop;
	}
}

class OverlordHorn2 : OverlordFlesh1
{
	States
	{
	Spawn:
		OVF6 BDFHJ 5;
		loop;
	Death:
		OVF6 L -1;
		stop;
	}
}
