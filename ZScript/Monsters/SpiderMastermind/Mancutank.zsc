//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	???
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by ???)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Mancutank : DMR_SpiderMastermind
{
	Default
	{
		Tag "Catastrophe";
		Health 4000;
		Scale 1.1;
		PainChance 35;
		DropItem "RocketBox", 256, 5;
		DropItem "RocketBox", 128, 5;
		DropItem "RocketBox", 128, 5;
		SeeSound "MancuTank/sight";
		PainSound "MancuTank/pain";
		DeathSound "MancuTank/death";
		ActiveSound "MancuTank/active";
		Obituary "%o was wrecked by a Catastrophe";
		BloodColor "Red";
	}
	
	States
	{
	Spawn:
		BS1W AC 10 A_Look;
		Loop;
	See:
		TNT1 A 0 A_PlaySound("Mancutank/Tank",CHAN_7,1,True);		
		BS1W ABBCCDD 3 A_Chase;
		Goto See+1;
	Missile:
		TNT1 A 0 A_Jump(196, "Chainguns", "Rockets");
		Goto IncendiaryRockets;
	Chainguns:
		BS1T A 18 Bright A_FaceTarget;	
		BS1T BBAA 2 Bright Light("DMR_RifleFire") {
			A_FaceTarget();
			A_Playsound("MamaSpider/Fire", CHAN_WEAPON);				
			// fire from left gun
			A_SpawnProjectile("SpiderSlowBullet",53,32,frandom(-12,12),CMF_OFFSETPITCH,frandom(-1,1));
			// fire from right gun
			A_SpawnProjectile("SpiderSlowBullet",53,-32,frandom(-12,12),CMF_OFFSETPITCH,frandom(-1,1));
		}
		TNT1 A 0 A_MonsterRefire(10,"See");
		Goto Chainguns+1;
	Rockets:
		BS1T A 16 A_FaceTarget;	
		BS1T C 3 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",71,-49,random(-2,2));
		BS1T C 3 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",71,49,random(-2,2));
		BS1T A 4 A_FaceTarget;
		BS1T D 3 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",64,-49,random(-2,2));
		BS1T D 3 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",64,49,random(-2,2));
		BS1T A 4 A_FaceTarget;
		BS1T E 3 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",53,-49,random(-2,2));
		BS1T E 3 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",53,49,random(-2,2));
		Goto See;
	IncendiaryRockets:
		BS1T A 16 A_FaceTarget;	
		BS1T C 3 Bright Light("DMR_RifleFire") A_SpawnProjectile("MancutankRocket",71,-49,random(-2,2));
		BS1T C 3 Bright Light("DMR_RifleFire") A_SpawnProjectile("MancutankRocket",71,49,random(-2,2));
		BS1T A 4 A_Facetarget;
		BS1T D 3 Bright Light("DMR_RifleFire") A_SpawnProjectile("MancutankRocket",64,-49,random(-2,2));
		BS1T D 3 Bright Light("DMR_RifleFire") A_SpawnProjectile("MancutankRocket",64,49,random(-2,2));
		BS1T A 4 A_Facetarget;
		BS1T E 3 Bright Light("DMR_RifleFire") A_SpawnProjectile("MancutankRocket",53,-49,random(-2,2));
		BS1T E 3 Bright Light("DMR_RifleFire") A_SpawnProjectile("MancutankRocket",53,49,random(-2,2));
		Goto See;
	Pain:
		BS1W A 3;
		BS1W A 3 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 A_StopSound(CHAN_7);
		BS1B A 6 A_Scream;
		BS1B B 6 A_NoBlocking;
		BS1B CDEFGH 6 Bright {
			A_SpawnItemEx("DMR_MastermindDeathExplosionSpawner");		
		}
		BS1B I 6 Bright {
			A_SpawnItemEx("DMR_BossDeathFinalExplosion");
			A_SpawnItemEx("DMR_MastermindDeathExplosionSpawner");		
		}
		BS1B JK 6;
		BS1B L 50;
		BS1B L -1 A_BossDeath;
		Stop;
	}
}

class MancutankRocket : DMR_SmallRocket
{
	Default
	{
		DamageFunction(random(10,30));
		Scale 1.0;
	}
	States
	{
	Spawn:
		FBRS A 1 Bright Light("DMR_SROCKET") {
			A_Weave(random(1,2), random(1,2), frandom(0.5,1.5), frandom(0.5,1.5));
			A_SpawnItemEx("DMR_FireballTrail",-28,0,3,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			A_SpawnItemEx("DMR_SmokeCloud",-28,0,3,frandom(-1,0));
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_BurningFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_ORIGINATOR|SXF_TRANSFERPOINTERS);
		}
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 10;fxLoop++) {
				A_SpawnItemEx("DMR_FireBouncingDebris",0,0,0,frandom(-12,12),frandom(-12,12),frandom(-4,10)); 
			}
		}
		MISL B 8 Bright Light("DMR_SROCKET_X1") A_Explode(20,64);
		MISL C 6 Bright Light("DMR_SROCKET_X2");
		MISL D 4 Bright Light("DMR_SROCKET_X3");
		Stop;
	}
}