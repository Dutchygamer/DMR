//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios	
//	ZScript:	Dutchygamer
//======================================================================

class DMR_SpiderMastermind : DMR_MonsterBase 
{
	Default
	{
		Tag "Spider Mastermind";
		BloodColor "Purple"; //TODO recolour sprites to use purple blood
		Health 3000;
		// no gibhealth cause it already gibs
		Species "DMR_BigSpooder";
		Radius 96;
		Height 100;
		Mass 1000;
		Speed 16;
		PainChance 40;
		SeeSound "spider/sight";
		PainSound "spider/pain";
		DeathSound "spider/death";
		ActiveSound "spider/active";
		Obituary "$OB_SPIDER";
		+BOSS
		+MISSILEMORE
		+NORADIUSDMG
		+DONTMORPH
		+BOSSDEATH
		+DONTHARMSPECIES;
		+NOINFIGHTSPECIES;
	}
	States
	{
	Spawn:
		SPID AB 10 A_Look;
		Loop;
	See:
		SPID A 3 A_Metal;
		SPID ABB 3 A_Chase;
		SPID C 3 A_Metal;
		SPID CDD 3 A_Chase;
		SPID E 3 A_Metal;
		SPID EFF 3 A_Chase;
		Loop;
	Missile:
		SPID A 20 BRIGHT A_FaceTarget;
		SPID GHGH 2 Bright Light("DMR_RifleFire") {
			A_FaceTarget();
			A_StartSound("MamaSpider/Fire", CHAN_WEAPON);				
			A_SpawnProjectile("SpiderSlowBullet",41,0,frandom(-12,12),CMF_OFFSETPITCH,frandom(-1,1));
		}
		TNT1 A 0 A_MonsterRefire(10,"See");
		Goto Missile+1;	
	Pain:
		SPID I 3;
		SPID I 3 A_Pain;
		Goto See;
	Death:
		SPID J 20 A_Scream;
		SPID K 10 A_NoBlocking();
		SPID LMNOPQR 6 Bright {
			A_SpawnItemEx("DMR_MastermindDeathExplosionSpawner");		
		}
		SPID S 50;
		SPID S -1 A_BossDeath;
		Stop;
	}
}

class SpiderSlowBullet : DMR_SlowBullet
{
	Default
	{
		DamageFunction(random(8,24));
		Decal "BigBulletChip";
	}
}