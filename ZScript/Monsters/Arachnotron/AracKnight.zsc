//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eric Ou
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Eric Ou)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class AracKnight : DMR_Arachnotron
{
	Default
	{		
		Tag "Mygalotron";
		Obituary "%o got wasted by a Mygalotron.";
		Health 750;
		Speed 10;
		Scale 1.1;
		Painchance 64;
	}
	states
	{
	Spawn:
		ARAC AB 10 A_Look;
		Loop;
	See:
		ARAC A 20;
		ARAC A 3 A_BabyMetal;
		ARAC ABBCC 3 A_Chase;
		ARAC D 3 A_BabyMetal;
		ARAC DEEFF 3 A_Chase;
		Goto See+1;
	Missile:
		TNT1 A 0 A_Jump(128, "Spam");
		Goto LargePlasma;
	LargePlasma:
		ARAC A 20 Bright A_FaceTarget;
		ARAC G 5 Bright Light("DMR_DemonPlasmaFire") A_SpawnProjectile("AracKnightPlasmaBolt",21,0,0,0,0);
		ARAC H 5 Bright;
		TNT1 A 0 A_Jump(48, "Spam"); //small chance to switch to spam attack
		ARAC A 8 Bright A_SpidRefire;
		Goto LargePlasma+1;
	Spam:
		ARAC A 20 Bright A_FaceTarget;
		ARAC G 3 Bright Light("DMR_DemonPlasmaFire") A_SpawnProjectile("AracKnightSpamPlasma",21,0,frandom(-0.5,0.5),0,0);
		ARAC H 3 Bright A_FaceTarget;
		ARAC G 3 Bright Light("DMR_DemonPlasmaFire") A_SpawnProjectile("AracKnightSpamPlasma",21,0,frandom(-0.5,0.5),0,0);
		ARAC H 3 Bright A_FaceTarget;
		ARAC G 3 Bright Light("DMR_DemonPlasmaFire") A_SpawnProjectile("AracKnightSpamPlasma",21,0,frandom(-0.5,0.5),0,0);
		ARAC H 3 Bright A_FaceTarget;
		TNT1 A 0 A_Jump(48, "LargePlasma"); //small chance to switch to large plasma attack
		ARAC A 6 Bright A_SpidRefire;
		Goto Spam+1;
	Pain:
		ARAC I 3;
		ARAC I 3 A_Pain;
		Goto See+1;
	Death:
		ARAC J 20 A_Scream;
		ARAC K 7 A_NoBlocking;
		ARAC LMNO 7;
		ARAC P -1 A_BossDeath;
		Stop;
	Raise:
		ARAC P 5;
		ARAC ONMLKJ 5;
		Goto See+1;
	}
}

class AracKnightPlasmaBolt : NoblePraetorianPlasmaRune
{  
	Default
	{
		Speed 25;
		DamageFunction(random(20,60));
		Scale 1.5;
		DeathSound "weapons/hellrunebig";
	}
	States
	{
	Spawn:
		PLRN AAABBB 2 Bright {
			A_SeekerMissile(1, 1);
			A_Weave(frandom(1,2.5), frandom(1,2.5), frandom(0.7,1.2), frandom(0.7,1.2));
			A_SetRoll(roll+15);
			A_SpawnItemEx("DMR_PlasmaRuneTrail",frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
		}
		Loop;
	Death:
		PLRX G 3 Bright A_Explode(10,48);
		PLRX HIJKLM 3 Bright;
		Stop;
	}
}

class AracKnightSpamPlasma : NoblePraetorianPlasmaRune
{  
	Default
	{
		DamageFunction(random(8,24));
		Speed 30;
		Scale 0.8;
	}
}