//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eric Ou
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Eric Ou)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Mygalotron : DMR_Arachnotron
{
	Default
	{		
		Tag "Mygalotron";
		Obituary "%o got wasted by a Mygalotron.";
		Health 750;
		Speed 10;
		Scale 1.1;
		Painchance 64;
	}
	states
	{
	Spawn:
		ARAC AB 10 A_Look;
		Loop;
	See:
		ARAC A 20;
		ARAC A 3 A_BabyMetal;
		ARAC ABBCC 3 A_Chase;
		ARAC D 3 A_BabyMetal;
		ARAC DEEFF 3 A_Chase;
		Goto See+1;
	Missile:
		TNT1 A 0 A_Jump(128, "Spam");
		Goto LargePlasma;
	LargePlasma:
		ARAC A 20 Bright A_FaceTarget;
		ARAC G 5 Bright Light("DMR_HellRuneFire") A_SpawnProjectile("DMR_HellRuneExplosive",21,0,0,0,0);
		ARAC H 5 Bright;
		TNT1 A 0 A_Jump(48, "Spam"); //small chance to switch to spam attack
		ARAC A 8 Bright A_SpidRefire;
		Goto LargePlasma+1;
	Spam:
		ARAC A 20 Bright A_FaceTarget;
		ARAC G 3 Bright Light("DMR_HellRuneSmall") A_SpawnProjectile("MygalotronBall",21,0,frandom(-0.5,0.5),0,0);
		ARAC H 3 Bright A_FaceTarget;
		ARAC G 3 Bright Light("DMR_HellRuneSmall") A_SpawnProjectile("MygalotronBall",21,0,frandom(-0.5,0.5),0,0);
		ARAC H 3 Bright A_FaceTarget;
		ARAC G 3 Bright Light("DMR_HellRuneSmall") A_SpawnProjectile("MygalotronBall",21,0,frandom(-0.5,0.5),0,0);
		ARAC H 3 Bright A_FaceTarget;
		TNT1 A 0 A_Jump(48, "LargePlasma"); //small chance to switch to large plasma attack
		ARAC A 6 Bright A_SpidRefire;
		Goto Spam+1;
	Pain:
		ARAC I 3;
		ARAC I 3 A_Pain;
		Goto See+1;
	Death:
		ARAC J 20 A_Scream;
		ARAC K 7 {
			A_NoBlocking();
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(10,-10),0,random(20,30));
		}
		ARAC LMNO 7;
		ARAC P -1 A_BossDeath;
		Stop;
	Raise:
		ARAC P 5;
		ARAC ONMLKJ 5;
		Goto See+1;
	}
}

//TODO unused
class MygalotronBall : DMR_HellRune
{  
	Default
	{
		DamageFunction(random(8,24));
		Speed 20;
		Scale 0.8;
	}
}

class MygalotronExplosiveBall : DMR_HellRuneExplosive
{  
	Default
	{
		Speed 16;
		DamageFunction(random(20,60));
	}
}