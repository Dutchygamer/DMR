//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Raven Software
//	ZScript:	Dutchygamer
//======================================================================

Class DMR_Arachnotron : DMR_MonsterBase 
{
	Default
	{
		Tag "Arachnotron";
		Bloodcolor "purple";
		Health 500;
		Gibhealth -30; // all medium tier monsters (demon -> hellknight) have 30 gibhealth
		Species "DMR_Spider";
		Radius 64;
		Height 64;
		Mass 600;
		Speed 12;
		PainChance 128;
		SeeSound "baby/sight";
		PainSound "baby/pain";
		DeathSound "baby/death";
		ActiveSound "baby/active";
		Obituary "$OB_BABY";
		+BOSSDEATH
		+DONTHARMSPECIES;
		+NOINFIGHTSPECIES;
	}
	States
	{
	Spawn:
		BSPI AB 10 A_Look;
		Loop;
	See:
		BSPI A 20;
		BSPI A 3 A_BabyMetal;
		BSPI ABBCC 3 A_Chase;
		BSPI D 3 A_BabyMetal;
		BSPI DEEFF 3 A_Chase;
		Goto See+1;
	Missile:
		BSPI A 20 bright A_FaceTarget;
		BSPI G 4 bright Light("DMR_ArachnotronFire") A_SpawnProjectile("DMR_ArachnotronPlasma",20,0,0,0);
		BSPI H 4 bright;
		BSPI H 1 bright A_SpidRefire;
		goto Missile+1;
	Pain:
		BSPI I 3;
		BSPI I 3 A_Pain;
		Goto See+1;
	Death:
		BSPI J 20 A_Scream;
		BSPI K 7 {
			A_NoBlocking();
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(10,-10),0,random(20,30));
		}
		BSPI LMNO 7;
		BSPI P -1 A_BossDeath;
		Stop;
	Raise:
		BSPI P 5;
		BSPI ONMLKJ 5;
		Goto See+1;
	}
}

class DMR_ArachnotronPlasma : DMR_ZombiePlasma
{
	Default
	{
		DamageFunction(random(10,30));
		SeeSound "baby/attack";
		DeathSound "baby/shotx";
		Decal "ArachnotronScorch";
		DMR_Projectile.ParticleColors "#1bff1b", "#53ff53", "#008300", "#c7ffc7";
	}
	states
	{
	Spawn:
		APLS AAABBB 2 Bright  {
			A_SpawnItemEx("DMR_ArachnotronPlasmaTrail",frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-20,-18),frandom(-3,3),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_ArachnotronPlasmaImpactFlare");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		APBX ABCDE 5 Bright;
		Stop;
	}
}