//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Tormentor667, Captain Toenail
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Tormentor667, Captain Toenail)
//	GLDEFs: 	Dreadopp
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class Weavotron : DMR_Arachnotron
{
	Default
	{
		Tag "Weavotron";
		Health 550;
		Obituary "%o was tagged by an Weavotron.";
	}
	States
	{
	Spawn:
		RSPI AB 10 A_Look;
		Loop;
	See:
		RSPI A 20;
		RSPI A 3 A_BabyMetal;
		RSPI ABBCC 3 A_Chase;
		RSPI D 3 A_BabyMetal;
		RSPI DEEFF 3 A_Chase;
		Goto See+1;
	Missile:
		TNT1 A 0 A_Jump(96,"Plasma");
	Laser:
		//TNT1 A 0 A_Jump(64,2);
		TNT1 A 0 A_JumpIfCloser(400,"PlasmaClose");
		RSPI G 20 A_FaceTarget;
		RSPI H 10 Bright Light("DMR_LaserFire") A_SpawnProjectile("DMR_LaserProjectile",20,0,random(-2,2));
		RSPI G 6;
		Goto See+1;
	Plasma:
		RSPI G 20 A_FaceTarget;
		RSPI H 4 Bright Light("DMR_LaserFire") {
			A_StartSound("baby/attack", CHAN_WEAPON);
			A_SpawnProjectile("WeavotronPlasma",20,0);
		}
		RSPI G 4 Bright;
		RSPI G 1 Bright A_SpidRefire;
		Goto Plasma+1;
	PlasmaClose: // up close start spamming plasma in a random arc
		RSPI G 20 A_FaceTarget;
		RSPI H 3 Bright Light("DMR_LaserFire") {
			A_StartSound("baby/attack", CHAN_WEAPON);
			A_SpawnProjectile("WeavotronPlasma",20,0,random(-2,2));
		}
		RSPI G 3 Bright;
		RSPI G 1 Bright A_SpidRefire;
		Goto PlasmaClose+1;
	Pain:
		RSPI I 3;
		RSPI I 3 A_Pain;
		Goto See+1;
	Death:
		RSPI J 20 A_Scream;
		RSPI K 7 {
			A_NoBlocking();
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(10,-10),0,random(20,30));
		}
		RSPI LMNO 7;
		RSPI P -1 A_BossDeath;
		Stop;
    Raise:
		RSPI P 5;
		RSPI ONMLKJ 5;
		Goto See+1;
	}
}


class WeavotronPlasma : DMR_ArachnotronPlasma
{
	Default
	{
		Speed 35;		
		DamageFunction(random(4,12));
		Scale 0.4;
		SeeSound "";
		Decal "RedPlasmaScorch";
		DMR_Projectile.ParticleColors "#ff0000", "#d20000", "#b90000", "#ff0f0f";
	}
	states
	{
	Spawn:
		LASB A 2 Bright {
			A_SpawnItemEx("DMR_RedPlasmaTrail",frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-20,-18),frandom(-3,3),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_RedPlasmaImpactFlare");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		HPLB EFGHIJ 4 Bright;
		Stop;
	}
}