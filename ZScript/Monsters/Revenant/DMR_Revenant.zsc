//======================================================================
//  Original credits
//======================================================================
//  XDeath Sprites: 	Eriance
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer	
//======================================================================

Class DMR_Revenant : DMR_MonsterBase 
{
	Default
	{
		Tag "Revenant";
		Health 300;
		Gibhealth -30; // all medium tier monsters (demon -> hellknight) have 30 gibhealth
		Species "DMR_Skelly";
		Radius 20;
		Height 56;
		Mass 500;
		Speed 10;
		PainChance 100;
		MeleeThreshold 196;
		SeeSound "skeleton/sight";
		PainSound "skeleton/pain";
		DeathSound "skeleton/death";
		ActiveSound "skeleton/active";
		MeleeSound "skeleton/melee";
		HitObituary "$OB_UNDEADHIT";
		Obituary "$OB_UNDEAD";
		+MISSILEMORE;
		+DONTHARMSPECIES;
		+NOINFIGHTSPECIES;
	}
	States
	{
	Spawn:
		SKEL AB 10 A_Look;
		Loop;
	See:
		SKEL AABBCCDDEEFF 2 A_Chase;
		Loop;
	Melee:
		TNT1 A 0 A_FaceTarget;
		SKEL G 6 A_SkelWhoosh;
		SKEL H 6 A_FaceTarget;
		SKEL I 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		goto See;
	Missile:
		SKEL J 10 Bright A_FaceTarget;
		TNT1 A 0 A_Jump(128,"MissileRight");
		Goto MissileLeft;
	MissileLeft:
		SKEL K 5 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_RevenantTracer",56,-10);
		SKEL K 15 A_FaceTarget;
		Goto See;
	MissileRight:
		SKEL K 5 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_RevenantTracer",56,10);
		SKEL K 15 A_FaceTarget;
		Goto See;
	Pain:
		SKEL L 5;
		SKEL L 5 A_Pain;
		Goto See;
	Death:
		SKEL LM 7;
		SKEL N 7 A_Scream;
		SKEL O 7 A_NoBlocking;
		SKEL P 7;
		SKEL Q -1;
		Stop;
	XDeath:
		SKEL R 5;
		SKEL S 5 A_XScream;
		SKEL T 5 A_NoBlocking;
		SKEL UVWX 5;
		SKEL Y -1;
		stop;
	Raise:
		SKEL Q 5;
		SKEL PONML 5;
		Goto See;
	}
}

class DMR_RevenantTracer : DMR_Projectile
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 10;
		DamageFunction(random(16,48));
		+SEEKERMISSILE 
		+RANDOMIZE
		SeeSound "skeleton/attack";
		DeathSound "skeleton/tracex";
		RenderStyle "Add";
		Decal "RevenantScorch";
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(128,"Homing");
	Normal:
		FATB AB 2 Bright {
			A_SpawnItemEx("DMR_FireballTrail",-9,0,4,frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Homing:
		FATB AB 2 Bright {
			A_SeekerMissile(1,8,SMF_PRECISE); 
			A_SpawnItemEx("DMR_FireballTrail",-9,0,4,frandom(-1,0),frandom(-1,1),frandom(0,1));
			A_SpawnItemEx("DMR_SmokeCloud",-9,0,4,frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {	
			A_SpawnItemEx("DMR_FireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		FBXP A 8 Bright;
		FBXP B 6 Bright;
		FBXP C 4 Bright;
		Stop;
	}
}