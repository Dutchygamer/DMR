//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance, Blue Shadow, MagicWazard
//	Sounds:		Midway, Rogue Entertainment, Raven Software
//  Sprites: 	Id Software, Midway, Raven Software (Monster); Nintendo (Puff) (edits by Eriance (Monster); MagicWazard (Puff))
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//	Sounds:		Westwood Studios	
//======================================================================

class Ghoul : DMR_Revenant
{
	Default
	{
		Tag "Ghoul";
		Health 500;
		PainChance 80;
		Obituary "%o was killed by a ghoul's glaring eyes.";
		HitObituary "%o was smashed by a ghoul.";
		SeeSound "monster/incsit";
		DeathSound "monster/incdth";
		ActiveSound "monster/incact";
		AttackSound "monster/incatk";
		Speed 12;
	}
	States
	{
	Spawn:
		INCA AB 10 A_Look;
		Loop;
	See:
		INCA AABBCCDDEEFF 2 A_Chase;
		Loop;
	Melee:
		INCA G 0 A_Jump(128,5); //half chance to do a double-punch
		INCA G 0 A_FaceTarget;
		INCA G 6 A_SkelWhoosh;
		INCA H 6 A_FaceTarget;
		INCA I 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		Goto See;
		INCA R 0 A_FaceTarget;
		INCA R 6 A_SkelWhoosh;
		INCA S 6 A_FaceTarget;
		INCA T 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		INCA G 0 A_FaceTarget;
		INCA G 6 A_SkelWhoosh;
		INCA H 6 A_FaceTarget;
		INCA I 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		Goto See;
	Missile:	
		INCA J 10 Bright A_FaceTarget;
		INCA U 5 Bright A_SpawnProjectile("GhoulTracer",58,0);
		INCA K 15 A_FaceTarget;
		Goto See;
	Pain:
		INCA L 5;
		INCA L 5 A_Pain;
		Goto See;
	Death:	
		INCA LM 7;
		INCA N 7 A_Scream;
		INCA O 7 A_NoBlocking;
		INCA P 7;
		INCA Q -1;
		Stop;
	XDeath:
		INCX A 10 Bright A_Playsound("monster/incexp");
		INCX BC 5 Bright;
		INCX D 5 Bright A_NoBlocking;
		TNT1 A 0 A_SpawnItem("HadesSphere",0,32);
		INCX EFGHIJ 5 Bright;
		INCX K -1;
		Stop;
	Raise:
		INCA Q 0 A_KillChildren;
		INCA QPONML 5;
		Goto See;
	}
}

class GhoulTracer : DMR_RevenantTracer
{
	Default
	{
		Speed 15;
		DamageFunction(random(16,48));
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(128,"Homing"); 
		FATB AB 2 Bright {
			A_SeekerMissile(1,5,SMF_PRECISE|SMF_CURSPEED);
			A_SpawnItemEx("DMR_FireballTrail",-12,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1)); 			
			A_SpawnItemEx("DMR_SmokeCloud",-12,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
		Loop;
	Death:
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 3;fxLoop++) {
				A_SpawnItemEx("DMR_FireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-5,5),frandom(-5,5),frandom(-1,5));
			}
		}
		FBXP A 8 Bright;
		FBXP B 6 Bright;
		FBXP C 4 Bright;
		Stop;
	}
}