//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance, Blue Shadow, MagicWazard
//	Sounds:		Midway, Rogue Entertainment, Raven Software
//  Sprites: 	Id Software, Midway, Raven Software (Monster); Nintendo (Puff) (edits by Eriance (Monster); MagicWazard (Puff))
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//	Sounds:		Westwood Studios	
//======================================================================

class Ghoul : DMR_Revenant
{
	Default
	{
		Tag "Ghoul";
		Health 500;
		PainChance 80;
		Obituary "%o was haunted by a ghoul.";
		HitObituary "%o was smashed by a ghoul.";
		SeeSound "monster/incsit";
		DeathSound "monster/incdth";
		ActiveSound "monster/incact";
		AttackSound "monster/incatk";
		Speed 12;
	}
	States
	{
	Spawn:
		INCA AB 10 A_Look;
		Loop;
	See:
		INCA AABBCCDDEEFF 2 A_Chase;
		Loop;
	Melee:
		TNT1 A 0 A_Jump(128,5); // half chance to do a double-punch
		TNT1 A 0 A_FaceTarget;
		INCA G 6 A_SkelWhoosh;
		INCA H 6 A_FaceTarget;
		INCA I 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		Goto See;
		TNT1 A 0 A_FaceTarget;
		INCA R 6 A_SkelWhoosh;
		INCA S 6 A_FaceTarget;
		INCA T 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		TNT1 A 0 A_FaceTarget;
		INCA G 6 A_SkelWhoosh;
		INCA H 6 A_FaceTarget;
		INCA I 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		Goto See;
	Missile:	
		TNT1 A 0 A_Jump(128, "RapidBalls");
	Homing:	
		INCA J 10 Bright A_FaceTarget;
		INCA U 5 Bright A_SpawnProjectile("GhoulTracer",60,0);
		INCA K 15 A_FaceTarget;
		Goto See;
	RapidBalls:
		TNT1 A 0 {
			// rapid balls fire from too high and don't lock on at start, so use homing tracer instead
			if (target && Distance3D(target) < 300) SetStateLabel("Homing");
		}
		INCA JJ 10 Bright A_FaceTarget;
		TNT1 A 0 { attackloop = 0; }
	RapidBallsLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 3,"RapidBallsEnd");
		INCA U 4 Bright {
			A_FaceTarget();
			attackloop++;
			A_SpawnProjectile("GhoulRapidBall",58,-6,5,CMF_AIMOFFSET);
			A_SpawnProjectile("GhoulRapidBall",58,6,-5,CMF_AIMOFFSET);
		}
		Loop;
	RapidBallsEnd:
		INCA K 8 A_FaceTarget;
		Goto See;
	Pain:
		INCA L 5;
		INCA L 5 A_Pain;
		Goto See;
	Death:	
		INCA LM 7;
		INCA N 7 A_Scream;
		INCA O 7 A_NoBlocking;
		INCA P 7;
		INCA Q -1;
		Stop;
	XDeath:
		INCX A 10 Bright A_Playsound("monster/incexp");
		INCX BC 5 Bright;
		INCX D 5 Bright A_NoBlocking;
		TNT1 A 0 A_SpawnItem("HadesSphere",0,32);
		INCX EFGHIJ 5 Bright;
		INCX K -1;
		Stop;
	Raise:
		INCA Q 0 A_KillChildren;
		INCA QPONML 5;
		Goto See;
	}
}

class GhoulTracer : DMR_HellRune
{  
	Default
	{
		Speed 10;
		DamageFunction(random(16,48));
		Scale 1.2;
	}
	States
	{
	Spawn:
		// wait a bit before acquiring target
		ARAX AABBAABB 2 Bright;
		TNT1 A 0 {
			A_SetRenderStyle(1.0,STYLE_Add);
			A_ChangeVelocity(vel.x*1.5,vel.y*1.5,vel.z,CVF_REPLACE);
		}
	SpawnLoop:
		// actually start weaving and homing
		PLRN AAAABBBB 1 Bright {
			A_SeekerMissile(1,5,SMF_PRECISE|SMF_CURSPEED);	
			A_Weave(frandom(1,2.5), frandom(1,2.5), frandom(0.7,1.2), frandom(0.7,1.2));
			A_SetRoll(roll+15);
			A_SpawnItemEx("DMR_PlasmaRuneTrail",frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
		}
		Loop;
	}
}

class GhoulRapidBall : DMR_HellRune
{  
	Default
	{
		DamageFunction(random(8,24));
		Speed 18;
		Scale 0.8;
	}
}