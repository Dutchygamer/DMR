//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance, Blue Shadow, MagicWazard
//	Sounds:		Midway, Rogue Entertainment, Raven Software
//  Sprites: 	Id Software, Midway, Raven Software (Monster); Nintendo (Puff) (edits by Eriance (Monster); MagicWazard (Puff))
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//	Sounds:		Westwood Studios	
//======================================================================

class Ghoul : DMR_Revenant
{
	Default
	{
		Tag "Ghoul";
		Health 500;
		PainChance 80;
		Obituary "%o was haunted by a Ghoul.";
		HitObituary "%o was smashed by a Ghoul.";
		SeeSound "monster/incsit";
		DeathSound "monster/incdth";
		ActiveSound "monster/incact";
		AttackSound "monster/incatk";
		Speed 12;
		+MISSILEMORE;
	}
	States
	{
	Spawn:
		INCA AB 10 A_Look;
		Loop;
	See:
		INCA AABBCCDDEEFF 2 A_Chase;
		Loop;
	Melee:
		INCA G 6 A_SkelWhoosh;
		INCA H 6 A_FaceTarget;
		INCA I 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		TNT1 A 0 A_Jump(128,"See");
		INCA G 6 A_SkelWhoosh;
		INCA H 6 A_FaceTarget;
		INCA I 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		Goto See;
	Missile:	
		TNT1 A 0 A_Jump(128, "RapidBalls");
	Homing:	
		INCA J 10 Bright A_FaceTarget;
		INCA U 5 Bright Light("DMR_HellRuneFire") A_SpawnProjectile("GhoulTracer",56,0);
		INCA K 15 A_FaceTarget;
		Goto See;
	RapidBalls:
		TNT1 A 0 {
			// rapid balls fire straight forward from too high at start, so use homing tracer instead
			if (target && Distance3D(target) < 300) SetStateLabel("Homing");
		}
		INCA JJ 10 Bright A_FaceTarget;
		TNT1 A 0 { attackloop = 0; }
	RapidBallsLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 3,"RapidBallsEnd");
		INCA U 4 Bright Light("DMR_HellRuneFire") {
			A_FaceTarget();
			attackloop++;
			A_SpawnProjectile("DMR_HellRuneSmall",56,-6,5,CMF_AIMOFFSET);
			A_SpawnProjectile("DMR_HellRuneSmall",56,6,-5,CMF_AIMOFFSET);
		}
		Loop;
	RapidBallsEnd:
		INCA K 8 A_FaceTarget;
		Goto See;
	Pain:
		INCA L 5;
		INCA L 5 A_Pain;
		Goto See;
	Death:	
		INCA LM 7;
		INCA N 7 A_Scream;
		INCA O 7 A_NoBlocking;
		INCA P 7;
		INCA Q -1;
		Stop;
	XDeath:
		INCX A 5 Bright A_StartSound("monster/incexp");
		INCX BC 5 Bright;
		INCX D 5 Bright {			
			A_SpawnItemEx("GhoulDeathExplosion",0,0,56);
			A_NoBlocking();
		}
		INCX EFGHIJ 5 Bright;
		INCX K -1;
		Stop;
	Raise:
		INCA QPONML 5;
		Goto See;
	}
}

// basically the same as DMR_HellRune with more accurate homing
class GhoulTracer : DMR_HellRune
{
	States
	{
	Spawn:
		// wait a bit before acquiring target
		ARAX AABBAABB 2 Bright;
		TNT1 A 0 {
			A_SetRenderStyle(1.0,STYLE_Add);
			A_ChangeVelocity(vel.x*1.5,vel.y*1.5,vel.z,CVF_REPLACE);
		}
	SpawnLoop:
		// actually start weaving and homing
		PLRN AAAABBBB 1 Bright {
			A_SeekerMissile(1,5,SMF_PRECISE|SMF_CURSPEED);	
			A_Weave(frandom(1,2.5), frandom(1,2.5), frandom(0.7,1.2), frandom(0.7,1.2));
			A_SetRoll(roll+15);
			A_SpawnItemEx("DMR_PlasmaRuneTrail",frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
		}
		Loop;
	}
}

Class GhoulDeathExplosion : DMR_BossDeathExplosion
{
	Default
	{
		+NOINTERACTION;
		Renderstyle "Add";
		DMR_ComplexFXBase.ParticleColors "#ff0000", "#d20000", "#b90000", "#ff0f0f";
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_StartSound("weapons/rocklx", CHAN_WEAPON);
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop<=50;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(18,24),frandom(12,14),random(0,360),frandom(0,11),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		MISL B 8 Bright Light("DMR_RedRocket_X1");
		MISL C 6 Bright Light("DMR_RedRocket_X2");
		MISL D 4 Bright Light("DMR_RedRocket_X3");
		Stop;
	}
}