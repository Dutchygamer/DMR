//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance, Blue Shadow, MagicWazard
//	Sounds:		Midway, Rogue Entertainment, Raven Software
//  Sprites: 	Id Software, Midway, Raven Software (Monster); Nintendo (Puff) (edits by Eriance (Monster); MagicWazard (Puff))
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//	Sounds:		Westwood Studios	
//======================================================================

class Ghoul : DMR_Revenant
{
	Default
	{
		Tag "Ghoul";
		Health 500;
		PainChance 80;
		Obituary "%o was haunted by a ghoul.";
		HitObituary "%o was smashed by a ghoul.";
		SeeSound "monster/incsit";
		DeathSound "monster/incdth";
		ActiveSound "monster/incact";
		AttackSound "monster/incatk";
		Speed 12;
	}
	States
	{
	Spawn:
		INCA AB 10 A_Look;
		Loop;
	See:
		INCA AABBCCDDEEFF 2 A_Chase;
		Loop;
	Melee:
		INCA G 0 A_Jump(128,5); // half chance to do a double-punch
		INCA G 0 A_FaceTarget;
		INCA G 6 A_SkelWhoosh;
		INCA H 6 A_FaceTarget;
		INCA I 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		Goto See;
		INCA R 0 A_FaceTarget;
		INCA R 6 A_SkelWhoosh;
		INCA S 6 A_FaceTarget;
		INCA T 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		INCA G 0 A_FaceTarget;
		INCA G 6 A_SkelWhoosh;
		INCA H 6 A_FaceTarget;
		INCA I 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		Goto See;
	Missile:	
		INCA J 10 Bright A_FaceTarget;
		INCA U 5 Bright A_SpawnProjectile("GhoulTracer",58,0);
		INCA K 15 A_FaceTarget;
		Goto See;
	Pain:
		INCA L 5;
		INCA L 5 A_Pain;
		Goto See;
	Death:	
		INCA LM 7;
		INCA N 7 A_Scream;
		INCA O 7 A_NoBlocking;
		INCA P 7;
		INCA Q -1;
		Stop;
	XDeath:
		INCX A 10 Bright A_Playsound("monster/incexp");
		INCX BC 5 Bright;
		INCX D 5 Bright A_NoBlocking;
		TNT1 A 0 A_SpawnItem("HadesSphere",0,32);
		INCX EFGHIJ 5 Bright;
		INCX K -1;
		Stop;
	Raise:
		INCA Q 0 A_KillChildren;
		INCA QPONML 5;
		Goto See;
	}
}

class GhoulTracer : DMR_RevenantTracer
{
	Default
	{
		Speed 15;
		DamageFunction(random(16,48));
		Scale 0.8;
	}
	States
	{
	Spawn:
		SHBA A 1 Bright {
			A_SeekerMissile(1,5,SMF_PRECISE|SMF_CURSPEED);			
			A_SpawnItemEx("GhoulTracerTrail",-25);
		}
		SHBA A 1 Bright {
			A_SpawnItemEx("GhoulTracerTrail",-25);
		}
		SHBA B 1 Bright {
			A_SeekerMissile(1,5,SMF_PRECISE|SMF_CURSPEED);
			A_SpawnItemEx("GhoulTracerTrail",-25);
		}
		SHBA B 1 Bright {
			A_SpawnItemEx("GhoulTracerTrail",-25);
		}
		Loop;
	Death:
		SHBA CDEFGHIJKL 4 Bright;
		Stop;
	}
}

class GhoulTracerTrail : DMR_ProjectileTrail
{
	Default
	{
		Alpha 0.5;
		Scale 1.5;
	}
	States
	{
	Spawn:
		SBTR ABCDEFG 2 Bright {		
			A_FadeOut(0.05);
			A_SetScale(Scale.X-0.08);
		}
		Stop;
	}
}
