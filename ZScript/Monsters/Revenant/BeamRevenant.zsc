//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Captain Ventris
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Spectator)
//	GLDEFs: 	Captain Ventris
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//	Sounds:		Westwood Studios
//  Sprites:	Dutchygamer (recolour of original sprites)
//======================================================================

class Dybbuk : DMR_Revenant
{
	Default
	{
		Tag "Dybbuk";
		HitObituary "%o was punched by a Dybbuk.";
		obituary "%o was shafted by a Dybbuk.";
	}
	States
	{
	Spawn:
		RASK AB 10 A_Look;
		Loop;
	See:
		RASK AABBCCDDEEFF 2 A_Chase;
		Loop;
	Melee:
		RASK G 0 A_FaceTarget;
		RASK G 6 A_SkelWhoosh;
		RASK H 6 A_FaceTarget;
		RASK I 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(96, "Laser");
		Goto Ball;
	Laser:
		RASK J 20 Bright A_FaceTarget;
		RASK K 5 Bright Light("DMR_LaserFire") A_SpawnProjectile("SmallRedLaser",65,13,random(-2,2));
		RASK K 15 A_FaceTarget;
		Goto See;
	Ball:
		RASK J 20 Bright A_FaceTarget;
		RASK KKK 8 Bright Light("DMR_LaserFire") {
			A_PlaySound("baby/attack", CHAN_WEAPON);
			A_SpawnProjectile("OpiliotronPlasma",65,13);
		}
		RASK K 15 A_FaceTarget;
		Goto See;
	Pain:
		RASK L 5;
		RASK L 5 A_Pain;
		Goto See;
	Death:
	XDeath:
		RASK LM 7;
		RASK N 7 A_Scream;
		RASK O 7 A_NoBlocking;
		RASK P 7;
		RASK Q -1;
		Stop;
	Raise:
		RASK Q 5;
		RASK PONML 5;
		Goto See;
	}
}

// laser projectile
class SmallRedLaser : DMR_LaserBase
{
	Default
	{
		DamageFunction(random(10,30));
		Translation "112:127=176:191";
		MissileType "DMR_LaserTrailBase";
		MissileHeight 10;
		Seesound "BeamRevenant/Fire";
		Decal "RedLaserLightning";
	}
}

//TODO unused
class SmallRedLaserTrail : DMR_LaserTrailBase { }

//TODO unused
class DybbukTracer : DMR_RevenantTracer
{
	Default
	{
		DamageFunction(random(6,16));
		Speed 15;
		Scale 0.5; //0.4;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(128,"Homing"); 
		LASB AA 2 Bright {
			A_SpawnItemEx("DMR_SmallFireballTrail",-8,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1));
		}
		Goto Spawn+1;
	Homing:
		LASB AA 2 Bright {
			A_SeekerMissile(1,8,SMF_PRECISE); 
			A_SpawnItemEx("DMR_SmallFireballTrail",-8,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1));
			//A_SpawnItemEx("DMR_SmallSmokeCloud",-8,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1));
		}
		Loop;
	Death:
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 3;fxLoop++) {
				A_SpawnItemEx("DMR_FireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-5,5),frandom(-5,5),frandom(-1,5));
			}
		}
		HPLB E 6 Bright;
		HPLB F 4 Bright;
		HPLB G 3 Bright;
		HPLB H 3 Bright;
		Stop;
	}
}