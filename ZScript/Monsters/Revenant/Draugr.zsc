//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eric Ou
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Eric Ou)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//	Sounds:		Westwood Studios
//======================================================================

class Draugr : DMR_Revenant
{
	Default
	{
		Tag "Draugr";
		Health 650;
		Obituary "%o got blasted by a draugr.";
		HitObituary "%o was beaten to death by a draugr.";
		SeeSound "skeleton/sight";
		PainSound "skeleton/pain";
		DeathSound "skeleton/death";
		ActiveSound "skeleton/active";
		MeleeSound "skeleton/melee";
		//Speed 8;
		PainChance 70;
		//MeleeThreshold 0;
	}
	
	States 
	{ 
	Spawn:
		SKE2 AB 10 A_Look;
		Loop;
	See:    
		SKE2 AABBCCDDEEFF 2 A_Chase;
		Loop;
	Melee:
		SKE2 J 0 A_FaceTarget;
		SKE2 J 6 A_SkelWhoosh;
		SKE2 K 6 A_FaceTarget;
		SKE2 L 6 A_CustomMeleeAttack(random(16,48),"skeleton/melee");
		TNT1 A 0 {
			// toast your target if still within range
			if (target && Distance3D(target) < 180) SetStateLabel("Flamethrower");
		}
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(128,"ClusterRocket","Flamethrower");
		Goto See;
	HERocket:
		SKE2 GG 6 Bright {
			targetLock = false;
			A_FaceTarget();
		}
		SKE2 G 6 {
			if (target && CheckSight(target)) {
				targetLock = true;
			}
			A_FaceTarget();
		}
		SKE2 H 8 Bright Light("DMR_RifleFire") {
			if (targetLock) { A_SpawnProjectile("DraugrHomingTracer",64,-14); }
			else { A_SpawnProjectile("DraugrTracer",64,-14); }
		}
		SKE2 I 10;
		Goto See;
	ClusterRocket:
		TNT1 A 0 {
			// cluster missiles suck at close range, so use normal rocket instead
			if (target && Distance3D(target) < 300) SetStateLabel("HERocket");
		}
		SKE2 GG 7 Bright A_FaceTarget;
		SKE2 H 8 Bright Light("DMR_RifleFire") A_SpawnProjectile("DraugrClusterMissile",64,-14);
		SKE2 I 10;
		Goto See;
	Flamethrower:
		TNT1 A 0 {
			if (target && Distance3D(target) > 300) SetStateLabel("HERocket");
		}
		SKE2 GG 7 Bright A_FaceTarget;
		TNT1 A 0 { 
			attackloop = 0; 
			A_PlaySound("flamethrower/fire", CHAN_WEAPON);
		}
	FlamethrowerLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 24,"FlamethrowerEnd");
		SKE2 H 1 Bright Light("DMR_RifleFire") {
			A_FaceTarget();
			attackloop++;
			A_SpawnProjectile("DMR_FlamethrowerBall",64,-14,random(-3,3));
		}
		Loop;
	FlamethrowerEnd:
		SKE2 G 8 A_FaceTarget;
		Goto See;
	Pain:
		SKE2 M 5;
		SKE2 M 5 A_Pain;
		Goto See;
	Death:
	XDeath:
		SKE2 N 7;
		SKE2 O 7 A_Scream;
		SKE2 P 7 A_NoBlocking;
		SKE2 QR 7;
		SKE2 S -1;
		Stop;
	Raise:
		SKE2 SRQPON 5;
		Goto See;
	} 
}

class DraugrTracer : DMR_SmallRocket
{
	Default
	{
		DamageFunction(random(16,48));
		Speed 20;
	}
	States
	{
	Spawn:
		FBRS A 1 Bright {
			A_SpawnItemEx("DMR_FireballTrail",-30,0,1,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			A_SpawnItemEx("DMR_SmokeCloud",-30,0,1,frandom(-1,0));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-30,-26),frandom(-6,6),frandom(-5,5),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0,STYLE_Add);
			A_SetScale(1.0);
		}
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 4;fxLoop++) {
				A_SpawnItemEx("DMR_FireBouncingDebris",random(-2,2),random(-2,2),random(-2,2),frandom(-8,8),frandom(-8,8),frandom(-2,8)); 
			}
		}
		MISL B 8 Bright A_Explode(48, 48);
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}

class DraugrHomingTracer : DraugrTracer
{
	Default
	{
		Speed 10;
		+SEEKERMISSILE;
		Scale 1.0;
	}
	States
	{
	Spawn:
		FBRS A 1 Bright {
			A_SeekerMissile(1,5,SMF_PRECISE|SMF_CURSPEED);
			A_SpawnItemEx("DMR_FireballTrail",-28,0,3,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			A_SpawnItemEx("DMR_SmokeCloud",-28,0,3,frandom(-1,0));
		}
		Loop;
	}
}

// homing missile that breaks up in partially homing clusters near its target
class DraugrClusterMissile : DMR_Rocket
{
	Default
	{
		Speed 10;
		DamageFunction(random(10,30));
		Obituary "%o couldn't dodge a draugr's clustermissile.";
		Scale 0.8;
		+SEEKERMISSILE;
		SeeSound "draugr/cluster";
		DeathSound "";
		Decal "Scorch";
	}
	States
	{
	Spawn:
		HMIS A 1 Bright {
			A_SeekerMissile(1,8,SMF_PRECISE);
			A_SpawnItemEx("DMR_FireballTrail",-28,0,2,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			A_SpawnItemEx("DMR_SmokeCloud",-28,0,2,frandom(-1,0));
		}
		TNT1 A 0 A_JumpIfTracerCloser(150, "Cluster");
		Loop;
	Cluster:
		TNT1 A 0 {
			A_PlaySound("weaponssmallrocket/launch",1);
			A_SpawnProjectile("CoreDefenderClusterSubProjectile",2,0,random(-5,-10),CMF_AIMDIRECTION|CMF_TRACKOWNER,random(0,-5));
			A_SpawnProjectile("CoreDefenderClusterSubProjectile",2,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(0,-5));
			A_SpawnProjectile("CoreDefenderClusterSubProjectile",2,0,random(5,10),CMF_AIMDIRECTION|CMF_TRACKOWNER,random(0,-5));
		}
	Death:
		TNT1 A 0 {
			A_Stop();
			A_SetRenderStyle(1.0,STYLE_Add);	
			A_PlaySound("weapons/rocklx",2);
		}
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 3;fxLoop++) {
				A_SpawnItemEx("DMR_FireBouncingDebris",random(-2,2),random(-2,2),random(-2,2),frandom(-8,8),frandom(-8,8),frandom(-2,8)); 
			}
		}
		MISL B 8 Bright A_Explode(10,48);
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}