//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eric Ou
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Eric Ou)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//	Sounds:		Westwood Studios
//======================================================================

class Draugr : DMR_Revenant
{
	Default
	{
		Tag "Draugr";
		Health 650;
		Obituary "%o got blasted by a draugr.";
		HitObituary "%o was beaten to death by a draugr.";
		SeeSound "skeleton/sight";
		PainSound "skeleton/pain";
		DeathSound "skeleton/death";
		ActiveSound "skeleton/active";
		MeleeSound "skeleton/melee";
		Speed 8;
		Painchance 80;
		MeleeThreshold 0;
	}
	
	States 
	{ 
	Spawn:
		SKE2 AB 10 A_Look;
		Loop;
	See:    
		SKE2 AABBCCDDEEFF 2 A_Chase;
		Loop;
	Melee:
		TNT1 A 0 A_Jump(128, "Flamethrower"); //chance to use flamethrower for melee
		SKE2 J 0 A_FaceTarget;
		SKE2 J 6 A_SkelWhoosh;
		SKE2 K 6 A_FaceTarget;
		SKE2 L 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(256, "ClusterRocket", "Flamethrower");
		Goto See;
	HERocket:
		TNT1 A 0 A_Jump(128, "HomingRocket");
	FastRocket:
		SKE2 G 15 A_FaceTarget;
		SKE2 H 5 Bright A_SpawnProjectile("DraugrTracer", 64, -14);
		SKE2 I 15 A_FaceTarget;
		Goto See;
	HomingRocket:
		SKE2 G 15 A_FaceTarget;
		SKE2 H 5 Bright A_SpawnProjectile("DraugrHomingTracer", 64, -14);
		SKE2 I 15 A_FaceTarget;
		Goto See;
	ClusterRocket:
		TNT1 A 0 {
			// cluster missiles suck at close range, so use normal rocket instead
			if (target && Distance3D(target) < 300) SetStateLabel("HERocket");
		}
		SKE2 G 15 Bright A_FaceTarget;
		SKE2 H 5 Bright A_SpawnProjectile("DraugrClusterMissile", 64, -14);
		SKE2 I 15 A_FaceTarget;
		Goto See;
	Flamethrower:
		TNT1 A 0 {
			if (target && Distance3D(target) > 300) SetStateLabel("HERocket");
		}
		SKE2 GG 10 Bright A_FaceTarget;
		TNT1 A 0 { attackloop = 0; }
		TNT1 A 0 Bright A_PlaySound("flamethrower/fire", CHAN_WEAPON);		
	FlamethrowerLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 12,"FlamethrowerEnd");
		SKE2 H 1 Bright {
			A_FaceTarget();
			attackloop++;
			A_SpawnProjectile("DMR_FlamethrowerBall", 64, -14, random(-3,3));
		}
		Loop;
	FlamethrowerEnd:
		SKE2 G 8 A_FaceTarget;
		Goto See;
	Pain:
		SKE2 M 5;
		SKE2 M 5 A_Pain;
		Goto See;
	Death:
	XDeath:
		SKE2 N 7;
		SKE2 O 7 A_Scream;
		SKE2 P 7 A_NoBlocking;
		SKE2 QR 7;
		SKE2 S -1;
		Stop;
	Raise:
		SKE2 SRQPON 5;
		Goto See;
	} 
}

class DraugrTracer : DMR_SmallRocket
{
	Default
	{
		DamageFunction(random(16,48));
		Speed 20;
	}
	States
	{
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0,STYLE_Add);
			A_SetScale(1.0);
		}
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 4;fxLoop++) {
				A_SpawnItemEx("DMR_FireBouncingDebris",random(-2,2),random(-2,2),random(-2,2),frandom(-8,8),frandom(-8,8),frandom(-2,8)); 
			}
		}
		MISL B 8 Bright A_Explode(48, 48);
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}

class DraugrHomingTracer : DraugrTracer
{
	Default
	{
		Speed 10;
		+SEEKERMISSILE;
		Scale 1.0;
	}
	States
	{
	Spawn:
		FBRS A 1 Bright {
			A_SeekerMissile(1,5,SMF_PRECISE|SMF_CURSPEED);
			A_SpawnItemEx("DMR_FireballTrail",-28,0,3,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			A_SpawnItemEx("DMR_SmokeCloud",-28,0,3,frandom(-1,0));
		}
		Loop;
	}
}

// homing missile that breaks up in partially homing clusters near its target
class DraugrClusterMissile : DMR_Rocket
{
	Default
	{
		Speed 10;
		DamageFunction(random(10,30));
		Obituary "%o couldn't dodge a draugr's clustermissile.";
		Scale 0.8;
		+SEEKERMISSILE;
		SeeSound "draugr/cluster";
		DeathSound "weapons/rocklx";
	}
	States
	{
	Spawn:
		HMIS A 1 Bright {
			A_SeekerMissile(1,8,SMF_PRECISE);
			A_SpawnItemEx("DMR_FireballTrail",-28,0,2,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			A_SpawnItemEx("DMR_SmokeCloud",-28,0,2,frandom(-1,0));
		}
		TNT1 A 0 A_JumpIfTracerCloser(120, "Death");
		Loop;
	Death:
		TNT1 A 0 {
			A_Stop();
			A_SetRenderStyle(1.0,STYLE_Add);
			// split up in subprojectiles, which can randomly home in or not
			A_SpawnProjectile("DraugrSubProjectile",0,0,0+random(0,370));
			A_SpawnProjectile("DraugrSubProjectile",0,0,0+random(0,370));
			A_SpawnProjectile("DraugrSubProjectile",0,0,0+random(0,370));
		}
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 3;fxLoop++) {
				A_SpawnItemEx("DMR_FireBouncingDebris",random(-2,2),random(-2,2),random(-2,2),frandom(-8,8),frandom(-8,8),frandom(-2,8)); 
			}
		}
		MISL B 8 Bright A_Explode(10,48);
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}

// exactly the same as DMR_ClusterSubProjectile, seperately so we can apply the custom damagetype in the Guncaster patch
class DraugrSubProjectile : DMR_ClusterSubProjectile 
{ 
	Default
	{
		Obituary "%o couldn't dodge a draugr's clustermissile.";	
	}
}