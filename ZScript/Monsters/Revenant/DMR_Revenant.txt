//======================================================================
//  Original credits
//======================================================================
//  XDeath Sprites: 	Eriance
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//	Sounds:		Westwood Studios	
//======================================================================

Class DMR_Revenant : Revenant 
{
	Default
	{
		Tag "Revenant";
		Species "DMR_Skelly";
		+DONTHARMSPECIES;
		+NOINFIGHTSPECIES;
		Gibhealth -30; ///all medium tier monsters (demon -> hellknight) have 30 gibhealth
	}
	States
	{		
	Missile:
		SKEL J 10 Bright A_FaceTarget;
		TNT1 A 0 A_Jump(128,"MissileRight");
		Goto MissileLeft;
	MissileLeft:
		SKEL K 5 Bright A_SpawnProjectile("RevenantTracer",56,-10);
		SKEL K 15 A_FaceTarget;
		Goto See;
	MissileRight:
		SKEL K 5 Bright A_SpawnProjectile("RevenantTracer",56,10);
		SKEL K 15 A_FaceTarget;
		Goto See;
	Melee:
		SKEL G 0 A_FaceTarget;
		SKEL G 6 A_SkelWhoosh;
		SKEL H 6 A_FaceTarget;
		SKEL I 6 A_CustomMeleeAttack(random(12,36),"skeleton/melee");
		goto See;
	XDeath:
		SKEL R 5;
		SKEL S 5 A_XScream;
		SKEL T 5 A_NoBlocking;
		SKEL UVWX 5;
		SKEL Y -1;
		stop;
	}
}

class DMR_RevenantTracer : RevenantTracer replaces RevenantTracer
{
	Default
	{
		DamageFunction(random(16,48));
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(128,"Homing"); 
		FATB AB 2 Bright {
			If(GetCvar("dmr_fancydoom")) {				
				A_SpawnItemEx("DMR_FireballTrail",-12,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			}
		}
		Goto Spawn+1;
	Homing:
		FATB AB 2 Bright {
			// 16.875 should be the same value as the vDoom Revenant fireball homing thingmajing
			// however, we need to divide that by two again to account for the 50/50 chance of homing or not
			A_SeekerMissile(1,8,SMF_PRECISE); 
			If(GetCvar("dmr_fancydoom")) {
				A_SpawnItemEx("DMR_FireballTrail",-12,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			}
			//always show the smoke, ignoring the config
			A_SpawnItemEx("DMR_SmokeCloud",-12,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
		Loop;
	Death:
		TNT1 AAA 0 {
			If(GetCvar("dmr_fancydoom")) { 
				A_SpawnItemEx("DMR_FireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-5,5),frandom(-5,5),frandom(-1,5));
			}
		}
		FBXP A 8 Bright;
		FBXP B 6 Bright;
		FBXP C 4 Bright;
		Stop;
	}
}