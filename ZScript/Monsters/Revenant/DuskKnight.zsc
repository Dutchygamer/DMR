//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Blue Shadow
//	Sounds:		Raven Software
//  Sprites: 	Raven Software (edits by Blue Shadow)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class DuskKnight : DMR_Revenant
{
	Default
	{
		Tag "Dusk Warrior";
		Health 450;
		Obituary "%o was slain by a Dusk Warrior";
		HitObituary "%o was axed by a Dusk Warrior";
		PainChance 80;
		SeeSound "DuskWarrior/Sight";
		PainSound "DuskWarrior/Pain";
		DeathSound "DuskWarrior/Death";
		ActiveSound ""; //TODO for now //"hknight/active";
		+MISSILEMORE;
	}
	States
	{
	Spawn:
		XNIG AB 10 A_Look;
		Loop;
	See:
		XNIG AABBCCDD 2 A_Chase;
		Loop;
	Melee:
		TNT1 A 0 A_FaceTarget;
		XNIG E 6 A_SkelWhoosh;
		XNIG F 6 A_FaceTarget;
		XNIG G 6 A_CustomMeleeAttack(random(12,36),"hknight/melee");
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(128, "FastAxe");
		TNT1 A 0 A_Jump(64, "CombiAxe");
	Axe:
		XNIG EF 5 A_FaceTarget;
		XNIG G 10 A_SpawnProjectile("DuskKnightTracer",35);
		XNIG G 10 A_FaceTarget;
		Goto See;
	FastAxe:
		XNIG HI 8 A_FaceTarget;
		XNIG J 10 A_SpawnProjectile("DuskKnightFastTracer",35);
		XNIG G 10 A_FaceTarget;
		Goto See;
	CombiAxe:
		XNIG EF 10 A_FaceTarget;
		XNIG G 10 A_SpawnProjectile("DuskKnightTracer",35);
		XNIG HI 5 A_FaceTarget;
		XNIG J 10 A_SpawnProjectile("DuskKnightFastTracer",35);
		XNIG EF 5 A_FaceTarget;
		XNIG G 10 A_SpawnProjectile("DuskKnightTracer",35);
		XNIG G 10 A_FaceTarget;
		Goto See;
	Pain:
		XNIG K 5;
		XNIG K 5 A_Pain;
		Goto See;
	Death:
		XNIG LM 7;
		XNIG N 7 A_Scream;
		XNIG O 7 A_NoBlocking;
		XNIG PQ 7;
		XNIG R -1;
		Stop;
	XDeath: //temp for now
		SKEL R 5;
		SKEL S 5 A_XScream;
		SKEL T 5 A_NoBlocking;
		SKEL UVWX 5;
		SKEL Y -1;
		stop;
	Raise:
		XNIG R 5;
		XNIG QPONLM 5;
		Goto See;
	}
}

class DuskKnightTracer : DMR_RevenantTracer
{
	Default
	{
		SeeSound "hknight/axewhoosh";
		DeathSound "hknight/hit";
		RenderStyle "Add";
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn:
		FX15 ABC 2 Bright {
			A_SeekerMissile(1,8,SMF_PRECISE); 
			A_SpawnItemEx("DMR_TranslucentAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {	
			A_SpawnItemEx("DMR_FireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		HPLB DE 4 Bright;
		HPLB FG 3 Bright;
		Stop;
	}
}

class DuskKnightFastTracer : DuskKnightTracer
{
	Default
	{		
		DamageFunction(random(18,52));
		Speed 20; //18;
		Scale 1.2;
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn:
		FX15 ABC 2 Bright {
			A_SpawnItemEx("DMR_FireballTrail",-8,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {	
			A_SpawnItemEx("DMR_FireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		HPLB DE 4 Bright;
		HPLB FG 3 Bright;
		Stop;
	}
}