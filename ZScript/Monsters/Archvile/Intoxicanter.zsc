//======================================================================
//  Original credits
//
//======================================================================
//  DECORATE: 	???
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by ZZYZX)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Intoxicanter : DMR_Archvile
{
	Default
	{
		Tag "Intoxicanter";
		Obituary "%o's immune system was overwhelmed by the Intoxicanter.";
		Health 1000;
		BloodColor "Green";
		+MISSILEMORE;
		DamageFactor "ToxinCloud", 0.0;
	}
	States
	{	
	Spawn:
		VIL1 AB 10 A_Look;
		Loop;
	See:
		VIL1 AABBCCDDEEFF 2 A_VileChase;
		Loop;
	Missile:
		TNT1 A 0 A_Jump(96,"GasBalls","ToxinSpray");
		Goto VileAttack;
	VileAttack:	
		VIL2 G 0 BRIGHT A_VileStart;
		VIL2 G 10 BRIGHT A_FaceTarget;
		VIL2 H 8 BRIGHT A_VileTarget("IntoxicanterFire");
		VIL2 IJKLMN 8 BRIGHT A_FaceTarget;
		VIL2 O 8 BRIGHT {
			A_VileAttack("gas/explode",20,40,70,0.5,"ToxinCloud");
			// only spawn toxin gasses when target is in LoS
			if (A_JumpIfTargetInLOS("Null",360)) {
				A_VileTarget("IntoxicanterFireBlast");
			}
		}
		VIL2 P 20 BRIGHT;
		Goto See;
	GasBalls:
		VIL2 AA 6 BRIGHT A_FaceTarget;
		VIL2 KLM 6 BRIGHT A_FaceTarget;
		TNT1 A 0 {
			A_FaceTarget();
			A_SpawnProjectile("WithererGasBall",32,0,-8);
			A_SpawnProjectile("CorpulentToxin",32,0,0);
			A_SpawnProjectile("WithererGasBall",32,0,8);
		}
		VIL2 NOP 8 BRIGHT;
		Goto See;
	//TODO some kind of lobbed gas grenade thingy
	ToxinSpray:
		TNT1 A 0 {
			if (target && Distance3D(target) > 300) SetStateLabel("VileAttack");
		}
		VIL2 KLMN 6 A_FaceTarget;
		TNT1 A 0 { 
			attackloop = 0;
			A_PlaySound ("CacoLich/Spray", CHAN_WEAPON);		
		}
	ToxinSprayLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 6,"See");
		VIL2 O 1 Light("DMR_WithererSpray") {
			A_FaceTarget();
			attackloop++;
			// 0
			A_SpawnProjectile("CacolichToxinSpray",32,0,random(-4,4));
			// 60
			A_SpawnProjectile("CacolichToxinSpray",32,0,random(56,64));
			// 120
			A_SpawnProjectile("CacolichToxinSpray",32,0,random(116,124));
			// 180
			A_SpawnProjectile("CacolichToxinSpray",32,0,random(176,184));
			// 240
			A_SpawnProjectile("CacolichToxinSpray",32,0,random(236,244));
			// 300
			A_SpawnProjectile("CacolichToxinSpray",32,0,random(296,304));
		}
		Loop;
	Heal:
		VIL2 AAA 10 BRIGHT;
		Goto See;
	Pain:
		SILE Q 5;
		SILE Q 5 A_Pain;
		Goto See;
	Death:
		SILE Q 7;
		VIL3 R 7 A_Scream;
		VIL3 S 7 A_NoBlocking;
		VIL3 TUVW 7;
		VIL3 XY 5;
		VIL3 Z -1;
		Stop;
	}
}

class IntoxicanterFire : DMR_ArchvileFire
{
	States
	{
	Spawn:
		FIRG A 2 Bright A_StartFire;
		FIRG BAB 2 Bright A_Fire;
		FIRG C 2 Bright A_FireCrackle;
		FIRG BCBCDCDCDEDED 2 Bright A_Fire;
		FIRG E 2 Bright A_FireCrackle;
		FIRG FEFEFGHGHGH 2 Bright A_Fire;
		Stop;
	}
}

class IntoxicanterFireBlast : DMR_ArchvileFire
{
	States
	{
	Spawn:	
		TNT1 A 0 NoDelay {		
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,-30,frandom(-0.5,0.5),frandom(-0.5,0.5),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,-30,frandom(-0.5,0.5),frandom(-0.5,0.5),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,-30,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,-30,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,-30,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
		}
		stop;
	}
}