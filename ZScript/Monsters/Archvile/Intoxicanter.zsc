//======================================================================
//  Original credits
//
//======================================================================
//  DECORATE: 	???
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by ZZYZX)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Intoxicanter : DMR_Archvile
{
	Default
	{
		Tag "Intoxicanter";
		Obituary "%o's immune system was overwhelmed by the Intoxicanter.";
		Health 1000;
		BloodColor "Green";
		+MISSILEMORE;
		DamageFactor "ToxinCloud", 0.0;
	}
	
	int fxcircle;
	
	States
	{	
	Spawn:
		VIL1 AB 10 A_Look;
		Loop;
	See:
		VIL1 AABBCCDDEEFF 2 A_VileChase;
		Loop;
	Missile:
		TNT1 A 0 A_Jump(96,"GasBalls","ToxinSpray");
		Goto VileAttack;
	VileAttack:	
		VIL2 G 0 BRIGHT A_VileStart;
		VIL2 G 10 BRIGHT A_FaceTarget;
		VIL2 H 8 BRIGHT A_VileTarget("IntoxicanterFire");
		VIL2 IJKLMN 8 BRIGHT A_FaceTarget;
		VIL2 O 8 BRIGHT {
			A_VileAttack("gas/explode",20,40,70,0.5,"ToxinCloud");
			if (target && CheckSight(target)) {
				A_VileTarget("IntoxicanterFireBlast");
			}
		}
		VIL2 P 20 BRIGHT;
		Goto See;
	GasBalls:
		VIL2 AA 6 BRIGHT A_FaceTarget;
		VIL2 KLM 6 BRIGHT A_FaceTarget;
		TNT1 A 0 {
			A_FaceTarget();
			A_SpawnProjectile("WithererGasBall",32,0,-8);
			A_SpawnProjectile("CorpulentToxin",32,0,0);
			A_SpawnProjectile("WithererGasBall",32,0,8);
		}
		VIL2 NOP 8 BRIGHT;
		Goto See;
	//TODO some kind of lobbed gas grenade thingy
	ToxinSpray:
		TNT1 A 0 {
			if (target && Distance3D(target) > 300) SetStateLabel("VileAttack");
		}
		VIL2 KLMN 6 A_FaceTarget;
		TNT1 A 0 { 
			attackloop = 0;
			A_StartSound ("CacoLich/Spray", CHAN_WEAPON);		
		}
	ToxinSprayLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 6,"See");
		VIL2 O 1 Light("DMR_WithererSpray") {
			A_FaceTarget();
			attackloop++;
			for (fxcircle = 0;fxcircle<=360;fxcircle = fxcircle+60) {
				A_SpawnProjectile("CacolichToxinSpray",32,0,random(fxcircle-4,fxcircle+4));
			}
		}
		Loop;
	Heal:
		VIL2 AAA 10 BRIGHT;
		Goto See;
	Pain:
		SILE Q 5;
		SILE Q 5 A_Pain;
		Goto See;
	Death:
		SILE Q 7;
		VIL3 R 7 A_Scream;
		VIL3 S 7 A_NoBlocking;
		VIL3 TUVW 7;
		VIL3 XY 5;
		VIL3 Z -1;
		Stop;
	}
}

class IntoxicanterFire : DMR_ArchvileFire
{
	Default
	{
		DMR_ComplexFXBase.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_StartFire;
		XXBF RSR 2 A_Fire;
		TNT1 A 0 {
			A_StartSound("vile/firecrkl",1,CHANF_LOOPING);
			fxloop = 200;
		}		
		XXBF QRQRPQPOPNPNPNMLKJIHGFEDCBA 2 {
			fxloop -= 4;
			A_Fire();
			A_SpawnItemEx("DMR_GasTrail",frandom(-30,30),frandom(-30,30),frandom(0,10),frandom(-1,1),0,frandom(3,5),random(0,360),0,fxloop);
			A_SpawnItemEx("DMR_LargeGreenFireballTrail",frandom(-30,30),frandom(-30,30),frandom(0,10),frandom(-1,1),0,frandom(3,5),random(0,360),0,fxloop);
			A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-30,30),frandom(-30,30),frandom(0,10),frandom(-1,1),0,frandom(3,5),0,0,-0.05,1,-1,-0.5);
		}
		Stop;
	}
}

class IntoxicanterFireBlast : DMR_ArchvileFireBlast
{
	Default
	{
		DMR_ComplexFXBase.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			if (tracer) SetOrigin(tracer.pos,false);
			A_StartSound("Vile/Explode",1);
			for (fxcircle = 0;fxcircle<=360;fxcircle = fxcircle+15) {
				A_SpawnItemEx("DMR_GasTrail",0,0,6,10,0,0,fxcircle);
			}
			for (fxloop = 0;fxloop<=35;fxloop++) {
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(14,20),frandom(16,20),random(0,360),0,0,frandom(5,7),frandom(8,12),0,frandom(-1,1),0,0,0,1,-1,-0.75);
			}
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,5,frandom(-0.5,0.5),frandom(-0.5,0.5),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,5,frandom(-0.5,0.5),frandom(-0.5,0.5),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
		}
		VIBL FFFGGGHHHIIIJJJ 1 {
			A_SpawnItemEx("DMR_LargeGreenFireballTrail",frandom(2,18),0,6,-2,0,frandom(3,7),random(0,360));
			A_FadeOut(0.03);
		}
		Stop;
	}
}