//======================================================================
//  Original credits
//
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Raven Software, Eriance
//  Sprites: 	Raven Software (edits by Eriance)
//	GLDEFs: 	Ghastly Dragon
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

Class Summoner : DMR_Archvile
{
	Default
	{
		Tag "Summoner";
		Obituary "An Acolyte was promoted for killing %o.";
		Health 800;
		SeeSound "monster/acosit";
		PainSound "monster/acopai";
		DeathSound "monster/acodth";
		ActiveSound "monster/acoact";
	}
	States 
	{ 
	Spawn: 
		ACOL AB 10 A_Look;
		Loop;
	See: 
		ACOL AABBAABBAABB 2 A_VileChase;
		Loop;
	Missile:
		TNT1 A 0 A_Jump(176, "VileAttack"); // large chance to use vile attack
		TNT1 A 0 A_JumpIfCloser(448, "SpawnAttack"); // only spawn enemies when nearby
		Goto VileAttack; // when not in range and initially not picking the vile attack, use vile attack anyway
	SpawnAttack:
		ACOL D 8 Bright Light("DMR_ArchvileRes") A_FaceTarget;
		ACOL E 5 Bright Light("DMR_ArchvileRes") A_FaceTarget;
		ACOL F 3 Bright Light("DMR_ArchvileRes") A_SpawnProjectile("SummonerBall",34,0,0,0);	
		Goto See;
	VileAttack:
		ACOL D 0 BRIGHT A_VileStart;
		ACOL D 10 BRIGHT Light("DMR_ArchvileCharge_X1") A_FaceTarget;
		ACOL D 8 BRIGHT Light("DMR_ArchvileCharge_X2") A_VileTarget("SummonerFire");
		ACOL EE 8 BRIGHT Light("DMR_ArchvileCharge_X3") A_FaceTarget;
		ACOL EEE 8 BRIGHT Light("DMR_ArchvileCharge_X4") A_FaceTarget;
		ACOL E 8 BRIGHT Light("DMR_ArchvileCharge_X5") A_FaceTarget;
		ACOL F 8 BRIGHT Light("DMR_ArchvileCharge_X6") A_VileAttack("vile/stop",50,30,70,1.0);
		ACOL F 20 BRIGHT Light("DMR_ArchvileCharge_X7");
		Goto See;
	Heal:
		ACOL EFE 10 BRIGHT Light("DMR_ArchvileRes");
		Goto See;
	Pain: 
		ACOL C 2;
		ACOL C 2 A_Pain;
		TNT1 A 0 A_Jump(128, "SpawnAttack"); // chance to summon reinforcements
		Goto See;
	Death: 
		ACOL G 8 Bright A_Scream; 
		ACOL H 5 Bright;
		ACOL I 5 Bright;
		ACOL J 5 Bright A_NoBlocking ;
		ACOL KLM 5 Bright;
		ACOL N 5 A_SpawnProjectile("SummonerGhost",54,0,0,0);
		ACOL O 5;
		ACOL P -1;
		Stop;
	} 
}
        
class SummonerBall : DMR_Projectile
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 18;
		//RenderStyle "ADD";
		//Alpha 0.75;
		Scale 0.75;
		Gravity 0.6;
		-NOGRAVITY;
		BounceType "Doom";
		BounceCount 3;
		Seesound "weapons/smlbrn";
		DeathSound "vile/firecrkl";
		ProjectileKickBack 500;
	}
	States
	{
	Spawn:
		BOSF ABCD 2 Bright;
		loop;
	Death:
		FIRE A 3 Bright A_Explode(10, 96);
		FIRE B 3 Bright;
		TNT1 A 0 A_SpawnItem("SummonerBallRandomizer",0,0);
		FIRE CDEFGH 3 Bright;
		Stop;
	}
}

class SummonerBallRandomizer : RandomSpawner
{
	Default
	{
		DropItem "Zombieman", 255;
		DropItem "DoomImp", 255;
		DropItem "Shotgunguy", 255;
		DropItem "Chaingunguy", 255;
		DropItem "LostSoul", 255;
	}
}

class SummonerFire : DMR_ArchvileFire
{
	States
	{
	Spawn:
		FIRE A 2 BRIGHT  A_StartFire;
		FIRE BAB 2 BRIGHT  A_Fire;
		FIRE C 2 BRIGHT  A_FireCrackle;
		FIRE BCBCDCDCDEDED 2 BRIGHT  A_Fire;
		FIRE E 2 BRIGHT  A_FireCrackle;
		FIRE FEFEFGHGHGH 2 BRIGHT  A_Fire;
		Stop;
	}
}

class SummonerGhost : Actor
{   
	Default
	{
		Radius 0;
		Height 2;
		Speed 0;
		PROJECTILE;
		RENDERSTYLE "Translucent";
		ALPHA 0.67;
	}
	States
	{
	Spawn:
		ACOL QRSTUVW 4;
		Stop;
	}
}
