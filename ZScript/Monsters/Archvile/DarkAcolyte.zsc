//======================================================================
//  Original credits
//
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Raven Software, Eriance
//  Sprites: 	Raven Software (edits by Eriance)
//	GLDEFs: 	Ghastly Dragon
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

Class Summoner : DMR_Archvile
{
	Default
	{
		Tag "Summoner";
		Obituary "%o was unsummoned by a Summoner.";
		Health 800;
		SeeSound "monster/acosit";
		PainSound "monster/acopai";
		DeathSound "monster/acodth";
		ActiveSound "monster/acoact";
	}
	States 
	{ 
	Spawn: 
		ACOL AB 10 A_Look;
		Loop;
	See: 
		ACOL AABBAABBAABB 2 A_VileChase;
		Loop;
	Missile:
		TNT1 A 0 A_Jump(72,"SpawnAttack");
		Goto VileAttack;
	SpawnAttack:
		TNT1 A 0 {
			if (target && Distance3D(target) > 448) SetStateLabel("VileAttack");
		}
		ACOL D 8 Bright Light("DMR_ArchvileRes") A_FaceTarget;
		ACOL E 5 Bright Light("DMR_ArchvileRes") A_FaceTarget;
		ACOL F 3 Bright Light("DMR_ArchvileRes") A_SpawnProjectile("SummonerBall",34,0,0,0);	
		Goto See;
	VileAttack:
		ACOL D 0 BRIGHT A_VileStart;
		ACOL D 10 BRIGHT Light("DMR_ArchvileCharge_X1") A_FaceTarget;
		ACOL D 8 BRIGHT Light("DMR_ArchvileCharge_X2") A_VileTarget("SummonerFire");
		ACOL EE 8 BRIGHT Light("DMR_ArchvileCharge_X3") A_FaceTarget;
		ACOL EEE 8 BRIGHT Light("DMR_ArchvileCharge_X4") A_FaceTarget;
		ACOL E 8 BRIGHT Light("DMR_ArchvileCharge_X5") A_FaceTarget;
		ACOL F 8 BRIGHT Light("DMR_ArchvileCharge_X6") {
			A_VileAttack("vile/stop",50,30,70,1.0);
			if (target && CheckSight(target)) {
				A_VileTarget("SummonerFireBlast");
			}
		}
		ACOL F 20 BRIGHT Light("DMR_ArchvileCharge_X7");
		Goto See;
	Heal:
		// rezz nearby corpses as well
		TNT1 A 0 A_RadiusGive("RadiusResurrect",100,RGF_KILLED);
		ACOL EFE 10 BRIGHT Light("DMR_ArchvileRes");
		Goto See;
	Pain: 
		ACOL C 2;
		ACOL C 2 A_Pain;
		TNT1 A 0 A_Jump(128, "SpawnAttack"); // chance to summon reinforcements
		Goto See;
	Death: 
		ACOL G 8 Bright A_Scream; 
		ACOL H 5 Bright;
		ACOL I 5 Bright;
		ACOL J 5 Bright A_NoBlocking ;
		ACOL KLM 5 Bright;
		ACOL N 5 A_SpawnProjectile("SummonerGhost",54);
		ACOL O 5;
		ACOL P -1;
		Stop;
	} 
}
        
class SummonerBall : DMR_Projectile
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 18;
		Scale 0.75;
		Gravity 0.6;
		-NOGRAVITY;
		BounceType "Doom";
		BounceCount 3;
		Seesound "weapons/smlbrn";
		DeathSound "vile/firecrkl";
		ProjectileKickBack 500;
	}
	States
	{
	Spawn:
		BOSF ABCD 2 Bright;
		loop;
	Death:
		FIRE A 3 Bright A_Explode(10, 96);
		FIRE B 3 Bright;
		TNT1 A 0 A_SpawnItem("SummonerBallRandomizer",0,0);
		FIRE CDEFGH 3 Bright;
		Stop;
	}
}

class SummonerBallRandomizer : RandomSpawner
{
	Default
	{
		DropItem "Zombieman", 255;
		DropItem "DoomImp", 255;
		DropItem "Shotgunguy", 255;
		DropItem "Chaingunguy", 255;
		DropItem "LostSoul", 255;
	}
}

class SummonerFire : DMR_ArchvileFire
{
	Default
	{
		DMR_ComplexFXBase.ParticleColors "#710071", "#870087", "#c300c3", "#c800c8";
	}
	
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_StartFire;
		XXBF RSR 2 A_Fire;
		TNT1 A 0 {
			A_StartSound("vile/firecrkl",1,CHANF_LOOPING);
			fxloop = 200;
		}		
		XXBF QRQRPQPOPNPNPNMLKJIHGFEDCBA 2 {
			fxloop -= 4;
			A_Fire();
			// don't bother with the Y offset / speed as we turn spawn in a random angle anyway
			A_SpawnItemEx("DMR_PurpleFireballTrail",frandom(-10,10),0,frandom(0,10),frandom(-1,1),0,frandom(3,5),random(0,360),0,fxloop);
			A_SpawnItemEx("DMR_LargePurpleFireballTrail",frandom(-10,10),0,frandom(0,10),frandom(-1,1),0,frandom(3,5),random(0,360),0,fxloop);
			A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
		}
		Stop;
	}
}

class SummonerFireBlast : DMR_ArchvileFireBlast
{
	Default
	{
		DMR_ComplexFXBase.ParticleColors "#710071", "#870087", "#c300c3", "#c800c8";
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			if (tracer) SetOrigin(tracer.pos,false);
			A_StartSound("Vile/Explode",1);
			for (fxcircle = 0;fxcircle<=360;fxcircle = fxcircle+15) {
				A_SpawnItemEx("DMR_PurpleFireballTrail",0,0,6,10,0,0,fxcircle);
			}
			for(fxloop = 0;fxloop<=35;fxloop++) {
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(14,20),frandom(16,20),random(0,360),0,0,frandom(5,7),frandom(8,12),0,frandom(-1,1),0,0,0,1,-1,-0.75);
			}
		}
		VIBL KKKLLLMMMNNNOOO 1 {
			A_SpawnItemEx("DMR_LargePurpleFireballTrail",frandom(2,18),0,6,-2,0,frandom(3,7),random(0,360));
			A_FadeOut(0.03);
		}
		Stop;
	}
}


Class RadiusResurrect : CustomInventory
{
	Default
	{
		+INVENTORY.ALWAYSPICKUP;
	}
	States
	{
	Pickup:
		TNT1 A 0 Thing_Raise(0);
		Stop;
	}
}

class SummonerGhost : Actor
{   
	Default
	{
		Radius 0;
		Height 2;
		Speed 0;
		PROJECTILE;
		RENDERSTYLE "Translucent";
		ALPHA 0.67;
	}
	States
	{
	Spawn:
		ACOL QRSTUVW 4;
		Stop;
	}
}
