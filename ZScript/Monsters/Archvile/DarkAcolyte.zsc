//======================================================================
//  Original credits
//
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Raven Software, Eriance
//  Sprites: 	Raven Software (edits by Eriance)
//	GLDEFs: 	Ghastly Dragon
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

Class Runecaster : DMR_Archvile
{
	Default
	{
		Tag "Runecaster";
		Obituary "%o was enchanted by a Runecaster.";
		Health 800;
		SeeSound "monster/acosit";
		PainSound "monster/acopai";
		DeathSound "monster/acodth";
		ActiveSound "monster/acoact";
	}
	int hasSpawner;
	//int spawnCount;
	
	States 
	{ 
	Spawn: 
		ACOL AB 10 A_Look;
		Loop;
	See: 
		ACOL AABBAABBAABB 2 A_VileChase;
		Loop;
	Missile:
		TNT1 A 0 A_Jump(96,"SpawnBall"); //72
		Goto VileAttack;
	NonSpawnAttacks:
		Goto VileAttack;
	RedoSpawnBall:
		// nuke spawner, reset flag and throw a new spawner
		TNT1 A 0 {			
			hasSpawner = 0;
			A_RemoveMaster(RMVF_EVERYTHING);
		}
	SpawnBall:
		TNT1 A 0 {
			// don't spawn a new Spawner when on cooldown
			if (CountInv("SummonCooldown") != 0) SetStateLabel("NonSpawnAttacks");
			// don't spawn when at the cap
			if (CountInv("RunecasterSpawnCount") >= 10) {
				// when we still have a spawner active nuke it first
				if (hasSpawner) SetStateLabel("RemoveSpawn");
				else SetStateLabel("NonSpawnAttacks");
			}
			// don't spawn enemies when target is far away
			if (target && Distance3D(target) > 448) SetStateLabel("NonSpawnAttacks");
			// if really far away from spawner nuke it
			if (hasSpawner && master && Distance3D(master) > 1024) SetStateLabel("RemoveSpawn");
		}
		// when we have a spawner active, try spawning something
		TNT1 A 0 A_JumpIf(hasSpawner,"TrySpawn");
		// if not throw out a spawner
		ACOL D 8 Bright Light("DMR_ArchvileRes") A_FaceTarget;
		ACOL E 5 Bright Light("DMR_ArchvileRes") A_FaceTarget;
		ACOL F 3 Bright Light("DMR_ArchvileRes") {
			A_SpawnItemEx("RunecasterThrownSpawner",0,0,36,frandom(15,20),0,frandom(4,8),0,SXF_ISMASTER);
			hasSpawner = 1;
		}
		Goto See;
	TrySpawn:
		// check if spawn is in valid pos by checking if we fit at the pos
		TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,8,0,WARPF_TESTONLY,"SpawnContinue");
		// if not redo the spawn 
		ACOL C 30 A_Pain;
		Goto RedoSpawnBall;	
	SpawnContinue:	
		ACOL D 8 Bright Light("RunecasterCharge_X1");
		ACOL E 5 Bright Light("RunecasterCharge_X3");
		ACOL F 3 Bright Light("RunecasterCharge_X7") {
			// spawn an enemy at the spawner
			SpawnEnemy("RunecasterRandomSpawner");
		}
		Goto See;
	RemoveSpawn:
		// reset flag, nuke spawner and give a cooldown before we can spawn again
		TNT1 A 0 {			
			hasSpawner = 0;
			A_RemoveMaster(RMVF_EVERYTHING);
			A_GiveInventory("SummonCooldown");
		}
		ACOL C 30 A_Pain;
		Goto See;
	VileAttack:
		ACOL D 0 BRIGHT A_VileStart;
		ACOL D 10 BRIGHT Light("RunecasterCharge_X1") A_FaceTarget;
		ACOL D 8 BRIGHT Light("RunecasterCharge_X2") A_VileTarget("RunecasterFire");
		ACOL EE 8 BRIGHT Light("RunecasterCharge_X3") A_FaceTarget;
		ACOL EEE 8 BRIGHT Light("RunecasterCharge_X4") A_FaceTarget;
		ACOL E 8 BRIGHT Light("RunecasterCharge_X5") A_FaceTarget;
		ACOL F 8 BRIGHT Light("RunecasterCharge_X6") {
			A_VileAttack("vile/stop",50,30,70,1.0);
			if (target && CheckSight(target)) {
				A_VileTarget("RunecasterFireBlast");
			}
		}
		ACOL F 20 BRIGHT Light("RunecasterCharge_X7");
		Goto See;
	Heal:
		// rezz nearby corpses as well
		//TNT1 A 0 A_RadiusGive("RadiusResurrect",100,RGF_KILLED);
		ACOL EFE 10 BRIGHT Light("RunecasterRes");
		Goto See;
	Pain: 
		ACOL C 2;
		ACOL C 2 A_Pain;
		TNT1 A 0 A_Jump(128, "SpawnBall"); // chance to summon reinforcements
		Goto See;
	Death: 
		TNT1 A 0 {		
			A_RemoveMaster(RMVF_EVERYTHING);
		}
		ACOL G 8 Bright A_Scream; 
		ACOL H 5 Bright;
		ACOL I 5 Bright;
		ACOL J 5 Bright A_NoBlocking ;
		ACOL KLM 5 Bright;
		ACOL N 5 A_SpawnProjectile("RunecasterGhost",54);
		ACOL O 5;
		ACOL P -1;
		Stop;
	} 
	
	// summon the given actor from the master's pos (the dropped spawner)
	// contains some creative C/P from Doom2's SpawnShot A_SpawnFly
	void SpawnEnemy(name enemy)
	{
		// safety net
		if (master) {		
			// first spawn the fire as the bosscube does
			{
				Class<Actor> spawntype = "SpawnFire";
				Sound snd = "brain/spawn";
			
				Actor fog = Spawn(spawntype, master.pos);
				if (fog) A_StartSound(snd, CHAN_BODY);
			}		
		
			// then spawn a new enemy
			{
				Actor newmobj = Spawn(enemy,master.pos);
				// more safety nets
				if (newmobj != null) {
					// make the new monster hate what the we hate
					newmobj.CopyFriendliness(self, false);
					
					// Make it act as if it was around when the player first made noise
					// (if the player has made noise).
					newmobj.LastHeard = newmobj.CurSector.SoundTarget;

					// turn in a random angle and yeet them out of the way so the next enemy can spawn in the spot
					newmobj.A_SetAngle(random(0,360));
					newmobj.Thrust(random(2,6),newmobj.angle);	

					// change the monster's state to the see state
					if (newmobj.SeeState != null && newmobj.LookForPlayers (true))
					{
						newmobj.SetState (newmobj.SeeState);
					}										
				}
			}
			
			// add to our cap
			GiveInventory("RunecasterSpawnCount",1);
		}
	}
}
        
class RunecasterThrownSpawner : DMR_Projectile
{
	Default
	{
		Radius 28; //8;
		Height 20; //8;
		Speed 18;
		Scale 1.0 ;//0.75;
		Gravity 0.6;
		+DONTREFLECT;
		+CANBOUNCEWATER;
		-NOGRAVITY;
		BounceType "Doom";
		BounceFactor 0.6;
		//BounceCount 3;
		Seesound "weapons/smlbrn";
		DeathSound "vile/firecrkl";
		//ProjectileKickBack 500;
		DMR_Projectile.ParticleColors "#710071", "#870087", "#c300c3", "#c800c8";
	}
	States
	{
	Spawn:
		BOSF ABCD 2 Bright;
		loop;
	Death:
		TNT1 A 0 {
			bFLATSPRITE = true;
			A_SetScale(3.0);
		}
		PLRN AAAAABBBBB 1 {
			for(fxloop = 0;fxloop<=2;fxloop++) {
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,25,frandom(15,16),random(0,360),frandom(0,20),0,frandom(0,4),0,0,frandom(2,3),0,0,0,1,-1,-1);
			}
		}
		Goto Death+1;
	}
}

class RunecasterRandomSpawner : RandomSpawner
{
	Default
	{
		DropItem "Zombieman", 255;
		DropItem "DoomImp", 255;
		DropItem "Shotgunguy", 255;
		DropItem "Chaingunguy", 255;
		DropItem "LostSoul", 255;
	}
}

Class SummonCooldown : Powerup { Default { Powerup.Duration -20; } }
Class RunecasterSpawnCount : Inventory { Default { Inventory.MaxAmount 10; } }



//Class EcclesiarchCastEffect : Actor
//{
//  Default
//  {
//  +NOINTERACTION;
//  +BRIGHT;
//  }
//  
//  States
//  {
//  Spawn:
//	TNT1 A 1 
//		{
//		for(int a = 0;a<2;a++)
//			{
//			A_SpawnParticle("00FF00",SPF_FULLBRIGHT|SPF_RELATIVE,random(25,30),frandom(15,25),0,frandom(-2,2),frandom(-2,2)+33,frandom(94,98),frandom(-1,1),frandom(-1,1),frandom(2,3),0,0,0,1,-1,-1);
//			A_SpawnParticle("00FF00",SPF_FULLBRIGHT|SPF_RELATIVE,random(25,30),frandom(15,25),0,frandom(-2,2),frandom(-2,2)-33,frandom(94,98),frandom(-1,1),frandom(-1,1),frandom(2,3),0,0,0,1,-1,-1);
//			}
//		}
//    Wait;
//	}
//
//  override void Tick()
//	{
//	Super.Tick();
//	A_Warp(AAPTR_MASTER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
//	}
//}





class RunecasterFire : DMR_ArchvileFire
{
	Default
	{
		RenderStyle "Add";
		+FLATSPRITE;
		+BRIGHT;
		Scale 9.0; //TODO temp until bigger sprite
		DMR_ComplexFXBase.ParticleColors "#710071", "#870087", "#c300c3", "#c800c8";
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			A_StartSound("vile/firecrkl",1,CHANF_LOOPING);
			fxloop = 200;
		}
		TNT1 A 0 A_JumpIf(fxloop <= 80,"Death");
		PLRN AAAAABBBBB 1 {
			fxloop -= 3;
			A_Fire();
			A_SpawnItemEx("DMR_PlasmaRuneTrail",frandom(-30,30),frandom(-30,30),frandom(0,10),frandom(-1,1),0,frandom(3,5),random(0,360),0,fxloop);
			A_SpawnItemEx("DMR_LargePurpleFireballTrail",frandom(-30,30),frandom(-30,30),frandom(0,10),frandom(-1,1),0,frandom(3,5),random(0,360),0,fxloop);
			A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-30,30),frandom(-30,30),frandom(0,10),frandom(-1,1),0,frandom(3,5),0,0,-0.05,1,-1,-0.5);
		}
		Goto Spawn+1;
	Death:
		PLRN A 2 {
			A_FadeOut();
			A_Fire();
		}
		Wait;
	}
}

class RunecasterFireBlast : DMR_ArchvileFireBlast
{
	Default
	{
		DMR_ComplexFXBase.ParticleColors "#710071", "#870087", "#c300c3", "#c800c8";
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			if (tracer) SetOrigin(tracer.pos,false);
			A_StartSound("Vile/Explode",1);
			for (fxcircle = 0;fxcircle<=360;fxcircle = fxcircle+15) {
				A_SpawnItemEx("DMR_PurpleFireballTrail",0,0,6,10,0,0,fxcircle);
			}
			for(fxloop = 0;fxloop<=35;fxloop++) {
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(14,20),frandom(16,20),random(0,360),0,0,frandom(5,7),frandom(8,12),0,frandom(-1,1),0,0,0,1,-1,-0.75);
			}
		}
		VIBL KKKLLLMMMNNNOOO 1 {
			A_SpawnItemEx("DMR_LargePurpleFireballTrail",frandom(2,18),0,6,-2,0,frandom(3,7),random(0,360));
			A_FadeOut(0.03);
		}
		Stop;
	}
}


Class RadiusResurrect : CustomInventory
{
	Default
	{
		+INVENTORY.ALWAYSPICKUP;
	}
	States
	{
	Pickup:
		TNT1 A 0 Thing_Raise(0);
		Stop;
	}
}

class RunecasterGhost : Actor
{   
	Default
	{
		Radius 0;
		Height 2;
		Speed 0;
		PROJECTILE;
		RENDERSTYLE "Translucent";
		ALPHA 0.67;
	}
	States
	{
	Spawn:
		ACOL QRSTUVW 4;
		Stop;
	}
}
