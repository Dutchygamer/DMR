//======================================================================
//  Original credits
//
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Id Software, Raven Software, Rogue Entertainment, Eriance
//  Sprites: 	Id Software, Raven Software (edits by Eriance)
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Diabolist : DMR_Archvile
{
	Default
	{
		Tag "Diabolist";
		Obituary "%o was set ablaze by a Diabolist";
		Health 950;
		BloodColor "08 08 08";
		+MISSILEMORE;
		SeeSound "monster/diasit";
		PainSound "monster/diapai";
		DeathSound "monster/diadth";
		ActiveSound "monster/diaact";
	}
	States
	{
	Spawn:
		DIAB AB 10 A_Look;
		Loop;
	See:
		DIAB AABBCCDDEEFF 2 A_VileChase;
		Loop;
	Missile:
		TNT1 A 0 A_Jump (180,"VileAttack");
		Goto Flamebeam;
	VileAttack:	
		DIAB G 0 BRIGHT A_VileStart;
		DIAB G 10 BRIGHT A_FaceTarget;
		DIAB H 8 BRIGHT A_VileTarget("DiabolistFire");
		DIAB IJKLMN 8 BRIGHT A_FaceTarget;
		DIAB O 8 BRIGHT A_VileAttack("vile/stop",20,75,110,1.0,"Fire");
		DIAB P 20 BRIGHT;
		Goto See;
	/*
	Flamewall:	
		TNT1 A 0 BRIGHT A_VileStart;
		DIAB G 10 BRIGHT A_FaceTarget;
		DIAB H 8 BRIGHT A_VileTarget("DiabolistFire");
		DIAB IHIHIHG 8 BRIGHT A_FaceTarget;
		Goto See;*/
	Flamebeam:
		DIAB G 0 BRIGHT A_VileStart;
		DIAB G 10 BRIGHT A_FaceTarget;
		DIAB H 8 BRIGHT A_FaceTarget;
		DIAB IJKLMN 8 BRIGHT A_FaceTarget;
		DIAB OPOPOPOP 2 BRIGHT {
			A_FaceTarget();
			A_SpawnProjectile("DiabolistBeam",32,0,0);
		}
		DIAB OP 8 BRIGHT;
		Goto See;
	Heal:
		DIA2 ABC 10 BRIGHT;
		Goto See;
	Pain:
		DIAB Q 5;
		DIAB Q 5 A_Pain;
		Goto See;
	Death:
		DIAB Q 7;
		DIAB R 7 A_Scream;
		DIAB S 7 A_NoBlocking;
		DIAB TUVW 7;
		DIAB XY 5;
		DIAB Z -1;
		Stop;
	}
}

class DiabolistFire : ArchvileFire
{
	States
	{
	Spawn:	
		HLFR A 2 BRIGHT  A_StartFire;
		HLFR BAB 2 BRIGHT  A_Fire;
		HLFR C 2 BRIGHT  A_FireCrackle;
		HLFR BCBCDCDCDEDED 2 BRIGHT  A_Fire;
		HLFR E 2 BRIGHT  A_FireCrackle;
		HLFR FEFEFGHGHGH 2 BRIGHT  A_Fire;
		/*
		HLFR A 2 Bright A_StartFire;
		HLFR BAB 2 Bright A_Fire;
		HLFR C 2 Bright A_FireCrackle;
		HLFR BCBCDCDCDEDED 2 Bright {
			A_Fire();
			A_Explode(2,32);
		}
		HLFR E 2 Bright {
			A_FireCrackle();
			A_Explode(2,32);
		}
		HLFR FEFEFGHGHGHGH 2 Bright {
			A_Fire();
			A_Explode(2,32);
		}*/
		stop;
	}
}

class DiabolistBeam : FastProjectile
{	
	Default
	{
		Radius 2;
		Height 2;
		Speed 65;
		DamageFunction(random(6,8)); // use a very small range due to the stream of projectiles
		DamageType "fire";
		RenderStyle "Add";
		Alpha 0.85;
		MissileType "DiabolistBeamTrail";
		MissileHeight 8;
		Seesound "weapons/firmfi";
		DeathSound "weapons/firex4";
	}
	States
	{
	Spawn:
		VBAL AB 3 Bright;
		Loop;
	Death:
		CBAL CDEFG 3 Bright;
		Stop;
	}
}

class DiabolistBeamTrail: actor
{
	Default
	{
		RenderStyle "Add";
		Alpha 0.67;
		+NOINTERACTION;
		+NOGRAVITY;
		+FLOATBOB;
	}
	States
	{
	Spawn:
		FDFX ABCDEF 4 Bright;
		Stop;
	}
}

//TODO below is unused currently
class DiabolistFloorTracer: actor
{
	Default
	{
		Radius 5;
		Height 5;
		Speed 12;
		ReactionTime 100;
		Damage 5;
		DamageType "fire";
		RenderStyle "ADD";
		Alpha 0.67;
		PROJECTILE;
		+SEEKERMISSILE;
		+FLOORHUGGER;
		+THRUGHOST;
		-NOGRAVITY;
		Seesound "weapons/diasht";
		DeathSound "weapons/firex3";
	}
	States
	{
	Spawn:
		TNT1 A 1 Bright A_SeekerMissile (10,15);
		TNT1 A 0 A_Countdown;
		TNT1 A 0 A_SpawnProjectile("DiabolistFloorTracerTrail",0,0,0,0);
		loop;
	Death:
		FTRA K 4 Bright;
		FTRA L 4 Bright A_Explode(25,64);
		FTRA MNO 3 Bright;
		stop;
	}
}

class DiabolistFloorTracerTrail: actor
{
	Default
	{
		Radius 1;
		Height 1;
		Speed 0;
		RENDERSTYLE "ADD";
		DamageType "fire";
		ALPHA 0.67;
		PROJECTILE;
		+FLOORHUGGER;
		-NOGRAVITY;
	}
	States
	{
	Spawn:
		FTRA ABCDEFGHIJ 3 Bright A_Explode(4,8);
		stop;
	}
}