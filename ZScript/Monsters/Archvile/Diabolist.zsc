//======================================================================
//  Original credits
//
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Id Software, Raven Software, Rogue Entertainment, Eriance
//  Sprites: 	Id Software, Raven Software (edits by Eriance)
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Diabolist : DMR_Archvile
{
	Default
	{
		Tag "Diabolist";
		Obituary "%o was set ablaze by a Diabolist";
		Health 950;
		BloodColor "08 08 08";
		+MISSILEMORE;
		SeeSound "monster/diasit";
		PainSound "monster/diapai";
		DeathSound "monster/diadth";
		ActiveSound "monster/diaact";
	}
	States
	{
	Spawn:
		DIAB AB 10 A_Look;
		Loop;
	See:
		DIAB AABBCCDDEEFF 2 A_VileChase;
		Loop;
	Missile:
		TNT1 A 0 A_Jump(96,"Flamebeam", "Fireballs");
		Goto VileAttack;
	VileAttack:	
		DIAB G 0 BRIGHT A_VileStart;
		DIAB G 10 BRIGHT A_FaceTarget;
		DIAB H 8 BRIGHT A_VileTarget("DiabolistFire");
		DIAB IJKLMN 8 BRIGHT A_FaceTarget;
		DIAB O 8 BRIGHT {
			A_VileAttack("vile/stop",20,75,110,1.0,"Fire");
			if (target && CheckSight(target)) {
				A_VileTarget("DiabolistFireBlast");
			}
		}
		DIAB P 20 BRIGHT;
		Goto See;
	Flamebeam:
		DIAB KLMN 6 BRIGHT A_FaceTarget;
		DIAB NONONONO 2 BRIGHT {
			A_FaceTarget();
			A_SpawnProjectile("DiabolistBeam",32,0,0);
		}
		DIAB P 8 BRIGHT;
		Goto See;
	Fireballs:
		DIAB KLMN 6 BRIGHT A_FaceTarget;
		DIAB O 6 BRIGHT {
			A_FaceTarget();
			A_SpawnProjectile("DiabolistBall",32,0,0);
		}
		DIAB N 6 BRIGHT A_FaceTarget;
		DIAB O 6 BRIGHT {
			A_FaceTarget();
			A_SpawnProjectile("DiabolistBall",32,0,0);
		}
		DIAB N 6 BRIGHT A_FaceTarget;
		DIAB O 6 BRIGHT {
			A_FaceTarget();
			A_SpawnProjectile("DiabolistBall",32,0,0);
		}
		DIAB N 6 BRIGHT A_FaceTarget;
		DIAB P 8 BRIGHT;
		Goto See;
	Heal:
		DIA2 ABC 10 BRIGHT;
		Goto See;
	Pain:
		DIAB Q 5;
		DIAB Q 5 A_Pain;
		Goto See;
	Death:
		DIAB Q 7;
		DIAB R 7 A_Scream;
		DIAB S 7 A_NoBlocking;
		DIAB TUVW 7;
		DIAB XY 5;
		DIAB Z -1;
		Stop;
	}
}

class DiabolistFire : DMR_ArchvileFire
{
	Default
	{
		DMR_ComplexFXBase.ParticleColors "#df0f0f", "#ff1b1b", "#ff6f00", "#ff664b";
	}
	
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_StartFire;
		XXBF RSR 2 A_Fire;
		TNT1 A 0 {
			A_StartSound("vile/firecrkl",1,CHANF_LOOPING);
			fxloop = 200;
		}		
		XXBF QRQRPQPOPNPNPNMLKJIHGFEDCBA 2 {
			fxloop -= 4;
			A_Fire();
			// don't bother with the Y offset / speed as we turn spawn in a random angle anyway
			A_SpawnItemEx("DMR_FireballTrail",frandom(-10,10),0,frandom(0,10),frandom(-1,1),0,frandom(3,5),random(0,360),0,fxloop);
			A_SpawnItemEx("DMR_LargeFireballTrail",frandom(-10,10),0,frandom(0,10),frandom(-1,1),0,frandom(3,5),random(0,360),0,fxloop);
			A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
		}
		Stop;
	}
}

class DiabolistFireBlast : DMR_ArchvileFireBlast
{
	Default
	{
		DMR_ComplexFXBase.ParticleColors "#df0f0f", "#ff1b1b", "#ff6f00", "#ff664b";
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			if (tracer) SetOrigin(tracer.pos,false);
			A_StartSound("Vile/Explode",1);
			for (fxcircle = 0;fxcircle<=360;fxcircle = fxcircle+15) {
				A_SpawnItemEx("DMR_FireballTrail",0,0,6,10,0,0,fxcircle);
			}
			for (fxloop = 0;fxloop<=35;fxloop++) {
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(14,20),frandom(16,20),random(0,360),0,0,frandom(5,7),frandom(8,12),0,frandom(-1,1),0,0,0,1,-1,-0.75);
			}
			A_SpawnItemEx("DMR_BurningFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_ORIGINATOR|SXF_TRANSFERPOINTERS);
		}
		VIBL AAABBBCCCDDDEEE 1 {
			A_SpawnItemEx("DMR_LargeFireballTrail",frandom(2,18),0,6,-2,0,frandom(3,7),random(0,360));
			A_FadeOut(0.03);
		}
		Stop;
	}
}


class DiabolistBeam : DMR_FastProjectile
{	
	Default
	{
		Radius 2;
		Height 2;
		Speed 65;
		DamageFunction(random(6,8)); // use a very small range due to the stream of projectiles
		DamageType "fire";
		RenderStyle "Add";
		Alpha 0.85;
		Scale 1.5;
		MissileType "DiabolistBeamTrail";
		MissileHeight 8;
		Seesound "weapons/firmfi";
		DeathSound "weapons/firex4";
	}
	States
	{
	Spawn:
		VBAL AB 3 Bright;
		Loop;
	Death:
		CBAL CDEFG 3 Bright;
		Stop;
	}
}

class DiabolistBeamTrail : DMR_ProjectileTrail
{
	Default
	{
		Alpha 0.67;
		+FLOATBOB;
		-ROLLSPRITE;
	}
	States
	{
	Spawn:
		FDFX ABCDEF 4 Bright;
		Stop;
	}
}

class DiabolistBall : DMR_SmallRocket
{
	Default
	{
		Speed 20;		
		FastSpeed 30;
		DamageFunction(random(10,30));
		DamageType "Fire";
		Scale 1.0;
		RenderStyle "Add";
		//ALPHA 0.67;
		SeeSound "pyro/flame";
		DeathSound "pyro/flamepoof";
	}
	States
	{
	Spawn: 
		SEF1 AABBCC 2 {
			A_SpawnItemEx("DMR_LargeFireballTrail",-5,0,0,frandom(-0.5,0),frandom(-0.5,0.5),frandom(0,-0.5)); 
		}
		loop;
	Death:
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 10;fxLoop++) {
				A_SpawnItemEx("DMR_MediumFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
				A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
			}
		}
		TNT1 A 0 A_Explode(64,64);
		SEF1 DEFGH 4;
		Stop;
	}
}

//TODO below is unused currently
class DiabolistFloorTracer: actor
{
	Default
	{
		Radius 5;
		Height 5;
		Speed 12;
		ReactionTime 100;
		Damage 5;
		DamageType "fire";
		RenderStyle "ADD";
		Alpha 0.67;
		PROJECTILE;
		+SEEKERMISSILE;
		+FLOORHUGGER;
		+THRUGHOST;
		-NOGRAVITY;
		Seesound "weapons/diasht";
		DeathSound "weapons/firex3";
	}
	States
	{
	Spawn:
		TNT1 A 1 Bright A_SeekerMissile (10,15);
		TNT1 A 0 A_Countdown;
		TNT1 A 0 A_SpawnProjectile("DiabolistFloorTracerTrail",0,0,0,0);
		loop;
	Death:
		FTRA K 4 Bright;
		FTRA L 4 Bright A_Explode(25,64);
		FTRA MNO 3 Bright;
		stop;
	}
}

class DiabolistFloorTracerTrail: actor
{
	Default
	{
		Radius 1;
		Height 1;
		Speed 0;
		RENDERSTYLE "ADD";
		DamageType "fire";
		ALPHA 0.67;
		PROJECTILE;
		+FLOORHUGGER;
		-NOGRAVITY;
	}
	States
	{
	Spawn:
		FTRA ABCDEFGHIJ 3 Bright A_Explode(4,8);
		stop;
	}
}