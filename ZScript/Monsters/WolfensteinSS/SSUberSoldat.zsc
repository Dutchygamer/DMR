//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	n/a
//	Sounds:		Id Software
//  Sprites: 	???
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class SSUbersoldat : DMR_NaziBase
{
	Default
	{
		Tag "SS Ubersoldat";	
		Health 1000;
		Speed 12;
		PainChance 20;
		Scale 1.25;
		SeeSound "wolfss/sight";
		PainSound "wolfss/pain";
		DeathSound "wolfss/death";
		ActiveSound "wolfss/active";
		//AttackSound "wolfss/attack";
		Mass 500;
		Obituary "%o didn't like the results of German Superscience.";
		+BOSS; // for extra funsies
		DropItem "Clip", 256, 5;
		DropItem "Clip", 256, 5;
		DropItem "Clip", 256, 5;
		DropItem "Clip", 128, 5;
		DropItem "Clip", 128, 5;
		DropItem "RocketAmmo", 256, 1;
		DropItem "RocketAmmo", 128, 1;
		DropItem "RocketAmmo", 128, 1;
	}
	States
	{
	Spawn:
		DTKN AB 10 A_Look;
		Loop;
	See:
		DTKN AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 {		
			attackloop = 0;
		}
		TNT1 A 0 A_Jump(256, "ChaingunRockets","ChaingunGrenades");		
		Goto See;
	ChaingunRockets:
		DTKN E 10 A_FaceTarget;
	ChaingunRocketsLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 8,"MissileEnd");
		DTKN F 4 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",62,-18);
			A_CustomBulletAttack(22.5,0,2,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			A_StartSound("Chaingunner/Fire", CHAN_WEAPON );
			attackloop++;
		}
		DTKN G 4 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",62,18);
			A_CustomBulletAttack(22.5,0,2,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			A_StartSound("Chaingunner/Fire", CHAN_WEAPON );
			attackloop++;
		}
		DTKN H 4 Bright Light("DMR_RifleFire") {
			A_CustomBulletAttack(22.5,0,2,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			A_StartSound("Chaingunner/Fire", CHAN_WEAPON );
		}
		DTKN H 1 A_CPosRefire;
		Loop;
	ChaingunGrenades:
		DTKN E 10 A_FaceTarget;
	ChaingunGrenadesLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 8,"MissileEnd");
		DTKN F 4 Bright Light("DMR_RifleFire") {
			A_LobProjectile("CyborgAcolyteGrenade",62,-18);
			A_CustomBulletAttack(22.5,0,2,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			A_StartSound("Chaingunner/Fire", CHAN_WEAPON );
			attackloop++;
		}
		DTKN G 4 Bright Light("DMR_RifleFire") {
			A_LobProjectile("CyborgAcolyteGrenade",62,18);
			A_CustomBulletAttack(22.5,0,2,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			A_StartSound("Chaingunner/Fire", CHAN_WEAPON );
			attackloop++;
		}
		DTKN H 4 Bright Light("DMR_RifleFire") {
			A_CustomBulletAttack(22.5,0,2,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			A_StartSound("Chaingunner/Fire", CHAN_WEAPON );
		}
		DTKN H 1 A_CPosRefire;
		Loop;
	MissileEnd:
		DTKN E 10;
		Goto See;
	Pain:
		DTKN Q 3;
		DTKN Q 3 A_Pain;
		Goto See;
	Death:
	XDeath:
		DTKN Q 5 A_Scream;
		DTKN QQQQI 5 {
			A_SpawnItemEx("DMR_BossDeathExplosion",random(8,-8),0,random(20,40));
		}
		DTKN J 5;
		DTKN K 5 {
			A_NoBlocking();
			A_SpawnItemEx("DMR_MonsterDeathFinalExplosion");
		}
		DTKN L 5;
		DTKN M -1;
		Stop;
	Raise:
		DTKN N 5;
		DTKN MLKJI 5;
		Goto See;
	}
}