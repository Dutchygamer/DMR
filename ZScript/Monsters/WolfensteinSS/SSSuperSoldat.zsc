class SSSupersoldat : SSGuard
{
	Default
	{
		Tag "SS Supersoldat";	
		Health 450;
		Speed 12;
		Scale 1.25;
		PainChance 40;
		SeeSound "wolfss/sight";
		PainSound "wolfss/pain";
		DeathSound "wolfss/death";
		ActiveSound "wolfss/active";
		AttackSound "wolfss/attack";
		Obituary "$OB_WOLFSS";
		DropItem "Clip", 256, 5;
	}
	States
	{
	Spawn:
		SSWV AB 10 A_Look;
		Loop;
	See:
		SSWV AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		SSWV E 10 A_FaceTarget;
	Shotgun:
		SSWV E 10 A_FaceTarget;
		SSWV FE 5 Bright Light("DMR_RifleFire") {
			A_FaceTarget();
			A_PlaySound("autoshotgun/fire", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,7.1,3,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
		}
		SSWV E 4 A_MonsterRefire(40,"ShotgunEnd");
		TNT1 A 0 A_Jump(16,"ShotgunEnd");
		Goto Shotgun+1;
	ShotgunEnd:
		SSWV E 10;
		Goto See;
	Pain:
		SSWV H 3;
		SSWV H 3 A_Pain;
		Goto See;
	Death:
		SSWV I 5;
		SSWV J 5 A_Scream;
		SSWV K 5 A_NoBlocking;
		SSWV L 5;
		SSWV M -1;
		Stop;
	XDeath:
		SSWV N 5 ;
		SSWV O 5 A_XScream;
		SSWV P 5 A_NoBlocking;
		SSWV QRSTU 5;
		SSWV V -1;
		Stop;
	Raise:
		SSWV M 5;
		SSWV LKJI 5;
		Goto See ;
	}
}