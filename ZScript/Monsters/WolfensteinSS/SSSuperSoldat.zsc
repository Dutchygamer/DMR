//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	n/a
//	Sounds:		Id Software
//  Sprites: 	???
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class SSSupersoldat : DMR_NaziBase
{
	Default
	{
		Tag "SS Supersoldat";	
		Health 450;
		Speed 12;
		PainChance 40;
		Scale 1.1;
		SeeSound "wolfss/sight";
		PainSound "wolfss/pain";
		DeathSound "wolfss/death";
		ActiveSound "wolfss/active";
		//AttackSound "wolfss/attack";
		Obituary "%o was torn to pieces by a SS Supersoldat.";
		DropItem "Shell", 256, 4;
		DropItem "Shell", 128, 4;
		DropItem "Shell", 128, 4;
		DropItem "Shell", 64, 4;
	}
	States
	{
	Spawn:
		HNS3 AB 10 A_Look;
		Loop;
	See:
		HNS3 AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		HNS3 E 10 A_FaceTarget;
	Shotgun:
		HNS3 E 10 A_FaceTarget;
		HNS3 FG 5 Bright Light("DMR_RifleFire") {
			A_FaceTarget();
			A_StartSound("autoshotgun/fire", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,7.1,3,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
		}
		HNS3 E 1 A_MonsterRefire(40,"ShotgunEnd");
		TNT1 A 0 A_Jump(16,"ShotgunEnd");
		Goto Shotgun+1;
	ShotgunEnd:
		HNS3 E 10;
		Goto See;
	Pain:
		HNS3 H 3;
		HNS3 H 3 A_Pain;
		Goto See;
	Death:
	XDeath:
		HNS3 I 5;
		HNS3 J 5 A_Scream;
		HNS3 K 5 A_NoBlocking;
		HNS3 L 5;
		HNS3 M -1;
	Raise:
		HNS3 M 5;
		HNS3 LKJI 5;
		Goto See ;
	}
}