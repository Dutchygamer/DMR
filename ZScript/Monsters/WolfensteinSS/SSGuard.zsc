//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	n/a
//	Sounds:		Id Software
//  Sprites: 	???
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class SSGuard : DMR_NaziBase
{
	Default
	{
		Tag "SS Guard";	
		Health 45;
		Speed 10;
		PainChance 170;
		Scale 1.1; // sprites are shorter then Doom monsters
		SeeSound "wolfssguard/sight";
		PainSound "wolfssguard/pain";
		DeathSound "wolfssguard/death";
		ActiveSound "wolfssguard/active";
		Obituary "%o was taken down by a SS Guard.";
		DropItem "Clip", 256, 5;
	}
	States
	{
	Spawn:
		NGRI AA 10 A_Look;
		Loop;
	See:
		NGRD AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		NGRD E 8 A_FaceTarget;
		NGRD F 8 BRIGHT Light("DMR_RifleFire") {
			A_CustomBulletAttack(22.5,0,1,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
			A_StartSound("grunt/attack", CHAN_WEAPON );
		}
		Goto See;
	Pain:
		NGRD H 3;
		NGRD H 3 A_Pain;
		Goto See;
	Death:
		NGRD I 5;
		NGRD J 5 A_Scream;
		NGRD K 5 A_NoBlocking;
		NGRD L 5;
		NGRD M -1;
		Stop;
	// for now use SS XDeath sprites
	XDeath:
		TNT1 A 0 A_SetScale(1.0);
		SSWV N 5 ;
		SSWV O 5 A_XScream;
		SSWV P 5 A_NoBlocking;
		SSWV QRSTU 5;
		SSWV V -1;
		Stop;
	Raise:
		NGRD M 5;
		NGRD LKJI 5;
		Goto See ;
	}
}