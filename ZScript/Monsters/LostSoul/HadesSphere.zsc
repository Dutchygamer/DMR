//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance, Blue Shadow, MagicWazard
//	Sounds:		Raven Software
//  Sprites: 	Raven Software (edits by Eriance, Cryptic)
//	GLDEFs: 	Ghastly Dragon
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class HadesSphere : DMR_LostSoul
{
	Default
	{		
		Tag "Hades sphere";
		Health 35;
		Damage 0; // no damage from ramming
		Speed 10;
		Obituary "%o was blown away by a hades sphere.";
		SeeSound "monster/hadsit";
		AttackSound "monster/hadwlk";
		DeathSound "monster/hadexp";
		+Bright;
		+LOOKALLAROUND;
		+NOBLOOD;
		+DONTGIB;
		+NOICEDEATH;
		+NOTARGET;
		+FORCEXYBILLBOARD;
		+NOPAIN;
	}
	
	int lifetime;	
	
	States 
	{
	Spawn:
		TNT1 A 0 NoDelay {
			lifetime = random(75,100);
		}
		HADE GH 10 A_Look;
		Goto Spawn+1;
	See:
		HADE EFGH 4;
	SeeLoop:
		TNT1 A 0 A_JumpIf(lifetime < 1,"Death"); // have a limited lifetime
		HADE A 2 A_Playsound("monster/hadwlk");
		HADE ABBCCDD 2 A_Chase;
		TNT1 A 0 {
			lifetime -= 1;
			// [Blue Shadow] Don't bother trying to teleport out while "skull-flying"
			if(bSkullFly == true) { 
				return ResolveState("SeeLoop");
			}
			else {
				return ResolveState(null);
			}
		}
		TNT1 A 0 A_Jump(32, "TeleportOut");
		Loop;
	Missile:
		HADE A 4 BRIGHT A_SkullAttack;
		HADE BCDA 4 BRIGHT;
		Goto Missile+1;
	TeleportOut:
		HADE H 2 {
			A_Playsound("monster/hadtel");
			A_UnSetSolid();
			A_UnSetShootable();
		}
		HADE GFEIJKL 2;
	SeeTeleport:
		TNT1 AAAAA 2 A_Chase(null,null);
		TNT1 A 0 A_Jump(96, "TeleportIn");
		Loop;
	TeleportIn:
		HADE L 2 Bright A_Playsound("monster/hadtel");
		HADE KJIEFG 2;
		TNT1 A 0 {
			A_SetSolid();
			A_SetShootable();
		}
		Goto SeeLoop;
	Melee:
		TNT1 A 0 {
			A_SetInvulnerable();
			A_Playsound("monster/hadsit");
		}
		HADE HGFE 4;
		TNT1 A 0 {
			A_UnSetInvulnerable();
			A_SetShootable();
			A_Die();
		}
		Stop;
	Death:
		HADE M 4 {		
			A_NoBlocking();
			A_SetTranslucent(0.9, 1);
			A_Scream();
			A_Explode(64, 112, XF_NOTMISSILE);
		}
		HADE NOPQ 5;
		Stop;
	} 
}