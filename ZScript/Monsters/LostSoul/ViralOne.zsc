//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	MagicWazard
//	Sounds:		Id Software, Midway Software
//  Sprites: 	Id Software (edits by MagicWazard)
//	GLDEFs: 	MagicWazard
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class ViralOne : DMR_LostSoul
{
	Default
	{
		Tag "Viral One";	
		BloodColor "green";
		Health 60;
		Speed 12;	
		DamageFactor "ToxinCloud", 0;
		Obituary "%o got his medicine from a Viral One";
		+MISSILEEVENMORE
	}
	States 
	{
	Spawn:
		PHNT AB 10 A_Look;
		Loop;
	See:
		PHNT AB 6 A_Chase;
		Loop;
	Missile:
		PHNT C 10 BRIGHT A_FaceTarget;
		PHNT D 4 BRIGHT A_SkullAttack;
		PHNT CD 4 BRIGHT;
		Goto Missile+2;
	Pain:
		PHNT E 3 Bright;
		PHNT E 3 Bright A_Pain;
		Goto See;
	Death:
		PHNT EEF 6 BRIGHT;
		PHNT G 6 BRIGHT A_Scream;
		TNT1 A 0 {
			A_SpawnItemEx("ViralOneExplosion",0,0,10);
			A_Explode(24,72);
			A_StartSound("gas/explode");	
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,10,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,10,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,10,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
		}
		PHNT H 6 Bright;
		PHNT I 6 Bright A_Noblocking;
		PHNT JK 6;
		Stop;
	} 
}

Class ViralOneExplosion : DMR_ComplexFXBase
{
	Default
	{
		+NOINTERACTION;
		DMR_ComplexFXBase.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	
	States
	{
	Spawn:
		TNT1 A 1 NoDelay
		{	
			A_SpawnItemEx("DMR_GasBallImpactSpawner");
			for (fxLoop = 0;fxLoop<=25;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		Stop;		
	}
}
