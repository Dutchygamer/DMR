//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Ben2k9/Virtue
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Ben2k9/Virtue)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class CyberBaronOfHell : DMR_BaronOfHell
{
	Default
	{
		Tag "Cyber Baron of Hell";
		Health 1250;
		DropItem "RocketAmmo", 128;
		DropItem "RocketAmmo", 128;
		obituary "%o was blasted by a cyber baron";
		hitobituary "%o was torn apart by a cyber baron";
	}
	States
	{
	Spawn:
		CBOS AB 10 A_Look;
		loop;
	See:
		CBOS AABBCC 3 A_Chase;
		CBOS D 3 A_Playsound("monster/bruwlk");
		CBOS D 3 A_Chase;
		loop;
	Melee:
		CBOS PQ 8 A_FaceTarget;
		CBOS R 8 A_CustomComboAttack("DMR_BaronBall",32,random(20,60),"baron/melee");
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(128, "Rocket");
		Goto Ball;
	Ball:
		CBOS PQ 8 A_FaceTarget;
		CBOS R 8 A_CustomComboAttack("DMR_BaronBall",32,random(20,60),"baron/melee");
		Goto See;
	Rocket:
		CBOS EE 8 A_FaceTarget;
		CBOS F 10 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",27,16);
		CBOS E 8 A_FaceTarget;
		CBOS F 10 Bright Light("DMR_RifleFire") A_SpawnProjectile("DMR_SmallRocket",27,16);
		CBOS E 8;
		Goto See;
	Pain:
		CBOS H 2;
		CBOS H 2 A_Pain;
		goto See;
	Death:
	XDeath:	
		CBOS I 8;
		CBOS J 8 A_Scream;
		CBOS K 8 bright {
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(8,-8),0,random(20,40));
		}
		CBOS L 8 bright A_NoBlocking;
		CBOS M 8 bright;
		CBOS N 8 bright;
		CBOS O -1;
		stop;
	Raise:
		CBOS ONMLKJI 8;
		Goto See;
	}
}
