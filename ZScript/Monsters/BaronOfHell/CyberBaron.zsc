//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Ben2k9/Virtue
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Ben2k9/Virtue)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class CyberBaronOfHell : DMR_BaronOfHell
{
	Default
	{
		Tag "Cyber Baron of Hell";
		Health 1250;
		DropItem "RocketAmmo", 256, 1;
		DropItem "RocketAmmo", 128, 1;
		obituary "%o was blasted by a Cyber Baron of Hell";
		hitobituary "%o was torn apart by a Cyber Baron of Hell";
		SeeSound "cybaron/sight";
		PainSound "cybaron/pain";
		DeathSound "cybaron/death";
	}
	States
	{
	Spawn:
		CBOS AB 10 A_Look;
		loop;
	See:
		CBOS AABBCC 3 A_Chase;
		TNT1 A 0 A_StartSound("cyknight/step");
		CBOS DD 3 A_Chase;
		loop;
	Melee:
		CBOS PQ 8 A_FaceTarget;
		CBOS R 8 A_CustomComboAttack("DMR_BaronBall",32,random(20,60),"baron/melee");
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(128, "Rocket"); //,"LargeRocket");
		Goto Ball;
	Ball:
		CBOS PQ 8 A_FaceTarget;
		CBOS R 8 A_CustomComboAttack("DMR_BaronBall",32,random(20,60),"baron/melee");
		TNT1 A 0 Bright A_Jump(96, "See");
		CBOS E 10 A_FaceTarget;
		CBOS F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",27,16);
		}
		Goto See;
	Rocket:
		CBOS EE 10 A_FaceTarget;
		CBOS F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",27,16);
		}
		CBOS E 10 A_FaceTarget;
		CBOS F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",27,16);
		}
		TNT1 A 0 Bright A_Jump(96, "See");
		CBOS E 10 A_FaceTarget;
		CBOS F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",27,16);
		}
		Goto See;
	LargeRocket:
		CBOS EE 10 A_FaceTarget;
		CBOS F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",27,16);
		}
		Goto See;
	Pain:
		CBOS H 2;
		CBOS H 2 A_Pain;
		goto See;
	Death:
	XDeath:	
		CBOS I 15 A_Scream;
		CBOS J 5;
		CBOS K 5 bright {
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(8,-8),0,random(20,40));
		}
		CBOS L 5 bright A_NoBlocking;
		CBOS M 5 bright;
		CBOS N 5 bright;
		CBOS O -1 A_BossDeath;
		stop;
	Raise:
		CBOS ONMLKJI 8;
		Goto See;
	}
}
