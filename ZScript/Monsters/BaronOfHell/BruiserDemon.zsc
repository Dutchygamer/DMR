//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Vader
//	Sounds:		Id Software, Blizzard
//  Sprites: 	Id Software (edits by Vader)
//	GLDEFs: 	Ghastly Dragon, Keksdose, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class FireborneNoble : DMR_BaronOfHell
{
	Default
	{
		Tag "Fireborne Noble";
		Health 1500;
		PainChance 40;
		bloodcolor "orange";
		+MISSILEEVENMORE;
		SeeSound "superbaron/scream";
		PainSound "superbaron/pain";
		DeathSound "superbaron/death";
		ActiveSound "superbaron/act";
		Obituary "%o was slaughtered by a Fireborne Noble.";
		HitObituary "%o was cremated by a Fireborne Noble.";
	}
	
	int spamBallAngle;
	
	States
	{
	Spawn:
		BRUS AB 10 Bright A_Look;
		Loop;
	See:
		BRUS AABBCCDD 3 Bright {
			A_Chase();
			A_SpawnItemEx("FireborneNobleWalkFire",0,0,0,0,0,0,0,0,64);
		}
		Loop;
	Melee:
		BRUS EF 6 Bright A_FaceTarget;
		BRUS G 6 Bright A_CustomComboAttack("AfritBall",39,random(22,66),"baron/melee");
		Goto See;
	Missile: 
		TNT1 A 0 A_Jump(128,"Comet","Spam","FireWaves");		
	Balls:
		BRUS EF 8 Bright A_FaceTarget;
		BRUS G 8 Bright A_SpawnProjectile("AfritBall",39);
		BRUS HI 8 Bright A_FaceTarget;
		BRUS J 8 Bright A_SpawnProjectile("AfritBall",39);
		TNT1 A 0 Bright A_Jump(96, "See");
		BRUS EF 8 Bright A_FaceTarget;
		BRUS G 8 Bright A_SpawnProjectile("AfritBall",39);
		Goto See;
	Comet:
		BRUS EEF 8 Bright A_FaceTarget;
		BRUS G 8 Bright A_SpawnProjectile("AfritComet",39);
		BRUS HI 8 Bright A_FaceTarget;
		BRUS J 8 Bright A_SpawnProjectile("AfritComet",39);
		Goto See;
	Spam:
		TNT1 A 0 { 
			spamBallAngle = -20; 
		}
		BRUS EF 8 Bright A_FaceTarget;
		BRUS GGGGGGG 1 Bright {
			A_SpawnProjectile("FireborneNobleSpamBall",39,0,spamBallAngle,0);
			spamBallAngle += 4;
		}
		TNT1 A 0 A_Jump(96, 1);
		Goto See;
		TNT1 A 0 { 
			spamBallAngle = 20; 
		}
		BRUS HI 8 Bright A_FaceTarget;
		BRUS JJJJJJJ 1 Bright {
			A_SpawnProjectile("FireborneNobleSpamBall",39,0,spamBallAngle,0);
			spamBallAngle -= 4;
		}
		Goto See;
	FireWaves:
		TNT1 A 0 {
			if (target && Distance3D(target) > 300) SetStateLabel("Comet");
		}
		BRUS KKK 8 Bright A_FaceTarget;
		BRUS L 8 Bright A_FaceTarget;
		BRUS M 8 Bright A_SpawnProjectile("FireborneNobleFireSpawner",16);
		Goto See;
	Pain:
		BRUS H 2 Bright;
		BRUS H 2 Bright A_Pain;
		Goto See;
	Death:
	XDeath:
		BRUD A 6 Bright A_Scream;
		BRUD BCDEFG 4 Bright;
		BRUD H 4 Bright {
			A_Fall();
			for (fxLoop = 0;fxLoop <= 2;fxLoop++) {
				A_SpawnItemEx("DMR_AshCloud",random(-5,5),random(-2,2),random(24,56),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.2,0.5));
			}
		}
		BRUD IJKLMNOPQ 4 Bright {
			for (fxLoop = 0;fxLoop <= 2;fxLoop++) {
				A_SpawnItemEx("DMR_AshCloud",random(-5,5),random(-2,2),random(24,56),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.2,0.5));
			}
		}
		BRUD RSTUV 4;
		BRUD W -1 A_BossDeath;
		Stop;
	Raise: 
		BRUD W 3;
		BRUD VUTSRQPONMLKJIHGFEDCBA 3 Bright;
		Goto See;
	}
}

class FireborneNobleSpamBall : DMR_DoomImpBall
{
	Default
	{
		DamageFunction(random(6,16));
		Alpha 0.9;
	}
	States
	{
	Spawn:
		BRB2 AAABBB 2 BRIGHT {
			A_SpawnItemEx("DMR_FireballTrail",-7,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1));
			A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
		}
		Loop;
	Death:		
		TNT1 A 0 {
			A_SpawnItemEx("DMR_FireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=20;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		BRB2 CDEFGHI 3 Bright;
		Stop;
	}
}

class FireborneNobleFireSpawner : DMR_Projectile
{
	Default
	{
		Radius 5;
		Height 5;
		Speed 30;
		DamageFunction(random(20,60));
		+THRUGHOST;
		+FLOORHUGGER;
		+BLOODLESSIMPACT;
		-NOGRAVITY;
		+DONTSPLASH;
		+BRIGHT
		RenderStyle "Add";
		//DamageType "fire";
		SeeSound "flamebringer/firefx";
		DeathSound "weapons/rocklx";		
	}
	int fuelLevel;
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			fuelLevel = 25; // set fuel lifetime
		}
	SpawnLoop:
  		TNT1 A 1 Bright {
			if (fuelLevel < 1) {
				SetStateLabel("Death");
			}
			fuelLevel--;
			
			A_SpawnProjectile("FireborneNobleFloorFire",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER);
		}
		Loop;
	Death:
		Stop;
		/*TNT1 A 0 {
			A_Stop();
		}
  		XXBF AB 3 Bright;
		TNT1 A 0 {
			A_Explode(32,96);
		}
		XXBF CDEFGHIJKLMNOPQRST 3 Bright;
		Stop;*/
	}
}

class FireborneNobleFloorFire : DMR_Projectile
{
	Default
	{
		Radius 1;
		Height 1;
		Speed 0;
		RENDERSTYLE "ADD";
		DamageType "fire";
		ALPHA 0.9;
		+FLOORHUGGER;
		+FORCERADIUSDMG;
	}
	States
	{
	Spawn:
		XXBF AB random(1,2) Bright;
		TNT1 A 0 {
			A_Explode(32,96);
		}
		XXBF ABCDEFGHIJKLMNOPQRST random(1,2) Bright;
	 	Stop;
	}
}

//======================================================================
//  Original credits
//======================================================================
//  Zscript: 	DustedPandomic
//======================================================================
Class FireborneNobleWalkFire : Actor
{
	Default
	{
		+NOCLIP;
		+DROPOFF;
		+RANDOMIZE;
		+NOINTERACTION;
		RenderStyle "Add";
		Alpha 0.67;
		YScale 0.8;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_CheckSightOrRange(2000,"Null");
		TNT1 A 8;
		FRTF ABC 3 Bright;
		TNT1 AAA 0 {
			if (Translation)
				A_SpawnItemEx("FireborneNobleWalkFire",0,0,0,frandom(0,2),0,0,random(0,360),SXF_TRANSFERTRANSLATION,220);
			else
				A_SpawnItemEx("FireborneNobleWalkFire",0,0,0,frandom(0,2),0,0,random(0,360),0,220);
		}
		FRTF DEFGHIJKLMNO 3 Bright;
		Stop;
	}
}
