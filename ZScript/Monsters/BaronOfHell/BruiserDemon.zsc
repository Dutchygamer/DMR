//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Vader
//	Sounds:		Id Software, Blizzard
//  Sprites: 	Id Software (edits by Vader)
//	GLDEFs: 	Ghastly Dragon, Keksdose, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class BruiserDemon : DMR_BaronOfHell
{
	Default
	{
		Tag "Fireborne";
		Health 1500;
		PainChance 40;
		bloodcolor "orange";
		+MISSILEEVENMORE;
		SeeSound "superbaron/scream";
		PainSound "superbaron/pain";
		DeathSound "superbaron/death";
		ActiveSound "superbaron/act";
		Obituary "%o was slaughtered by a Fireborne.";
		HitObituary "%o was cremated by a Fireborne.";
	}
	States
	{
	Spawn:
		BRUS AB 10 Bright A_Look;
		Loop;
	See:
		BRUS AABBCCDD 3 Bright A_Chase;
		Loop;
	Missile: 
		TNT1 A 0 A_Jump(96, "Spam");
		TNT1 A 0 A_Jump(96, "FireWaves");
		Goto Ball;
	Ball:
		BRUS EF 6 Bright A_FaceTarget;
		BRUS G 6 Bright A_SpawnProjectile("BruiserBall",39,0,0,0);
		TNT1 A 0 Bright A_Jump(96, 1);
		Goto See;
		BRUS HI 6 Bright A_FaceTarget;
		BRUS J 6 Bright A_SpawnProjectile("BruiserBall",39,0,0,0);
		Goto See;
	Spam:
		BRUS E 6 Bright A_FaceTarget;
		BRUS F 4 Bright A_FaceTarget;
		BRUS F 2 Bright A_SpawnProjectile("BruiserSpamBall",39,0,-40,0);
		BRUS G 1 Bright A_SpawnProjectile("BruiserSpamBall",39,0,-30,0);
		BRUS G 1 Bright A_SpawnProjectile("BruiserSpamBall",39,0,-20,0);
		BRUS G 1 Bright A_SpawnProjectile("BruiserSpamBall",39,0,-10,0);
		BRUS G 1 Bright A_SpawnProjectile("BruiserSpamBall",39,0,0,0);
		BRUS G 1 Bright A_SpawnProjectile("BruiserSpamBall",39,0,10,0);
		BRUS G 1 Bright A_SpawnProjectile("BruiserSpamBall",39,0,20,0);
		TNT1 A 0 A_Jump(96, 1);
		Goto See;
		BRUS H 6 Bright A_FaceTarget;
		BRUS I 4 Bright A_FaceTarget;
		BRUS I 2 Bright A_SpawnProjectile("BruiserSpamBall",39,0,40,0);
		BRUS J 1 Bright A_SpawnProjectile("BruiserSpamBall",39,0,30,0);
		BRUS J 1 Bright A_SpawnProjectile("BruiserSpamBall",39,0,20,0);
		BRUS J 1 Bright A_SpawnProjectile("BruiserSpamBall",39,0,10,0);
		BRUS J 1 Bright A_SpawnProjectile("BruiserSpamBall",39,0,0,0);
		BRUS J 1 Bright A_SpawnProjectile("BruiserSpamBall",39,0,-10,0);
		BRUS J 1 Bright A_SpawnProjectile("BruiserSpamBall",39,0,-20,0);
		Goto See;
	FireWaves:
		BRUS KL 6 Bright A_FaceTarget;
		BRUS M 2 Bright A_SpawnProjectile("BruiserFireSpawner",16,0,0,0);
		Goto See;
	Melee:
		BRUS EF 6 Bright A_FaceTarget;
		BRUS G 6 Bright A_CustomComboAttack("BruiserBall",39,random(22,66),"baron/melee");
		Goto See;
	Pain:
		BRUS H 2 Bright;
		BRUS H 2 Bright A_Pain;
		Goto See;
	Death:
	XDeath:
		BRUD A 6 Bright A_Scream;
		BRUD BCDEFG 4 Bright;
		BRUD H 4 Bright A_Fall;
		BRUD IJKLMNOPQRSTUV 4 Bright;
		BRUD W -1 A_BossDeath;
		Stop;
	Raise: 
		BRUD W 3;
		BRUD VUTSRQPONMLKJIHGFEDCBA 3 Bright;
		Goto See;
	}
}

class BruiserBall : DMR_BaronBall
{
	Default
	{
		DamageFunction(random(20,60));
		Speed 16;
		Alpha 0.9;
	}
	States
	{
	Spawn:
		BRBA AABB 2 BRIGHT {
			A_SpawnItemEx("DMR_LargeFireballTrail",-5,0,5,frandom(-1,0),frandom(-1,1),frandom(0,1));
			A_SpawnItemEx("BruiserBallTrail",0,0,0,0,0,0);
		}
		Loop;
	Death:
		TNT1 AAAA 0 {
			A_SpawnItemEx("DMR_MediumFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
			A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		BRBA KLMNOPQRSTUVWX 3 Bright;
		Stop;
	}
}

class BruiserSpamBall : DMR_DoomImpBall
{
	Default
	{
		Speed 10;
		DamageFunction(random(6,16));
		Alpha 0.9;
	}
	States
	{
	Spawn:
		BRB2 AB 6 BRIGHT {
			A_SpawnItemEx("DMR_FireballTrail",0,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1));
		}
		Loop;
	Death:		
		TNT1 AAA 0 {
			A_SpawnItemEx("DMR_FireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
			A_SpawnItemEx("DMR_FireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		BRB2 CDEFGHI 3 Bright;
		Stop;
	}
}

class BruiserBallTrail : DMR_ProjectileTrail
{
	Default
	{
		ALPHA 0.70;
	}
	States
	{
	Spawn:
	 	BRBA CDEFGHIJ 4 Bright;
	 	Stop;
	}
}

class BruiserFireSpawner : Actor
{
	Default
	{
		Radius 5;
		Height 5;
		Speed 12;
		DamageFunction(random(20,60));
		Projectile;
		+THRUGHOST;
		+FLOORHUGGER;
		+BLOODLESSIMPACT;
		-NOGRAVITY;
		+DONTSPLASH;
		+BRIGHT
		DamageType "fire";
		SeeSound "flamebringer/firefx";
		DeathSound "weapons/rocklx";		
	}
	int dmgtic;
	
	States
	{
	Spawn:
  		TNT1 A 1 Bright {
			// only spawn fire X amount of tics instead of every tick
			dmgtic++;
			if(dmgtic >= 2)
			{
				A_SpawnItem("BruiserFire",0,0);
				dmgtic = 0;
			}	
		}
		Loop;
	Death:
  		XXBF AB 3 Bright;
		XXBF C 3 Bright A_Explode(32,96);
		XXBF DEFGHIJKLMNOPQRST 3 Bright;
		Stop;
	}
}

class BruiserFire: Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Speed 0;
		PROJECTILE;
		RENDERSTYLE "ADD";
		DamageType "fire";
		ALPHA 0.9;
		+NOCLIP;
		+FLOORHUGGER;
	}
	States
	{
	Spawn:
		XXBF ABCDEFGHIJKLMNOPQRST random(1,2) Bright A_Explode(3,8,0);
	 	Stop;
	}
}
