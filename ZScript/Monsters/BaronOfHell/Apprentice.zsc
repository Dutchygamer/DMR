//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Vader
//	Sounds:		Id Software, Vader
//  Sprites: 	Id Software (edits by Vader)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//	DustedPandomic for the charge / ram logic
//======================================================================

class Hellsmith : DMR_BaronOfHell
{
	Default
	{
		Tag "Hellsmith";
		Health 1300;
		PainChance 45;
		Obituary "%o felt the wrath of a Hellsmith.";
		HitObituary "%o got crushed by a Hellsmith.";
		+MISSILEMORE;
		SeeSound "Hellsmith/sight";
		PainSound "Hellsmith/pain";
		ActiveSound "Hellsmith/active";
		DeathSound "Hellsmith/death";
		Scale 0.7;
		bloodcolor "red";
	}
	States
	{
	Spawn:
		SMT1 AB 10 A_Look;
		Loop;
	See:
		SMT1 AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 A_Jump(96, "Charge");
	Balls: 
		SMT1 E 8 A_FaceTarget;
		SMT1 H 8 A_StartSound ("hellsmith/swing", CHAN_WEAPON);
		SMT1 I 10 {
			A_StartSound ("weapons/hellfi", CHAN_WEAPON);
			A_SpawnProjectile("HellsmithBall",36,0,0);
			A_SpawnProjectile("HellsmithBall",36,0,-8);
		}
		TNT1 A 0 A_Jump (128,1);
		Goto See;
		SMT1 J 8 A_FaceTarget;
		SMT1 M 8 A_StartSound ("hellsmith/swing", CHAN_WEAPON);
		SMT1 N 10 {
			A_StartSound ("weapons/hellfi", CHAN_WEAPON);
			A_SpawnProjectile("HellsmithBall",36,0,0);
			A_SpawnProjectile("HellsmithBall",36,0,8);
		}
		Goto See;
	Charge:
		TNT1 A 0 {
			if (target && Distance3D(target) > 360) {
				return ResolveState("Balls");
			}
			return ResolveState(null);
		}
		SMT1 JJJJJJ 2 A_FaceTarget;
		TNT1 A 0 {
			A_StartSound("Hellsmith/sight", CHAN_VOICE);
			A_FaceTarget();
			A_Recoil(-3);
		}
	ChargeLoop:
		SMT1 AAABBBCCCDDD 1 {
			if (target && Distance3D(target) < 80 || vel.x == 0 && vel.y == 0) {
				return ResolveState("Ram");
			}
			A_FaceTarget(1.5,180);
			A_Recoil(-3);
			return ResolveState(null);
		}
		Loop;
	Ram:
		SMT1 E 2 A_FaceTarget(30,180);
		SMT1 F 6 {
			A_FaceTarget(20,180);
			A_CustomBulletAttack(0,0,1,random(20,60),"SatyrRamImpactor",80,CBAF_NORANDOM|CBAF_AIMFACING);
			A_Stop();
		}
		SMT1 A 2 A_FaceTarget;
		Goto See;
	Melee:
		SMT1 E 8 A_FaceTarget;
		SMT1 F 8 {
			A_FaceTarget();
			A_StartSound ("hellsmith/swing");
		}
		SMT1 G 8 A_CustomMeleeAttack(random(20,60), "hellsmith/hit");
		TNT1 A 0 A_Jump (192,1);
		Goto See;
		SMT1 J 8 A_FaceTarget;
		SMT1 K 8 {
			A_FaceTarget();
			A_StartSound ("hellsmith/swing");
		}
		SMT1 L 8 A_CustomMeleeAttack(random(20,60), "hellsmith/hit");
		Goto See;
	Pain:
		SMT1 P 2;
		SMT1 P 2 A_Pain;
		Goto See;
	Death:
	XDeath:
		SMT1 Q 6 A_SpawnItemEx("HellsmithDroppedHammer",0,-28,82,6,0,0,30,SXF_SETMASTER);
		SMT1 R 6 A_Scream;
		SMT1 STU 6;
		SMT1 V 6 A_NoBlocking;
		SMT1 X 6;
		SMT1 Y -1 A_BossDeath;
		Stop;
	Raise: 
		SMT1 YXVUTSRQ 3;
		Goto See;
	}
}

class HellsmithBall : DMR_BaronBall
{
	Default
	{
		Speed 20;
		FastSpeed 30;
		DamageFunction(random(20,60));
		//ALPHA 0.95;
		SeeSound "";
		DeathSound "weapons/hellex";
		Scale 0.8;
	}
	States
	{
	Spawn: 
		HBAL ABCD 3 Bright {
			A_SpawnItemEx("DMR_RedPuffTrail",0,0,0,0,0,0);
			A_SpawnItemEx("DMR_MediumFireballTrail",-3,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
		loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_MediumFireballImpactSpawner");
		}
		HBAL EFGHI 3 Bright;
		stop;
	}
}

// death
class HellsmithDroppedHammer : DMR_DroppedShield
{
	Default
	{
		+FORCEXYBILLBOARD; //TODO really neccesary?
		DeathSound "hellsmith/hitfloor";
		Scale 0.7;
		Bouncefactor 0.5;
	}
	States
	{
	Spawn:
		MAUL ABC 4;
		Loop;
	Death:
		MAUL D -1 ;
		stop;
	}
}
