//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Id Software, Eriance
//  Sprites: 	Id Software, Raven Software, Wolfendoom (edits by Eriance)
//	GLDEFs: 	Dreadopp, SandyPaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//  Sprites:	Dutchygamer (modification of original sprites)
//======================================================================

class NoblePraetorian : DMR_BaronOfHell
{
	Default
	{
		Tag "Kur Praetorian";
		Health 1300;
		PainChance 35;
		DropItem "RocketAmmo", 256, 1;
		DropItem "RocketAmmo", 128, 1;
		DropItem "RocketAmmo", 128, 1;
		Obituary "%o was blown away by a kur praetorian.";
		HitObituary "%o was smashed by a kur praetorian.";
		+MISSILEMORE;
		SeeSound "monster/brusit";
		DeathSound "monster/brudth";
		Scale 1.05;
	}
	States
	{
	Spawn:
		BRU2 AB 10 A_Look;
		Loop;
	See:
		BRU2 AABB 3 A_Chase;
		TNT1 A 0 A_PlaySound("noblebaron/step");
		BRU2 CCDD 3 A_Chase;
		TNT1 A 0 A_PlaySound("noblebaron/step");
		Loop;
	Missile: 
		TNT1 A 0 A_Jump(72, "Plasma");
		TNT1 A 0 A_Jump(256, "Rockets", "RocketBurst");
	Rockets:
		BRU2 EE 8 A_FaceTarget;
		BRU2 F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",39,16);
		}
		BRU2 E 8 A_FaceTarget;
		BRU2 F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",39,16);
		}
		BRU2 E 8 A_FaceTarget;
		BRU2 F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",39,16);
		}
		BRU2 E 8;
		Goto See;
	RocketBurst:
        TNT1 A 0 A_PlaySound("heavyweapon/load");
		BRU2 EEE 10 A_FaceTarget;
		BRU2 F 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",39,16);
		}
		BRU2 E 2 A_FaceTarget;
		BRU2 F 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",39,16);
		}
		BRU2 E 2 A_FaceTarget;
		BRU2 F 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",39,16);
		}
		BRU2 E 8;
		Goto See;
	Plasma:
		BRU2 EE 8 A_FaceTarget;
		BRU2 F 8 Bright Light("DMR_HellRuneFire") A_SpawnProjectile("DMR_HellRune",39,16);
		BRU2 E 6 A_FaceTarget;
		BRU2 F 8 Bright Light("DMR_HellRuneFire") A_SpawnProjectile("DMR_HellRune",39,16);
		Goto See;
	Melee:
		BRU2 GH 8 A_FaceTarget;
		BRU2 I 8 A_CustomMeleeAttack(random(22,66),"baron/melee");
		Goto See;
	Pain:
		BRU2 J 2;
		BRU2 J 2 A_Pain;
		Goto See;
	Death:
	XDeath:
		BRU2 K 6 Bright A_Scream;
		BRU2 L 6 Bright;
		BRU2 MN 6 Bright {
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(8,-8),0,random(20,40));
		}
		BRU2 O 6 Bright A_NoBlocking;
		BRU2 QRS 6 Bright;
		BRU2 T -1 A_BossDeath;
		Stop;
	Raise: 
		BRU2 TSRQPONMLK 5;
		Goto See;
	}
}

//TODO unused
class NoblePraetorianHellRune : DMR_HellRune
{  
	Default
	{
		DamageFunction(random(20,60));
		Speed 18;
	}
}