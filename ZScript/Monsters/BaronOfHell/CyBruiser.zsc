//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Id Software, Eriance
//  Sprites: 	Id Software, Raven Software, Wolfendoom (edits by Eriance)
//	GLDEFs: 	Dreadopp, SandyPaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//  Sprites:	Dutchygamer (modification of original sprites)
//======================================================================

class CyBruiser : DMR_BaronOfHell
{
	Default
	{
		Tag "Noble Praetorian";
		Health 1300;
		PainChance 35;
		Obituary "%o was blown away by a noble praetorian.";
		HitObituary "%o was smashed by a noble praetorian.";
		+MISSILEMORE;
		SeeSound "monster/brusit";
		DeathSound "monster/brudth";
		Scale 1.05;
	}
	States
	{
	Spawn:
		BRU2 AB 10 A_Look;
		Loop;
	See:
		BRU2 A 1 A_Playsound("monster/bruwlk");
		BRU2 A 2 A_Chase;
		BRU2 A 3 A_Chase;
		BRU2 BB 3 A_Chase;
		BRU2 C 1 A_Playsound("monster/bruwlk");
		BRU2 C 2 A_Chase;
		BRU2 C 3 A_Chase;
		BRU2 DD 3 A_Chase;
		Loop;
	Missile: 
		TNT1 A 0 A_Jump(180, "Rockets");
		Goto Plasma;
	Rockets:
		BRU2 EE 8 A_FaceTarget;
		BRU2 F 12 Bright A_SpawnProjectile("DMR_SmallRocket",39,16,0,0);
		BRU2 E 12 A_FaceTarget;
		BRU2 F 12 Bright A_SpawnProjectile("DMR_SmallRocket",39,16,0,0);
		BRU2 E 12 A_FaceTarget;
		BRU2 F 12 Bright A_SpawnProjectile("DMR_SmallRocket",39,16,0,0);
		Goto See;
	Plasma:
		BRU2 EE 8 A_FaceTarget;
		BRU2 F 8 Bright A_SpawnProjectile("BruiserPlasmaRune",39,16,0,0);
		BRU2 E 6 A_FaceTarget;
		BRU2 F 8 Bright A_SpawnProjectile("BruiserPlasmaRune",39,16,0,0);		
		Goto See;
	Melee:
		BRU2 GH 8 A_FaceTarget;
		BRU2 I 8 A_CustomMeleeAttack(random(22,66),"baron/melee");
		Goto See;
	Pain:
		BRU2 J 2;
		BRU2 J 2 A_Pain;
		Goto See;
	Death:
	XDeath:
		BRU2 K 6 Bright A_Scream;
		BRU2 LMN 6 Bright;
		BRU2 O 6 Bright A_NoBlocking;
		BRU2 QRS 6 Bright;
		BRU2 T -1 A_BossDeath;
		Stop;
	Raise: 
		BRU2 TSRQPONMLK 5;
		Goto See;
	}
}

class BruiserPlasmaRune : BaronBall
{  
	Default
	{
		DamageFunction(random(20,60));
		Speed 18;
		Alpha 1;
		+SEEKERMISSILE
		SeeSound "Paladin/Shoot";
		DeathSound "weapons/hellrune";
	}
	States
	{
	Spawn:
		PLRN AAABBB 2 Bright {
			A_SeekerMissile(1, 1);
			A_Weave(frandom(1,2.5), frandom(1,2.5), frandom(0.7,1.2), frandom(0.7,1.2));
			A_SetRoll(roll+15);
			A_SpawnItemEx("DMR_PlasmaRuneTrail",frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
		}
		Loop;
	Death:
		PLRX ABCDEF 3 Bright;
		Stop;
	}
}