//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Finalizer, Tormentor667, Vader
//	Sounds:		Id Software
//  Sprites: 	Id Software Raven Software (edits by Wolfendoom, Eriance, Vader, HorrorMovieGuy, Finalizer)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		EA Games
//	ZScript:	Dutchygamer
//  Sprites:	Dutchygamer (modification of original sprites)
//	DustedPandemonic for improvements to the ShieldWall logic
//======================================================================

class KurLegionary : DMR_BaronOfHell
{
	Default
	{
		Tag "Kur Legionary";
		Health 1400;
		PainChance 35;
		DropItem "RocketAmmo", 256, 1;
		DropItem "RocketAmmo", 128, 1;
		DropItem "RocketAmmo", 128, 1;
		Obituary "%o couldn't get past a Kur Legionary's shield.";
		HitObituary "A Kur Legionary shield-bashed %o to bits.";
		SeeSound "kurlegionary/sight";
		PainSound "kurlegionary/pain";
		DeathSound "kurlegionary/death";
		//ActiveSound "Paladin/Active";
		
		PainChance "DMR_ShieldBroken", 256;
	}
	
	int painYelp;	
	
	States
	{ 
	Spawn: 
		HPAL AB 10 A_Look;
		Loop;
	See:
		TNT1 A 0 {
			A_RemoveChildren(1,RMVF_EVERYTHING);
		}
		HPAL AABB 3 A_Chase;
		TNT1 A 0 A_StartSound("cybruiser/step");
		HPAL CCDD 3 A_Chase;
		TNT1 A 0 A_StartSound("cybruiser/step");
		Loop;
	SeeShield:
		HPAL EEFF 5 A_Chase;
		TNT1 A 0 A_StartSound("cybruiser/step");
		HPAL GGHH 5 A_Chase;
		TNT1 A 0 A_StartSound("cybruiser/step");
		TNT1 A 0 A_Jump(96,"ShieldBlast");
		Loop;
	Melee:
		HPAL MN 6 A_FaceTarget;
		HPAL O 6 A_CustomMeleeAttack(random(24,72),"Paladin/Melee","Paladin/Swing");
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(100,"RaiseShield","Plasma","BigRocket");
	Grenade:
		HPAL I 10 A_FaceTarget;
		HPAL J 10 Bright Light("DMR_RifleFire") {
			A_LobProjectile("DMR_FiredGrenade",32,16);
		}
		HPAL I 8 A_FaceTarget;
		HPAL J 10 Bright Light("DMR_RifleFire") {
			A_LobProjectile("DMR_FiredGrenade",32,16);
		}
		HPAL I 8 A_FaceTarget;
		HPAL J 10 Bright Light("DMR_RifleFire") {
			A_LobProjectile("DMR_FiredGrenade",32,16);
		}
		HPAL I 8;
		Goto See;		
	Plasma:
		HPAL II 8 A_FaceTarget;
		HPAL J 8 Bright Light("DMR_HellRuneFire") A_SpawnProjectile("DMR_HellRune",32,16);
		HPAL I 6 A_FaceTarget;
		HPAL J 8 Bright Light("DMR_HellRuneFire") A_SpawnProjectile("DMR_HellRune",32,16);
		Goto See;
	BigRocket:
        TNT1 A 0 A_StartSound("heavyweapon/load");
		HPAL III 10 A_FaceTarget;
		HPAL J 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",32,16);
		}
		HPAL I 10 A_FaceTarget;
		HPAL J 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_Rocket",32,16);
		}
		HPAL I 10;
		Goto See;	
	RaiseShield:
		TNT1 A 0 {
			A_SpawnItemEx("KurLegionaryShieldWall",0,0,0,0,0,0,0,SXF_SETMASTER);
		}
		HPAL KKKKK random(4,6) A_FaceTarget;
		TNT1 A 0 A_Jump(128, "ShieldBlast", "SeeShield");
		HPAL KKKKK random(4,6) A_FaceTarget;
		TNT1 A 0 {
			A_RemoveChildren(1,RMVF_EVERYTHING);
		}
		Goto See;
	ShieldBlast:
		TNT1 A 0 {
			A_SpawnItemEx("KurLegionaryShieldWall",0,0,0,0,0,0,0,SXF_SETMASTER);			
		}
		HPAL K 4 A_FaceTarget;
		HPAL KKKKK 4 {
			A_FaceTarget();
			A_SpawnItemEx("DMR_PlasmaRuneTrail",0,frandom(-1,1),frandom(27,28),0,0,0,0,SXF_SETMASTER);
		}
		HPAL L 4 Bright Light("DMR_HellRuneFire") A_SpawnProjectile("KurLegionaryHellRune",28,0);
		HPAL L 4 Bright Light("DMR_HellRuneFire") A_SpawnProjectile("KurLegionaryHellRune",28,0,-10);
		HPAL L 4 Bright Light("DMR_HellRuneFire") A_SpawnProjectile("KurLegionaryHellRune",28,0,10);
		TNT1 A 0 {
			A_RemoveChildren(1,RMVF_EVERYTHING);
		}
		Goto See;
	Pain.DMR_ShieldBroken: // upon suffering a broken shield, stay in this painstate for a bit
		TNT1 A 0 {
			bNoPain = true; // don't switch to the default pain state when we are in this one
			A_StartSound("shieldwall/death");
		}
		HPAL P 24 {
			if (random(1,5) == 5) {
				A_Pain(); 
			}
		}
		TNT1 A 0 {
			bNoPain = false;
		}
		Goto See;
	Pain:
		TNT1 A 0 {
			A_RemoveChildren(1,RMVF_EVERYTHING);
		}
		HPAL P 2;
		HPAL P 2 A_Pain;
		TNT1 A 0 A_Jump(128, "RaiseShield");
		Goto See;
	Death:
	XDeath:
		HPLX A 15 Bright {
			A_Scream();
			A_RemoveChildren(1,RMVF_EVERYTHING);
			A_SpawnItemEx("KurLegionaryDroppedShield",0,0,25,6,0,0,60);
		}
		HPLX BCD 5 Bright {
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(8,-8),0,random(20,40));
		}
		HPLX E 5 Bright A_NoBlocking;
		HPLX FGH 5 Bright;
		HPLX I 5;
		HPLX J -1 A_BossDeath;
		Stop;
	Raise: 
		HPLX I 8 A_KillChildren;
		HPLX HGFEDCBA 8;
		Goto See;
	} 
}

Class KurLegionaryShieldWall : DMR_ShieldWall
{
	Default
	{
		Health 250; // a bit tougher shield
	}
}

Class KurLegionaryHellRune : DMR_HellRuneSmall
{	
	Default
	{
		+THRUSPECIES;
		Species "DMR_ShieldWall"; // so the shield projectiles can pass through the shield
	}
}

Class KurLegionaryDroppedShield : DMR_DroppedShield
{
	States
	{
	Spawn:
		HPLS ABCDEFGH 3;
		Loop;
	Death:
		TNT1 A 0 {
			if (GetCVar("dmr_shieldarmor")) { 
				A_DropItem("KurLegionaryShieldBonus"); 
				return ResolveState("DeathEnd");
			}
			else {			
				return ResolveState(null);
			}
		}
	DeathLoop:
		HPLS I -1;
		Loop;
	DeathEnd:
		HPLS A 0;
		Stop;
	}
}

class KurLegionaryShieldBonus : DMR_ShieldArmor
{
	Default
	{
		Armor.SaveAmount 25;
		Inventory.PickupMessage "Salvaged some armor from a Kur Legionary's shield!";
	}
	States
	{
	Spawn:
		HPLS I -1;
		Stop;
	}
}