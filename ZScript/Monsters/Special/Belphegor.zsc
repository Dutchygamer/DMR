//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	???
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by ??? for Brutal Doom Hell On Earth starter pack)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

// Meant to replace Hell On Earth starter pack's Belphegor
Class Belphegor : DMR_BaronOfHell
{
	Default
	{
		Tag "Belphegor";
		Health 2500;
		Speed 8;
		PainChance 25;
		Obituary "%o was no match for a Belphegor.";
		HitObituary "%o was no match for a Belphegor.";
		+MISSILEMORE;
		SeeSound "baron/sight";
		PainSound "baron/pain";
		DeathSound "baron/death";
		ActiveSound "baron/active";
		Scale 1.3;
	}
	States
	{
	Spawn:
		BOS3 AB 10 A_Look;
		Loop;
	See:
		BOS3 AABBCCDD 2 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 A_Jump(256, "ExplosiveBall", "Clustershot", "Charge");
		Goto See;
	Melee:
	ExplosiveBall:
		BOS3 FG 8 A_FaceTarget;
		BOS3 H 8 A_CustomComboAttack("BelphegorBall",45,random(30,90),"baron/melee");
		Goto See;
	Clustershot:
		BOS3 FG 8 A_FaceTarget;
		BOS3 H 3 Bright A_SpawnProjectile("DMR_BaronBall",39,0,-10,0);
		BOS3 H 3 Bright A_SpawnProjectile("DMR_BaronBall",39,0,0,0);
		BOS3 H 3 Bright A_SpawnProjectile("DMR_BaronBall",39,0,10,0);
		Goto See;
	Charge:
		BOS3 AAAAAA 2 A_FaceTarget;
		TNT1 A 0 {
			A_StartSound("baron/sight", CHAN_VOICE);
			A_FaceTarget();
			A_Recoil(-3);
		}
	ChargeLoop:
		BOS3 AAABBBCCCDDD 1 {
			if (target && Distance3D(target) < 80 || vel.x == 0 && vel.y == 0) { SetStateLabel("Ram"); }
			A_FaceTarget(1.5,180);
			A_Recoil(-3);
		}
		Loop;
	Ram:
		BOS3 F 2 A_FaceTarget(30,180);
		BOS3 G 6 {
			A_FaceTarget(20,180);
			A_CustomBulletAttack(0,0,1,random(34,102),"BelphegorRamImpactor",80,CBAF_NORANDOM|CBAF_AIMFACING);
			A_Stop();
		}
		BOS3 A 2 A_FaceTarget;
		Goto See;
	Pain:
		BOS3 E 2;
		BOS3 E 2 A_Pain;
		Goto See;
	Death:
	XDeath:
		BOS3 I 8;
		BOS3 J 8 A_Scream;
		BOS3 K 8 A_NoBlocking;
		BOS3 L -1 A_BossDeath;
		Stop;
	Raise:
		BOS3 KJI 8;
		Goto See;
	}
}

class BelphegorBall : DMR_BaronBall
{
	Default
	{
		Radius 10;
		Height 22;
		Speed 22;
		FastSpeed 22;
		DamageFunction(random(16,48));
		SeeSound "baron/attack";
		DeathSound "baron/shotx";
		//Decal "BaronScorch";
		Scale 1.5;
	}
	states
	{
	Spawn:
		BAL7 AABB 2 Bright {
			A_SpawnItemEx("DMR_MediumGreenFireballTrail",-3,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1));
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_MediumGreenFireballImpactSpawner");
			A_QuakeEx(3,3,3,15,0,600,"",QF_RELATIVE|QF_SCALEDOWN|QF_3D,falloff:400,rollIntensity:.75);
		}
		BAL7 C 6 Bright A_Explode(64,96);
		BAL7 DE 6 Bright;
		Stop;
	}
}


Class BelphegorRamImpactor : DMR_RamImpactor
{
	Default
	{
		ProjectileKickback 1500;
	}
}
