//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Nanami, Ghastly_dragon
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Espi, Ebola)
//	GLDEFs: 	Keksdose, Herculine (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//	DustedPandomic for the fix to prevent certain Boom/ZDoom map things from breaking with flying enemies (the first block in the 'See' state)
//======================================================================

Class SuperAfrit : Afrit
{
	Default
	{
		Tag "Afrit Lord";
		Health 1300;
		Speed 12;
		PainChance 25;
		Translation "168:191=208:223"; // recolour red parts to orange
		SeeSound "baron/sight";
		PainSound "baron/pain";
		DeathSound "baron/death";
		ActiveSound "baron/active";
		Obituary "%o was torched by an afrit lord.";
		HitObituary "%o was reduced to ashes by the afrit lord.";
		Scale 1.2;
	}
	States
	{
	Spawn:
		FRIT AB 10 A_Look;
		Loop;
	See:
		TNT1 A 0 A_BeginFloat();
		FRIT AABBCCDD 3 A_Chase;
		Loop;
	Melee:
		//FRIT S 0 A_Jump(64,"HellFire") //low chance to perform hellfire summoning
		FRIT ST 6 A_FaceTarget;
		FRIT U 6 A_CustomMeleeAttack(random(20,60),"baron/melee");
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(96,"Comet"); //chance to throw comet
		Goto Ball;
	Ball:
		FRIT ST 6 A_FaceTarget;
		FRIT U 6 {
			A_SpawnProjectile("AfritBall",44,0,-20,0);
			A_SpawnProjectile("AfritBall",44,0,-10,0);
			A_SpawnProjectile("AfritBall",44,0,0,0);
			A_SpawnProjectile("AfritBall",44,0,10,0);
			A_SpawnProjectile("AfritBall",44,0,20,0);
		}
		Goto See;
	Comet:
		FRIT EF 8 A_FaceTarget;
		FRIT G 8 {
			A_SpawnProjectile("AfritComet",44,0,-10,0);
			A_SpawnProjectile("AfritComet",44,0,0,0);
			A_SpawnProjectile("AfritComet",44,0,10,0);
		}
		Goto See;
	Pain:
		FRIT H 4;
		FRIT H 4 A_Pain;
		Goto See;
	Death:
	XDeath:
		TNT1 A 0 A_NoGravity;
		FRIT I 6 A_Scream;
		TNT1 A 0 A_PlaySound("dragonfam/attack");
		FRIT J 5 A_NoBlocking;
		FRIT KLMNOPQR 4;
		Stop;
	Raise:
		Stop; // no rezzing this one
	}
}