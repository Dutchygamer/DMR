//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Bouncy
//	Sounds:		Id Software
//  Sprites: 	The Innocent Crew
//	GLDEFs: 	Dreadopp
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

// Meant to replace Epic 2's AlienFighter
class AlienFighter : DMR_MonsterBase
{
	Default
	{
		Tag "???";
		Health 75;
		Gibhealth -20; // all lower tier monsters (zombies -> imp) have 20 gibhealth
		Species "DMR_Lmao";
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 150;
		MaxTargetRange 896; // don't shock from across the map
		SeeSound "alienfighter/sight";
		PainSound "";
		DeathSound "alienfighter/death";
		OBITUARY "%o never got a clear picture of his killer.";
		DropItem "Cell", 256, 20;
		+DONTHARMSPECIES;
		+NOINFIGHTSPECIES;
	}
	States
	{
	Spawn:
		GMAN AB 10 A_Look;
		Loop;
	See:
		GMAN AABBCCDD 2 A_Chase();
		Loop;
	Missile:
		GMAN EF 5 A_FaceTarget;
		GMAN G 4 BRIGHT Light("DMR_PlasmaFire") {
			A_FaceTarget();
			A_StartSound("alienfighter/fire", CHAN_WEAPON);			A_CustomRailgun(random(8,24),4,"","",RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,1,25,"",0,0,0,0,7.5,0,"TeslaRayParticle");
		}
		GMAN F 4 A_FaceTarget;
		GMAN G 4 BRIGHT Light("DMR_PlasmaFire") {
			A_FaceTarget();
			A_StartSound("alienfighter/fire", CHAN_WEAPON);			A_CustomRailgun(random(8,24),4,"","",RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,1,25,"",0,0,0,0,7.5,0,"TeslaRayParticle");
		}
		GMAN FE 5 A_FaceTarget;
		Goto See;
	Pain:
		GMAN H 3;
		GMAN H 3 A_Pain;
		Goto See;
	Death:
		GMAN I 5;
		GMAN J 5 A_Scream;
		GMAN K 5 A_NoBlocking;
		GMAN L 5;
		GMAN M -1;
		Stop;
	XDeath:
		GMAN N 5 ;
		GMAN O 5 A_XScream;
		GMAN Q 5 A_NoBlocking;
		GMAN RSTU 5;
		GMAN V -1;
		Stop;
	Raise:
		GMAN MLKJI 5;
		Goto See;
	}
}

class TeslaRayParticle : Actor
{
	Default
	{
		+NOINTERACTION;
		+DONTSPLASH;
		+NOBLOCKMAP;
		+BRIGHT;
		+ROLLSPRITE;
		+FLATSPRITE;
		Renderstyle "Add";
		XScale 0.4;
		YScale 0.3;
	}

	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			A_SetRoll(random(0,360));
			A_SetAngle(random(0,360));
			A_SetPitch(random(0,360));
		}
		SHCK AAA 1 BRIGHT A_SetScale(Scale.x - 0.25);
		Stop;
	}
}