//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Id Software, Eriance
//  Sprites: 	Id Software, Raven Software, Wolfendoom (edits by Eriance)
//	GLDEFs: 	Dreadopp, SandyPaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

// Meant to replace Eviternity's Annihilator
// relies on Eviternity's sprite replacements to display properly, so don't use in other mapsets as it will just use the (mismatching) vanilla Baron sprites
class NobleAnnihilator : DMR_BaronOfHell
{
	Default
	{
		Tag "Noble Annihilator";
		Health 1300;
		PainChance 20;
		Speed 10;
		DropItem "RocketAmmo", 256, 1;
		DropItem "RocketAmmo", 128, 1;
		DropItem "RocketAmmo", 128, 1;
		Obituary "%o underestimated the firepower of a noble annihilator.";
		HitObituary "%o was smashed by a noble annihilator.";
		+MISSILEMORE;
		SeeSound "monster/brusit";
		DeathSound "monster/brudth";
		Scale 1.05;
	}
	int boolet;
	
	States
	{
	Spawn:
		BOSS AB 10 A_Look;
		Loop;
	See:
		BOSS AABB 3 A_Chase;
		TNT1 A 0 A_PlaySound("noblebaron/step");
		BOSS CCDD 3 A_Chase;
		TNT1 A 0 A_PlaySound("noblebaron/step");
		Loop;
	Missile: 
		TNT1 A 0 A_Jump(72, "Plasma");
		TNT1 A 0 A_Jump(256, "Rockets", "RocketBurst");
	Rockets:
		BOSS EE 8 A_FaceTarget;
		BOSS F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",39,16);
		}
		BOSS E 8 A_FaceTarget;
		BOSS F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",39,16);
		}
		BOSS E 8 A_FaceTarget;
		BOSS F 10 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",39,16);
		}
		TNT1 A 0 A_Jump(128, "Shotcannon");
		BOSS E 8;
		Goto See;
	RocketBurst:
        TNT1 A 0 A_PlaySound("heavyweapon/load");
		BOSS EEE 10 A_FaceTarget;
		BOSS F 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",39,16);
		}
		BOSS E 2 A_FaceTarget;
		BOSS F 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",39,16);
		}
		BOSS E 2 A_FaceTarget;
		BOSS F 8 Bright Light("DMR_RifleFire") {
			A_SpawnProjectile("DMR_SmallRocket",39,16);
		}
		TNT1 A 0 A_Jump(128, "Shotcannon");
		BOSS E 8;
		Goto See;
	Plasma:
		BOSS EE 8 A_FaceTarget;
		BOSS F 8 Bright Light("DMR_HellRuneFire") A_SpawnProjectile("DMR_HellRune",39,16);
		BOSS E 6 A_FaceTarget;
		BOSS F 8 Bright Light("DMR_HellRuneFire") A_SpawnProjectile("DMR_HellRune",39,16);
		TNT1 A 0 A_Jump(128, "Shotcannon");
		Goto See;
	Shotcannon:
        TNT1 A 0 A_PlaySound("heavyweapon/load");
		BOSS GS 8 A_Facetarget;
		BOSS HIHIHI 5 Bright Light("DMR_RifleFire") {
			A_Playsound("Terminator/tershotB");
			for (boolet = 0;boolet<=2;boolet++) {
				A_SpawnProjectile("DMR_SlowBullet",39,-16,frandom(-10,10),CMF_OFFSETPITCH,frandom(-5,5));
			}	
		}
		Goto See;
	Melee:
		BOSS GH 8 A_FaceTarget;
		BOSS I 8 A_CustomMeleeAttack(random(22,66),"baron/melee");
		Goto See;
	Pain:
		BOSS J 2;
		BOSS J 2 A_Pain;
		Goto See;
	Death:
	XDeath:
		BOSS K 20 Bright A_Scream;
		BOSS LLLL 5 Bright {
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(8,-8),0,random(20,40));
		}
		BOSS M 5 Bright{
			A_NoBlocking();
			A_SpawnItemEx("DMR_MonsterDeathFinalExplosion");
		}
		BOSS NOPQ 5 Bright {
			A_SpawnItemEx("DMR_MonsterDeathExplosion",random(8,-8),0,random(20,40));
		}
		BOSS R -1 A_KeenDie; // the important one
		Stop;
	Raise: 
		BOSS TSRQPONMLK 5;
		Goto See;
	}
}