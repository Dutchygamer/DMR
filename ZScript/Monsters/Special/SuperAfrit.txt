//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Nanami, Ghastly_dragon
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Espi, Ebola)
//	GLDEFs: 	Keksdose, Herculine (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//	DustedPandomic for the fix to prevent certain Boom/ZDoom map things from breaking with flying enemies (the first block in the 'See' state)
//======================================================================

Class SuperAfrit : Afrit
{
	Default
	{
		Tag "Afrit Lord";
		Health 1300;
		Speed 12;
		PainChance 25;
		Translation "168:191=208:223"; // recolour red parts to orange
		SeeSound "baron/sight";
		PainSound "baron/pain";
		DeathSound "baron/death";
		ActiveSound "baron/active";
		Obituary "%o was torched by an afrit lord.";
		HitObituary "%o was reduced to ashes by the afrit lord.";
		Scale 1.2;
	}
	States
	{
	Spawn:
		FRIT AB 10 A_Look;
		Loop;
	See:
		TNT1 A 0
		{
			A_NoGravity(); //these are only here to prevent certain Boom/ZDoom map things from breaking with flying enemies
			bFloat = true;
			bNoGravity = true;
			bFloorClip = false;
		}
		FRIT AABBCCDD 3 Light("DMR_AFRIT") A_Chase;
		Loop;
	Melee:
		//FRIT S 0 A_Jump(64,"HellFire") //low chance to perform hellfire summoning
		FRIT ST 6 Light("DMR_AFRIT") A_FaceTarget;
		FRIT U 6 Light("DMR_AFRIT") A_CustomMeleeAttack(random(20,60),"baron/melee");
		Goto See +2;
	Missile:
		TNT1 A 0 A_Jump(96,"Comet"); //chance to throw comet
		Goto Ball;
	Ball:
		FRIT ST 6 Light("DMR_AFRIT") A_FaceTarget;
		FRIT U 6 Light("DMR_AFRIT") {
			A_SpawnProjectile("AfritBall",44,0,-20,0);
			A_SpawnProjectile("AfritBall",44,0,-10,0);
			A_SpawnProjectile("AfritBall",44,0,0,0);
			A_SpawnProjectile("AfritBall",44,0,10,0);
			A_SpawnProjectile("AfritBall",44,0,20,0);
		}
		Goto See +2;
	Comet:
		FRIT EF 8 Light("DMR_AFRIT") A_FaceTarget;
		FRIT G 8 Light("DMR_AFRIT") {
			A_SpawnProjectile("AfritComet",44,0,-10,0);
			A_SpawnProjectile("AfritComet",44,0,0,0);
			A_SpawnProjectile("AfritComet",44,0,10,0);
		}
		Goto See +2;
	/*HellFire:
		FRIT F 0 A_SpawnProjectile("DMR_HellFireFX", 44, 0, 0)
		FRIT E 6 Bright A_FaceTarget
		FRIT F 6 Bright A_SpawnProjectile("DMR_HellFireSpawner", 44, 0, 0)
		FRIT G 6 Bright A_FaceTarget
		Goto See +2*/
	Pain:
		FRIT H 4 Light("DMR_AFRIT");
		FRIT H 4 Light("DMR_AFRIT") A_Pain;
		Goto See +2;
	Death:
	XDeath:
		TNT1 A 0 Light("DMR_AFRIT") A_NoGravity;
		FRIT I 6 Light("DMR_AFRIT") A_Scream;
		TNT1 A 0 Light("DMR_AFRIT") A_PlaySound("dragonfam/attack");
		FRIT J 5 Light("DMR_AFRIT") A_NoBlocking;
		FRIT KLMNOPQR 4 Light("DMR_AFRIT");
		Stop;
	}
}