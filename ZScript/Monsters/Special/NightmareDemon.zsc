//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Carnevil
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Neoworm, Vader)
//	GLDEFs: 	Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//	DustedPandomic for the charge / ram logic
//======================================================================

// Meant to replace Eviternity's NightmareDemon
// relies on Eviternity's sprite replacements to display properly, so don't use in other mapsets as it will just use the (mismatching) vanilla MBFHelperDog sprites
class DMR_NightmareDemon : DMR_Demon
{
	Default
	{
		Tag "Nightmare Demon";
		Health 300;
		PainChance 100;		
		Speed 15;
		SeeSound "dog/sight";
		AttackSound "dog/attack";
		PainSound "dog/pain";
		DeathSound "dog/death";
		ActiveSound "dog/active";
		Obituary "%o was crushed by a Nightmare Demon.";
		HitObituary "%o was crushed by a Nightmare Demon.";
		MeleeThreshold 64;
	}
	States
	{
	Spawn:
		DOGS AB 10 A_Look;
		Loop;
	See:
		DOGS AABBCCDD 2 Fast A_Chase;
		Loop;
	Melee:
		DOGS E 6 Fast A_FaceTarget;
		DOGS F 7 Fast A_FaceTarget;
		DOGS G 8 Fast A_CustomMeleeAttack(random(8,24));
		Goto See;
	Missile:
		DOGS E 0 A_JumpIfCloser(360, "Charge"); //only do the lunge if closer then this, but further away then MeleeThreshold
		Goto See;
	Charge:
		DOGS E 10 A_FaceTarget;
		TNT1 A 0 {
			A_PlaySound("dog/attack", CHAN_VOICE);
			A_FaceTarget();
			A_Recoil(-3);
		}
	ChargeLoop:
		DOGS AAABBB 1 {
			if (target && Distance3D(target) < 80 || vel.x == 0 && vel.y == 0) { SetStateLabel("Ram"); }
			A_FaceTarget(1.5,180);
			A_Recoil(-3);
		}
		DOGS CCCDDD 1 {
			if (target && Distance3D(target) < 80 || vel.x == 0 && vel.y == 0) { SetStateLabel("Ram"); }
			A_FaceTarget(1.5,180);
			A_Recoil(-3);
		}
		Loop;
	Ram:
		DOGS E 2 A_FaceTarget(30,180);
		DOGS F 8 {
			A_FaceTarget(20,180);
			A_CustomBulletAttack(0,0,1,random(10,30),"DMR_RamImpactor",80,CBAF_NORANDOM|CBAF_AIMFACING);
			A_Stop();
		}
		DOGS A 2 A_FaceTarget;
		Goto See;
	Pain: 
		DOGS H 2 Fast;
		DOGS H 2 Fast A_Pain;
		Goto See;
	Death:
	XDeath:
		DOGS I 8;
		DOGS J 8 A_Scream;
		DOGS K 4;
		DOGS L 4 A_NoBlocking;
		DOGS M 4;
		DOGS N -1; 
		Stop;
	Raise:
		DOGS NMLKJI 5;
		Goto See;
	}
}