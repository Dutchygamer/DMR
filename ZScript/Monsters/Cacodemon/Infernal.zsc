//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	???
//	Sounds:		???
//  Sprites: 	??? (edits by ???)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Infernal : DMR_Cacodemon
{  
	Default
	{
		Tag "Infernal";	
		Health 450;
		HitObituary "%o ended up in the stomach of an infernal.";
		Obituary "%o got blasted by an infernal.";
		DamageFactor "Fire", 0.5;
		+DONTFALL;
		+NOICEDEATH;
		+BRIGHT;
		SeeSound "infernal/see" ;
		PainSound "infernal/pain"; 
		DeathSound "infernal/death";
		bloodcolor "orange";
	}
	States 
	{ 
	Spawn: 
		INFR A 10 A_Look;
		Loop;
	See: 
		INFR BCD 3 A_Chase;
		Loop;
	Missile:
		//TNT1 A 0 A_JumpIfCloser(256, "Flamethrower");
		INFR HI 5 A_FaceTarget;
		INFR J 5 A_SpawnProjectile("InfernalBall",24,0,0,0);
		goto See;
	Flamethrower:
		INFR HI 5 A_FaceTarget;
		TNT1 A 0 A_PlaySound("weapons/flamethrower", CHAN_WEAPON);
		INFR J 1 A_SpawnProjectile("DMR_FlamethrowerBallLarge",28,0,random(-3,3));
		INFR J 1 A_SpawnProjectile("DMR_FlamethrowerBallLarge",28,0,random(-3,3));
		INFR J 1 A_SpawnProjectile("DMR_FlamethrowerBallLarge",28,0,random(-3,3));
		INFR J 1 A_FaceTarget;
		INFR J 1 A_SpawnProjectile("DMR_FlamethrowerBallLarge",28,0,random(-3,3));
		INFR J 1 A_SpawnProjectile("DMR_FlamethrowerBallLarge",28,0,random(-3,3));
		INFR J 1 A_SpawnProjectile("DMR_FlamethrowerBallLarge",28,0,random(-3,3));
		INFR J 1 A_FaceTarget;
		INFR J 1 A_SpawnProjectile("DMR_FlamethrowerBallLarge",28,0,random(-3,3));
		INFR J 1 A_SpawnProjectile("DMR_FlamethrowerBallLarge",28,0,random(-3,3));
		INFR J 1 A_SpawnProjectile("DMR_FlamethrowerBallLarge",28,0,random(-3,3));
		goto See;
	Melee: 
		INFR AFG 5 A_FaceTarget;
		INFR E 5 A_CustomMeleeAttack(random(20,60),"baron/melee");
		Goto See;
	Pain: 
		INFR K 3;
		INFR K 3 A_Pain;
		INFR K 6;
		Goto See;
	Death: 
	XDeath:
		INFR L 6;
		INFR M 6 A_Scream;
		TNT1 A 0 A_SpawnProjectile("DMR_FlamethrowerExplodeLarge", 0, 0);
		INFR N 6;
		INFR O 6;
		INFR P 6 A_NoBlocking;
		INFR Q 6;
		Stop;
	Raise: //no raise as it explodes upon death
		Stop;
	} 
}

class InfernalBall : DMR_CacodemonBall
{
	Default
	{
		DamageFunction(random(10,30));
		DamageType "fire";
		ALPHA 0.95;
		+BRIGHT;
		SeeSound "weapons/firmfi";
		DeathSound "weapons/firex3";
	}
	States
	{
	Spawn: 
		BAL3 AABB 2 {
			A_SpawnItemEx("DMR_FireballTrail",-7,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1));
			A_SpawnItemEx("DMR_RedPuffTrail",0,0,0,0,0,0,0,128,0);
		}
		loop;
	Death:
		TNT1 AAA 0 {
			A_SpawnItemEx("DMR_FireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
		}
		TNT1 AAAAA 0 {
			A_SpawnItemEx("DMR_FireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		BAL3 C 5 A_Explode(20,64,0);
		BAL3 DE 5;
		stop;
	}
}