//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	???
//	Sounds:		???
//  Sprites: 	??? (edits by ???)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Infernal : DMR_Cacodemon
{  
	Default
	{
		Tag "Infernal";	
		Health 450;
		HitObituary "%o ended up in the stomach of an infernal.";
		Obituary "%o got blasted by an infernal.";
		DamageFactor "Fire", 0.5;
		+DONTFALL;
		+NOICEDEATH;
		+BRIGHT;
		SeeSound "infernal/see" ;
		PainSound "infernal/pain"; 
		DeathSound "infernal/death";
		bloodcolor "orange";
	}
	States 
	{ 
	Spawn: 
		INFR A 10 A_Look;
		Loop;
	See: 
		INFR BCD 3 A_Chase;
		Loop;
	Melee: 
		INFR FG 5 A_FaceTarget;
		INFR E 5 A_CustomMeleeAttack(random(20,60),"baron/melee");
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(128,"ExplosiveBall");
		goto Balls;
	Balls:
		INFR HI 5 A_FaceTarget;
		INFR J 5 A_SpawnProjectile("InfernalBall",24);
		INFR I 5 A_FaceTarget;
		INFR J 5 A_SpawnProjectile("InfernalBall",24);
		goto See;
	ExplosiveBall:
		INFR HI 5 A_FaceTarget;
		INFR J 5 A_SpawnProjectile("InfernalExplosiveBall",24);
		goto See;
	Pain: 
		INFR K 3;
		INFR K 3 A_Pain;
		INFR K 6;
		Goto See;
	Death: 
	XDeath:
		INFR L 6;
		INFR M 6 A_Scream;
		INFR N 6;
		INFR O 6;
		INFR P 6 A_NoBlocking;
		INFR Q 6;
		Stop;
	Raise: // no raise as it explodes upon death
		Stop;
	} 
}

class InfernalBall : DMR_CacodemonBall
{
	Default
	{
		DamageFunction(random(10,30));
		+BRIGHT;
		Scale 1.3;
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn: 
		BAL1 AABB 2 {
			A_SpawnItemEx("DMR_MediumFireballTrail",-7,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		loop;
	Death:
		TNT1 A 0 {	
			A_SpawnItemEx("DMR_MediumFireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		BAL1 CDE 5;
		stop;
	}
}

class InfernalExplosiveBall : DMR_CacodemonBall
{
	Default
	{
		DamageFunction(random(10,30));
		Scale 1.1;
		+BRIGHT;
		SeeSound "weapons/firmfi";
		DeathSound "weapons/firex3";
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn: 
		BAL3 AABB 2 {
			A_SpawnItemEx("DMR_LargeFireballTrail",-7,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		loop;
	Death:
		TNT1 A 0 {
			A_SetScale(1);
			A_SpawnItemEx("DMR_MediumFireballLargeImpactSpawner");
			for (fxLoop = 0;fxLoop<=35;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		BAL3 C 5 A_Explode(25,64,0);
		BAL3 DE 5;
		stop;
	}
}