//======================================================================
//  Original credits
//======================================================================
//	ZScript: Skulltag, Gothic
//	Sprites: Skulltag, 3DRealms (Octobrain bits), Midway (D64 Cacodemon bits), David G, Vader, Gothic
//	Sounds: id Software (Doom 3)
//	GLDefs: Skulltag, Gothic
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Infernal : DMR_Cacodemon
{  
	Default
	{
		Tag "Infernal";	
		Health 450;
		Obituary "%o got blasted by an Infernal.";
		HitObituary "%o was chewed up by an Infernal.";
		DamageFactor "Fire", 0.5;
		//SeeSound "infernal/see" ;
		//PainSound "infernal/pain"; 
		//DeathSound "infernal/death";
		bloodcolor "orange";
	}
	States 
	{ 
	Spawn: 
		HED3 A 10 A_Look;
		Loop;
	See: 
		HED3 A 3 A_Chase;
		Loop;
	Melee: 
		HED3 BC 5 A_FaceTarget;
		HED3 D 5 A_CustomMeleeAttack(random(20,60),"baron/melee");
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(128,"ExplosiveBall");
		goto Balls;
	Balls:
		HED3 CD 5 A_FaceTarget;
		HED3 B 5 A_SpawnProjectile("InfernalBall",24);
		HED3 B 5 A_FaceTarget;
		HED3 B 5 A_SpawnProjectile("InfernalBall",24);
		goto See;
	ExplosiveBall:
		HED3 CD 5 A_FaceTarget;
		HED3 B 5 A_SpawnProjectile("InfernalExplosiveBall",24);
		goto See;
	Pain: 
		HED3 E 3;
		HED3 E 3 A_Pain;
		HED3 F 6;
		Goto See;
	Death: 
	XDeath:
		HED3 G 6 A_NoBlocking;
		HED3 H 6 A_Scream;
		HED3 IJKLMN 6;
		HED3 O -1 A_SetFloorClip;
		Stop;
	Raise:
		HED3 O 6 A_UnSetFloorClip;
		HED3 NMLKJIHG 6;
		Goto See;
	} 
}

class InfernalBall : DMR_CacodemonBall
{
	Default
	{
		Speed 12;
 		FastSpeed 25;
		DamageFunction(random(10,30));
		+BRIGHT;
		Scale 1.3;
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn: 
		BAL1 AABB 2 {
			A_SpawnItemEx("DMR_MediumFireballTrail",-7,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		loop;
	Death:
		TNT1 A 0 {	
			A_SpawnItemEx("DMR_MediumFireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		BAL1 CDE 5;
		stop;
	}
}

class InfernalExplosiveBall : DMR_CacodemonBall
{
	Default
	{
		DamageFunction(random(10,30));
		Scale 1.1;
		+BRIGHT;
		SeeSound "weapons/firmfi";
		DeathSound "weapons/firex3";
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn: 
		BAL3 AABB 2 {
			A_SpawnItemEx("DMR_LargeFireballTrail",-7,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		loop;
	Death:
		TNT1 A 0 {
			A_SetScale(1);
			A_SpawnItemEx("DMR_MediumFireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=35;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		BAL3 C 5 A_Explode(25,64,0);
		BAL3 DE 5;
		stop;
	}
}