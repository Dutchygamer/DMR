//======================================================================
//  Original credits
//======================================================================
//	ZScript: Bouncy, TheDarkArchon, Ghastly (ZScript conversion)
//	Sprites: Vader
//	Sounds: Raven Software
//	GLDefs: Ghastly
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios / EA Games
//	ZScript:	Dutchygamer
//======================================================================

class Witherer : DMR_Cacodemon
{  
	Default
	{
		Tag "Witherer";	
		bloodcolor "Green";
		Health 500;
		HitObituary "%o's ended as dust in a witherer's stomache.";
		Obituary "%o withered away.";
		DamageFactor "ToxinCloud", 0.0;
		SeeSound "CacoLich/Sight";
		ActiveSound "CacoLich/Active";
		PainSound "CacoLich/Pain";
		DeathSound "CacoLich/Death";
	}	
	States 
	{ 
	Spawn: 
		CALI A 10 A_Look;
		Loop;
	See: 
		CALI A 3 A_Chase;
		Loop;
	Melee: 
		CALI FG 5 A_FaceTarget;
		CALI E 5 A_CustomMeleeAttack(random(20,60),"baron/melee");
		Goto See;
	Missile:
		TNT1 A 0 A_JumpIfCloser(256, "ToxinSpray");
		TNT1 A 0 A_Jump(96,"GasBall");
		goto Ball;
	Ball:
		CALI B 5 A_FaceTarget;
		CALI C 5 BRIGHT A_SpawnProjectile("WithererBall",35);
		CALI D 5;
		goto See;
	GasBall:
		CALI EF 5 A_FaceTarget;
		CALI G 5 BRIGHT A_SpawnProjectile("WithererGasBall",26);
		goto See;
	ToxinSpray:
		CALI EEF 8 A_FaceTarget;
		TNT1 A 0 { 
			attackloop = 0;
			A_PlaySound ("CacoLich/Spray", CHAN_WEAPON);		
		}
	ToxinSprayLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 9,"See");
		CALI G 1 Light("DMR_WithererSpray") {
			A_FaceTarget();
			attackloop++;
			A_SpawnProjectile("CacolichToxinSpray",26,0,random(-4,4));
		}
		Loop;
	Pain: 
		CALI H 3;
		CALI H 3 A_Pain;
		CALI I 6;
		Goto See;
	Death: 
	XDeath:
		TNT1 A 0 A_SetFloorClip;
		CALI J 6 A_Scream;
		CALI KLMN 6;
		Wait;
	Crash:
		TNT1 A 0 {
			A_PlaySound("gas/explode");
			A_SpawnItemEx("DMR_LargeGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,0,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
		}
		CALI OP 6;
		CALI Q 6 A_NoBlocking;
		CALI R 6;
		CALI S -1;
		Stop;
	Raise:
		CALI S 8 A_UnSetFloorClip;
		CALI RQPONMLK 8;
		Goto See;
	} 
}

class WithererBall : DMR_CacodemonBall
{
	Default
	{
		DamageFunction(random(10,30));
		DamageType "ToxinCloud";
		Scale 1.5;
		ALPHA 0.67;
		Seesound "vulgar/fire";
		DeathSound "weapons/bloodx";
	}
	States
	{
	Spawn: 
		FVUL AABB 2 {
			A_SpawnItemEx("DMR_GasTrail",-5,0,0,frandom(-0.5,0),frandom(-0.5,0.5),frandom(0,-0.5)); 
		}
		loop;
	Death:
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 4;fxLoop++) {
				A_SpawnItemEx("DMR_MediumGreenFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
				A_SpawnItemEx("DMR_MediumGreenFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
			}
		}
		FVUL CDEF 4;
		stop;
	}
}

class WithererGasBall : DMR_CacodemonBall
{
	Default
	{
		DamageFunction(random(10,30));
		DamageType "ToxinCloud";
		Scale 0.9;
		Seesound "vulgar/fire";
		DeathSound "weapons/bloodx";
	}
	States
	{
	Spawn: 
		HECF AABB 2 {
			A_SpawnItemEx("DMR_GasTrail",-5,0,0,frandom(-0.5,0),frandom(-0.5,0.5),frandom(0,-0.5)); 
		}
		loop;
	Death:
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 4;fxLoop++) {
				A_SpawnItemEx("DMR_GasDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
				A_SpawnItemEx("DMR_GasBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
			}
		}
		HECF C 4 Bright A_Explode(10,56);
		TNT1 A 0 {		
			A_PlaySound("gas/explode");
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,-30,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,-30,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,-30,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
		}
		HECF DE 4 Bright;
		stop;
	}
}

class CacolichToxinSpray : DMR_FlamethrowerBall
{
	Default
	{
		Speed 10;
		DamageType "ToxinCloud";
		RenderStyle "Translucent";
		Alpha 0.5;
		-BRIGHT;
	}
	States
	{
	Spawn:
		GAS2 AABBCCDDEEFFGGHHIIJJKKLLMMNN 1 {
			A_SetScale(Scale.X + 0.03);
			// only deal damage X amount of tics instead of every tick
			dmgtic++;
			if(dmgtic >= 3)
			{
				A_Explode(3,32,0,0,32);
				dmgtic = 0;
			}		
		}
		GAS2 KKLLMMNN 1 {
			A_SetScale(Scale.X + 0.03);
		}
		Stop;
	}
}