//======================================================================
//  Additional credits
//======================================================================
//  XDeath Sprites: 	Dutchygamer
//	ZScript:			Dutchygamer
//======================================================================

Class DMR_Cacodemon : DMR_FlyingMonsterBase 
{
	Default
	{
		Tag "Cacodemon";
		bloodcolor "blue";
		Health 400;
		Gibhealth -30; // all medium tier monsters (demon -> hellknight) have 30 gibhealth
		Species "Caco"; // use 'Caco' instead of 'DMR_Caco' to maintain Project: Demolitionist Caco petting 
		Radius 31;
		Height 56;
		Mass 400;
		Speed 8;
		PainChance 128;
		SeeSound "caco/sight";
		PainSound "caco/pain";
		DeathSound "caco/death";
		ActiveSound "caco/active";
		Obituary "$OB_CACO";
		HitObituary "$OB_CACOHIT";
		+DONTHARMSPECIES;
		+NOINFIGHTSPECIES;
	}
	States
	{
	Spawn:
		HEAD A 10 A_Look;
		Loop;
	See:
		HEAD A 3 A_Chase;
		Loop;
	Melee: 
		HEAD CD 5 A_FaceTarget;
		HEAD B 5 A_CustomMeleeAttack(random(20,60),"baron/melee");
		Goto See;
	Missile:
		HEAD BC 5 A_FaceTarget;
		HEAD D 5 BRIGHT A_SpawnProjectile("DMR_CacodemonBall",26);	
		goto See;
	Pain:
		HEAD E 3;
		HEAD E 3 A_Pain;
		HEAD F 6;
		Goto See;
	Death:
		HEAD G 8;
		HEAD H 8 A_Scream;
		HEAD IJ 8;
		HEAD K 8 A_NoBlocking;
		HEAD L -1 A_SetFloorClip;
		Stop;
	XDeath:
		HEAD M 5;
		HEAD N 5 A_XScream;
		HEAD O 5 A_NoBlocking;
		HEAD PQRSTU 5;
		HEAD V -1;
		stop;
	Raise:
		HEAD L 8 A_UnSetFloorClip;
		HEAD KJIHG 8;
		Goto See;
	}
}

class DMR_CacodemonBall : DMR_Projectile
{
	Default
	{
 		Radius 6;
		Height 8;
		Speed 10;
 		FastSpeed 20;
		DamageFunction(random(10,30));
		Projectile;
		+RANDOMIZE
		RenderStyle "Add";
		Alpha 1;
		Decal "CacoScorch";
		SeeSound "caco/attack";
 		DeathSound "caco/shotx";
		Decal "CacoScorch";
		DMR_Projectile.ParticleColors "#9f009a", "#d70000", "#ff903b", "#ffff00";
	}
	States
	{
	Spawn:
		BAL2 AABB 2 Bright {
			//A_SpawnItemEx("DMR_LightningBallTrail",-5,0,2,frandom(-0.5,0.5),frandom(-2.5,2.5),frandom(-2.5,2.5));
			
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		BAL2 CDE 6 Bright;
		Stop;
	}
}

