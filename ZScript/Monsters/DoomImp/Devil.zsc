//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Id Software, Midway, Eriance
//  Sprites: 	Id Software (edits by Eriance)
//	GLDEFs: 	n/a
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Devil : DMR_DoomImp
{
	Default
	{
		Tag "Boggart";
		obituary "%o was fried by a boggart.";
		hitobituary "%o was flayed by a boggart.";
		health 90;
		painchance 150;
		seesound "monster/dvlsit";
		painsound "monster/dvlpai";
		deathsound "monster/dvldth";
		activesound "monster/dvlact";
	}
	States
	{
	Spawn:
		TRO2 AB 10 A_Look;
		Loop;
	See:
		TRO2 AABBCCDD 3 A_Chase;
		Loop;
	Melee:
	Missile:
		TNT1 A 0 A_Jump(128,"ExplosiveBall");
		goto Balls;
	Balls:
		TRO2 EF 6 A_FaceTarget;
		TRO2 G 4 A_CustomComboAttack("DevilBall",32,random(6,16),"imp/melee");
		TRO2 B 2;
		TRO2 VW 6 A_FaceTarget;
		TRO2 X 4 A_CustomComboAttack("DevilBall",32,random(6,16),"imp/melee");
		TRO2 D 2;
		goto See;
	ExplosiveBall:
		TRO2 EEEE 6 A_FaceTarget;
		TRO2 F 6 A_FaceTarget;
		TRO2 G 4 A_CustomComboAttack("DevilExplosiveBall",32,random(6,16),"imp/melee");
		TRO2 B 2;
		goto See;
	Pain:
		TRO2 H 2;
		TRO2 H 2 A_Pain;
		Goto See;
	Death:
		TRO2 I 8;
		TRO2 J 8 A_Scream;
		TRO2 K 6;
		TRO2 L 6 A_NoBlocking;
		TRO2 M -1;
		Stop;
	XDeath:
		TRO2 N 5;
		TRO2 O 5 A_XScream;
		TRO2 P 5;
		TRO2 Q 5 A_NoBlocking;
		TRO2 RST 5;
		TRO2 U -1;
		Stop;
	Raise:
		TRO2 ML 8;
		TRO2 KJI 6;
		Goto See;
	}
}

class DevilBall : DMR_DoomImpBall
{
	Default
	{
		Scale 0.7;
		DamageFunction(random(6,16));
	}
	States
	{
	Spawn: 
		BAL3 AABB 2 Bright {
			A_SpawnItemEx("DMR_FireballTrail",-5,0,2,frandom(-1,0),frandom(-1,1),frandom(0,1));
		}
		Loop;
	Death:
		TNT1 AAA 0 {
			A_SpawnItemEx("DMR_FireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
			A_SpawnItemEx("DMR_FireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		BAL3 CDE 5 Bright;
		stop;
	}
}

class DevilExplosiveBall : DMR_DoomImpBall
{
	Default
	{
		Speed 15;
		DamageFunction(random(8,16));
		ALPHA 0.95;
		SeeSound "weapons/firmfi";
		DeathSound "weapons/firex3";
	}
	States
	{
	Spawn: 
		BAL3 AABB 2 Bright {
			A_SpawnItemEx("DMR_FireballTrail",-7,0,3,frandom(-1,0),frandom(-1,1),frandom(0,1));
			A_SpawnItemEx("DMR_RedPuffTrail",0,0,0,0,0,0,0,128,0);
		}
		loop;
	Death:
		TNT1 AAA 0 {
			A_SpawnItemEx("DMR_FireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
		}
		TNT1 AAAAA 0 {
			A_SpawnItemEx("DMR_FireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
		BAL3 C 5 Bright A_Explode(10,64,0);
		BAL3 DE 5 Bright;
		stop;
	}
}