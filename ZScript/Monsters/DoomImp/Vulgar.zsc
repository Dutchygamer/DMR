//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Raven Software, Blizzard Entertainment, Eriance
//  Sprites: 	Eriance
//	GLDEFs: 	Dreadopp
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Vulgar : DMR_DoomImp
{
	Default
	{
		Tag "Vulgar";
		obituary "%o was poisoned by a vulgar.";
		hitobituary "A vulgar had %o for lunch.";
		health 110;
		painchance 150;
		seesound "vulgar/sight";
		painsound "vulgar/pain";
		deathsound "vulgar/death";
		activesound "vulgar/active";
		damagefactor "ToxinCloud", 0;
	}
	States
	{
	Spawn:
		VULG AB 10 A_Look;
		Loop;
	See:
		VULG AABBCCDD 3 A_Chase;
		Loop;
	Melee:
		VULG EF 8 A_FaceTarget;
		VULG G 6 A_CustomComboAttack("VulgarBall",37,random(8,24),"imp/melee");
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(96,"GasBall");
		goto Ball;
	Ball:
		VULG EF 8 A_FaceTarget;
		VULG G 4 A_SpawnProjectile("VulgarBall",37);
		VULG G 4 {
			if (random(1,2) == 1)
				A_SpawnProjectile("VulgarBall",37,0,-2);
			else
				A_SpawnProjectile("VulgarBall",37,0,2);
		}
		Goto See;
	GasBall:
		VULG EEEF 8 A_FaceTarget;
		VULG G 10 A_SpawnProjectile("VulgarGasBall",37);
		Goto See;
	Pain:
		VULG H 2;
		VULG H 2 A_Pain;
		Goto See;
	Death:
		VULG I 8;
		VULG J 8 A_Scream;
		VULG KL 6;
		VULG M 6 A_NoBlocking;
		VULG N -1;
		Stop;
	XDeath:
		VULG O 5;
		VULG P 5 A_XScream;
		VULG Q 5;
		VULG R 5 A_NoBlocking;
		VULG STU 5;
		VULG V -1;
		Stop;
	Raise:
		VULG ML 8;
		VULG KJI 6;
		Goto See;
	}
}

class VulgarBall : DMR_DoomImpBall
{
	Default
	{
		DamageFunction(random(8,24));
		Scale 1.1;
		DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	States
	{
	Spawn:
		FVUL AABB 2 Bright {
			A_SpawnItemEx("DMR_SmallGasTrail",-5,0,0,frandom(-0.5,0),frandom(-0.5,0.5),frandom(0,-0.5)); 
			A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
		}
		loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_GreenFireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=20;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		FVUL CDEF 4 Bright;
		stop;
	}
}

class VulgarGasBall : DMR_DoomImpBall
{
	Default
	{
		Speed 15;
		DamageFunction(random(10,30));
		Scale 0.7;
		DamageType "ToxinCloud";
		Seesound "vulgar/fire";
		DeathSound "weapons/bloodx";
		DMR_Projectile.ParticleColors "#00d423", "#19ed3c", "#a9ffb7", "#b8ffc4";
	}
	States
	{
	Spawn:
		HECF AABB 2 Bright {
			A_SpawnItemEx("DMR_SmallGasTrail",-2,0,3,frandom(-0.5,0),frandom(-0.5,0.5),frandom(0,0.5)); 
			A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
		}
		loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_GasBallImpactSpawner");
			for (fxLoop = 0;fxLoop<=20;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		HECF C 4 Bright A_Explode(8,56);
		TNT1 A 0 {	
			A_PlaySound("gas/explode");	
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_SmallGasCloud",0,0,5,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
		}
		HECF DE 4 Bright;
		stop;
	}
}
