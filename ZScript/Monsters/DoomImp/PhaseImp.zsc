//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Daniel
//	Sounds:		Id Software, Midway
//  Sprites: 	Id Software (edits by Tormentor667)
//	GLDEFs: 	Tormentor667
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Phaser : DMR_DoomImp
{
	Default
	{
		Tag "Phaser";
		Health 90;
		Speed 12;
		SeeSound "phaser/sight";
		PainSound "phaser/pain";
		DeathSound "phaser/death";
		ActiveSound "phaser/active";
		Obituary "%o was incinerated by a Phaser.";
		HitObituary "%o was scratched by a Phaser.";	
	}
	bool isStealthed;
	
	States
	{
	Spawn:
		DIMP AB 10 A_Look;
		Loop;
	See:
		DIMP AABBCCDD 3 A_Chase;
		TNT1 A 0 A_Jump(64,"GoStealthed");
		Loop;
    StealthedSee:
        DIMP AABBCCDDAABBCCDD 1 {
			A_Chase(null, null);
			// chance to dash left or right	
			if(random(1,4) == 4) A_DashSideways(8);
			A_SpawnItemEx("DMR_SubtractAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION);
		}
    StealthedSeeLoop:
        DIMP AABBCCDD 1 {
			A_Chase();
			A_SpawnItemEx("DMR_SubtractAfterImage",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION);
		}
        Loop;
	Melee:
	Missile:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
        DIMP EF 8 A_FaceTarget;
        DIMP G 6 A_CustomComboAttack("PhaserBall",32,random(6,16),"imp/melee");
        Goto See;
	GoStealthed:
		TNT1 A 0 {
			if (isStealthed) { SetStateLabel("See"); }
		}
		DIMP E 5 {
			isStealthed = true;
			A_StartSound("phaser/teleport");
		}
		DIMP EEEEE 1 { // to 0.5 alpha in total			
			A_SetRenderStyle(Alpha-0.1,STYLE_Subtract);
		}
		TNT1 A 0 A_Chase(null,null, CHF_FASTCHASE);
        Goto StealthedSee;
	Pain:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
		DIMP H 2;
		DIMP H 2 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
        DIMP J 8 A_Scream;
        DIMP K 6;
        DIMP L 6 A_Fall;
        DIMP M -1;
		Stop;
	XDeath:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
        DIMP O 5 A_XScream;
        DIMP P 5 A_Fall;
        DIMP RST 5;
        DIMP U -1;
		Stop;
	Raise:
        DIMP LK 8;
        DIMP JI 6;
		Goto See;
	}
}

class PhaserBall : DMR_DoomImpBall
{
	Default
	{
		Speed 12;
		FastSpeed 25;
		+SEEKERMISSILE;
		DMR_Projectile.ParticleColors "#710071", "#870087", "#c300c3", "#c800c8";
	}
    States
    {
    Spawn:
        DIMB AABB 2 Bright {
			A_SeekerMissile(1,1);
			A_SpawnItemEx("DMR_PurpleFireballTrail",-7,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("PhaserFireballTrail"); 
			A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
		}
        Loop;
    Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_PurpleFireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=20;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
        DIMB CDE 6 Bright;
        Stop;
    }
}

class PhaserFireballTrail : DMR_ProjectileTrail
{
	Default
	{
		Alpha 0.5;
		Scale 0.9;
	}
	States
	{
	Spawn:
		// trails need a small delay
		TNT1 A 2;
	SpawnLoop:
		DIMB AB 2 {
			A_FadeOut(0.10);
			A_SetScale(Scale.X-0.05);
		}
		Loop;
	}
}