//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Daniel
//	Sounds:		Id Software, Midway
//  Sprites: 	Id Software (edits by Tormentor667)
//	GLDEFs: 	Tormentor667
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Phaser : DMR_DoomImp
{
	Default
	{
		Tag "Phaser";
		Health 90;
		Speed 12;
		SeeSound "neodarkimp/sight";
		PainSound "neodarkimp/pain";
		DeathSound "neodarkimp/death";
		ActiveSound "neodarkimp/active";
		Obituary "%o was incinerated by a phaser.";
		HitObituary "%o was scratched by a phaser.";	
		+NODROPOFF; //don't yeet yourself off platforms	
	}
	bool isStealthed;
	
	States
	{
	Spawn:
		DIMP AB 10 A_Look;
		Loop;
	See:
		DIMP AABBCCDD 3 A_Chase;
		TNT1 A 0 A_Jump(64,"GoStealthed");
		Loop;
    StealthedSee:
		// do perform a melee attack when you happen to be next to your target, but don't throw a fireball
        DIMP AABBCCDD 2 A_Chase("Melee", null);
        DIMP AABBCCDD 2 A_Chase("Melee", null, CHF_FASTCHASE);
		TNT1 A 0 A_Jump(42,"See");
        Loop;
	Melee:
	Missile:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
        DIMP EF 8 A_FaceTarget;
        DIMP G 6 A_CustomComboAttack("PhaserBall",32,random(6,16),"imp/melee");
        Goto See;
	GoStealthed:
		TNT1 A 0 {
			if (isStealthed) { SetStateLabel("See"); }
		}
		DIMP E 5 {
			isStealthed = true;
			A_PlaySound("neodarkimp/teleport");
		}
		DIMP EEEEE 1 { //to 0.5 alpha in total
			A_SetRenderStyle(Alpha-0.1,STYLE_Subtract);
		}
		TNT1 A 0 A_Chase(null,null, CHF_FASTCHASE);
        Goto StealthedSee;
	Pain:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
		DIMP H 2;
		DIMP H 2 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
        DIMP J 8 A_Scream;
        DIMP K 6;
        DIMP L 6 A_Fall;
        DIMP M -1;
		Stop;
	XDeath:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
        DIMP O 5 A_XScream;
        DIMP P 5 A_Fall;
        DIMP RST 5;
        DIMP U -1;
		Stop;
	Raise:
        DIMP LK 8;
        DIMP JI 6;
		Goto See;
	}
}


class PhaserBall : DMR_DoomImpBall
{
	Default
	{
		+SEEKERMISSILE;
		Alpha 0.75;
	}
    States
    {
    Spawn:
        DIMB AABB 2 Bright {
			A_SeekerMissile(1,1);
			A_SpawnItemEx("DMR_PurpleFireballTrail",-7,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("PhaserFireballTrail"); 
		}
        Loop;
    Death:
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 3;fxLoop++) {
				A_SpawnItemEx("DMR_PurpleFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
				A_SpawnItemEx("DMR_PurpleFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
			}
		}
        DIMB CDE 6 Bright;
        Stop;
    }
}

class PhaserFireballTrail : DMR_ProjectileTrail
{
	Default
	{
		Alpha 0.5;
	}
	States
	{
	Spawn:
		// trails need a small delay
		TNT1 A 2;
	SpawnLoop:
		DIMB AB 2 {
			A_FadeOut(0.10);
			A_SetScale(Scale.X-0.05);
		}
		Loop;
	}
}