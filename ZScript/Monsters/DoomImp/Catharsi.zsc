//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Tormentor667
//	Sounds:		Id Software
//  Sprites: 	Id Software, LucasArts (edits by Rottking)
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		EA Games
//	ZScript:	Dutchygamer
//======================================================================

class Catharsi : DMR_DoomImp
{
	Default
	{
		Tag "Catharsi";
		Health 200;
		PainChance 150;
		Scale 1.1;
		SeeSound "catharsi/sit";
		PainSound "catharsi/pain";
		DeathSound "catharsi/death";
		ActiveSound "imp/active"; //TODO
		Obituary "%o was frosted over by a Catharsi.";
		DamageFactor "Ice", 0;
	}
	States
	{
	Spawn:
		SDDM AB 10 A_Look;
		Loop;
	See:
		SDDM AABBCCDD 3 A_Chase;
		Loop;
	Melee:
	Missile:
		TNT1 A 0 A_JumpIfCloser(418, "MissileClose");
		Goto SingleShot;
	MissileClose:
		TNT1 A 0 A_Jump(256, "SingleShot","RapidFire");
	SingleShot:
		SDDM EE 6 A_FaceTarget;
		SDDM F 2 BRIGHT A_SpawnProjectile("CatharsiBall", 35, 8);
		SDDM F 2 BRIGHT A_SpawnProjectile("CatharsiBall", 35, 8, random(-1,1));
		Goto See;
	RapidFire:
		SDDM EEE 6 A_FaceTarget;
		SDDM F 2 BRIGHT A_SpawnProjectile("CatharsiBall", 35, 8, random(-2,2));
		SDDM E 2 A_FaceTarget;
		SDDM F 2 BRIGHT A_SpawnProjectile("CatharsiBall", 35, 8, random(-2,2));
		SDDM E 2 A_FaceTarget;
		SDDM F 2 BRIGHT A_SpawnProjectile("CatharsiBall", 35, 8, random(-2,2));
		TNT1 A 0 A_Jump(192,"See");
		SDDM E 6 A_FaceTarget;
		SDDM E 2 A_FaceTarget;
		SDDM F 2 BRIGHT A_SpawnProjectile("CatharsiBall", 35, 8, random(-2,2));
		SDDM E 2 A_FaceTarget;
		SDDM F 2 BRIGHT A_SpawnProjectile("CatharsiBall", 35, 8, random(-2,2));
		SDDM E 2 A_FaceTarget;
		SDDM F 2 BRIGHT A_SpawnProjectile("CatharsiBall", 35, 8, random(-2,2));
		SDDM E 2;
		Goto See;
	Pain:
		SDDM G 3;
		SDDM G 3 A_Pain;
		Goto See;
	Death:
		SDDM H 5;
		SDDM I 5 A_Scream;
		SDDM J 5 A_NoBlocking;
		SDDM KLM 5;
		SDDM NNNN 12 A_StartSound("catharsi/thermtick", CHAN_WEAPON);
		TNT1 A 0 {
			A_StartSound("catharsi/icespill", CHAN_WEAPON);
			A_SpawnItemEx("DMR_MediumIceCloud",0,0,5,frandom(-1,1),frandom(-1,1));
			A_SpawnItemEx("DMR_MediumIceCloud",0,0,5,frandom(-1,1),frandom(-1,1));
		}
		SDDM N -1;
		Stop;
	XDeath:
		SDDM O 5;
		SDDM P 5 A_XScream;
		SDDM Q 5 A_NoBlocking;
		TNT1 A 0 {
			A_StartSound("catharsi/icespill", CHAN_WEAPON);
			A_SpawnItemEx("DMR_MediumIceCloud",0,0,5,frandom(-1,1),frandom(-1,1));
			A_SpawnItemEx("DMR_MediumIceCloud",0,0,5,frandom(-1,1),frandom(-1,1));
		}
		SDDM RSTUV 5;
		SDDM W -1;
		Stop;
	Raise:
		SDDM NMLKJIH 5;
		Goto See;
	}
}

class CatharsiBall : DMR_DoomImpBall
{
	Default
	{
		Speed 15;
		FastSpeed 25;
		SEESOUND "catharsi/icerifle";
		DEATHSOUND "catharsi/iceimpact";
		DamageType "Ice";
		Scale 0.8;
		DMR_Projectile.ParticleColors "#5353ff", "#9b9bff", "#8f8fff", "#1b1bff";
	}
	states
	{
	Spawn:
		CTFX A 2 BRIGHT {
			A_SpawnItemEx("CatharsiBallTrail"); 
			A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-12,-6),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
		}
		Loop;
	Death:
		TNT1 A 0 {	
			A_SpawnItemEx("DMR_FireballImpactSpawner");
			for (fxLoop = 0;fxLoop<=20;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-6,6),0,0,-0.1,1,-1,-0.6);
			}
		}
		CTFX BCDEF 3 BRIGHT;
		stop;
	}
}

class CatharsiBallTrail : DMR_ProjectileTrail
{
	Default
	{
		Alpha 0.5;
		Scale 0.9;
		-ROLLSPRITE;
	}
	States
	{
	Spawn:
		// trails need a small delay
		TNT1 A 2;
	SpawnLoop:
		CTFX A 2 {
			A_FadeOut(0.10);
			A_SetScale(Scale.X-0.05);
		}
		Loop;
	}
}