//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	ItsNatureToDie
//	Sounds:		Id Software, Midway
//  Sprites: 	Id Software (edits by Vader (Shape), ItsNatureToDie (Recolor))
//	GLDEFs: 	Unknown
//======================================================================
//  Additional credits
//======================================================================
//	Recolor:	Dutchygamer
//	ZScript:	Dutchygamer
//======================================================================

class Netherling : DMR_DoomImp
{
	Default
	{
		Tag "Netherling";
		Health 180;
		PainChance 130;		
		Scale 1.1;
		SeeSound "neodarkimp/sight";
		PainSound "neodarkimp/pain";
		DeathSound "neodarkimp/death";
		ActiveSound "neodarkimp/active";
		Obituary "%o was blasted by a netherling.";
		HitObituary "%o was sliced by a netherling.";
	}
	States
	{
	Spawn:
		TRO4 AB 10 A_Look;
		Loop;
	See:
		TRO4 AABBCCDD 3 A_Chase;
		Loop;
    Melee:
        TRO4 EF 8 A_FaceTarget;
        TRO4 G 6 A_CustomComboAttack("NetherImpBall",34,random(10,30),"neodarkimp/melee");
        Goto See;
	Missile:
        TNT1 A 0 A_Jump (64, "BigBall");
		Goto TriBall;
    TriBall:
        TRO4 EF 10 A_FaceTarget;
		TRO4 G 1 Bright A_SpawnProjectile("NetherImpBall",32,0,-7,0);
		TRO4 G 1 Bright A_SpawnProjectile("NetherImpBall",32,0,0,0);
		TRO4 G 4 Bright A_SpawnProjectile("NetherImpBall",32,0,7,0);
        Goto See;
    BigBall:
        TRO4 EF 8 A_FaceTarget;
		TRO4 G 8 Bright A_SpawnProjectile("BaronBall",32,0,0,0);
        Goto See;
	Pain:
		TRO4 H 2;
		TRO4 H 2 A_Pain;
		Goto See;
	Death:
		TRO4 I 8;
		TRO4 J 8 A_Scream;
		TRO4 K 6;
		TRO4 L 6 A_NoBlocking;
		TRO4 M -1;
		Stop;
	//for now use DoomImp XDeath sprites
	XDeath:
		TROO N 5;
		TROO O 5 A_XScream;
		TROO P 5;
		TROO Q 5 A_NoBlocking;
		TROO RST 5;
		TROO U -1;
		Stop;
	Raise:
		TRO4 ML 8;
		TRO4 KJI 6;
		Goto See;
	}
}

class NetherImpBall : DMR_DoomImpBall
{
	Default
	{
		DamageFunction(random(6,16));
		Scale 0.7;
	}
    States
    {
    Spawn:
        IBAL AABB 2 Bright {
			A_SpawnItemEx("NetherImpBallTrail",-5,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}		
        Loop;
    Death:
		TNT1 AA 0 {
			A_SpawnItemEx("DMR_GreenFireDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2)); 
			A_SpawnItemEx("DMR_GreenFireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-2,2),frandom(-2,2),frandom(-1,2));
		}
        IBAL CDE 4 Bright;
        Stop;
    }   
}

class NetherImpBallTrail : DMR_ProjectileTrail
{
	Default
	{
		Scale 0.1;
	}
    States
    {
    Spawn:
        ITRL ABCDEFGHIJKLMN 1 Bright;
        stop;
    }
}
