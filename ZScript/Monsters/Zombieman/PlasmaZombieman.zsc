//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Bouncy
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Tormentor667, Captain Toenail, Ghastly Dragon)
//	GLDEFs: 	Dreadopp
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

// own instance so they can hurt other zombieman and each other
class PlasmaZombieman : DMR_MonsterBase
{
	Default
	{
		Tag "Plasma zombieman";		
		Health 55;
		Gibhealth -20; // all lower tier monsters (zombies -> imp) have 20 gibhealth
		Species "DMR_Zambies";
		Radius 20;
		Height 56;
		Speed 8;
		PainChance 185;
		SeeSound "grunt/sight";
		PainSound "grunt/pain";
		DeathSound "grunt/death";
		ActiveSound "grunt/active";
		+DOHARMSPECIES // zombies are not plasma-proof
		OBITUARY "%o got burned by a plasma zombie.";
		DropItem "Cell";
		DropItem "PlasmaRifle", 10;
	}
 	States
	{
	Spawn:
		ZMAY AB 10 A_Look;
		Loop;
	See:
		ZMAY AABBCCDD 4 A_Chase;
		Loop;
	Missile:
		ZMAY E 10 A_FaceTarget;
		ZMAY F 4 BRIGHT Light("DMR_PlasmaFire") A_SpawnProjectile("DMR_ZombiePlasma");
		ZMAY E 4 A_FaceTarget;
		ZMAY F 4 BRIGHT Light("DMR_PlasmaFire") A_SpawnProjectile("DMR_ZombiePlasma");
		ZMAY E 4 A_FaceTarget;
		ZMAY F 4 BRIGHT Light("DMR_PlasmaFire") A_SpawnProjectile("DMR_ZombiePlasma");
		ZMAY E 4 A_FaceTarget;
		ZMAY E 10 A_MonsterRefire(25, "See");
		Goto Missile+1;
	Pain:
		ZMAY G 3;
		ZMAY G 3 A_Pain;
		Goto See;
	Death:
		ZMAY H 5;
		ZMAY I 5 A_Scream;
		ZMAY J 5 A_NoBlocking;
		ZMAY K 5;
		ZMAY L -1;
		Stop;
	XDeath:
		ZMAY O 5;
		ZMAY P 5 A_XScream;
		ZMAY Q 5 A_NoBlocking;
		ZMAY RSTU 5;
		ZMAY V -1;
		Stop;
	Raise:
		ZMAY KJIH 5;
		Goto See;
	}
}