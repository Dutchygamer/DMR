//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Graf Zahl
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Xim, Vegeta, Ghastly_dragon, MagicWazard)
//	GLDEFs: 	Ghastly_dragon, MagicWazard (brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios, EA Games
//	ZScript:	Dutchygamer
//======================================================================

class SpecOpsZombieman : DMR_Zombieman
{
	Default
	{
		Tag "SpecOps gunner";
		OBITUARY "%o was gunned down by a specops gunner.";
		Health 90;
		Speed 10;
		PainChance 170;
		ATTACKSOUND "";
		DropItem "Clip";
		+MISSILEMORE
		DamageFactor "ZombieGrenade", 0.5; //take less damage from own grenades
		+DOHARMSPECIES; //zombies aren't grenade-proof
	}
 	States
	{
	Spawn:
		ZSP2 AB 10 A_Look;
		Loop;
	See:
		ZSP2 AABBCCDD 4 A_Chase;
		Loop;
	Missile:
		TNT1 A 0 A_JumpIfCloser(512, "MissileClose"); //only start attacking when we are close
		Goto See;
	MissileClose:
		TNT1 A 0 A_Jump(64, "Grenades");
		Goto Rifle;
	Rifle:
		ZSP2 E 10 A_FaceTarget;		
		ZSP2 F 4 BRIGHT Light("DMR_ZOMBIEATK") {
			A_PlaySound("Zombies/ZSpecFire", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,0,1,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
		}
		ZSP2 E 4 A_FaceTarget;		
		ZSP2 F 4 BRIGHT Light("DMR_ZOMBIEATK") {
			A_PlaySound("Zombies/ZSpecFire", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,0,1,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
		}
		ZSP2 E 4;
		Goto See;
	Grenades:
		TNT1 A 0 A_JumpIfCloser(96, "Rifle"); //don't grenade yourself when standing in front of the target
		ZSP2 E 18 {
			A_PlaySound("Zombies/GrenadePrime");
			A_FaceTarget();
		}
		ZSP2 F 5 {
			A_PlaySound("Zombies/DuneThrow");
			A_SpawnProjectile("SpecOpsGrenade", 32, 0, random(9,-9), CMF_OFFSETPITCH, random(-2,2));
		}
		ZSP2 E 4;
		Goto See;
	Pain:
		ZSP2 G 3;
		ZSP2 G 3 A_Pain;
		Goto See;
	Death:
		ZSP2 H 5;
		ZSP2 I 5 A_Scream;
		ZSP2 J 5 A_NoBlocking;
		ZSP2 K 5;
		ZSP2 L -1;
		Stop;
	Raise:
		ZSP2 KJIH 5;
		Goto See;
	}
}

class SpecOpsGrenade : DMR_ThrownGrenade
{
	Default
	{
		DamageType "ZombieGrenade";
		DamageFunction(random(16,48));
	}
	States
	{
	Spawn:
		THR2 A 4;
		THR2 B 1 Bright;
		Loop;
	Death:
		TNT1 A 0 {
			bNoGravity = true;
			A_Stop();
		}		
		MISL B 8 Bright A_Explode(20, 64);
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}
