//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Tormentor667
//	Sounds:		Id Software, Rogue Entertainment
//  Sprites: 	Id Software, Raven Software (edits by Tormentor667 and NeoWorm)
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class AssassinZombieman : DMR_Zombieman
{
	Default
	{
		Tag "Dune warrior";
		OBITUARY "%o was killed by a dune warrior.";
		Health 45;
		Speed 12;
		PainChance 185;
		ATTACKSOUND "";
		DropItem "Clip";
		DamageFactor "ToxinCloud", 0.0; //immune to gas clouds
		+MISSILEMORE
		+NODROPOFF; //don't yeet yourself off platforms
		+DOHARMSPECIES; //zombies aren't grenade-proof
	}
	bool isStealthed;
	
 	States
	{
	Spawn:
		ZHRT AB 10 A_Look;
		Loop;
	See:
		ZHRT AABBCCDD 4 A_Chase;
		TNT1 A 0 A_Jump(64,"GoStealthed");
		Loop;
    StealthedSee:
        ZHRT AABBCCDD 2 A_Chase(null,null);
        ZHRT AABBCCDD 2 A_Chase(null,null, CHF_FASTCHASE);
		TNT1 A 0 A_Jump(64,"See");
        Loop;
	Missile:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
		TNT1 A 0 A_JumpIfCloser(512, "Grenades"); //when close to target, we can use grenades
		Goto Rifle; //else just use rifle fire
	GoStealthed:
		TNT1 A 0 {
			if(isStealthed) {
				return ResolveState("See");
			}
			else {
				return ResolveState(null);
			}
		}
		ZHRT E 5 {
			isStealthed = true;
			A_PlaySound("Zombies/Stealth");
		}
		ZHRT EEEEE 1 { //to 0.0 alpha in total
			A_SetRenderStyle(Alpha-0.2,STYLE_Translucent);
		}
		TNT1 A 0 A_Chase(null,null, CHF_FASTCHASE);
        Goto StealthedSee;
	Rifle:
		ZHRT E 10 A_FaceTarget;		
		ZHRT F 4 BRIGHT Light("DMR_RifleFire") {
			A_PlaySound("Zombies/DuneFire", CHAN_WEAPON);
			A_CustomBulletAttack(22.5,0,1,random(6,12),"BulletPuff",0,CBAF_NORANDOM);
		}
		ZHRT E 4;
		Goto See;
	Grenades:
		TNT1 A 0 A_JumpIfCloser(96, "Rifle"); //don't grenade yourself when standing in front of the target
		TNT1 A 0 A_Jump(180, "Rifle"); //as we will always go here when in grenade-range, high chance to fire the rifle instead
		TNT1 A 0 A_PlaySound("Zombies/GrenadePrime");
		ZHRT E 15 A_FaceTarget;
		TNT1 A 0 A_PlaySound("Zombies/DuneThrow");
		ZHRT F 5 A_SpawnProjectile("AssassinGrenade",32,0,random(9,-9),CMF_OFFSETPITCH,random(-2,2));
		Goto See;
	Pain:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
		ZHRT G 3;
		ZHRT G 3 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
		ZHRT H 5;
		ZHRT I 5 A_Scream;
		ZHRT J 5 A_NoBlocking;
		ZHRT K 5;
		ZHRT L -1;
		Stop;
	XDeath:
		TNT1 A 0 {
			isStealthed = false;
			A_SetRenderStyle(1,STYLE_Normal);
		}
		ZHRT O 5;
		ZHRT P 5 A_XScream;
		ZHRT Q 5 A_NoBlocking;
		ZHRT RTUV 5;
		ZHRT W -1;
		Stop;
	Raise:
		ZHRT KJIH 5;
		Goto See;
	}
}

class AssassinGrenade : DMR_ThrownGrenade
{
	States
	{
	Spawn:
		THR2 A 4;
		THR2 B 1 Bright;
		Loop;
	Death:
		TNT1 A 0 {
			bNoGravity = true;
			A_Stop();
			A_SetScale(2.5);
		}
		FVUL C 4 {
			A_PlaySound("gas/explode");
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,-30,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_SpawnItemEx("DMR_MediumGasCloud",0,0,-30,frandom(-1,1),frandom(-1,1),0,0,SXF_ORIGINATOR);
			A_Explode(8, 48);
		}
		FVUL DEF 4;
		Stop;
	}
}