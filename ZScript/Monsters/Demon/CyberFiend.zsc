//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Carnevil, hnsolo77
//	Sounds:		CyClones, Raven Software, Shadow Caster
//  Sprites: 	Id Software (edits by Neoworm, Vader, Eriance, hnsolo77)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//	DustedPandomic for the charge / ram logic
//======================================================================

class MaulerFiend : DMR_Demon
{
	Default
	{
		Tag "Maulerfiend";
		Health 300;
		Speed 13;
		PainChance 125;		
		+MISSILEMORE;
		SeeSound "cyfiend/sight";
		PainSound "cyfiend/pain";
		DeathSound "cyfiend/death";
		ActiveSound "cyfiend/active";
		Obituary "%o was violated by a Maulerfiend.";
		HitObituary "%o became the Maulerfiend's dinner.";
		Scale 1.1;
		MeleeThreshold 128; // don't charge in min range
		MaxTargetRange 400; // don't charge / fire projectiles at range
	}
	States
	{
	Spawn:
		CYFN AB 10 A_Look;
		Loop;
	See:
		CYFN A 0 A_StartSound("cyfiend/step");
		CYFN AABB 2 Fast A_Chase;
		CYFN C 0 A_StartSound("cyfiend/step");
		CYFN CCDD 2 Fast A_Chase;
		Loop;
	Melee:
		CYFN EF 4 Fast A_FaceTarget;
		CYFN G 6 Fast A_CustomMeleeAttack(random(8,24));
		Goto See;		
	Missile:
		TNT1 A 0 A_Jump(128, "Charge");
	BloodAttack:
		CYFN GF 6 Fast A_FaceTarget;
		CYFN E 6 Fast {
			A_StartSound("Weapons/bloodf", CHAN_WEAPON);
			A_LobProjectile("BloodfiendShot",30,0,frandom(-1,-2));
			A_LobProjectile("BloodfiendShot",30,0,0);
			A_LobProjectile("BloodfiendShot",30,0,frandom(1,2));
		}
		Goto See;
	Charge:
		CYFN E 10 A_FaceTarget;
		TNT1 A 0 {
			A_StartSound("cyfiend/melee", CHAN_VOICE);
			A_FaceTarget();
			A_Recoil(-3);
		}
	ChargeLoop:
		CYFN AAABBB 1 {
			if (target && Distance3D(target) < 80 || vel.x == 0 && vel.y == 0) { SetStateLabel("Ram"); }
			A_FaceTarget(1.5,180);
			A_Recoil(-3);
		}
		TNT1 A 0 A_StartSound("cyfiend/step");
		CYFN CCCDDD 1 {
			if (target && Distance3D(target) < 80 || vel.x == 0 && vel.y == 0) { SetStateLabel("Ram"); }
			A_FaceTarget(1.5,180);
			A_Recoil(-3);
		}
		TNT1 A 0 A_StartSound("cyfiend/step");
		Loop;
	Ram:
		CYFN E 2 A_FaceTarget(30,180);
		CYFN F 6 {
			A_FaceTarget(20,180);
			A_CustomBulletAttack(0,0,1,random(12,36),"MaulerFiendRamImpactor",80,CBAF_NORANDOM|CBAF_AIMFACING);
			A_Stop();
		}
		CYFN A 2 A_FaceTarget;
		Goto See;
	Pain:
		CYFN H 2 Fast;
		CYFN H 2 Fast A_Pain;
		Goto See;
	Death:
	XDeath:
		CYFN I 8 A_Scream;
		CYFN I 0 A_FaceTarget;
		CYFN J 0 A_SpawnItemEx("MaulerFiendArm", 10, 0, 32, 0, 8, 0, 0);
		CYFN J 8;
		CYFN K 4; 
		CYFN L 4 A_NoBlocking;
		CYFN M 4;
		CYFN N -1;
		Stop;
	Raise:
		CYFN NMLKJI 5;
		Goto See;
	}
}

Class MaulerFiendRamImpactor : DMR_RamImpactor
{
	Default
	{
		ProjectileKickback 800;
	}
}

Class MaulerFiendArm : DMR_DroppedShield
{
    States
	{
	Spawn:
         CYF1 ABCDEFGH 2;
         Loop;
	Death:
		CYF1 I -1;
		Stop;
	}
}