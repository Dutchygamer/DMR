//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Carnevil, hnsolo77
//	Sounds:		CyClones, Raven Software, Shadow Caster
//  Sprites: 	Id Software (edits by Neoworm, Vader, Eriance, hnsolo77)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//	DustedPandomic for the charge / ram logic
//======================================================================

class MaulerFiend : DMR_Demon
{
	Default
	{
		Tag "Maulerfiend";
		Health 300;
		Speed 13;
		PainChance 125;		
		+MISSILEMORE;
		SeeSound "CyberFiend/sight";
		PainSound "CyberFiend/pain";
		DeathSound "CyberFiend/death";
		ActiveSound "CyberFiend/active";
		Obituary "%o was violated by a maulerfiend.";
		HitObituary "%o became the maulerfiend's dinner.";
		MeleeThreshold 64;
		Scale 1.1;
	}
	States
	{
	Spawn:
		CYFN AB 10 A_Look;
		Loop;
	See:
		CYFN A 0 A_PlaySound("blooddemon/walk");
		CYFN AABB 2 Fast A_Chase;
		CYFN C 0 A_PlaySound("blooddemon/walk");
		CYFN CCDD 2 Fast A_Chase;
		Loop;
	Melee:
		CYFN EF 8 Fast A_FaceTarget;
		CYFN G 8 Fast A_CustomMeleeAttack(random(8,24));
		Goto See;		
	Missile:
		TNT1 A 0 A_Jump(128, "Charge");
	BloodAttack:
		TNT1 A 0 A_JumpIfCloser(256,1); // only start attacking when we are close
		Goto See;
		CYFN GF 6 Fast A_FaceTarget;
		CYFN E 6 Fast {
			A_PlaySound("Weapons/bloodf", CHAN_WEAPON);
			A_LobProjectile("BloodfiendShot",30,0,frandom(-1,-2));
			A_LobProjectile("BloodfiendShot",30,0,0);
			A_LobProjectile("BloodfiendShot",30,0,frandom(1,2));
		}
		Goto See;
	Charge:
		TNT1 A 0 A_JumpIfCloser(360,1); // only start attacking when we are close
		Goto See;
		CYFN E 10 A_FaceTarget;
		TNT1 A 0 {
			A_PlaySound("blooddemon/melee", CHAN_VOICE);
			A_FaceTarget();
			A_Recoil(-3);
		}
	ChargeLoop:
		CYFN AAABBB 1 {
			if (target && Distance3D(target) < 80 || vel.x == 0 && vel.y == 0) { SetStateLabel("Ram"); }
			A_FaceTarget(1.5,180);
			A_Recoil(-3);
		}
		TNT1 A 0 A_PlaySound("blooddemon/walk");
		CYFN CCCDDD 1 {
			if (target && Distance3D(target) < 80 || vel.x == 0 && vel.y == 0) { SetStateLabel("Ram"); }
			A_FaceTarget(1.5,180);
			A_Recoil(-3);
		}
		TNT1 A 0 A_PlaySound("blooddemon/walk");
		Loop;
	Ram:
		CYFN E 2 A_FaceTarget(30,180);
		CYFN F 6 {
			A_FaceTarget(20,180);
			A_CustomBulletAttack(0,0,1,random(12,36),"MaulerFiendRamImpactor",80,CBAF_NORANDOM|CBAF_AIMFACING);
			A_Stop();
		}
		CYFN A 2 A_FaceTarget;
		Goto See;
	Pain:
		CYFN H 2 Fast;
		CYFN H 2 Fast A_Pain;
		Goto See;
	Death:
	XDeath:
		CYFN I 8 A_Scream;
		CYFN I 0 A_FaceTarget;
		CYFN J 0 A_SpawnItemEx("MaulerFiendArm", 10, 0, 32, 0, 8, 0, 0);
		CYFN J 8;
		CYFN K 4; 
		CYFN L 4 A_NoBlocking;
		CYFN M 4;
		CYFN N -1;
		Stop;
	Raise:
		CYFN NMLKJI 5;
		Goto See;
	}
}

Class MaulerFiendRamImpactor : DMR_RamImpactor
{
	Default
	{
		ProjectileKickback 800;
	}
}

Class MaulerFiendArm : DMR_DroppedShield
{
    States
	{
	Spawn:
         CYF1 ABCDEFGH 2;
         Loop;
	Death:
		CYF1 I -1;
		Stop;
	}
}