//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	KDiZD Team
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Eriance)
//	GLDEFs: 	Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class Bloodfiend : DMR_Demon
{
	Default
	{
		Tag "Blood fiend";
		Health 190;
		PainChance 150;		
		SeeSound "monster/sg2sit";
		AttackSound "monster/sg2atk";
		PainSound "monster/pain";
		DeathSound "monster/death";
		ActiveSound "monster/sg2act";
		Obituary "%o died from the toxic blood of a blood fiend.";
		HitObituary "%o was eaten by a blood fiend.";
		MaxTargetRange 500; // don't try and fire projectiles at range
	}
	States
	{
	Spawn:
		SAR2 AB 10 A_Look;
		Loop;
	See:
		SAR2 AABBCCDD 2 Fast A_Chase;
		Loop;
	Melee:
		SAR2 EF 8 Fast A_FaceTarget;
		SAR2 G 8 Fast A_CustomMeleeAttack(random(8,24));
		Goto See;
	Missile:
		TNT1 A 0 A_JumpIfCloser(256,1); // only start attacking when we are close
		Goto See;
		SAR2 GF 8 Fast A_FaceTarget;
		SAR2 E 8 Fast {
			A_PlaySound("Weapons/bloodf", CHAN_WEAPON);
			A_LobProjectile("BloodfiendShot",28,0,frandom(-1,-2));
			A_LobProjectile("BloodfiendShot",28,0,frandom(1,2));
		}
		Goto See;
	Pain:
		SAR2 H 2 Fast;
		SAR2 H 2 Fast A_Pain;
		Goto See;
	Death:
		SAR2 I 8;
		SAR2 J 8 A_Scream;
		SAR2 K 4;
		SAR2 L 4 A_NoBlocking;
		SAR2 M 4;
		SAR2 N -1;
		Stop;
	XDeath:
		SAR2 O 5;
		SAR2 P 5 A_XScream;
		SAR2 Q 5 A_NoBlocking;
		SAR2 RSTUV 5;
		SAR2 W -1;
		Stop;
	Raise:
		SAR2 NMLKJI 5;
		Goto See;
	}
}

class BloodfiendShot : DMR_DoomImpBall
{
	Default
	{
		Speed 15;
		DamageFunction(random(4,12));
		//RENDERSTYLE "TRANSLUCENT";
		ALPHA 0.80;
		DeathSound "weapons/bloodx";
		-NOGRAVITY;
		Gravity 0.2;
	}
	States
	{
	Spawn:
		FBLD AABB 1 Bright {
			A_SpawnItemEx("DMR_BloodTrail",0,0);
		}
		loop;
	Death:
		TNT1 A 0 A_NoGravity;
		FBLD CDE 3 Bright;
		stop;
	}
}