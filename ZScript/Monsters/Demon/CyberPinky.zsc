//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Carnevil
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by Neoworm, Vader)
//	GLDEFs: 	Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//	DustedPandomic for the charge / ram logic
//======================================================================

class DMR_Mauler : DMR_Demon
{
	Default
	{
		Tag "Mauler";
		Health 200;
		PainChance 150;		
		SeeSound "cypinky/sight";
		PainSound "cypinky/pain";
		DeathSound "cypinky/death";
		ActiveSound "cypinky/active";
		Obituary "%o was gored by a Mauler";
		HitObituary "A mauler ate %o's heart out." ;
		MeleeThreshold 128; // don't charge in min range
		MaxTargetRange 400; // don't charge at max range
	}
	States
	{
	Spawn:
		SRG2 AB 10 A_Look;
		Loop;
	See:
		TNT1 A 0 A_StartSound("cypinky/step");
		SRG2 AABB 2 Fast A_Chase; 
		TNT1 C 0 A_StartSound("cypinky/step");
		SRG2 CCDD 2 Fast A_Chase;
		Loop;
	Melee:
		SRG2 EF 6 Fast A_FaceTarget;
		SRG2 G 6 Fast A_CustomMeleeAttack(random(8,24));
		Goto See;
	Missile:
		SRG2 E 10 A_FaceTarget;
		TNT1 A 0 {
			A_StartSound("cypinky/melee", CHAN_VOICE);
			A_FaceTarget();
			A_Recoil(-3);
		}
	ChargeLoop:
		SRG2 AAABBB 1 {
			if (target && Distance3D(target) < 80 || vel.x == 0 && vel.y == 0) { SetStateLabel("Ram"); }
			A_FaceTarget(1.5,180);
			A_Recoil(-3);
		}
		TNT1 A 0 A_StartSound("cypinky/step");
		SRG2 CCCDDD 1 {
			if (target && Distance3D(target) < 80 || vel.x == 0 && vel.y == 0) { SetStateLabel("Ram"); }
			A_FaceTarget(1.5,180);
			A_Recoil(-3);
		}
		TNT1 A 0 A_StartSound("cypinky/step");
		Loop;
	Ram:
		SRG2 E 2 A_FaceTarget(30,180);
		SRG2 F 8 {
			A_FaceTarget(20,180);
			A_CustomBulletAttack(0,0,1,random(10,30),"DMR_RamImpactor",80,CBAF_NORANDOM|CBAF_AIMFACING);
			A_Stop();
		}
		SRG2 A 2 A_FaceTarget;
		Goto See;
	Pain: 
		SRG2 H 2 Fast;
		SRG2 H 2 Fast A_Pain;
		Goto See;
	Death:
	XDeath:
		SRG2 I 8;
		SRG2 I 0 A_FaceTarget;
		SRG2 J 0 A_SpawnItemEx("DMR_MaulerArm", 10, 0, 32, 0, 8, 0, 0);
		SRG2 J 8 A_Scream;
		SRG2 K 4;
		SRG2 L 4 A_NoBlocking;
		SRG2 M 4;
		SRG2 N -1; 
		Stop;
	Raise:
		SRG2 NMLKJI 5;
		Goto See;
	}
}

Class DMR_MaulerArm : DMR_DroppedShield
{
    States
	{
	Spawn:
		SG2A ABCDEFGH 2;
		Loop;
	Death:
		SG2A I -1;
		Stop;
	}
}