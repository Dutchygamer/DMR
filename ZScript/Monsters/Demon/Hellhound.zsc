//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Monolith, Eriance
//  Sprites: 	Monolith, Eriance (edits by Eriance)
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class Hellhound : DMR_Demon
{
	Default
	{
		Tag "Hellhound";
		Health 200;
		Speed 12;
		PainChance 150;
		SeeSound "monster/dogsit";
		AttackSound "monster/dogatk";
		PainSound "monster/dogpai";
		DeathSound "monster/dogdth";
		ActiveSound "monster/dogact";
		Obituary "%o burned alive by a hellhound.";
		HitObituary "A hellhound had %o for dinner.";
		DamageFactor "Fire", 0.5;
	}
	int attackloop;
	
	States
	{
	Spawn:
		HDOG AB 10 A_Look;
		Loop;
	See:
		HDOG AABBCCDDEEFF 2 Fast A_Chase;
		Loop;
	Melee:
		HDOG GH 8 Fast A_FaceTarget;
		HDOG I 8 Fast A_CustomMeleeAttack(random(8,24),"monster/dogbit");
		Goto See;
	Missile:
		TNT1 A 0 {
			if(target && Distance3D(target) > 300) SetStateLabel("See");
		}
		HDOG G 10 A_FaceTarget;
		TNT1 A 0 { 
			attackloop = 0; 
			A_PlaySound ("weapons/flamethrower", CHAN_WEAPON);	
		}
	MissileLoop:
		TNT1 A 0 A_JumpIf(attackloop >= 12,"See");
		HDOG H 1 Bright {
			A_FaceTarget();
			attackloop++;
			A_SpawnProjectile("DMR_FlamethrowerBall",28,0,random(-3,3));
		}
		Loop;
	Pain:
		HDOG J 2 Fast;
		HDOG J 2 Fast A_Pain;
		Goto See;
	Death:
	XDeath:
		HDOG K 8;
		HDOG L 8 A_Scream;
		HDOG M 4;
		HDOG N 4 A_NoBlocking;
		HDOG OP 4;
		HDOG Q -1;
		Stop;
	Raise:
		HDOG QPONMLK 5;
		Goto See;
	}
}