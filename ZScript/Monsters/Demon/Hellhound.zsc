//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Eriance
//	Sounds:		Monolith, Eriance
//  Sprites: 	Monolith, Eriance (edits by Eriance)
//	GLDEFs: 	Ghastly Dragon, Sandypaper (Brightmaps)
//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//	ZScript:	Dutchygamer
//======================================================================

class Hellhound : DMR_Demon
{
	Default
	{
		Tag "Hellhound";
		Health 200;
		Speed 12;
		PainChance 150;
		SeeSound "hellhound/sight";
		AttackSound "hellhound/melee";
		PainSound "hellhound/pain";
		DeathSound "hellhound/death";
		ActiveSound "hellhound/active";
		Obituary "%o burned alive by a Hellhound.";
		HitObituary "A Hellhound has torn %o apart.";
		DamageFactor "Fire", 0.5;
		MaxTargetRange 400; // don't fire projectiles at range
	}
	
	States
	{
	Spawn:
		HDOG AB 10 A_Look;
		Loop;
	See:
		HDOG AABBCCDDEEFF 2 Fast A_Chase;
		Loop;
	Melee:
		HDOG GH 6 Fast A_FaceTarget;
		HDOG I 6 Fast A_CustomMeleeAttack(random(8,24),"hellhound/melee/hit");
		Goto See;
	Missile:
		TNT1 A 0 {
			if(target && Distance3D(target) > 300) SetStateLabel("See");
		}
		HDOG GG 8 Fast A_FaceTarget;
		HDOG H 8 Fast Bright Light("DMR_RifleFire") A_LobProjectile("HellhoundBall",28,0,random(-2,2));
		Goto See;
	Pain:
		HDOG J 2 Fast;
		HDOG J 2 Fast A_Pain;
		Goto See;
	Death:
	XDeath:
		HDOG K 8;
		HDOG L 8 A_Scream;
		HDOG M 4;
		HDOG N 4 A_NoBlocking;
		HDOG OP 4;
		HDOG Q -1;
		Stop;
	Raise:
		HDOG QPONMLK 5;
		Goto See;
	}
}

Class HellhoundBall : PyroTechnicianBall
{
	Default
	{
		Speed 20;
	}
}