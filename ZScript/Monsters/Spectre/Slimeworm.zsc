//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	Captain Toenail
//	Sounds:		Id Software (Quake 2, 4)
//  Sprites: 	Freedoom (Fredrik Johanson), 3D Realms (Blake Stone)
//	GLDEFs: 	Dreadopp
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class SlimeWorm : DMR_Demon
{
	Default
	{
		Tag "Slime worm";
		Health 210;
		PainChance 125;
		+MISSILEMORE;
		+SHADOW;
		RenderStyle "Subtract";
		Alpha 0.25;
		seesound "slimeworm/sight";
		attacksound "slimeworm/melee";
		painsound "slimeworm/pain";
		deathsound "slimeworm/death";
		activesound "slimeworm/active";
		bloodcolor "yellow";
		obituary "%o was dissolved by a slime worm.";
		hitobituary "%o was digested by a slime worm.";		
		MeleeThreshold 128;
		MaxTargetRange 500; // don't try and fire projectiles at range
	}
	States
	{
	Spawn:
		WORM AB 10 A_Look;
		Loop;
	See:
		WORM AABBCCDD 2 Fast A_Chase;
		Loop;
	Melee:
		WORM EF 8 A_FaceTarget;
		WORM G 8 A_CustomMeleeAttack(random(8,24),"slimeworm/melee","","Extreme");
		Goto See;
	Missile:
		WORM EE 6 A_FaceTarget;
		WORM F 8 {
			A_PlaySound ("Weapons/bloodf", CHAN_WEAPON);
			A_LobProjectile("SlimeBall1",40,0,frandom(-8,8));
			A_LobProjectile("SlimeBall2",40,0,frandom(-8,8));
			A_LobProjectile("SlimeBall3",40,0,frandom(-8,8));
			A_LobProjectile("SlimeBall4",40,0,frandom(-8,8));
			A_LobProjectile("SlimeBall5",40,0,frandom(-8,8));
		}
		goto See;
	Pain:
		WORM H 2 Fast;
		WORM H 2 Fast A_Pain;
		Goto See;
	Death:
	XDeath:
		WORM I 8;
		WORM J 8 A_Scream;
		WORM K 4;
		WORM L 4 A_NoBlocking;
		WORM M 4;
		WORM N -1;
		Stop;
	Raise:
		WORM NMLKJI 5;
		Goto See;
	}
}

class SlimeBall1 : BloodfiendShot
{
	Default
	{
		Speed 14;
		DamageFunction(random(2,5));
		Scale 0.5;
		+EXTREMEDEATH;
		SeeSound "";
		DeathSound "slimeball/splat";
		Decal "PlasmaScorchLower";
	}
	states
	{
	Spawn:
		BOGY ABC 2;
		loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0,STYLE_Add);
			A_NoGravity();
		}
		BOGY DEF 4;
		stop;
	}
}

class SlimeBall2 : SlimeBall1
{
	Default
	{
		Speed 16;
		Gravity 0.22;
	}
}

class SlimeBall3 : SlimeBall1
{
	Default
	{
		Speed 18;
		Gravity 0.25;
	}
}

class SlimeBall4 : SlimeBall1
{
	Default
	{
		Speed 20;
		Gravity 0.27;
	}
}

class SlimeBall5 : SlimeBall1
{
	Default
	{
		Speed 22;
		Gravity 0.3;
	}
}
