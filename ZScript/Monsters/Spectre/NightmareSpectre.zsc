//======================================================================
//  Original credits
//======================================================================
//  DECORATE: 	AgentSpork
//	Sounds:		Id Software
//  Sprites: 	Id Software (edits by AgentSpork)
//	GLDEFs: 	n/a
//======================================================================
//  Additional credits
//======================================================================
//	ZScript:	Dutchygamer
//======================================================================

class DMR_Nightmare : DMR_Spectre
{
	Default
	{
		Tag "Nightmare";
		Health 225;
		Speed 12;
		PainChance 150;		
		RenderStyle "Subtract";
		SeeSound "d64demon/sight";
		AttackSound "d64demon/melee";
		PainSound "d64demon/pain";
		DeathSound "d64demon/death";
		ActiveSound "cyfiend/active";
		Alpha 0.10;
		Obituary "%o was spooked by a Nightmare.";
	}
	States
	{
	Spawn:
		SAR3 AB 10 A_Look;
		Loop;
	See:
		SAR3 AABBCCDD 2 Fast A_Chase;
		Loop;
	Melee:
		SAR3 EF 8 Fast A_FaceTarget;
		SAR3 G 8 Fast A_CustomMeleeAttack(random(8,24));
		Goto See;
	Pain:
		SAR3 H 2 Fast {
			A_SetRenderStyle(Alpha+0.1,STYLE_Subtract);
			if(Alpha >= 1.0) { bNoPain = 1; }
		}
		SAR3 H 2 Fast A_Pain;
		Goto See;
	Death:
	XDeath:
		SAR3 I 8;
		SAR3 J 8 A_Scream;
		SAR3 K 4;
		SAR3 L 4 A_NoBlocking;
		SAR3 M 4;
		SAR3 N -1;
		Stop;
	Raise:
		SAR3 NMLKJI 5;
		Goto See;
	}
}