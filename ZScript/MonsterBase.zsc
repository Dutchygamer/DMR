//======================================================================
//  Original credits
//======================================================================
//	ZScript: 	DustedPandemonic, modified by Dutchygamer
//======================================================================

Class DMR_MonsterBase : Actor
{
	Default
	{
		Monster;
		+FLOORCLIP;
		
		// set a default so we can damage each other as intended unless we are the same species here as well
		DMR_MonsterBase.Species2 "None";
	}

	string spec2;
	property Species2: spec2;
	
	int attackLoop;
	int fxLoop;

	// check if the monster damaging us is part of the Species2
	// if so, treat it like it is the same species (unless DONTHARMSPECIES is false)
	override int DamageMobj(Actor inflictor, Actor source, int damage, Name mod, int flags, double angle)
	{
		if (source && source.bISMONSTER == true && source is "DMR_MonsterBase")
		{
			let source2 = DMR_MonsterBase(source);
			if (source.bDONTHARMSPECIES == true && spec2 != "None" && spec2 == source2.spec2)
			{
				return 0;
			}
		}
		return Super.DamageMobj(inflictor,source,damage,mod,flags,angle);
	}
	
	// variant of A_SpawnProjectile which adds a lob to the projectile's trajectory while taking the height difference between firer and target into account
	action actor A_LobProjectile(class<Actor> MissileType, double SpawnHeight = 32, double SpawnOfs_XY = 0, double Angle = 0, int Flags = 0, double Pitch = 0, int Ptr = AAPTR_TARGET)
	{
		actor proj = A_SpawnProjectile(MissileType,SpawnHeight,SpawnOfs_XY,Angle,Flags,Pitch,Ptr);
		actor other = GetPointer(Ptr);
		if (proj)
		{
			if (other)
			{
				int ZDiff = int(other.Pos.Z + other.Height / 2 - Pos.Z - SpawnHeight);
				proj.Vel.Z += (ZDiff + Distance2D(other)) / TICRATE * proj.GetGravity() / 2;
			}
			return proj;
		}
		return null;
	}
	
	// some creativity that for monster variants that can fly while the original monster doesn't (ie Afrit for HellKnight) they don't break certain Boom/ZDoom map things	
	void A_BeginFloat()
	{
		A_NoGravity(); 
		bFLOAT = true;
		bFLOORCLIP = false;
	}	
}


Class DMR_FlyingMonsterBase : DMR_MonsterBase
{
	Default
	{
		-FLOORCLIP;
		+FLOAT;
		+NOGRAVITY;
	}
}