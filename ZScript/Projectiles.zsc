
class EmptyBulletPuff : Actor
{
	Default
	{
		Radius 0;
		Height 1;
		PROJECTILE;
	}
	States
	{
	Spawn:
		TNT1 A 2;
		Stop;
	}
}

//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//======================================================================
class DMR_Rocket : DMR_Projectile
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 20;
		DamageFunction(random(40,120));
		+RANDOMIZE
		+EXTREMEDEATH;
		SeeSound "largerocket/launch";
		DeathSound "weapons/rocklx";
		Decal "BigScorch";
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn:
		MISL A 1 Bright {
			A_SpawnItemEx("DMR_MediumFireballTrail",-35,0,2,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			A_SpawnItemEx("DMR_MediumSmokeCloud",-35,0,2,frandom(-1,0));
			for (fxLoop = 0;fxLoop<=3;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-35,-30),frandom(-6,6),frandom(-6,6),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0,STYLE_Add);
			for (fxLoop = 0;fxLoop <= 14;fxLoop++) {
				A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-2,2),random(-2,2),random(-2,2),frandom(-8,8),frandom(-8,8),frandom(-2,8)); 
			}
			for (fxLoop = 0;fxLoop<=70;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(18,24),frandom(12,14),random(0,360),frandom(0,11),0,frandom(-8,8),frandom(0,8),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		MISL B 8 Bright A_Explode(128,128);
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}

//======================================================================
//  Additional credits
//======================================================================
//	Sounds:		Westwood Studios
//======================================================================
class DMR_SmallRocket : DMR_Rocket
{
	Default
	{
		Radius 9;
		Height 7;
		DamageFunction(random(20,60));
		SeeSound "smallrocket/launch";
		Scale 0.8;
		Decal "MediumScorch";
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
	}
	States
	{
	Spawn:
		MISL A 1 Bright {
			A_SpawnItemEx("DMR_FireballTrail",-30,0,1,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			A_SpawnItemEx("DMR_SmokeCloud",-30,0,1,frandom(-1,0));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-30,-26),frandom(-6,6),frandom(-5,5),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SetRenderStyle(1.0,STYLE_Add);
			A_SetScale(1.0);
			for (fxLoop = 0;fxLoop <= 10;fxLoop++) {
				A_SpawnItemEx("DMR_FireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-5,5),frandom(-5,5),frandom(-1,5));
			}
			for (fxLoop = 0;fxLoop<=50;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(18,24),frandom(12,14),random(0,360),frandom(0,9),0,frandom(-7,7),frandom(0,7),0,frandom(-7,7),0,0,-0.1,1,-1,-0.6);
			}
		}
		MISL B 8 Bright A_Explode(72,72);
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}

// grenade
Class DMR_GrenadeBase : DMR_Projectile
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 25;
		DamageFunction(random(20,60));
		-NOGRAVITY
		+THRUGHOST
		+CANBOUNCEWATER;
		BounceType "Doom";
		BounceFactor 0.45;
		WallBounceFactor 0.45;
		BounceCount 10; // in case it starts bouncing like a madman
		Gravity 0.5;
		SeeSound "Paladin/Grenade";
		BounceSound "Paladin/Bounce";
	}
	
	int grenadeTimer;	
}

class DMR_ThrownGrenade : DMR_GrenadeBase
{
	Default
	{
		Speed 20;
		DamageFunction(random(16,48));
		BounceFactor 0.3;
		WallBounceFactor 0.3;
		Gravity 0.5;
		SeeSound "handgrenade/throw";
		BounceSound "handgrenade/bounce";
		Scale 0.75;
	}
}

class DMR_FiredGrenade : DMR_GrenadeBase
{
	Default
	{
		+EXTREMEDEATH;
		DMR_Projectile.ParticleColors "#df660f", "#ff7e1b", "#ffff00", "#ffe74b";
		Decal "BigScorch";
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			grenadeTimer = random(0,10);
		}
		SGRN A 1 {
			A_SpawnItemEx("DMR_SmokeCloud",-20,0,4,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			for (fxLoop = 0;fxLoop<=3;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-35,-30),frandom(-6,6),frandom(-6,6),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
			grenadeTimer++;
		}
		Wait;
	// on ground, not hit anything, waiting for timer to run out
	Death:
		SGRN A 1 {		
			A_SpawnItemEx("DMR_SmokeCloud",-20,0,4,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-35,-30),frandom(-6,6),frandom(-6,6),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
			// detonate if timer runs out
			if (grenadeTimer >= 80) { SetStateLabel("XDeath"); }
			grenadeTimer++;
		}
		Wait;
	// actual explosion
	XDeath:
		TNT1 A 0 {
			bNoGravity = true;
			A_Stop();
			A_SetRenderStyle(1.0,STYLE_Add);
			A_StartSound("weapons/rocklx",2);
			for (fxLoop = 0;fxLoop <= 10;fxLoop++) {
				A_SpawnItemEx("DMR_FireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-5,5),frandom(-5,5),frandom(-1,5));
			}
			for (fxLoop = 0;fxLoop<=50;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(18,24),frandom(12,14),random(0,360),frandom(0,9),0,frandom(-7,7),frandom(0,7),0,frandom(-7,7),0,0,-0.1,1,-1,-0.6);
			}
		}
		MISL B 8 Bright A_Explode(96,96);
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}

// cluster missile
//TODO obsolete
class DMR_ClusterSubProjectile : DMR_Projectile
{
	Default
	{
		Radius 9;
		Height 7;
		Speed 10;
		DamageFunction(random(10,30));
		+RANDOMIZE
		+SEEKERMISSILE;
		SeeSound "smallrocket/launch";
		DeathSound "skeleton/tracex";
		Scale 0.5;
	}
	int fuelLevel;
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			fuelLevel = random(75,100); // set fuel lifetime
		}
		TNT1 A 0 A_Jump(128,"Homing"); 
	NonHoming:
		TNT1 A 0 A_JumpIf(fuelLevel < 1,"Death");
		HMIS A 1 Bright {
			If (fuelLevel > 20) { // stop fire exhaust if fuel is running low 
				A_SpawnItemEx("DMR_FireballTrail",-25,0,0,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			}
			fuelLevel--;
		}
		HMIS A 1 BRIGHT A_Weave(random(1,2), random(1,2), frandom(0.5,1.5), frandom(0.5,1.5));
		Loop;
	Homing:
		TNT1 A 0 A_JumpIf(fuelLevel < 1,"Death");
		HMIS A 1 Bright { 
			A_SeekerMissile(2,8,SMF_PRECISE); 
			If (fuelLevel > 20) { // stop fire exhaust if fuel is running low 
				A_SpawnItemEx("DMR_FireballTrail",-25,0,0,frandom(-1,0),frandom(-1,1),frandom(-1,0));
			}
			A_SpawnItemEx("DMR_SmokeCloud",-25,0,0,frandom(-1,0));
			fuelLevel--;
		}
		HMIS A 1 BRIGHT A_Weave(random(1,2), random(1,2), frandom(0.5,1.5), frandom(0.5,1.5));
		Loop;
	Death:
		TNT1 A 0 {
			A_Stop();
			A_SetRenderStyle(1.0,STYLE_Add);
			A_SetScale(0.65);
		}
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop <= 3;fxLoop++) {
				A_SpawnItemEx("DMR_FireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-5,5),frandom(-5,5),frandom(-1,5));
			}
		}
		MISL B 8 Bright;
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}

//======================================================================
//  Original credits
//======================================================================
//	ZScript: 	DustedPandemonic, modified by Dutchygamer
//======================================================================
Class DMR_FlamethrowerBall : DMR_Projectile
{
	Default
	{
		Radius 4;
		Height 4;
		Speed 20;
		RenderStyle "Add";
		DamageType "Fire";
		Alpha 0.67;
		Scale 0.4;
		+THRUACTORS;
		+BRIGHT
		+NODAMAGETHRUST;
		+DONTSPLASH;
		+ROLLSPRITE;
	}
	int dmgtic;
	bool fire;
	
	States
	{
	Spawn:
		FRFX AABBCCDDEEFFGGHHIIJJKK 1 {
			A_ScaleVelocity(0.98);
			A_SetScale(Scale.X + 0.03);
			
			// only deal damage X amount of tics instead of every tick
			dmgtic++;
			if(dmgtic >= 3)
			{
				A_Explode(3,32,0,0,32);
				dmgtic = 0;
			}		
		}
		FRFX LLMMNNOO 1 {
			// fall down towards end
			if (vel.z != 0 && vel.y != 0) { 
				A_ChangeVelocity(0,0,1);
			}
			A_SetScale(Scale.X + 0.03);
		}
		Stop;
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		A_SetRoll(random(0,360));
	}
	
	override void Tick()
	{
		Super.Tick();
		//if(vel.x == 0 && vel.y == 0 && fire == false && random(1,4) == 1) {
		//	A_SpawnProjectile("DMR_BurningFire",1,0,0,CMF_TRACKOWNER);
		//	fire = true;
		//}
	}
}

class DMR_FlamethrowerBallLarge : DMR_FlamethrowerBall
{
	Default
	{
		Radius 6;
		Height 6;
		Scale 0.7; //1;
	}
	States
	{
	Spawn:
		FRFX AABBCCDDEEFFGGHHIIJJKK 1 {
			A_SetScale(Scale.X + 0.03);
			// only deal damage X amount of tics instead of every tick
			dmgtic++;
			if(dmgtic >= 3)
			{
				A_Explode(3,64,0,0,64);
				dmgtic = 0;
			}		
		}
		FRFX LLMMNNOO 1 {
			A_SetScale(Scale.X + 0.03);
		}
		Stop;
	}
}

//unused
class DMR_FlamethrowerExplode : Actor
{
	Default
	{
		SeeSound "dragonfam/attack";
		Radius 8;
		Height 8;
		Gravity 0;
		Speed 0;
		Scale 0.7;
		+DONTSPLASH;
	}
	States
	{
	Spawn:
		NKXP B 2 Bright;
		NKXP C 2 Bright;
		NKXP D 2 Bright A_Explode(20, 96); //(15, 64)
		NKXP EFGHIJKLMNOPQRSTUV 2 Bright;
		Stop;
	}
}

//unused
class DMR_FlamethrowerExplodeLarge : DMR_FlamethrowerExplode
{
	Default
	{
		Scale 1.1;
	}
	States
	{
	Spawn:
		NKXP B 2 Bright;
		NKXP C 2 Bright;
		NKXP D 2 Bright A_Explode(30, 128); //(25, 96)
		NKXP EFGHIJKLMNOPQRSTUV 2 Bright;
		Stop;
	}
}

// laser
class DMR_LaserBase : DMR_FastProjectile
{
	Default
	{
		Radius 2;
		Height 2;
		Speed 125;
		DamageFunction(random(16,48));
		//this defines a laser
		+RANDOMIZE;
		+DONTSPLASH;
		+DONTREFLECT;
		-CANNOTPUSH;
		+NODAMAGETHRUST;
		+BLOODLESSIMPACT;
		RenderStyle "Add";
		Alpha 0.75;
		DamageType "Disintegrate";
		MissileType "LaserTrailBase";
		MissileHeight 10;
		decal "RedLaserLightning";
		Scale 0.5;
		DMR_FastProjectile.ParticleColors "#ff0000", "#d20000", "#b90000", "#ff0f0f";
	}
	States
	{
	Spawn:
		TNT1 A 1;
		Loop;
	Death:
		TNT1 A 0 {
			for (fxLoop = 0;fxLoop<=10;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		BFE1 ABCDEF 5 Bright;
		Stop;
	}
}

class DMR_LaserTrailBase : Actor
{
	Default
	{
		RenderStyle "Add";
		Scale 0.2;
		Alpha 1;
		+NOINTERACTION;
		+NOGRAVITY;
		+DONTSPLASH;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("DMR_LaserFlare",0,0,0,frandom(-1,0),frandom(-1,1),frandom(0,1));
		LASB A 1 Bright;
	SpawnLoop:
		LASB A 1 Bright A_FadeOut(0.2);
		Loop;
	}
}

// plasma
class DMR_ZombiePlasma : DMR_Projectile
{
	Default
	{		
		Radius 13;
		Height 8;
		Speed 25;
		DamageFunction(random(10,30));
		+RANDOMIZE
		RenderStyle "Add";
		Alpha 0.75;
		SeeSound "weapons/plasmaf";
		DeathSound "weapons/plasmax";
		Decal "PlasmaScorch";
		DMR_Projectile.ParticleColors "#1b1bff", "#5353ff", "#000083", "#c7c7ff";
	}
	states
	{
	Spawn:
		PLSS AAABBB 2 Bright {
			A_SpawnItemEx("DMR_PlasmaTrail",frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-20,-18),frandom(-3,3),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_PlasmaImpactFlare");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		PLSS ABCDE 5 Bright;
		Stop;
	}
}

// hell rune
class DMR_HellRune : DMR_Projectile
{  
	Default
	{
		Radius 6;
		Height 16;
		Speed 12;
		DamageFunction(random(16,48));
		+RANDOMIZE
		+SEEKERMISSILE
		SeeSound "Paladin/Shoot";
		DeathSound "weapons/hellrune";
		Decal "HellRuneScorch";
		DMR_Projectile.ParticleColors "#9f009a", "#cf00cf", "#6f006b", "#ff7bff";
	}
	States
	{
	Spawn:
		// wait a bit before acquiring target
		ARAX AABBAABB 2 Bright;
		TNT1 A 0 {
			A_SetRenderStyle(1.0,STYLE_Add);
			A_ChangeVelocity(vel.x*1.5,vel.y*1.5,vel.z,CVF_REPLACE);
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-20,-18),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
	SpawnLoop:
		// actually start weaving and homing
		PLRN AAAABBBB 1 Bright {
			A_SeekerMissile(0,1,SMF_PRECISE|SMF_CURSPEED);
			A_Weave(frandom(1,2.5), frandom(1,2.5), frandom(0.7,1.2), frandom(0.7,1.2));
			A_SetRoll(roll+10);
			A_SpawnItemEx("DMR_PlasmaRuneTrail",frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
			for (fxLoop = 0;fxLoop<=2;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(8,10),0,frandom(-20,-18),frandom(-4,4),frandom(-4,4),frandom(-1,1),0,frandom(-1,1),0,0,-0.05,1,-1,-0.5);
			}
		}
		Loop;
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_PurplePlasmaImpactFlare");
			for (fxLoop = 0;fxLoop<=30;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		PLRX ABCDEF 3 Bright;
		Stop;
	}
}

class DMR_HellRuneSmall : DMR_HellRune
{  
	Default
	{
		DamageFunction(random(8,24));
		Speed 16;
		Scale 0.8;
	}
}

class DMR_HellRuneExplosive : DMR_HellRune
{  
	Default
	{
		Scale 1.5;
		Speed 10;
		DeathSound "weapons/hellrunebig";
	}
	States
	{
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("DMR_PurplePlasmaImpactFlare");
			for (fxLoop = 0;fxLoop<=40;fxLoop++)
			{
				A_SpawnParticle(GetParticleColor(),SPF_FULLBRIGHT|SPF_RELATIVE,random(10,18),frandom(12,14),random(0,360),frandom(0,6),0,frandom(-4,4),frandom(0,6),0,frandom(-8,8),0,0,-0.1,1,-1,-0.6);
			}
		}
		PLRX G 3 Bright A_Explode(10,48);
		PLRX HIJKLM 3 Bright;
		Stop;
	}
}

// gas clouds
class DMR_GasCloudBase : DMR_Projectile
{
	Default
	{
		Radius 6;
		Height 6;
		Projectile;
		DamageType "ToxinCloud";
		RENDERSTYLE "translucent";
		ALPHA 0.50;
		// tags for the gascloud itself
		+THRUACTORS;
		+DONTTHRUST;
		+RANDOMIZE;
		+DONTSPLASH;
		+DONTREFLECT;
		+NOBLOOD;
		+CANBOUNCEWATER;
		// tags for the damage it deals
		+BLOODLESSIMPACT;
		+FORCERADIUSDMG;
		+NODAMAGETHRUST;
		BounceType "Hexen";
		BounceFactor 1.0;
		WallBounceFactor 1.0;
		//note: with Scale 2.0, A_Explode should use 45 as radius
	}
	int lifetime;
	int dmgtic;
	
	override void Tick()
	{
		GravityTick();
	}
}

class DMR_LargeGasCloud : DMR_GasCloudBase
{
	Default
	{
		Scale 2.0;
	}
	States
	{
	Spawn: 
		GGAS OPQR 6;
		TNT1 A 0 {
			lifetime = random(13,18); //should always be second, else it will run 1 time and die
		}
	SpawnLoop:
		TNT1 A 0 A_JumpIf(lifetime < 1,"Death");		
		GGAS ABCDEFGHIJKLMN 3 {	
			// only damage X amount of tics instead of every tick
			dmgtic++;	
			if (dmgtic >= 5)
			{
				A_Explode(5,45,0,0,45);
				dmgtic = 0;
			}	
		}
		TNT1 A 0 {
			lifetime = lifetime-3;
		}
		Loop;
	Death: 
		GGAS STUVW 3 {
			A_SetTranslucent(Alpha - 0.1);
		}
		Stop;
	}
}

class DMR_MediumGasCloud : DMR_GasCloudBase
{
	Default
	{
		Scale 1.5;
	}
	States
	{
	Spawn: 
		GGAS OPQR 6;
		TNT1 A 0 {
			lifetime = random(9,14); //should always be second, else it will run 1 time and die
		}
	SpawnLoop:
		TNT1 A 0 A_JumpIf(lifetime < 1,"Death");		
		GGAS ABCDEFGHIJKLMN 3 {	
			// only damage X amount of tics instead of every tick
			dmgtic++;	
			if (dmgtic >= 5)
			{
				A_Explode(5,34,0,0,34);
				dmgtic = 0;
			}	
		}
		TNT1 A 0 {
			lifetime = lifetime-3;
		}
		Loop;
	Death: 
		GGAS STUVW 3 {
			A_SetTranslucent(Alpha - 0.1);
		}
		Stop;
	}
}

class DMR_SmallGasCloud : DMR_GasCloudBase
{
	States
	{
	Spawn: 
		GGAS OPQR 6;
		TNT1 A 0 {
			lifetime = random(4,7); //should always be second, else it will run 1 time and die
		}
	SpawnLoop:
		TNT1 A 0 A_JumpIf(lifetime < 1,"Death");		
		GGAS ABCDEFGHIJKLMN 3 {	
			// only damage X amount of tics instead of every tick
			dmgtic++;	
			if (dmgtic >= 5)
			{
				A_Explode(5,28,0,0,28);
				dmgtic = 0;
			}	
		}
		TNT1 A 0 {
			lifetime = lifetime-3;
		}
		Loop;
	Death: 
		GGAS STUVW 3 {
			A_SetTranslucent(Alpha - 0.1);
		}
		Stop;
	}
}

class DMR_MediumIceCloud : DMR_MediumGasCloud
{
	Default
	{
		DamageType "Ice";
	}
	States
	{
	Spawn: 
		GAS3 HIJK 6;
		TNT1 A 0 {
			lifetime = random(9,14); //should always be second, else it will run 1 time and die
		}
	SpawnLoop:
		TNT1 A 0 A_JumpIf(lifetime < 1,"Death");		
		GAS3 ABCDEFG 3 {	
			// only damage X amount of tics instead of every tick
			dmgtic++;	
			if (dmgtic >= 3)
			{
				A_Explode(5,34,0,0,34);
				dmgtic = 0;
			}	
		}
		TNT1 A 0 {
			lifetime = lifetime-3;
		}
		Loop;
	Death: 
		GAS3 LMNO 3 {
			A_SetTranslucent(Alpha - 0.1);
		}
		Stop;
	}
}

// fire field
class DMR_BurningFire : Actor
{
	Default
	{
		Radius 4;
		Height 4;
		RenderStyle "Add";
		Alpha 0.9;
		DamageType "Fire";
		Scale 2.5; //3; //0.7
		+THRUACTORS;		
		+NOBLOOD;
		+RANDOMIZE;
		+FORCERADIUSDMG;
		+NODAMAGETHRUST;
		+DONTTHRUST;
		+DONTSPLASH;
		+PAINLESS; // else it WILL stunlock everything hit by a Mancutank rocket barrage
	}
	
	int lifetime;
	int dmgtic;
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			lifetime = random(6,10);
		}
	SpawnLoop:
		TNT1 A 0 A_JumpIf(lifetime < 1,"Death");		
		CFCF ABCDEFGHIJK 2 {	
			// only damage X amount of tics instead of every tick
			dmgtic++;	
			if (dmgtic >= 5)
			{
				A_Explode(5,48,0,0,45);
				dmgtic = 0;
			}	
		}
		TNT1 A 0 {
			lifetime = lifetime-2;
		}
	Death:
		CFCF KLMOP 2 Bright;
		Stop;
	}
}

// a visible alternative to hitscan attacks
class DMR_SlowBullet : DMR_Projectile
{
	Default
	{
		Radius 6;
		Height 3;
		Speed 60;
		DamageFunction(random(6,16));
		Renderstyle "Add";
		Decal "BulletChip";
		+BLOODSPLATTER;
	}
	States
	{
	Spawn:
		TRCR A 1 Bright;
		Loop;
	Death:
		TNT1 A 1 A_SpawnItemEx("BulletPuff");
		Stop;
	XDeath:
		TNT1 A 1;
		Stop;
	}
}