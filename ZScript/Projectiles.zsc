class EmptyBulletPuff : Actor
{
	Default
	{
		Radius 0;
		Height 1;
		PROJECTILE;
	}
	States
	{
	Spawn:
		TNT1 A 2;
		Stop;
	}
}

//rocket
class DMR_Rocket : Rocket
{
	Default
	{
		DamageFunction(random(40,120));
		-ROCKETTRAIL;
		-ZDOOMTRANS;
		+EXTREMEDEATH;
		SeeSound "weapons/rocklf";
		DeathSound "weapons/rocklx";
	}
	States
	{
	Spawn:
		MISL A 1 Bright {
			A_SpawnItemEx("DMR_MediumFireballTrail",-25,0,5,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("DMR_MediumSmokeCloud",-25,0,5,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
		Loop;
	Death:
		TNT1 AAAAAAAAAAAAAA 0 {
			A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-2,2),random(-2,2),random(-2,2),frandom(-8,8),frandom(-8,8),frandom(-2,8)); 
		}
		MISL B 8 Bright A_Explode(128,128);
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}

class DMR_SmallRocket : DMR_Rocket
{
	Default
	{
		DamageFunction(random(20,60));
		SeeSound "smallrocket/launch";
		Scale 0.8;
	}
	States
	{
	Spawn:
		MISL A 1 Bright {
			A_SpawnItemEx("DMR_FireballTrail",-20,0,4,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			A_SpawnItemEx("DMR_SmokeCloud",-20,0,4,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
		}
		Loop;
	Death:
		TNT1 A 0 A_SetScale(1.0);
		TNT1 AAAAAAAAAA 0 {
			A_SpawnItemEx("DMR_MediumFireBouncingDebris",random(-2,2),random(-2,2),random(-2,2),frandom(-8,8),frandom(-8,8),frandom(-2,8)); 
		}
		MISL B 8 Bright A_Explode(96,96);
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}

//grenade
Class DMR_GrenadeBase: Actor
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 25;
		DamageFunction(random(16,48));
		Projectile;
		-NOGRAVITY
		+THRUGHOST
		BounceType "Doom";
		BounceFactor 0.75;
		Gravity 0.5;
		SeeSound "Paladin/Grenade";
		DeathSound "Paladin/Bounce";
		BounceSound "Paladin/Bounce";
	}
	
	int grenadeTimer;	
}

class DMR_ThrownGrenade : DMR_GrenadeBase
{
	Default
	{
		Speed 20;
		DamageFunction(random(10,30));
		BounceFactor 0.75;
		Gravity 0.5;
		SeeSound "";
		DeathSound "Paladin/Explode";
		BounceSound "dune/bounce";
		Scale 0.75;
		+FORCEXYBILLBOARD
	}
}

//cluster missile
class DMR_ClusterSubProjectile : actor
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 10;
		DamageFunction(random(10,30));
		Projectile;
		+SEEKERMISSILE;
		+RANDOMIZE;
		Scale 0.5;
	}
	int fuelLevel;
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(128,"Homing"); 
		TNT1 A 0 {
			fuelLevel = random(75,100); //set fuel lifetime
		}
		TNT1 A 0 A_JumpIf(fuelLevel < 1,"Death");
		HMIS A 1 Bright {
			If(fuelLevel > 20) { //stop fire exhaust if fuel is running low 
				A_SpawnItemEx("DMR_FireballTrail",-25,0,1,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			}
			fuelLevel = fuelLevel-1; //decrease fuel lifetime
		}
		HMIS A 1 BRIGHT A_Weave(random(1,2), random(1,2), frandom(0.5,1.5), frandom(0.5,1.5));
		Goto Spawn+2;
	Homing:
		TNT1 A 0 {
			fuelLevel = random(75,100);	//set fuel lifetime
		}
		TNT1 A 0 A_JumpIf(fuelLevel < 1,"Death");
		HMIS A 1 Bright { 
			A_SeekerMissile(1,6,SMF_PRECISE); 
			If(fuelLevel > 20) { //stop fire exhaust if fuel is running low 
				A_SpawnItemEx("DMR_FireballTrail",-25,0,1,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			}
			A_SpawnItemEx("DMR_SmokeCloud",-25,0,1,frandom(-1,0),frandom(-1,1),frandom(0,1)); 
			fuelLevel = fuelLevel-1; //decrease fuel lifetime
		}
		HMIS A 1 BRIGHT A_Weave(random(1,2), random(1,2), frandom(0.5,1.5), frandom(0.5,1.5));
		Goto Homing+1;
	Death:
		TNT1 A 0 {
			A_Stop();
			A_PlaySound("skeleton/tracex",2);
		}
		TNT1 AAA 0 {
			A_SpawnItemEx("DMR_FireBouncingDebris",random(-1,1),random(-1,1),random(-1,1),frandom(-5,5),frandom(-5,5),frandom(-1,5));
		}
		FBXP A 8 Bright;
		FBXP B 6 Bright;
		FBXP C 4 Bright;
		Stop;
	}
}

//======================================================================
//  Original credits
//======================================================================
//	ZScript: 	DustedPandemonic, modified by Dutchygamer
//======================================================================
Class DMR_FlamethrowerBall: Actor
{
	Default
	{
		Radius 4;
		Height 4;
		Speed 13;
		Projectile;
		RenderStyle "Add";
		DamageType "Fire";
		Alpha 0.67;
		Scale 0.4;
		+THRUACTORS;
		+BRIGHT
		+NODAMAGETHRUST;
		+DONTSPLASH;
		VSpeed 0.5;
	}
	int dmgtic;
	
	States
	{
	Spawn:
		FRFX AABBCCDDEEFFGGHHIIJJKK 1 {
			A_SetScale(Scale.X + 0.03);
			// only deal damage X amount of tics instead of every tick
			dmgtic++;
			if(dmgtic >= 3)
			{
				A_Explode(2,32);
				dmgtic = 0;
			}		
		}
		FRFX LLMMNNOO 1 {
			A_SetScale(Scale.X + 0.03);
		}
		Stop;
	}
	
	// the 'float down to lowest point' logic
	// after waaay too many trial and errors with various combinations of actor flags, I realized it would be way easier to do this with Zscript overrides
	override void Tick()
	{
		Super.Tick();
		// check whether we are not on the floor
		// double check it by comparing the z value and the dedicated TestMobjZ function
		bool isInAir = ( (pos.z > floorz) && TestMobjZ() );
		// if in air, slowly increase the z velocity every tick so we float down slowly
		if ( isInAir ) {
			vel.z -= 0.08;
		// if not in the air, set our z velocity to 0
		} else {
			vel.z = 0;		
		}
	}
}

class DMR_FlamethrowerBallLarge : DMR_FlamethrowerBall
{
	Default
	{
		Radius 6;
		Height 6;
		Scale 0.7; //1;
	}
	States
	{
	Spawn:
		FRFX AABBCCDDEEFFGGHHIIJJKK 1 {
			A_SetScale(Scale.X + 0.03);
			// only deal damage X amount of tics instead of every tick
			dmgtic++;
			if(dmgtic >= 3)
			{
				A_Explode(2,64);
				dmgtic = 0;
			}		
		}
		FRFX LLMMNNOO 1 {
			A_SetScale(Scale.X + 0.03);
		}
		Stop;
	}
}

class DMR_FlamethrowerExplode : Actor
{
	Default
	{
		SeeSound "dragonfam/attack";
		Radius 8;
		Height 8;
		Gravity 0;
		Speed 0;
		Scale 0.7;
		+DONTSPLASH;
	}
	States
	{
	Spawn:
		NKXP B 2 Bright;
		NKXP C 2 Bright;
		NKXP D 2 Bright A_Explode(20, 96); //(15, 64)
		NKXP EFGHIJKLMNOPQRSTUV 2 Bright;
		Stop;
	}
}

class DMR_FlamethrowerExplodeLarge : DMR_FlamethrowerExplode
{
	Default
	{
		Scale 1.1;
	}
	States
	{
	Spawn:
		NKXP B 2 Bright;
		NKXP C 2 Bright;
		NKXP D 2 Bright A_Explode(30, 128); //(25, 96)
		NKXP EFGHIJKLMNOPQRSTUV 2 Bright;
		Stop;
	}
}

//laser
class DMR_LaserBase : FastProjectile
{
	Default
	{
		Radius 2;
		Height 2;
		Speed 125;
		DamageFunction(random(16,48));
		//this defines a laser
		+RANDOMIZE;
		+DONTSPLASH;
		+DONTREFLECT;
		-CANNOTPUSH;
		+NODAMAGETHRUST;
		RenderStyle "Add";
		Alpha 0.75;
		DamageType "Disintegrate";
		MissileType "LaserTrailBase";
		MissileHeight 10;
		seesound "BeamRevenant/Fire";
		decal "RailScorchLower";
		Scale 0.5;
	}
	States
	{
	Spawn:
		TNT1 A 1;
		Loop;
	Death:
		BFE1 ABCDEF 5 Bright;
		Stop;
	}
}

class DMR_LaserTrailBase : Actor
{
	Default
	{
		RenderStyle "Add";
		Scale 0.2;
		Alpha 0.9;
		+NOINTERACTION;
		+NOGRAVITY;
		+DONTSPLASH;
	}
	States
	{
	Spawn:
		TEFX C 1 bright;
		TEFX C 1 bright A_FadeOut(0.1);
		Loop;
	}
}

//gas clouds
class DMR_GasCloudBase : actor
{
	Default
	{
		Radius 6;
		Height 6;
		Projectile;
		DamageType "ToxinCloud";
		RENDERSTYLE "translucent";
		ALPHA 0.50;
		// tags for the gascloud itself
		+THRUACTORS;
		+DONTTHRUST;
		+RANDOMIZE;
		+DONTSPLASH;
		+DONTREFLECT;
		+NOBLOOD;
		// tags for the damage it deals
		+BLOODLESSIMPACT;
		+FORCERADIUSDMG;
		+NODAMAGETHRUST;
		+PAINLESS; // else it WILL stunlock everything hit by gasclouds		
		BounceType "Hexen";
		BounceFactor 1.0;
		WallBounceFactor 1.0;
		//note: with Scale 2.0, A_Explode should use 45 as radius
	}
	int lifetime;
	
	// the 'float down to lowest point' logic
	// after waaay too many trial and errors with various combinations of actor flags, I realized it would be way easier to do this with Zscript overrides
	override void Tick()
	{
		// check whether we are not on the floor
		// double check it by comparing the z value and the dedicated TestMobjZ function
		bool isInAir = ( (pos.z > floorz) && TestMobjZ() );
		// if in air, set a z velocity so we float down slowly
		if ( isInAir ) {
			vel.z = -0.4;
		// if not in the air, set our z velocity to 0
		} else {
			vel.z = 0;		
		}
		Super.Tick();
	}
}

class DMR_LargeGasCloud : DMR_GasCloudBase
{
	Default
	{
		Scale 2.0;
	}
	States
	{
	Spawn: 
		GGAS OPQR 6;
		TNT1 A 0 {
			lifetime = random(13,18); //should always be second, else it will run 1 time and die
		}
	SpawnLoop:
		TNT1 A 0 A_JumpIf(lifetime < 1,"Death");
		GGAS ABCDE 3 A_Explode(2,45,0);
		TNT1 A 0 {
			lifetime = lifetime-1;
		}
		GGAS FGHIJ 3 A_Explode(2,45,0);
		TNT1 A 0 {
			lifetime = lifetime-1;
		}
		GGAS KLMN 3 A_Explode(2,45,0);
		TNT1 A 0 {
			lifetime = lifetime-1;
		}
		Loop;
	Death: 
		GGAS S 3 A_SetTranslucent(0.40);
		TNT1 A 0 A_Explode(2,30,0);
		GGAS T 3 A_SetTranslucent(0.30);
		TNT1 A 0 A_Explode(2,20,0);
		GGAS U 3 A_SetTranslucent(0.20);
		TNT1 A 0 A_Explode(2,10,0);
		GGAS VW 3 A_SetTranslucent(0.10);
		Stop;
	}
}

class DMR_MediumGasCloud : DMR_GasCloudBase
{
	Default
	{
		Scale 1.5;
	}
	States
	{
	Spawn: 
		GGAS OPQR 6;
		TNT1 A 0 {
			lifetime = random(9,14); //should always be second, else it will run 1 time and die
		}
	SpawnLoop:
		TNT1 A 0 A_JumpIf(lifetime < 1,"Death");
		GGAS ABCDE 3 A_Explode(2,34,0);
		TNT1 A 0 {
			lifetime = lifetime-1;
		}
		GGAS FGHIJ 3 A_Explode(2,34,0);
		TNT1 A 0 {
			lifetime = lifetime-1;
		}
		GGAS KLMN 3 A_Explode(2,34,0);
		TNT1 A 0 {
			lifetime = lifetime-1;
		}
		Loop;
	Death: 
		GGAS S 3 A_SetTranslucent(0.40);
		TNT1 A 0 A_Explode(2,25,0);
		GGAS T 3 A_SetTranslucent(0.30);
		TNT1 A 0 A_Explode(2,18,0);
		GGAS U 3 A_SetTranslucent(0.20);
		TNT1 A 0 A_Explode(2,10,0);
		GGAS VW 3 A_SetTranslucent(0.10);
		Stop;
	}
}

class DMR_SmallGasCloud : DMR_GasCloudBase
{
	States
	{
	Spawn: 
		GGAS OPQR 6;
		TNT1 A 0 {
			lifetime = random(4,7); //should always be second, else it will run 1 time and die
		}
	SpawnLoop:
		TNT1 A 0 A_JumpIf(lifetime < 1,"Death");
		GGAS ABCDE 3 A_Explode(2,28,0);
		TNT1 A 0 {
			lifetime = lifetime-1;
		}
		GGAS FGHIJ 3 A_Explode(2,28,0);
		TNT1 A 0 {
			lifetime = lifetime-1;
		}
		GGAS KLMN 3 A_Explode(2,28,0);
		TNT1 A 0 {
			lifetime = lifetime-1;
		}
		Loop;
	Death: 
		GGAS S 3 A_SetTranslucent(0.40);
		TNT1 A 0 A_Explode(2,21,0);
		GGAS T 3 A_SetTranslucent(0.30);
		TNT1 A 0 A_Explode(2,15,0);
		GGAS U 3 A_SetTranslucent(0.20);
		TNT1 A 0 A_Explode(2,10,0);
		GGAS VW 3 A_SetTranslucent(0.10);
		Stop;
	}
}

class DMR_MediumIceCloud : DMR_MediumGasCloud
{
	Default
	{
		DamageType "Ice";
	}
	States
	{
	Spawn: 
		GAS3 HIJK 6;
		TNT1 A 0 {
			lifetime = random(9,14); //should always be second, else it will run 1 time and die
		}
	SpawnLoop:
		TNT1 A 0 A_JumpIf(lifetime < 1,"Death");
		GAS3 AB 3 A_Explode(2,34,0);
		TNT1 A 0 {
			lifetime = lifetime-1;
		}
		GAS3 CDE 3 A_Explode(2,34,0);
		TNT1 A 0 {
			lifetime = lifetime-1;
		}
		GAS3 FG 3 A_Explode(2,34,0);
		TNT1 A 0 {
			lifetime = lifetime-1;
		}
		Loop;
	Death: 
		GAS3 L 3 A_SetTranslucent(0.40);
		TNT1 A 0 A_Explode(2,25,0);
		GAS3 M 3 A_SetTranslucent(0.30);
		TNT1 A 0 A_Explode(2,18,0);
		GAS3 N 3 A_SetTranslucent(0.20);
		TNT1 A 0 A_Explode(2,10,0);
		GAS3 O 3 A_SetTranslucent(0.10);
		Stop;
	}
}

//fire field
class DMR_BurningFire : Actor
{
	Default
	{
		Radius 4;
		Height 4;
		RenderStyle "Add";
		Alpha 0.9;
		DamageType "Fire";
		Scale 2.5; //3; //0.7
		+THRUACTORS;		
		+NOBLOOD;
		+RANDOMIZE;
		+FORCERADIUSDMG;
		+NODAMAGETHRUST;
		+DONTTHRUST;
		+DONTSPLASH;
		+PAINLESS; // else it WILL stunlock everything hit by a Mancutank rocket barrage
	}
	
	int lifetime;
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			lifetime = random(6,10);
		}
	SpawnLoop:
		TNT1 A 0 A_JumpIf(lifetime < 1,"Death");
		CFCF ABCDE 2 Bright A_Explode(2,48,0);
		TNT1 A 0 {
			lifetime -= 1;
		}
		CFCF FGHIJ 2 Bright A_Explode(2,48,0);
		TNT1 A 0 {
			lifetime -= 1;
		}
		Loop;
	Death:
		CFCF KLMOP 2 Bright;
		Stop;
	}
}

// a visible alternative to hitscan attacks
class DMR_SlowBullet : Actor
{
	Default
	{
		Radius 6;
		Height 3;
		Speed 60;
		DamageFunction(random(6,16));
		Projectile;
		Scale 0.2;
		Decal ""; //"BulletChip";
		+BLOODSPLATTER;
	}
	States
	{
	Spawn:
		SHLL A 1 Bright;
		Loop;
	Death:
		TNT1 A 1 A_SpawnItemEx("BulletPuff");
		Stop;
	XDeath:
		TNT1 A 1;
		Stop;
	}
}
